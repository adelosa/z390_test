         MACRO
.*********************************************************************
.* Copyright 2005 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 09/30/05                                                 *
.*********************************************************************
.* 11/10/06 RPI 477 SUPPORT ASCII OPTION, REGISTER INPUT, RC=12 VS ERR
.*********************************************************************
.*
.*  CONVERT INPUT NUMERIC FIELD TO DISPLAY EBCDIC FORMAT
.*    TYPE    INPUT                OUTPUT
.*      1       128 BIT INTEGER      40 BYTE DECIMAL (SIGN+39 DIGITS)
.*      2       FP SHORT EH          40 BYTE SCIENTIFIC (N.N(7)+ENNNN)
.*      3       FP SHORT EB          40 BYTE SCIENTIFIC (N.N(7)+ENNNN)
.*      4       FP LONG  DH          40 BYTE SCIENTIFIC (N.N(14)+ENNNN)
.*      5       FP LONG  DB          40 BYTE SCIENTIFIC (N.N(14)+ENNNN)
.*      6       FP EXT.  LH          40 BYTE SCIENTIFIC (N.N(34)+ENNNN)
.*      7       FP EXT.  LB          40 BYTE SCIENTIFIC (N.N(34)+ENNNN)
.*
.* SVC 170 INPUT 
.*  R1 = A(TYPE,IN,OUT)
.* SVC 170 OUTPUT
.*  R15 = 0 OK, 12 INVALID REQUEST
.*********************************************************************
&N       CTD   &TYPE,        CONVERTION TYPE - SEE BELOW               X
               &IN=,         SOURCE FIELD (RX OR REG)                  X
               &OUT=,        TARGET FIELD (RX OR REG)                  X
               &LINKAGE=SVC  USE Z390 SVC ELSE USE CALL TO FPCONMFC
         AIF   (&N EQ '').SKIPDS
&N       DS    0H
.SKIPDS  ANOP 
         GBLB  &CTD_EQUS
         AIF   (&CTD_EQUS).SKIP_CTD_EQUS
&CTD_EQUS SETB 1
CTD_INT128 EQU 1 CONVERT 128 BIT INT TO 45 BYTE EBCDIC DECIMAL 
CTD_EH   EQU   2 CONVERT FP SHORT EH TO 45 BYTE EBCDIC N.N(6)+ENNNN
CTD_EB   EQU   3 CONVERT FP SHORT EB TO 45 BYTE EBCDIC N.N(7)+ENNNN
CTD_DH   EQU   4 CONVERT FP LONG  DH TO 45 BYTE EBCDIC N.N(14)+ENNNN
CTD_DB   EQU   5 CONVERT FP LONG  DB TO 45 BYTE EBCDIC N.N(15)+ENNNN
CTD_LH   EQU   6 CONVERT FP EXT.  LH TO 45 BYTE EBCDIC N.N(34)+ENNNN
CTD_LB   EQU   7 CONVERT FP EXT.  LB TO 45 BYTE EBCDIC N.N(34)+ENNNN
.SKIP_CTD_EQUS ANOP
         AIF   ('&IN'(1,1) EQ '=').LIT
         LA    1,=A(&TYPE,&IN,&OUT)
         AGO   .SKPLIT
.LIT     ANOP
         LA    1,=A(&TYPE,0,&OUT)
         LA    0,&IN
         ST    0,4(1)
.SKPLIT  ANOP
         AIF   ('&TYPE'(1,1) NE '(').CHKIN
         ST    &TYPE(1),0(1)
.CHKIN   ANOP
         AIF   ('&IN'(1,1) NE '(').CHKOUT
         ST    &IN(1),4(1)
.CHKOUT  ANOP
         AIF   ('&OUT'(1,1) NE '(').SVC
         ST    &OUT(1),8(1)
.SVC     ANOP
         AIF   (&LINKAGE NE 'SVC').CALL
         SVC   170 CTD R1=A(TYPE,IN,OUT)
         MEXIT
.CALL    ANOP
         L     15,=V(FPCONMFC) CFD MAINFRAME CALL ROUTINE
         BASR  14,15 CALL FPCONMFC,(TYPE,OUT,IN)
         MEND               
