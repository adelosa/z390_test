         MACRO                                                             
.********************************************************************
.*             AUTHOR.   DON HIGGINS.                                      
.*             DATE.     09/23/08.                                         
.*             REMARKS.  ADD SPM MACROS FOR CASENTRY, CASE, ENDCASE        
.********************************************************************
.* 09/23/08 RPI 909 upgrade to use ZSTRMAC SPE's and extend compat.
.* 11/10/08 RPI 946 fill address table to avoid INIT x'F6' in 390  
.********************************************************************
         CASE  &VALUE                                                                        
         COPY  ZSTRGBL
         AIF   (&ZSTR_LVL EQ 0)
               MNOTE 8,'CASE - MISSING CASENTRY'                                     
               MEXIT
         AEND     
         AIF   (&ZSTR_LVL_TYPE(&ZSTR_LVL) NE 'CASENTRY')
               MNOTE 8,'CASE - MISSING CASENTRY'                                     
               MEXIT
         AEND     
         AIF   (T'&VALUE NE 'N')
               MNOTE 8,'CASE - INVALID VALUE 0-N'
         AEND
         AIF   (&VALUE GT &ZSTR_CASE_MAX(&ZSTR_LVL))
               :&ZSTR_CASE_MAX(&ZSTR_LVL) SETA &VALUE
         AEND
         AIF   (&ZSTR_LVL_BCNT(&ZSTR_LVL) GT 0)
         B     #@CAS_&ZSTR_LVL_TCNT(&ZSTR_LVL)_E
         AEND
         :&ZSTR_LVL_BCNT(&ZSTR_LVL) SETA &ZSTR_LVL_BCNT(&ZSTR_LVL)+1
#@CAS_&ZSTR_LVL_TCNT(&ZSTR_LVL)_&VALUE DS 0H
#@CAS_&ZSTR_LVL_TCNT(&ZSTR_LVL)_L LOCTR
         AIF   (&VALUE GT &ZSTR_CASE_LAST_INDEX) rpi 946
               :&LFILL SETA (&VALUE-&ZSTR_CASE_LAST_INDEX-1)
               AIF (&LFILL GT 0)
         DC    (&LFILL)A(0)
               AEND
               :&ZSTR_CASE_LAST_INDEX SETA &VALUE
         AELSE
               MNOTE 8,'CASE VALUE NOT IN ASCENDING SEQUENCE - &VALUE'
         AEND         
         DC    A(#@CAS_&ZSTR_LVL_TCNT(&ZSTR_LVL)_&VALUE)
&SYSECT  CSECT         
         MEND
