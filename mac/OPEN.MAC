         MACRO
.*********************************************************************
.* Copyright 2005 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 09/30/05                                                 *
.*********************************************************************
.* 10/20/05 RPI27 - issue error for any undefined operands   
.* 11/13/05 RPI88 - correct test for DCB register parm  
.* 11/13/05 RPI96 - add additional validity checking for parms
.* 01/09/06 RPI163 SKIP LR FOR SAME REGISTER
.* 04/10/06 RPI244 IGNORE OR ISSUE UNSUPPORTED ERROR, SUPPORT (REG)
.* 06/14/07 RPI 641 DEFAULT MF=I
.* 08/03/07 RPI 668 check return code for each svc and exit if nz
.*********************************************************************
&N       OPEN  &DCBLIST,   ((DCB,OPT),(DCB,OPT),,) OR DCB OR (REG)     X
               &MF=I,      NOT SUPPORTED                               X
               &MODE=31,   IGNORED AS IT WORKS IN BOTH                 X
               &TYPE=      FLAG TYPE J AS UNSUPPORTED               
         AIF   (&N EQ '').SKIPDS
&N       DS    0H
.SKIPDS  ANOP
         AIF   (&MF NE 'I').ERR1
         AIF   (N'&SYSLIST EQ 1).NPOK
         MNOTE 12,'UNSUPPORTED OPERANDS - &SYSLIST(2)'
.NPOK    ANOP
.*
.* OPEN 1 OR MORE FILES DEFINED BY DCB'S WITH INPUT, OUTPUT, UPDATE
.* OPTIONS
.*
         AIF   (&TYPE EQ '').NOTYPE
         MNOTE 12,'OPEN TYPE= NOT SUPPORTED'
         MEXIT
.NOTYPE  ANOP
&NP      SETA  N'&SYSLIST(1)
&I       SETA  -1
.LOOP    ANOP
&I       SETA   &I+2
         AIF   (&I GT &NP).EXIT
         AIF   (&NP GT 1 OR '&DCBLIST'(1,1) NE '('                     X
               OR '&DCBLIST'(1,2) NE '(').SUBLISTS
&DCB     SETC  &DCBLIST
&OPTION  SETC  ''
         AGO   .DCBREG
.SUBLISTS ANOP
&DCB     SETC  &SYSLIST(1,&I)
&OPTION  SETC  &SYSLIST(1,&I+1,1)
         AIF   ('&DCB'(1,1) EQ '(').DCBREG
         AIF   ('&DCB'(1,1) GT '9' OR '&DCB'(1,1) LT '0').RXOK
         MNOTE 12,'INVALID DCB RX ADDRESS - &DCB'
         MEXIT
.RXOK    ANOP
         LA    1,&DCB
         AGO   .CHKOPT
.DCBREG  ANOP
         AIF   (&DCB(1) EQ '1' OR &DCB(1) EQ 'R1').CHKOPT  RPI163
         LR    1,&DCB(1)
.CHKOPT  ANOP
         AIF   ('&OPTION' EQ '').INPUT
         AIF   ('&OPTION' EQ 'INPUT').INPUT
         AIF   ('&OPTION' EQ 'OUTPUT').OUTPUT
         AIF   ('&OPTION' EQ 'UPDATE').UPDATE
         MNOTE 12,'OPEN OPTION NOT SUPPORTED - &OPTION'
         MEXIT
.INPUT   ANOP
         LA    0,DCBOFLGS_GM   READ ONLY
         AGO   .SVC
.OUTPUT  ANOP
         LA    0,DCBOFLGS_PM   WRITE ONLY
         AGO   .SVC
.UPDATE  ANOP
         LA    0,DCBOFLGS_RW   READ AND WRITE
.SVC     ANOP
         SVC   19 OPEN R0=TYPE,R1=DCB/ACB
         AIF   (&I+2 GT &NP).EXIT
         LTR   15,15
         BNZ   Z390_OPEN_EXIT&SYSNDX RPI668
         AGO   .LOOP
.EXIT    ANOP
         AIF   (&NP LE 2).EXIT1
Z390_OPEN_EXIT&SYSNDX EQU *
.EXIT1   ANOP
         MEXIT
.ERR1    MNOTE 12,'OPEN MF= NOT SUPPORTED'
         MEND
