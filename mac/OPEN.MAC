         MACRO
.*********************************************************************
.* Copyright 2005 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 09/30/05                                                 *
.*********************************************************************
&N       OPEN
.*
.* OPEN 1 OR MORE FILES DEFINED BY DCB'S WITH INPUT, OUTPUT, UPDATE
.* OPTIONS
.*
&N       DS    0H
&NP      SETA  N'&SYSLIST(1)
&I       SETA  -1
.LOOP    ANOP
&I       SETA   &I+2
         AIF   (&I GT &NP).EXIT
&DCB     SETC  &SYSLIST(1,&I,1)
&OPTION  SETC  &SYSLIST(1,&I+1,1)
         AIF   ('&DCB'(1,1) EQ '(').DCBREG
         LA    1,&DCB
         AGO   .CHKOPT
.DCBREG  ANOP
         LR    1,&DCB(1)
.CHKOPT  ANOP
         AIF   ('&OPTION' EQ 'INPUT').INPUT
         AIF   ('&OPTION' EQ 'OUTPUT').OUTPUT
         AIF   ('&OPTION' EQ 'UPDATE').UPDATE
         MNOTE 12,'OPEN OPTION NOT SUPPORTED - &OPTION'
         MEXIT
.INPUT   ANOP
         LA    0,DCBOFLGS_GM   READ ONLY
         AGO   .SVC
.OUTPUT  ANOP
         LA    0,DCBOFLGS_PM   WRITE ONLY
         AGO   .SVC
.UPDATE  ANOP
         LA    0,DCBOFLGS_RW   READ AND WRITE
.SVC     ANOP
         SVC   19
         AGO   .LOOP
.EXIT    ANOP
         MEND
