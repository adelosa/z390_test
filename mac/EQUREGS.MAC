        MACRO
.*********************************************************************
.* Copyright 2005 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 09/30/05                                                 *
.*********************************************************************
.*  EQUREGS ,        - default to R0 - R15 symbols for 0-15 general
.*                     purpose register references
.*  EQUREGS REGS=FPR - generate F0 - F15 symbols for 0-15 floating
.*                     point registers
.*  EQUREGS TYPE=HEX - generate R0 - RF symbols for 0-15 general
.*                     purpose registers      
.*  EQUREGS REGS=FPC,TYPE=HEX - generate F0 - FF symbols for FPR's
.*********************************************************************
.* 10/20/05 RPI27  - issue error for any undefined operands     
.* 12/07/05 RPI119 - add FPR and hex options                     
.* 04/19/08 RPI 833 add SETC quotes for HLASM compatibility
.*********************************************************************
        EQUREGS &REGS=GPR,&TYPE=DEC
&PFX    SETC   'R'
        AIF    ('&REGS' EQ 'GPR').SKPFPR
&PFX    SETC   'F'
.SKPFPR ANOP
        LCLC   &HEX(16)
&HEX(1) SETC   '0','1','2','3','4','5','6','7',                        X
               '8','9','A','B','C','D','E','F'
        AIF   ('&SYSLIST(1)' EQ '').NPOK
        MNOTE 12,'UNSUPPORTED OPERANDS - &SYSLIST(1)'
.NPOK   ANOP
&I      SETA   -1
.LOOP   ANOP
&I      SETA   &I+1
        AIF    ('&TYPE' EQ 'HEX').GENHEX
&PFX&I  EQU    &I
        AIF    (&I LT 15).LOOP
        MEXIT
.GENHEX ANOP
&HEX_SDT SETC   'X''&HEX(&I+1).'''
&PFX&I  EQU    &HEX_SDT
        AIF    (&I LT 15).LOOP
        MEND
