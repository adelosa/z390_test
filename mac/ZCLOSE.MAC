         MACRO
.*********************************************************************
.* Copyright 2005 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 09/30/05                                                 *
.*********************************************************************
.* 10/20/05 RPI27 - issue error for any undefined operands     
.* 11/13/05 RPI88 - correct error in checking for DCB register parm
.* 11/26/05 RPI112- check for invalid DCB RX form (reg) vs ((reg)) 
.* 01/09/06 RPI163 SKIP LR FOR SAME REGISTER
.* 04/10/06 RPI244 support (reg) and ignore other keywords
.* 06/14/07 RPI 641 DEFAULT MF=I
.* 08/03/07 RPI 668 check return code for each svc and exit if nz
.* 08/21/07 RPI 670 CORRECT REG OPTIMIZATION TO HANDLE ANY REG SYMBOL
.* 10/14/07 RPI 718 ISSUE MNOTE ERR FOR NN OR RNN WITHOUT ((REG),OPT)   
.* 04/15/08 RPI 838 CHANGE OPEN, CLOSE, READ, WRITE TO CALL Z VERSION                     
.* 04/19/08 RPI 833 add SETC quotes for HLASM compatibility
.* 09/12/08 RPI 764 upgrade to ZSTRMAC and allow CLOSE (reg)
.*********************************************************************
&N       ZCLOSE &DCBLIST, ((DCB,OPT),(DCB2,OPT2)..) OR (DCB) OR DCB    X
               &MODE=,   IGNORED AS IT WORKS IN EITHER MODE            X
               &MF=I,    NOT SUPPORTED                                 X
               &TYPE=    IGNORED
         AIF   ('&N' NE '')    
               :&N       DS    0H
         AEND
         AIF   ('&MF' NE 'I') 
               MNOTE 12,'CLOSE MF= NOT SUPPORTED'
               MEXIT
         AEND
         AIF   (N'&SYSLIST GT 1)
               MNOTE 12,'CLOSE UNSUPPORTED OPERANDS - &SYSLIST(2)'
               MEXIT
         AEND
.*
.* CLOSE 1 OR MORE FILES DEFINED IN DCBLIST 
.*
&NP      SETA  N'&DCBLIST
&I       SETA  1
         AWHILE (&I LE &NP)
               AIF   (&NP GT 1 OR '&DCBLIST'(1,1) NE '('               X
               OR '&DCBLIST'(1,2) NE '(')                
                   :&DCB     SETC  '&SYSLIST(1,&I)'
                   :&OPTION  SETC  '&SYSLIST(1,&I+1,1)'
               AELSE
                   :&DCB     SETC  '&DCBLIST'
                   :&OPTION  SETC  ''
               AEND
               AIF ('&DCB'(1,1) EQ '(' OR T'&DCB EQ 'N')  RPI 764
                   ZOPTLR 1,&DCB(1)
               AELSE
                   LA  1,&DCB
               AEND
               AIF   ('&OPTION' NE '')
                     MNOTE 12,'CLOSE OPTION NOT SUPPORTED - &OPTION'
                     MEXIT
               AEND
               SVC   20 CLOSE R1=DCB/ACB
               AIF    (&NP GT 2)
                      LTR   15,15
                      BNZ   Z390_CLOSE_EXIT&SYSNDX RPI 668
               AEND
               :&I SETA &I+2
         AEND
         AIF   (&NP GT 2)       
               :Z390_CLOSE_EXIT&SYSNDX EQU *
         AEND
         MEND
