         MACRO
.*********************************************************************
.* Copyright 2005 Automated Software Tools Corporation               * 
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 09/30/05                                                 *
.*********************************************************************
.* 10/20/05 RPI27  - issue error for any undefined operands     
.* 10/20/05 RPI89  - issue error for any undefined operands     
.* 11/16/05 RPI95  - add DDNAME and DSNAME support and doc RC   
.* 11/19/05 RPI102 - allow DDNAME/DSNAME to replace EP/EPLOC   
.* 12/06/05 RPI118 - use SR vs LA, reg chk, drop BALR/USING, fix VL
.* 01/09/06 RPI163 SKIP LR FOR SAME REGISTER
.* 03/28/06 RPI266 CORRECT MISSING .SKP1 TO .SKPLR1
.* 04/11/06 RPI244 ERROR ON UNSUPPORTED KEYWORDS, SUPPORT DE=,LOADPT=
.* 06/14/07 RPI 641 DEFAULT MF=I
.* 08/21/07 RPI 670 CORRECT REG OPTIMIZATION TO HANDLE ANY REG SYMBOL
.*********************************************************************
.* LOAD PGM VIA SVC 8
.*
.* INPUT 
.*   R0  = ADDR PGM NAME (8 BYTES SPACE FILLED)
.*   R15 = 0    USE SYS390 DEFAULT PATH FROM EZ390 PARM
.*   R15 = ADDR DSNAME FILE SPEC IF HIGH BIT OFF (NULL OR DQ DELIMITED)
.*   R15 = ADDR DDNAME IF HIGH BIT ON (8 BYTES SPACE FILLED)
.*
.* OUTPUT
.*   R0  = LOAD ADDRESS
.*   R15 = O IF SUCCESSFUL
.*   R15 = 4 IF LOAD FAILED
.*
.* NOTES:
.*   1.  DSNAME OR DDNAME MAY BE 1 OR MORE DIRECTORY PATHS TO SEARCH
.*       OR THEY MAY BE PATH AND FILE NAME WHICH OVERRIDES EP/EPLOC
.*********************************************************************
&N       LOAD  &DCB=,     IGNORED                                      X
               &DDNAME=,  1 TO 8 CHARACTER DDNAME                      X
               &DE=,      BLDL ENTRY                                   X
               &DSNAME=,  RX LABEL OR (REG) > NULL OR ".." DIR/DSN     X
               &EP=,      1 TO 8 CHARACTER PROGRAM NAME                X
               &EPLOC=,   RX LABEL OR (REG) > OF 8 BYTE PGM NAME       X
               &ERRET=,   NOT UNSUPPORTED                              X
               &EXTINFO=, IGNORED                                      X
               &LSEARCH=, IGNORED                                      X
               &LOADPT=,  STORE LOAD ADDRESS                           X
               &MF=I,     NOT SUPPORTED                                X
               &RELATED=  IGNORED
         AIF   (&N EQ '').SKIPDS
&N       DS    0H
.SKIPDS  ANOP
         AIF   (&MF NE 'I').ERR2
         AIF   (N'&SYSLIST EQ 0).NPOK
         MNOTE 12,'LOAD UNSUPPORTED OPERANDS - &SYSLIST(1)'
.NPOK    ANOP
         AIF   (&DE NE '').DE
         AIF   (&ERRET NE '').ERR1
         AIF   (&EP NE '').EP
         AIF   (&EPLOC NE '').EPLOC
         SR    0,0
         AIF   (&DDNAME NE '' OR &DSNAME NE '').CHKDSN
         MNOTE 12,'LOAD REQUIRES DE, EP, EPLOC, DDNAME, OR DSNAME'
         MEXIT
.DE      ANOP
         AIF   ('&DE'(1,1) EQ '(').DEREG
         LA    0,&DE
         AGO   .DEADD
.DEREG ANOP
         ZOPTLR 0,&DE(1)
.DEADD   ANOP
         AHI   0,2
         AIF   (&EP EQ '' AND &EPLOC EQ '').CHKDSN
         MNOTE 12,'LOAD DE EP AND EPLOC ARE MUTUALLY EXCLUSIVE'
         MEXIT
.EP      ANOP
         LA    0,=CL8'&EP'
         AIF   (&DE EQ '' AND &EPLOC EQ '').CHKDSN
         MNOTE 12,'LOAD DE EP AND EPLOC ARE MUTUALLY EXCLUSIVE'
         MEXIT
.EPLOC   ANOP
         AIF   ('&EPLOC'(1,1) EQ '(').EPLOCREG
         LA    0,&EPLOC
         AIF   (&DE EQ '' AND &EP EQ '').CHKDSN
         MNOTE 12,'LOAD DE EP AND EPLOC ARE MUTUALLY EXCLUSIVE'
         MEXIT
.EPLOCREG ANOP
         ZOPTLR 0,&EPLOC(1)
.CHKDSN  ANOP
         AIF   (&DSNAME EQ '').CHKDD
         AIF   (&DDNAME EQ '').DSNOK
         MNOTE 12,'LOAD DSNAME AND DDNAME ARE MUTUALLY EXCLUSIVE'
         MEXIT
.DSNOK   ANOP
         AIF   ('&DSNAME'(1,1) EQ '(').DSNREG
         LA    15,&DSNAME
         AGO   .SVC
.DSNREG  ANOP
         AIF   (&DSNAME(1) EQ '0' OR &DSNAME(1) EQ 'R0').DSNERR
         ZOPTLR 15,&DSNAME(1)
         AGO   .SVC
.DSNERR  ANOP
         MNOTE 12,'LOAD INVALID DSNAME REG'
         MEXIT
.CHKDD   ANOP
         AIF   (&DDNAME EQ '').DEFPATH
         AIF   ('&DDNAME'(1,1) EQ '(').DDREG
         LA    15,=CL8'&DDNAME'
         OILH  15,X'8000' DD VS DSN
         AGO   .SVC
.DDREG   ANOP
         AIF   (&DDNAME(1) EQ '0' OR &DDNAME(1) EQ 'R0').DDERR
         ZOPTLR 15,&DDNAME(1)
.SKPLR1  ANOP
         OILH  15,X'8000' DD VS DSN
         AGO   .SVC
.DDERR   ANOP
         MNOTE 12,'LOAD DDNAME INVALID REG'
         MEXIT
.DEFPATH ANOP
         SR    15,15 USE DEFAULT SYS390 SEARCH
.SVC     ANOP
         SVC   8 LOAD R0=A(NAME) R15=DDNAME/DSNAME ADDR OR 0
         AIF   (&LOADPT EQ '').EXIT
         AIF   ('&LOADPT'(1,1) EQ '(').PTREC
         ST    0,&LOADPT
         MEXIT
.PTREC   ANOP
         ST    0,0(&LOADPT(1))
.EXIT    MEXIT
.ERR1    MNOTE 12,'LOAD ERRET= NOT SUPPORTED'
         MEXIT
.ERR2    MNOTE 12,'LOAD MF= NOT SUPPORTED'
         MEXIT
         MEND
