         MACRO
.*********************************************************************
.* Copyright 2005 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 07/17/06                                                 *
.*********************************************************************
.* 11/10/06 RPI 477 SUPPORT ASCII OPTION, REGISTER INPUT, RC=12 VS ERR
.*********************************************************************
.*
.*  CONVERT FROM EBCDIC DECIMAL FIELD TO BINARY FIELD FORMAT
.*    TYPE    OUTPUT               INPUT 
.*     21       128 BIT INTEGER    45 BYTE DECIMAL (SIGN+39 DIGITS)
.*     22       FP SHORT EH        45 BYTE SCIENTIFIC (N.N(7)+ENNNN)
.*     23       FP SHORT EB        45 BYTE SCIENTIFIC (N.N(7)+ENNNN)
.*     24       FP LONG  DH        45 BYTE SCIENTIFIC (N.N(14)+ENNNN)
.*     25       FP LONG  DB        45 BYTE SCIENTIFIC (N.N(14)+ENNNN)
.*     26       FP EXT.  LH        45 BYTE SCIENTIFIC (N.N(34)+ENNNN)
.*     27       FP EXT.  LB        45 BYTE SCIENTIFIC (N.N(34)+ENNNN)
.*
.* IF LINKAGE=SVC
.*   USE CFD Z390 SVC 171 R1=A(TYPE,OUT,IN)
.* ELSE
.*   USE CFD MAINFRAME SERVICE ROUTINE MFCONMFS(TYPE,OUT,IN)
.* ENDIF
.*
.* R15 RETURN CODE
.*    0 OK
.*   12 INVALID REQUEST
.*
.*********************************************************************
&N       CFD   &TYPE,         SEE 7 CONVERSION TYPES BELOW             X
               &IN=,          SOURCE FIELD (RX OR REG)                 X
               &OUT=,         TARGET FIELD (RX OR REG)                 X
               &LINKAGE=SVC   SVC= Z390 171, MF=CALL TO FPCONMFC          
         AIF   (&N EQ '').SKIPDS
&N       DS    0H
.SKIPDS  ANOP 
         GBLB  &CFD_EQUS
         AIF   (&CFD_EQUS).SKIP_CFD_EQUS
&CFD_EQUS SETB 1
CFD_INT128 EQU 21 CONVERT TO 128 BIT INT FROM 45 BYTE SN(39) 
CFD_EH   EQU   22 CONVERT TO FP SHORT EH FROM 45 BYTE SN.N(7)+ENNNN
CFD_EB   EQU   23 CONVERT TO FP SHORT EB FROM 45 BYTE SN.N(7)+ENNNN
CFD_DH   EQU   24 CONVERT TO FP LONG  DH FROM 45 BYTE SN.N(14)+ENNNN
CFD_DB   EQU   25 CONVERT TO FP LONG  DB FROM 45 BYTE SN.N(14)+ENNNN
CFD_LH   EQU   26 CONVERT TO FP EXT.  LH FROM 45 BYTE SN.N(34)+ENNNN
CFD_LB   EQU   27 CONVERT TO FP EXT.  LB FROM 45 BYTE SN.N(34)+ENNNN
.SKIP_CFD_EQUS ANOP
         LA    1,=A(&TYPE,&OUT,&IN)
         AIF   ('&TYPE'(1,1) NE '(').CHKIN
         ST    &TYPE(1),0(1)
.CHKIN   ANOP
         AIF   ('&IN'(1,1) NE '(').CHKOUT
         ST    &IN(1),4(1)
.CHKOUT  ANOP
         AIF   ('&OUT'(1,1) NE '(').SVC
         ST    &OUT(1),8(1)
.SVC     ANOP
         AIF   (&LINKAGE NE 'SVC').CALL
         SVC   171 CFD Z390 SVC R1=A(TYPE,OUT,IN)
         MEXIT
.CALL    ANOP
         L     15,=V(FPCONMFC) CFD MAINFRAME CALL ROUTINE
         BASR  14,15 CALL FPCONMFC,(TYPE,OUT,IN)
         MEND               
