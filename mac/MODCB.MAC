         MACRO
.*********************************************************************
.* Copyright 2005 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 09/30/05                                                 *
.*********************************************************************
.* 08/10/07 INITIAL CODING TO SUPPORT AREA, RECLEN INITIALLY                         
.* 08/22/07 RPI 680 CORRECT REPLRECL
.* 08/27/07 RPI 688 SUPPORT RPL=(REG)
.* 09/03/07 RPI 690 ADD ARG= AND OPTCD= SUPPORT
.* 09/03/07 RPI 672 FIX 3 MISSPELLED LABELS,&OPTCDF,OPTF1/2,&MODS ERRS
.*          ADD CROSS CHECK FOR ACB/RPL OPTION MATCHS
.* 09/15/07 RPI 696 ADD SUPPORT FOR RPL OPTCD FWD DEFAULT OPTION SET   
.*          ADD RPL FIELD ACB ADDR
.* 09/17/07 RPI 682 ADD ACB DDNAME, DSNAME, VCLRN, 
.*          and use ZGENMACF and ZGENOPTD
.* 09/19/07 RPI 697 SET RC=0                                                       
.*********************************************************************
&N       MODCB &ACB=,        ACB CONTROL BLOCK ADDRESS (ALSO RPL FIELD)X
               &DDNAME=,     ACB DDNAME FOR VCDT VSAM CATALOG.ENTRY    X
               &DSNAME=,     ACB DSNAME OVERRIDE ADDR VCDT CAT.ENTRY   X
               &VCLRN=,      ACB VCDT CATALOG DEFAULT ENTRY NAME       X
               &MACRF=,      ACB OPTIONS                               X
               &RPL=,        RPL REQUEST PARAMATER LIST ADDRESS        X
               &AREA=,       RPL ADDR OF RECORD AREA                   X
               &ARG=,        RPL ADDR OF ARG FOR KEY/REC#/RBA/XRBA     X
               &OPTCD=,      RPL OPTION FLAGS                          X
               &RECLEN=      RPL RECORD LENGTH                              
         LCLB  &MODS         SET IF MOD FOUND
&NPOS    SETA  N'&SYSLIST
.POSLOOP ANOP
         AIF   (&NPOS EQ 0).POSEND
         MNOTE 12,'MODCB UNSUPPORTED OPERAND - &SYSLIST(&NPOS)'
&NPOS    SETA  &NPOS-1
         AIF   (&NPOS GT 0).POSLOOP
         MEXIT
.POSEND  ANOP  
         AIF   (&N EQ '').SKIPDS
&N       DS    0H
.SKIPDS  ANOP
.*
.*  CHECK ACB FIELDS
.*
         AIF   (&ACB  EQ '').CHK_RPL_FIELDS
         AIF   (&DDNAME NE '').DDNAME
.DDNAME_END ANOP
         AIF   (&DSNAME NE '').DSNAME
.DSNAME_END ANOP
         AIF   (&MACRF  NE '').MACRF
.MACRF_END ANOP
         AIF   (&VCLRN NE '').VCLRN 
.VCLRN_END ANOP
.*      
.* CHECK RPL FIELDS
.*
.CHK_RPL_FIELDS ANOP
         AIF   (&RPL  EQ '').CHK_MODS
         AIF   (&ACB  NE '').ACB
.ACB_END ANOP
         AIF   (&AREA NE '').AREA
.AREA_END ANOP
         AIF   (&ARG    NE '').ARG
.ARG_END ANOP
         AIF   (&OPTCD  NE '').OPTCD
.OPTCD_END ANOP
         AIF   (&RECLEN NE '').RECLEN
.RECLEN_END ANOP
.*
.* CHECK IF ANY FIELDS FOUND
.*
.CHK_MODS ANOP
         AIF   (&MODS).EXIT
         MNOTE 12,'MODCB NO SUPPORTED FIELDS FOUND'
         MEXIT
.EXIT    ANOP
         SR    15,15
         MEXIT
.*
.* MODIFY ACB DDNAME
.*
.DDNAME  ANOP
&MODS    SETB   1
         ZMODCB &ACB,ACBDDNAM-IHAACB,&DDNAME,8
         AGO   .DDNAME_END
.*
.* MODIFY ACB DSNAME
.*
.DSNAME  ANOP
&MODS    SETB   1
         ZMODCB &ACB,ACBDSNAM-IHAACB,&DSNAME,4
         AGO   .MACRF_END
.*
.* MODIFY ACB MACRF
.*
.MACRF   ANOP
         GBLC  &ZGENMACF1,&ZGENMACF2,&ZGENMACF3,&ZGENMACF4
         ZGENMACF MACRF=&MACRF
&MODS    SETB   1
         ZMODCB &ACB,ACBMACR1-IHAACB,B'&ZGENMACF1',1
         ZMODCB &ACB,ACBMACR2-IHAACB,B'&ZGENMACF2',1
         ZMODCB &ACB,ACBMACR3-IHAACB,B'&ZGENMACF3',1
         ZMODCB &ACB,ACBMACR4-IHAACB,B'&ZGENMACF4',1
         AGO   .MACRF_END
.*
.* MODIFY ACB VCLRN VCDT VSAM CATALOG ENTRY DEFAULT NAME 
.*
.VCLRN  ANOP
&MODS    SETB   1
         ZMODCB &ACB,ACBVCLRN-IHAACB,&VCLRN,8
         AGO   .VCLRN_END
.*
.* MODIFY RPL ACB ADDRESS FIELD    RPI 696
.*
.ACB     ANOP
&MODS    SETB   1
         ZMODCB &RPL,RPLACB-IHARPL,&ACB,4
         AGO   .ACB_END
.*
.* MODIFY RPL AREA
.*
.AREA    ANOP
&MODS    SETB   1
         ZMODCB &RPL,RPLAREA-IHARPL,&AREA,4
         AGO   .AREA_END
.*
.* MODIFY RPL ARG
.*
.ARG     ANOP
&MODS    SETB   1
         ZMODCB &RPL,RPLARG-IHARPL,&ARG,4
         AGO    .ARG_END
.*
.* MODIFY RPL OPTCD
.*
.OPTCD   ANOP
         GBLC   &ZGENOPT1,&ZGENOPT2
         ZGENOPTD OPTCD=&OPTCD
&MODS    SETB   1
         ZMODCB &RPL,RPLOPT1-IHARPL,B'&ZGENOPT1',1
         ZMODCB &RPL,RPLOPT2-IHARPL,B'&ZGENOPT2',1
         AGO   .OPTCD_END
.*
.* MODIFY RPL LREC
.*
.RECLEN  ANOP
&MODS    SETB   1
         ZMODCB &RPL,RPLLREC-IHARPL,&RECLEN,4
         AGO   .RECLEN_END
         MEND
