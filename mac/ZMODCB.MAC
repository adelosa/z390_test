         MACRO
.*********************************************************************
.* Copyright 2007 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 08/27/05                                                 *
.*********************************************************************
.* 08/27/07 INITIAL CODING TO SUPPORT MODCB ACB OR RPL FIELD MODS                         
.*********************************************************************
&N       ZMODCB &CB,         ACB/RPL ADDRESS OR (REG)                  X
               &OFFSET,      OFFSET TO TARGET FIELD                    X
               &SOURCE       ABS VAL, RX ADDR, (REG), (S,SCON),(*,SCON) 
         AIF   ('&CB'(1,1) EQ '(').REGCB
&STADDR  SETC  '&CB+&OFFSET'
         AGO   .SOURCE    
.REGCB   ANOP
&STADDR  SETC  '&OFFSET.&CB'
.SOURCE  ANOP
         AIF   ('&SOURCE'(1,1) EQ '(').CHK_SCON
&ADDR    SETC  '=A(&SOURCE)'
         AGO   .LST
.CHK_SCON ANOP
         AIF   ('&SOURCE'(1,3) EQ '(S,').SCON_ADDR
         AIF   ('&SOURCE'(1,3) EQ '(*,').SCON_IADDR
.STREG   ANOP
         ST    &SOURCE(1),&STADDR
         MEXIT
.SCON_ADDR ANOP
&ADDR    SETC  '&SOURCE'(4,K'&SOURCE-4)
         LA    0,&ADDR
         ST    0,&STADDR
         MEXIT
.SCON_IADDR ANOP
&ADDR    SETC  '&SOURCE'(4,K'&SOURCE-4)
.LST     ANOP
         L     0,&ADDR
         ST    0,&STADDR
         MEND
