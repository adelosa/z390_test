         MACRO
.*********************************************************************
.* Copyright 2007 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 08/27/05                                                 *
.*********************************************************************
.* 08/27/07 INITIAL CODING TO SUPPORT MODCB ACB OR RPL FIELD MODS                         
.* 09/03/07 RPI 690 ADD OPTCD LENGTH 2 SUPPORT                                            
.*********************************************************************
&N       ZMODCB &CB,         ACB/RPL ADDRESS OR (REG)                  X
               &OFFSET,      OFFSET TO TARGET FIELD                    X
               &SOURCE,      ABS VAL, RX ADDR, (REG), (S,SCON),(*,SCON)X
               &LENGTH       LENGTH FOR ABS VAL MODS LIKE MACRF/OPTCD
         AIF   ('&CB'(1,1) EQ '(').REGCB
&STADDR  SETC  '&CB+&OFFSET'
         AGO   .SOURCE    
.REGCB   ANOP
&STADDR  SETC  '&OFFSET.&CB'
.SOURCE  ANOP
         AIF   ('&SOURCE'(1,1) EQ '(').CHK_SCON
         AIF   (&LENGTH EQ 4).MV4
         AIF   (&LENGTH EQ 2).MV1  
         MNOTE 12,'ZMODCB INVALID LENGTH - &LENGTH'
         MEXIT
.MV1     ANOP
         MVI   &STADDR,&SOURCE
         MEXIT
.MV4     ANOP
         L     0,=A(&SOURCE)
         ST    0,&STADDR
         MEXIT
.CHK_SCON ANOP
         AIF   ('&SOURCE'(1,3) EQ '(S,').SCON_ADDR
         AIF   ('&SOURCE'(1,3) EQ '(*,').SCON_IADDR
.STREG   ANOP
         ST    &SOURCE(1),&STADDR
         MEXIT
.SCON_ADDR ANOP
&ADDR    SETC  '&SOURCE'(4,K'&SOURCE-4)
         LA    0,&ADDR
         ST    0,&STADDR
         MEXIT
.SCON_IADDR ANOP
&ADDR    SETC  '&SOURCE'(4,K'&SOURCE-4)
         L     0,&ADDR
         ST    0,&STADDR
         MEND
