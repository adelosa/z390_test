         MACRO
.*********************************************************************
.* Copyright 2005 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 09/30/05                                                 *
.*********************************************************************
.* 12/06/05 RPI 118- use RESTORE in RETURN and XCTL to restore regs
.* 04/26/07 RPI 601- support R0-R15 format args                         
.* 04/19/08 RPI 833 add SETC quotes for HLASM compatibility
.*********************************************************************
&N       RESTORE &REGS
         AIF   ('&N' EQ '').SKIPDS
&N       DS    0H
.SKIPDS  ANOP
         AIF   (N'&SYSLIST EQ 1).NPOK
         MNOTE 12,'RESTORE REQUIRES SINGLE REGISTER SUBLIST OPERAND'
         MEXIT
.NPOK    ANOP
         AIF   ('&REGS(1)'(1,1) EQ 'R').SETR1
&R1      SETA  &REGS(1)
         AGO   .CHKR2
.SETR1   ANOP
&R1      SETA  '&REGS(1)'(2,K'&REGS(1)-1)
.CHKR2   ANOP
&R2      SETA  &R1
         AIF   ('&REGS(2)' EQ '').R2OK
         AIF   ('&REGS(2)'(1,1) EQ 'R').SETR2
&R2      SETA  &REGS(2)
         AGO   .R2OK
.SETR2   ANOP
&R2      SETA  '&REGS(2)'(2,K'&REGS(2)-1)
.R2OK    ANOP
         AIF   (&R1 LT 0 OR &R1 GT 15).ERR
         AIF   (&R2 LT 0 OR &R2 GT 15).ERR
         AIF   (&R1 EQ 13).ERR
         AIF   (&R2 EQ 13).ERR
         AIF   (&R1 GE 14).R1_GE_14
         AIF   (&R1 GT &R2).ERR OVERLAPS 13        
         LM    &R1,&R2,20+4*&R1.(13)
         MEXIT
.R1_GE_14 ANOP
         AIF   (&R2 EQ 14 AND &R1 EQ 15).ERR OVERLAPS 13
         LM    &R1,&R2,12+4*(&R1-14)(13)
         MEXIT
.ERR     MNOTE 8,'RESTORE RANGE INVALID (&R1,&R2)'
         MEND
