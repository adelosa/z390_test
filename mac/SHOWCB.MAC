         MACRO
.*********************************************************************
.* Copyright 2005 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 09/30/05                                                 *
.*********************************************************************
.* 08/10/07 INITIAL CODING TO SUPPORT FDBK, AREA, RECLEN INITIALLY                 
.*********************************************************************
&N       SHOWCB &RPL=,        RPL REQUEST PARAMATER LIST ADDRESS       X
               &AREA=,        AREA TO RECEIVE RPL FIELD                X
               &LENGTH=,      LENGTH OF AREA                           X
               &FIELDS=       NAME OF FIELD TO RETRIEVE       
&NPOS    SETA  N'&SYSLIST
.POSLOOP ANOP
         AIF   (&NPOS EQ 0).POSEND
         MNOTE 12,'SHOWCB UNSUPPORTED OPERAND - &SYSLIST(&NPOS)'
&NPOS    SETA  &NPOS-1
         AIF   (&NPOS GT 0).POSLOOP
         MEXIT
.POSEND  ANOP  
         AIF   (&N EQ '').SKIPDS
&N       DS    0H
.SKIPDS  ANOP
         AIF   (&AREA EQ '').ERR1
         AIF   (&LENGTH EQ '').ERR2
&OFFSET  SETA  0
.LOOP    ANOP
         AIF   (&OFFSET GT &LENGTH).ERR3
&I       SETA  &I+1
         AIF   (&I GT N'&FIELDS).EXIT
         AIF   (&FIELDS(&I) EQ 'AREA').AREA
         AIF   (&FIELDS(&I) EQ 'FDBK').FDBK
         AIF   (&FIELDS(&I) EQ 'RBA').RBA
         AIF   (&FIELDS(&I) EQ 'RECLEN').RECLEN
         AIF   (&FIELDS(&I) EQ 'XRBA').XRBA
         MNOTE 12,'SHOWCB FIELD NOT SUPPORTED - &FIELDS(&I)'
.EXIT    ANOP
         MEXIT
.AREA    ANOP  RETURN ADDRESS OF RECORD AREA   
         ZSHOWCB &RPL,RPLAREA-IHARPL,&AREA,&OFFSET,4
&OFFSET  SETA  &OFFSET+4
         AGO   .LOOP
.FDBK    ANOP  RETURN REASON CODE FROM RPLFEEDB
         ZSHOWCB &RPL,RPLFEEDB-IHARPL+3,&AREA,&OFFSET,1
&OFFSET  SETA  &OFFSET+1
         AGO   .LOOP
.RBA     ANOP
         ZSHOWCB &RPL,RPLXRBA-IHARPL+4,&AREA,&OFFSET,4
&OFFSET  SETA  &OFFSET+4
         AGO   .LOOP
.RECLEN  ANOP
         ZSHOWCB &RPL,RPLLREC-IHARPL,&AREA,&OFFSET,4
&OFFSET  SETA  &OFFSET+4
         AGO   .LOOP
.XRBA    ANOP
         ZSHOWCB &RPL,RPLXRBA-IHARPL,&AREA,&OFFSET,8
&OFFSET  SETA  &OFFSET+8
         AGO   .LOOP
.ERR1    MNOTE 12,'SHOWCB REQUIRES AREA='
         MEXIT
.ERR2    MNOTE 12,'SHOWCB REQUIRES LENGTH='
         MEXIT
.ERR3    MNOTE 12,'SHOWCB FIELDS EXCEED AREA LENGTH'
         MEND
