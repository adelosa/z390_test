         MACRO
.*********************************************************************
.* Copyright 2005 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 09/30/05                                                 *
.*********************************************************************
.* MAINTENANCE
.* 10/12/05 ADD DCBREC AND DCBDSNAM FIELDS, AND DEFAULT DDNAME
.* 10/17/05 RPI26 - correct missing .DDDEF label
.* 10/17/05 RPI27 - issue error for any defined key word parms
.* 11/13/05 RPI88 - add DCBID EBCDIC/ASCII ID for I/O verification               
.* 11/16/05 RPI95 - add DDNAME and DSNAME conflict check                         
.* 11/17/05 RPI96 - add RECFM, LRECL, and BLKSIZE consistency check              
.*********************************************************************
&N       DCB   &DSORG=PS,    PHYSICAL SEQUENTIAL                       X
               &EODAD=0,     END OF FILE EXIT                          X
               &RECFM=FB,    RECORD FORMAT (F,FB,V,VB,RT,VT)           X                        
               &DDNAME=,     ENVIRONMENT VARIABLE WITH PATH OR FILE    X
               &MACRF=GM,    ACCESS METHOD (GM,PM,R,W)                 X
               &SYNAD=0,     ERROR EXIT                                X
               &BLKSIZE=0,   BLOCK SIZE                                X
               &LRECL=0,     RECORD SIZE                               X
               &RECORD=0,    RECORD AREA                               X
               &DSNAME=      DSN AREA WITH PATH OR FILE (NULL OR "..")   
&NPOS    SETA  N'&SYSLIST
.POSLOOP ANOP
         AIF   (&NPOS EQ 0).POSEND
         MNOTE 12,'UNSUPPORTED OPERAND - &SYSLIST(&NPOS)'
&NPOS    SETA  &NPOS-1
         AGO   .POSLOOP
.POSEND  ANOP        
         AIF   (&RECFM EQ 'F').CHKF
         AIF   (&RECFM EQ 'FB').CHKFB
         AIF   (&RECFM EQ 'V').CHKV
         AIF   (&RECFM EQ 'VB').CHKVB
         AIF   (&RECFM EQ 'FT').CHKFT
         AIF   (&RECFM EQ 'VT').CHKVT
         MNOTE 12,'INVALID RECFM - &RECFM'
         MEXIT
.CHKF    ANOP
         AIF   (&LRECL EQ 0 AND &BLKSIZE EQ 0).ERR_LRECL
         AIF   (&LRECL GT 0 AND &BLKSIZE GT 0 AND &LRECL NE &BLKSIZE).EX
               RR_LRECL
         AGO   .CHKEND
.CHKFB   ANOP
         AIF   (&LRECL EQ 0 AND &BLKSIZE EQ 0).ERR_LRECL
         AIF   (&LRECL GT 0 AND &BLKSIZE GT 0 AND &BLKSIZE/&LRECL*&LRECX
               L NE &BLKSIZE).ERR_LRECL
         AGO   .CHKEND
.CHKV    ANOP
         AIF   (&LRECL EQ 0 AND &BLKSIZE EQ 0).ERR_LRECL
         AIF   (&LRECL GT 0 AND &BLKSIZE GT 0 AND &BLKSIZE NE &LRECL).EX
               RR_LRECL
         AGO   .CHKEND
.CHKVB   ANOP
         AIF   (&LRECL EQ 0 AND &BLKSIZE EQ 0).ERR_LRECL
         AIF   (&LRECL GT 0 AND &BLKSIZE GT 0 AND (&BLKSIZE LT (&LRECL+X
               4))).ERR_LRECL
         AGO   .CHKEND
.CHKFT   ANOP
         AIF   (&LRECL EQ 0 AND &BLKSIZE EQ 0).ERR_LRECL
         AIF   (&LRECL GT 0 AND &BLKSIZE GT 0 AND &BLKSIZE NE &LRECL).EX
               RR_LRECL
         AGO   .CHKEND
.CHKVT   ANOP
         AIF   (&LRECL EQ 0 AND &BLKSIZE EQ 0).ERR_LRECL
         AIF   (&LRECL GT 0 AND &BLKSIZE GT 0 AND &BLKSIZE NE &LRECL).EX
               RR_LRECL
         AGO   .CHKEND
.ERR_LRECL MNOTE 12,'INVALID LRECL - &LRECL'
         MEXIT
.CHKEND  ANOP
         AIF   (&DDNAME EQ '' OR &DSNAME EQ '').DSNOK
         MNOTE 12,'DSNAME AND DDNAME ARE MUTUALLY EXCLUSIVE'
.DSNOK   ANOP
&N       DS    0D
         ORG   &N+DCBID-IHADCB        RPI88
         DC    C'DCBV1001'            RPI88
         ORG   &N+DCBDSORG-IHADCB
         DC    AL1(DCBDSORG_&DSORG)
         ORG   &N+DCBEODAD-IHADCB
         DC    A(&EODAD)
         ORG   &N+DCBRECFM-IHADCB
         DC    AL1(DCBRECFM_&RECFM)
         ORG   &N+DCBDDNAM-IHADCB
         AIF   (&DDNAME EQ '').DDDEF
         DC    CL8'&DDNAME'
         AGO   .DDEND
.DDDEF   ANOP
         DC    CL8'&N'
.DDEND   ANOP
         ORG   &N+DCBOFLGS-IHADCB
         DC    AL1(DCBOFLGS_&MACRF)
         ORG   &N+DCBMACRF-IHADCB
         DC    AL2(DCBMACRF_&MACRF)
         ORG   &N+DCBSYNAD-IHADCB
         DC    A(&SYNAD)
         ORG   &N+DCBBLKSI-IHADCB
         DC    H'&BLKSIZE'
         ORG   &N+DCBLRECL-IHADCB
         DC    H'&LRECL'
         ORG   &N+DCBREC-IHADCB
         DC    A(&RECORD)
         ORG   &N+DCBDSNAM-IHADCB
         AIF   (&DSNAME EQ '').DSNZ
         DC    A(&DSNAME)
         AGO   .SKPDSNZ
.DSNZ    ANOP
         DC    A(0)
.SKPDSNZ ANOP
         ORG   &N+DCBLEN
         MEND
