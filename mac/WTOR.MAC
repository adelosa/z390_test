         MACRO
.*********************************************************************
.* Copyright 2005 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 11/28/05                                                 *
.*********************************************************************
.* 01/09/06 RPI163 SKIP LR FOR SAME REGISTER
.* 01/21/06 RPI188 REQUIRE LENGTH IF REPLY REG FORM  
.*********************************************************************
.*
.*  WTOR - write to operator with reply (routed to GUAM window if 
.*         GUI option active and copy of msg and reply to log)
.*
.*  WTOR   'msg',reply,reply_len,ecb
.*
.*         'msg' - text to be displayed prior to waiting for reply
.*         reply - rx label or (reg) pointing to reply field
.*         reply_len - length of reply field (omit to use rx length)
.*         ecb       - rx label or (reg) of 4 byte ECB which should be
.*                     zeros when WTOR issued.  Set to X'80000000'
.*                     while waiting for reply and X'40000000' when
.*                     reply complete.  Use WAIT ECB=ecb to wait for
.*                     reply posting.
.*  INPUT:
.*    R0  - REPLY FIELD ADDR
.*    R1  - WTO MSG ADDR WITH 4 BYTE PREFIX
.*    R14 - LENGTH OF REPLY FIELD
.*    R15 - ECB ADDR
.*********************************************************************
.* 10/20/05 RPI47  - add WTOR as part of first GUAM release in v1008  
.* 12/02/05 RPI114 - make wto label more unique to avoid duplicates   
.*********************************************************************
&N       WTOR  &MSG,&REPLY,&REPLYLEN,&ECB
         AIF   (N'&SYSLIST LE 4).NPOK
         MNOTE 12,'UNSUPPORTED OPERANDS - &SYSLIST(5)'
.NPOK    ANOP
&N       BAL   1,*+(WTOR#&SYSNDX._EOT-*+1)/2*2
         DC    AL2(WTOR#&SYSNDX._EOT-*,0),C&MSG
WTOR#&SYSNDX._EOT EQU *
         AIF   ('&REPLY'(1,1) EQ  '(').REPLY_REG
         LA    0,&REPLY
         AGO   .CHKLEN
.REPLY_REG ANOP
         AIF   (&REPLY(1) EQ '0' OR &REPLY(1) EQ 'R0').CHKLEN  RPI163
         LR    0,&REPLY(1)
         AIF   (&REPLYLEN NE '').CHKLENREG
         MNOTE 12,'WTOR REPLY LENGTH REQUIRED'
         MEXIT
.CHKLEN  ANOP
         AIF   (&REPLYLEN EQ '').DEFLEN
.CHKLENREG ANOP
         AIF   ('&REPLYLEN'(1,1) EQ '(').LEN_REG
         LA    14,&REPLYLEN
         AGO   .CHKECB
.LEN_REG ANOP
         AIF   (&REPLYLEN(1) EQ '0' OR &REPLYLEN(1) EQ 'R0').ERR1
         LR    14,&REPLYLEN(1)
         AGO   .CHKECB
.DEFLEN  ANOP
         LA    14,L'&REPLY
.CHKECB  ANOP
         AIF   ('&ECB'(1,1) EQ '(').ECBREG
         LA    15,&ECB
         AGO   .SVC
.ECBREG  ANOP
         AIF   (&ECB(1) EQ '15' OR &ECB(1) EQ 'R15').SVC  RPI163
         LR    15,&ECB(1)
.SVC     ANOP
         SVC   160
         MEXIT
.ERR1    MNOTE 12,'WTOR REPLY LENGTH REGISTER 0 NOT ALLOWED'
         MEND

