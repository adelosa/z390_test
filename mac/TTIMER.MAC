         MACRO
.*********************************************************************
.* Copyright 2005 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 04/23/05                                                 *
.*********************************************************************
.* 04/23/06 RPI 279 - add TTIMER to test or cancel STIMER REAL         
.*********************************************************************
&N       TTIMER &CANCEL,   CANCEL TTIMER INTERVAL IF DEFINED           X
               &TYPE,      TU (DEFAULT RETURN IN R0) OR MIC,MICADDR    X
               &ADDR,      DOUBLE WORD TO STORE MIC TIME REMAINING     X
               &ERRET=,    IGNORE                                      X 
               &MF=        NOT SUPPORTED
         AIF   (&N EQ '').SKIPDS
&N       DS    0H
.SKIPDS  ANOP
         AIF   (&MF NE '').ERR2
         AIF   (&CANCEL EQ 'CANCEL').CANCEL
         AIF   (&TYPE EQ '').TU
         AIF   (&TYPE EQ 'TU').TU
         AIF   (&TYPE EQ 'MIC').MIC
         MNOTE 12,'TTIMER - INVALID TYPE - &TYPE'
         MEXIT
.CANCEL  ANOP
         LA    0,1
         AGO   .SVC
.TU      ANOP
         LA    0,2
         AGO   .SVC
.MIC     ANOP
         LA    0,3
         AIF   (&ADDR EQ '').ERR1
         AIF   ('&ADDR'(1,1) EQ '(').REGADDR
         LA    1,&ADDR
         AGO   .SVC
.REGADDR ANOP
         AIF   (&ADDR EQ '1' OR &ADDR EQ 'R1').SVC
         LR    1,&ADDR(1)
.SVC     ANOP
         SVC   X'2E' TTIMER (R0=FUNCTION, R1=A(MIC ADDR)
         MEXIT
.ERR1    MNOTE 12,'TTIMER MIC REQUIRES DOUBLE WORD ADDRESS PARM'
         MEXIT
.ERR2    MNOTE 12,'TTIMER MF= NOT SUPPORTED'
         MEND

