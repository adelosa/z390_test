         MACRO
.*********************************************************************
.* Copyright 2005 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 09/30/05                                                 *
.*********************************************************************
.* 08/10/07 INITIAL CODING TO TEST FDBK OR RECLEN FIELDS
.* 08/19/07 RPI 680 SHOWCB TO TESTCB                       
.* 09/14/07 RPI 672 VERIFY RPL VS ACB                                              
.*********************************************************************
&N       TESTCB &RPL=,        RPL REQUEST PARAMATER LIST ADDRESS       X
               &ACB=,         ACB ACCESS CONTROL BLOCK ADDRESS         X
               &FDBK=,        TEST RPL FEEDBACK REASON CODE BYTE       X
               &RECLEN=       TEST RPL LENGTH OF RECORD                     
         LCLB  &MODS
&NPOS    SETA  N'&SYSLIST
.POSLOOP ANOP
         AIF   (&NPOS EQ 0).POSEND
         MNOTE 12,'TESTCB UNSUPPORTED OPERAND - &SYSLIST(&NPOS)'
&NPOS    SETA  &NPOS-1
         AGO   .POSLOOP
.POSEND  ANOP  
         AIF   (&N EQ '').SKIPDS
&N       DS    0H
.SKIPDS  ANOP
         AIF   (&FDBK   NE '').FDBK
.FDBK_END ANOP
         AIF   (&RECLEN NE '').RECLEN
.RECLEN_END ANOP
         AIF   (&MODS).EXIT
         MNOTE 12,'TESTCB FIELD NOT SUPPORTED YET'
.EXIT    ANOP
         MEXIT
.FDBK    ANOP
&MODS    SETB  1
         ZTESTCB &RPL,RPLFEEDB-IHARPL+3,&FDBK,1
         AIF   (&RPL NE '' AND &ACB EQ '').FDBK_END
         MNOTE 'TESTCB RPL REQUIRED FOR FDBK'
         AGO   .FDBK_END
.RECLEN  ANOP
&MODS    SETB  1
         ZTESTCB &RPL,RPLLREC-IHARPL,&RECLEN,4
         AIF   (&RPL NE '' AND &ACB EQ '').RECLEN_END
         MNOTE 'TESTCB RPL REQUIRED FOR RECLEN'
         AGO     .RECLEN_END
         MEND
