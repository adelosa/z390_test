         MACRO
.*********************************************************************
.* Copyright 2005 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 11/30/05                                                 *
.*********************************************************************
.* 
.* TN3270 MACRO TO SUPPORT DEFINITION OF TN3270 DATA STREAMS 
.*
.*   IF FIRST PARAMETER IS SYMBOLS, EQUATES FOR TN3270 CONTROL
.*   CHARACTERS AND ATTRIBUTES ARE GENERATED.
.*
.*   ANY NUMBER OF TN3270 SYMBOLS AND ASSOCIATED DATA BYTES
.*   CAN BE GENERATED.  SYMBOLS REQUIRING DATA BYTES MAY BE
.*   ENCLOSED IN SUBLIST WITH DATA BYTES.  FOR SBA THE ROW AND
.*   COLUMN MAY BE SPECIFIED IN SUBLIST FOR MACRO TO CALCULATE ADDR.                 
.*
.*********************************************************************
.* 11/20/05 RPI47 - add TN3270 support functions and equates
.*********************************************************************   
&N       TN3270 
&N       EQU   *
         AIF   (&SYSLIST(1) EQ 'SYMBOLS').SYMBOLS
         LCLA  &TAB(64)
&TAB(1)  SETA  X'40',X'C1',X'C2',X'C3',X'C4',X'C5',X'C6',X'C7',        X
               X'C8',X'C9',X'4A',X'4B',X'4C',X'4D',X'4E',X'4F',        X
               X'50',X'D1',X'D2',X'D3',X'D4',X'D5',X'D6',X'D7',        X
               X'D8',X'D9',X'5A',X'5B',X'5C',X'5D',X'5E',X'5F',        X
               X'60',X'E1',X'E2',X'E3',X'E4',X'E5',X'E6',X'E7',        X
               X'E8',X'E9',X'6A',X'6B',X'6C',X'6D',X'6E',X'6F',        X
               X'F0',X'F1',X'F2',X'F3',X'F4',X'F5',X'F6',X'F7',        X
               X'F8',X'F9',X'7A',X'7B',X'7C',X'7D',X'7E',X'7F'           
&NP      SETA  N'&SYSLIST
&OPS     SETC  ''
.LOOP1   ANOP
&I       SETA  &I+1
         AIF   (&I GT &NP).GENDC
&NPS     SETA  N'&SYSLIST(&I)
         AIF   (&NPS EQ 1).GENONE
         AIF   (&SYSLIST(&I,1) EQ 'SBA').GENSBA
.LOOP2   ANOP
&J       SETA  &J+1
         AIF   (&J GT &NPS).LOOP1
&OPS     SETC  '&OPS,&SYSLIST(&I,&J)'
         AGO   .LOOP2         
.GENDC   ANOP
&OPS     SETC  '&OPS'(2,K'&OPS-1)
         DC    AL1(&OPS)
         MEXIT
.GENONE  ANOP
&OPS     SETC  '&OPS,&SYSLIST(&I)'
         AGO   .LOOP1
.GENSBA  ANOP
         AIF   (N'&SYSLIST(&I) NE 3).ERR1
&ROW     SETA  &SYSLIST(&I,2)-1
&COL     SETA  &SYSLIST(&I,3)-1
         AIF   (&ROW LT 0 OR &ROW GT 23).ERR2
         AIF   (&COL LT 0 OR &COL GT 79).ERR3
&OFFSET  SETA  &ROW*80+&COL
&SBA1    SETA  &OFFSET/64
&SBA2    SETA  &OFFSET-&SBA1*64
&OPS     SETC  '&OPS,SBA,&TAB(&SBA1+1),&TAB(&SBA2+1)'
         AGO   .LOOP1
.SYMBOLS ANOP
*
* TN3270 CALL OPTIONS FROM TOP 16 BITS OF FIRST WORD OF PARMLIST
*
TN3270_TGET EQU X'8000' READ FROM 3270 
TN3270_TPUT EQU X'4000' WRITE TO  3270
TN3270_WAIT EQU X'2000' WAIT FOR INPUT READY ELSE R15=4
TN3270_EDIT EQU X'1000' EBCDIC TEXT VS 3270 DATA STREAM
*
* TN3270 DATA STREAM CONTROL CODES
*
ENTER    EQU   X'7D'       ENTER KEY
PF1      EQU   X'F1'       PROGRAM FUNCTION KEY 1
PF2      EQU   X'F2'       PROGRAM FUNCTION KEY 2
PF3      EQU   X'F3'       PROGRAM FUNCTION KEY 3
PF4      EQU   X'F4'       PROGRAM FUNCTION KEY 4
PF5      EQU   X'F5'       PROGRAM FUNCTION KEY 5
PF6      EQU   X'F6'       PROGRAM FUNCTION KEY 6
PF7      EQU   X'F7'       PROGRAM FUNCTION KEY 7
PF8      EQU   X'F8'       PROGRAM FUNCTION KEY 8
PF9      EQU   X'F9'       PROGRAM FUNCTION KEY 9
PF10     EQU   X'7A'       PROGRAM FUNCTION KEY 10
PF11     EQU   X'7B'       PROGRAM FUNCTION KEY 11
PF12     EQU   X'7C'       PROGRAM FUNCTION KEY 12
PF13     EQU   X'C1'       PROGRAM FUNCTION KEY 13
PF14     EQU   X'C2'       PROGRAM FUNCTION KEY 14
PF15     EQU   X'C3'       PROGRAM FUNCTION KEY 15
PF16     EQU   X'C4'       PROGRAM FUNCTION KEY 16
PF17     EQU   X'C5'       PROGRAM FUNCTION KEY 17
PF18     EQU   X'C6'       PROGRAM FUNCTION KEY 18
PF19     EQU   X'C7'       PROGRAM FUNCTION KEY 19
PF20     EQU   X'C8'       PROGRAM FUNCTION KEY 20
PF21     EQU   X'C9'       PROGRAM FUNCTION KEY 21
PF22     EQU   X'4A'       PROGRAM FUNCTION KEY 22
PF23     EQU   X'4B'       PROGRAM FUNCTION KEY 23
PF24     EQU   X'4C'       PROGRAM FUNCTION KEY 24
PA1      EQU   X'6C'       PROGRAM ATTENTION 1
PA2      EQU   X'6E'       PROGRAM ATTENTION 2
PA3      EQU   X'6B'       PROGRAM ATTENTION 3
CLEAR    EQU   X'6D'       CLEAR SCREEN
ESC      EQU   X'27'       ESCAPE  
ERAWRT   EQU   X'F5'       ERASE/WRITE
WCC      EQU   X'C3'       WCC CLEAR SCREEN, RESET KBD, RESET MDT
SBA      EQU   X'11'       SET BUFFER ADDRESS (2 ADDR BYTES)
SF       EQU   X'1D'       START FIELD        (1 - ATTRIBUTE BYTE)
IC       EQU   X'13'       INSERT CURSOR AT CURRENT SBA
PT       EQU   X'05'       PROGRAM TAB (ADVANCE TO NEXT UNPROT FIELD) 
RA       EQU   X'3C'       REPEAT TO ADDR (2 ADDR BYTES, REPEAT CHAR)
EUA      EQU   X'12'       ERASE UNPROTECTED FIELDS TO (2 ADDR BYTES)
*
* TN3270 ATTRIBUTE BYTES - PROT(2),ALPHA(3),INTENSITY(4),MDT(7)
*
ATPRTHGH EQU   X'38'       PROTECTED HIGH INTENSITY TEXT
ATPRTEXT EQU   X'30'       PROTECTED NORMAL TEXT
ATUPTEXT EQU   X'10'       UNPROTECTED ALPHA INPUT FIELD
ATUPNUM  EQU   X'00'       UNPROTECTED NUMERIC INPUT FIELD
         MEND

