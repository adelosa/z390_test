         TITLE 'TESTASM1 - MISC ASSEMBLER TESTS'
*********************************************************************
* Copyright 2005 Automated Software Tools Corporation               *
* This source code is part of z390 assembler/emulator package       *
* The z390 package is distributed under GNU general public license  *
* Author - Don Higgins                                              *
* Date   - 09/30/05                                                 *
*********************************************************************
* MAINTENANCE
* 10/03/05 RPI9   TEST LENGTH OPERATOR IN INS AND DC
* 11/13/05 RPI87  TEST ORG PRECEEDED BY ABS VALUE CALC
* 11/13/05 RPI90  TEST SINGLE CHARACTER B,C,X SYMBOLS
* 01/13/06 RPI171 TEST UNARY OPERATORS                 
* 03/16/06 RPI230 TEST COM, RSECT, START LIMITED SUPPORT
* 03/17/06 RPI228 TEST CVTDCB OS FLAG INDICATING 31 BIT AND MVS+
* 03/21/06 RPI258 TEST ORG , DELIMITER                                 
*********************************************************************
         START , RPI 230 TEST START CSECT
         USING *,R15
         L     R15,=A(TESTASM1)
         BR    R15
         DROP  R15
         LTORG
TESTASM1 RT1   MAIN
         EQUREGS
F1       DC    A(0)
F2       DC    A(L'X2)
A        DC    C'ABC'
         ORG   A
         DC    C'12'
         ORG
         DC    C'D'
* TEST ORG AFTER ABS
RPI87    DC    C'ABCD'
ABS1     EQU   0
         ORG   RPI87+1
         DC    C'XX'
ABS2     EQU   0
         ORG 
* TEST ORG COMMA DELIMITER
         ORG    *-16   BACKUP  16
         ORG    ,      FORWARD 16 AGAIN 
* TEST B,C,X SYMBOLS
B        DC    C'B'
C        DC    C'C'
X        DC    C'X'
         RT1   START
* TEST ALIGNMENT OPERATOR
         CNOP  0,4
         CNOP  2,4
         CNOP  0,4
         CNOP  0,8
         CNOP  6,8
         CNOP  4,8
         CNOP  2,8
         CNOP  0,8
* TEST LENGTH OPERATOR
X2       MVC   F1(L'F2),F2
         RT1   CCE,F1,=A(6)
* TEST ORG VALUE AFTER OVERLAY 
         CLC   A(4),=C'12CD'
         RT1   CCE
* TEST ORG AFTER ABS VALUE CALC FOR RPI87
         CLC   RPI87(4),=C'AXXD'
         RT1   CCE
* TEST B,C,X SYMBOLS FOR RPI90
         CLI   B,C'B'
         RT1   CCE
         CLI   C,C'C'
         RT1   CCE
         CLI   X,C'X'
         RT1   CCE
* TEST UNARY OPERATORS
         CLC   =A(-1),=A(1-2)
         RT1   CCE
         CLC   =A(--1),=A(1)
         RT1   CCE
         CLC   =A(-1*-1),=A(1)
         RT1   CCE
         CLC   =A(-1*(-1+2)),=A(1-2)
         RT1   CCE
         CLC   =A(+1*+1),=A(1)
         RT1   CCE
         CLC   =A(+1*(+1+2)),=A(3)
         RT1   CCE
         L     R1,=A(MYCOM)
         CALL  MYRSECT
         RT1   END
         LTORG
MYCOM    COM   , RPI 230 TEST COM SECTION (NO LINKER SUPPORT YET)
MYCOM1   DC    F'123'
MYCOM2   DC    F'456'
MYRSECT  RSECT , RPI 230 TEST RSECT (NO WARNINGS YET)
         USING *,R15
         CLC   0(8,R1),=F'123,456'
         BNE   1
         L     R1,16
         USING IHACVT,R1
         CLI   CVTDCB,X'9B'  VERIFY X'80' 31 BIT AND X'13' MVS+ SUPPORT
         BNE   1
         BR    14
         LTORG
         CVTD
         END         
