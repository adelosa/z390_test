         MACRO
         UNREFTAB &UNREFENT
&UNREFLEN SETA K'&UNREFENT
         DC    AL2(&UNREFLEN),C'&UNREFENT'
         MEND
*
UNREF    SUBENTRY
*
* READS THE .PRN FILE AFTER ASSEMBLY AND PRODUCES A .URF FILE
* WITH ALL THE UNREFERENCED SYMBOLS
* IT'S JUST A COPY OF THE INPUT FILE LINE
*
* AN INTERNAL TABLE ELIMINATES ALL THE COMMON ONES LIKE REG EQU
* AND DCB FIELDS
*
         OPEN  (PRNFILE,INPUT,URFFILE,OUTPUT)
*
GETLOOP  EQU   *
         GET   PRNFILE            GET A .PRN RECORD
* NOT INTERESTED IN ANYTHING BEFORE 'SYMBOL TABLE'
         CLC   PRNAREA(20),=C'Symbol Table Listing'
         BNE   GETLOOP
*
         GET   PRNFILE            MISS THE BLANK LINE
SYMLOOP  EQU   *
         GET   PRNFILE            GET A SYMBOL LINE
         CLC   PRNAREA+1(3),=C'SYM' IGNORE IF NOT A SYM LINE
         BNE   SYMLOOP            LOOP IF NOT
* GOT A SYM LINE
         LA    R2,PRNAREA+6       SCAN START
         LA    R3,70              SCAN LIMIT
XREFLOOP EQU   *
         CLC   0(5,R2),=C'XREF='  FIND XREF=
         BE    GOTXREF            EXIT IF FOUND
         AHI   R2,1               BUMP POINTER
         BCT   R3,XREFLOOP        LOOP
         ABEND 999,DUMP           OH DEAR
*
GOTXREF  EQU   *
* NOW FIND BLANK AFTER SOURCE LINE NO.
         AHI   R2,5               SCAN START
         LA    R3,6               THAT'S A BIG PROGRAM !
SPCELOOP EQU   *
         CLI   0(R2),C' '         FIND A SPACE
         BE    GOTSPACE           EXIT IF FOUND
         AHI   R2,1               BUMP POINTER
         BCT   R3,SPCELOOP        LOOP
         ABEND 998,DUMP           OH DEAR
*
GOTSPACE EQU   *
         CLI   1(R2),C' '         IS NEXT CHAR A SPACE ?
         BNE   SYMLOOP            XREF IS OK, LOOP BACK
* FOUND AN UNREFERENCED SYMBOL
*
* ELIMINATE TYPE=CST
         LA    R2,PRNAREA+6       SCAN START
         LA    R3,70              SCAN LIMIT
CSTLOOP  EQU   *
         CLC   0(8,R2),=C'TYPE=CST' FIND TYPE=CST
         BE    SYMLOOP            LOOP IF FOUND
         AHI   R2,1               BUMP POINTER
         BCT   R3,CSTLOOP         LOOP
* EXTRACT SYMBOL
         LA    R2,PRNAREA+5       SCAN START
         LA    R3,70              THAT'S A LONG NAME !
EXTLOOP  EQU   *
         CLI   0(R2),C' '         FIND THE SPACE AFTER
         BE    GOTEXT             EXIT IF FOUND
         AHI   R2,1               BUMP POINTER
         BCT   R3,EXTLOOP         LOOP
         ABEND 997,DUMP           OH DEAR
*
GOTEXT   EQU   *
         S     R2,=A(PRNAREA+5)   R2=ACTUAL LENGTH
         STH   R2,SYMLEN          SAVE IT
         BCTR  R2,0               -1 FOR EXECUTE
         XC    SYMNAME,SYMNAME    CLEAR SYMBOL NAME
         EX    R2,SYMMVC          MOVE SYMBOL NAME
* MATCH SYMBOL NAME AND LENGTH TO TABLE
         LA    R2,SYMTABLE        TABLE ADDRESS
TABLOOP  EQU   *
         LH    R3,0(R2)           LOAD LENGTH
         BCTR  R3,0               -1 FOR EXECUTE
         CLC   0(2,R2),SYMLEN     MATCH LENGTHS
         BNE   TABBAD             EXIT IF NOT MATCHED
         EX    R3,TABCLC          MATCH SYMBOLS
         BE    SYMLOOP            EXIT IF MATCHED
TABBAD   EQU   *
         LA    R2,3(R2,R3)        BUMP TO NEXT TABLE ENTRY
         CLI   0(R2),X'FF'        STOPPER ?
         BNE   TABLOOP            LOOP IF NOT
* IT'S A REAL ONE, WRITE IT
         PUT   URFFILE            WRITE IT
         B     SYMLOOP            LOOP BACK
SYMMVC   MVC   SYMNAME(0),PRNAREA+5 MOVE SYMBOL NAME
TABCLC   CLC   2(0,R2),SYMNAME    MATCH SYMBOLS
*
EOFPRN   EQU   *
         CLOSE (PRNFILE,,URFFILE)
         SUBEXIT
*
SYMLEN   DS    H                  LENGTH OF SYMBOL NAME
SYMNAME  DS    CL30               SYMBOL NAME
*
SYMTABLE EQU   *
* LIST ALL THE USUAL SUSPECTS
* GENREG EQUATES
         UNREFTAB R0
         UNREFTAB R1
         UNREFTAB R2
         UNREFTAB R3
         UNREFTAB R4
         UNREFTAB R5
         UNREFTAB R6
         UNREFTAB R7
         UNREFTAB R8
         UNREFTAB R9
         UNREFTAB R10
         UNREFTAB R11
         UNREFTAB R12
         UNREFTAB R13
         UNREFTAB R14
         UNREFTAB R15
* FPREG EQUATES
         UNREFTAB F0
         UNREFTAB F1
         UNREFTAB F2
         UNREFTAB F3
         UNREFTAB F4
         UNREFTAB F5
         UNREFTAB F6
         UNREFTAB F7
         UNREFTAB F8
         UNREFTAB F9
         UNREFTAB F10
         UNREFTAB F11
         UNREFTAB F12
         UNREFTAB F13
         UNREFTAB F14
         UNREFTAB F15
* DCB FIELDS
         UNREFTAB DCBDSORG_DA
         UNREFTAB DCBIOBAD
         UNREFTAB DCBMACRF_R
         UNREFTAB DCBMACRF_RW
         UNREFTAB DCBMACRF_PM
         UNREFTAB DCBMACRF_W
         UNREFTAB DCBOFLGS_OPEN
         UNREFTAB DCBOFLGS_PM
         UNREFTAB DCBOFLGS_R
         UNREFTAB DCBOFLGS_RW
         UNREFTAB DCBOFLGS_W
         UNREFTAB DCBRECFM_F
         UNREFTAB DCBRECFM_FA
         UNREFTAB DCBRECFM_FB
         UNREFTAB DCBRECFM_FBA
         UNREFTAB DCBRECFM_FBM
         UNREFTAB DCBRECFM_FM
         UNREFTAB DCBRECFM_V
         UNREFTAB DCBRECFM_VA
         UNREFTAB DCBRECFM_VB
         UNREFTAB DCBRECFM_VBA
         UNREFTAB DCBRECFM_VBM
         UNREFTAB DCBRECFM_VM
         UNREFTAB DCBRECFM_VT
* CFD/CTD EQUATES
         UNREFTAB CFD_DB
         UNREFTAB CFD_DH
         UNREFTAB CFD_EB
         UNREFTAB CFD_ED
         UNREFTAB CFD_EH
         UNREFTAB CFD_INT128
         UNREFTAB CFD_LB
         UNREFTAB CFD_LD
         UNREFTAB CFD_LH
         UNREFTAB CTD_DB
         UNREFTAB CTD_DH
         UNREFTAB CTD_EB
         UNREFTAB CTD_ED
         UNREFTAB CTD_EH
         UNREFTAB CTD_LB
         UNREFTAB CTD_LD
         UNREFTAB CTD_LH
* TN3270 CALL OPTIONS
         UNREFTAB TGET_EDIT_WAIT
         UNREFTAB TGET_EDIT_NOWAIT
         UNREFTAB TGET_ASIS_WAIT
         UNREFTAB TGET_ASIS_NOWAIT
         UNREFTAB TPUT_EDIT
         UNREFTAB TPUT_ASIS
         UNREFTAB TPUT_FULLSCR
* TN3270 COMMANDS
         UNREFTAB ESC
         UNREFTAB WRT
         UNREFTAB WRT_EW
         UNREFTAB WRT_EWA
         UNREFTAB WCC_BKD
         UNREFTAB WCC_KD
* TN3270 ORDERS
         UNREFTAB PT
         UNREFTAB GE
         UNREFTAB SBA
         UNREFTAB SF
         UNREFTAB EUA
         UNREFTAB IC
         UNREFTAB SFE
         UNREFTAB RA
* TN3270 AIDS
         UNREFTAB NOAID
         UNREFTAB ENTER
         UNREFTAB PF1
         UNREFTAB PF2
         UNREFTAB PF3
         UNREFTAB PF4
         UNREFTAB PF5
         UNREFTAB PF6
         UNREFTAB PF7
         UNREFTAB PF8
         UNREFTAB PF9
         UNREFTAB PF10
         UNREFTAB PF11
         UNREFTAB PF12
         UNREFTAB PF13
         UNREFTAB PF14
         UNREFTAB PF15
         UNREFTAB PF16
         UNREFTAB PF17
         UNREFTAB PF18
         UNREFTAB PF19
         UNREFTAB PF20
         UNREFTAB PF21
         UNREFTAB PF22
         UNREFTAB PF23
         UNREFTAB PF24
         UNREFTAB PA1
         UNREFTAB PA2
         UNREFTAB PA3
         UNREFTAB CLEAR
* TN3270 ATTRIBUTES
         UNREFTAB ATT_PAH
         UNREFTAB ATT_PA
         UNREFTAB ATT_UA
         UNREFTAB ATT_UN
* TN3270 EXTENDED ATTRIBUTES
         UNREFTAB SFE_BFA
         UNREFTAB SFE_HL
         UNREFTAB SFE_COLOR
         UNREFTAB SFE_DEFAULT
         UNREFTAB SFE_BLUE
         UNREFTAB SFE_RED
         UNREFTAB SFE_PINK
         UNREFTAB SFE_GREEN
         UNREFTAB SFE_TURQUOISE
         UNREFTAB SFE_YELLOW
         UNREFTAB SFE_WHITE
* GUAM EQUATES
         UNREFTAB GUI_WINDOW_TITLE
         UNREFTAB GUI_WINDOW_LOC
         UNREFTAB GUI_WINDOW_SIZE
         UNREFTAB GUI_WINDOW_FONT
         UNREFTAB GUI_WINDOW_VIEW
         UNREFTAB GUI_WINDOW_VIEW_NONE
         UNREFTAB GUI_WINDOW_VIEW_MCS
         UNREFTAB GUI_WINDOW_VIEW_SCREEN
         UNREFTAB GUI_WINDOW_VIEW_GRAPH
         UNREFTAB GUI_WINDOW_GETVIEW
         UNREFTAB GUI_SCREEN_READ
         UNREFTAB GUI_WAIT
         UNREFTAB GUI_NOWAIT
         UNREFTAB GUI_SCREEN_WRITE
         UNREFTAB GUI_SCREEN_FIELD
         UNREFTAB GUI_SCREEN_CURSOR
         UNREFTAB GUI_SCREEN_COLOR
         UNREFTAB GUI_GRAPH_POINT
         UNREFTAB GUI_GRAPH_LINE
         UNREFTAB GUI_GRAPH_FILL
         UNREFTAB GUI_GRAPH_TEXT
         UNREFTAB GUI_KEYBOARD_READ
         UNREFTAB GUI_MOUSE_READ
         UNREFTAB GUI_BUTTON_DOWN
         UNREFTAB GUI_BUTTON_UP
         UNREFTAB GUI_SOUND_PLAY
* PSA FIELDS
         UNREFTAB PSACVT
         UNREFTAB PSACVT2
* CVT FIELDS
         UNREFTAB CVTDATE
         UNREFTAB CVTDCB
* ZCVT FIELDS
         UNREFTAB ZCVTCAP
         UNREFTAB ZCVTEIBP
         UNREFTAB ZCVTEND
         UNREFTAB ZCVTEPIE
         UNREFTAB ZCVTESTA
         UNREFTAB ZCVTEXIT
         UNREFTAB ZCVTFQ24
         UNREFTAB ZCVTFQ31
         UNREFTAB ZCVTGECB
         UNREFTAB ZCVTIPLP
         UNREFTAB ZCVTSAV2
         UNREFTAB ZCVTSAVE
         UNREFTAB ZCVTUPGM
* EIB FIELDS
         UNREFTAB EIBTIME
         UNREFTAB EIBDATE
         UNREFTAB EIBTRNID
         UNREFTAB EIBTASKN
         UNREFTAB EIBTRMID
         UNREFTAB EIBRSVD1
         UNREFTAB EIBCPOSN
         UNREFTAB EIBCALEN
         UNREFTAB EIBAID
         UNREFTAB EIBFN
         UNREFTAB EIBRESP
         UNREFTAB EIBLENG
* TCTTE FIELDS
         UNREFTAB TCTTETI
         UNREFTAB TCTTEDA
         UNREFTAB TCTTETC
         UNREFTAB TCTTECA
         UNREFTAB TCTTECAL
         UNREFTAB TCTTETS
         UNREFTAB TCTTELEN
* EISTG FIELDS
         UNREFTAB DFHSA
         UNREFTAB DFHEIPL
         UNREFTAB DFHEIUSR
* ESPIE FIELDS
         UNREFTAB EPIEID
         UNREFTAB EPIEPARM
         UNREFTAB EPIEPSW
         UNREFTAB EPIEGPR
         UNREFTAB EPIEL
* ESTAE FIELDS
         UNREFTAB ESTAID
         UNREFTAB ESTAPARM
         UNREFTAB ESTAPSW
         UNREFTAB ESTAGPR
         UNREFTAB ESTAL
*
         DC    X'FF'              STOPPER
*
PRNFILE  DCB   DSORG=PS,                                               X
               EODAD=EOFPRN,                                           X
               RECFM=FT,                                               X
               DDNAME=PRNFILE,                                         X
               MACRF=GM,                                               X
               BLKSIZE=200,                                            X
               LRECL=200,                                              X
               RECORD=PRNAREA
PRNAREA  DS    CL200
*
URFFILE  DCB   DSORG=PS,                                               X
               RECFM=FT,                                               X
               DDNAME=URFFILE,                                         X
               MACRF=PM,                                               X
               BLKSIZE=200,                                            X
               LRECL=200,                                              X
               RECORD=PRNAREA
*
* TEST LABELS
TESTLAB1 EQU   *
TESTLAB2 EQU   *
TESTLAB3 EQU   *
*
         DCBD
         EQUREGS
         END
