* C:\MFATC\z390 - z390 Mainframe Assembler display square root of 2
* Author.  Don Higgins
* Date.    07/09/06
* Remarks. For more information visit www.mfatc.org
* 1.  This demo caculates and displays square root of 2 
*     in short, long, and extended IEEE floating point.
* 2.  To verify the results visit this NASA website with 1M digits:
*     http://antwrp.gsfc.nasa.gov/htmltest/gifcity/sqrt2.1mil
*     The sqrt 2 = 1.4142135623 7309504880 1688724209 6980785696
*     The short     32 bit IEEE format has about  8 digit precision
*     The long      64 bit IEEE format has about 15 digit precision
*     The extended 128 bit IEEE format has about 34 digit precision
* 3.  This program uses z390 macro CTD and svc 170 convert to display
*     service to convert these formats to EBCDIC display format.
* 4.  Another challenging execise would be to write the CTD service
*     as an HLASM subroutine for portability to the mainframe. 
*     Converting LB from binary to decimal is non-trivial as the CVDG 
*     instruction only supports 31 digits versus the required 34 for
*     LB mantissa and 39 digits for 128 bit integers.  Also scaling
*     binary mantissa and exponent into equivalent decimal scientific
*     notation is non-trival.  See www.z390.org Java source code in
*     pz390 and sz390 as useful examples of code performing same
*     functions.
SQRTS    SUBENTRY
         WTO   'SQRT2 DISPLAY SQUARE ROOT OF 2 IN E, D, AND L FORMATS'
         SQEB  F0,EB_2             CALC SQARE ROOT OF 2 AS EB SHORT
         STE   F0,EB_SQRT2            STORE RESULT
         CTD   CTD_EB,EB_SQRT2,D40    FORMAT AS EBCDIC DISPLAY TEXT
         MVC   DTYPE,=CL20'EB SHORT SQARE ROOT 2'
         WTO   MF=(E,WTOMSG)          DISPLAY RESULT
         SQDB  F0,DB_2             CALC SQARE ROOT OF 2 AS DB LONG 
         STD   F0,DB_SQRT2            STORE RESULT
         CTD   CTD_DB,DB_SQRT2,D40    FORMAT AS EBCDIC DISPLAY TEXT
         MVC   DTYPE,=CL20'DB LONG  SQARE ROOT 2'
         WTO   MF=(E,WTOMSG)          DISPLAY RESULT
         LD    F1,LB_2
         LD    F3,LB_2+8
         SQXBR F0,R1
         STD   F0,LB_SQRT2
         STD   F2,LB_SQRT2+8
         CTD   CTD_LB,LB_SQRT2,D40
         MVC   DTYPE,=CL20'LB LONG  SQARE ROOT 2'
         WTO   MF=(E,WTOMSG)
         SUBEXIT
EB_2     DC    EB'2'
EB_SQRT2 DC    EB'0'
DB_2     DC    DB'2'
DB_SQRT2 DC    DB'0'
LB_2     DC    LB'2'
LB_SQRT2 DC    LB'0'
WTOMSG   DC    AL2(WTOEND-*,0)       WTO MESSAGE LENGTH AND FLAGS
DTYPE    DC    CL20' ',C' = '        DISPLAY DESCRIPTIONS
D40      DC    CL40' '               DISPLAY VALUE                  
WTOEND   EQU   *
         EQUREGS REGS=GPR
         EQUREGS REGS=FPR
         END

