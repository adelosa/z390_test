.*********************************************************************
.* Copyright 2005 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 11/30/05                                                 *
.*********************************************************************
.* 
.*  SBA3270 - SET BUFFER ADDRESS FOR 3270
.*
.*  CALL SBA3270,(ROW,COL,SBA)
.*
.*       ROW - FULL WORD ROW 1-24
.*       COL - FULL WORD COL 1-80
.*       SBA - FULL WORD ADDRESS OF 2 BYTE SBA ADDRESS
.*
.*********************************************************************
.* 11/20/05 RPI47 - add TN3270 support routine for DEMOGUI2.MLC
.*********************************************************************   
CVTSBA   SUBENTRY
         L     R0,0(R1)
         BCTR  R0,0
         MHI   R0,80          R0=80*(ROW-1)
         A     R0,4(R1)
         BCTR  R0,0           R0=80*(ROW-1)+(COL-1)
         L     R1,8(R1)       R1=A(SBA)
         SLL   R0,2
         STCM  R0,2,0(R1)     SBA1 = HIGH 6 BITS
         SRL   R0,2
         NILL  R0,X'3F'
         STC   R0,1(R1)       SBA2 = LOW 6 BITS
         TR    0(2,R1),TRSBA  TANSLATE SBA TO 3270 DATA STREAM CODE
         SUBEXIT        
TRSBA    DC    X'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F' 
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F' 
         DC    X'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F'       
         DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'
         EQUREGS
         END

