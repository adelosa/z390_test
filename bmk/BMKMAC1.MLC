*********************************************************************
* Copyright 2005 Automated Software Tools Corporation               *
* This source code is part of z390 assembler/emulator package       *
* The z390 package is distributed under GNU general public license  *
* Author - Don Higgins                                              *
* Date   - 11/21/05                                                 *
*********************************************************************
* RUN 8 QUEENS RECURSIVE MACRO WITH NOLISTCALL TO MEASURE EXECUTION
* SPEED - SEE STATISTICS AT END OF BMKMAC1.BAL.  PERFORAMCE LOG:
*   10/20/05 Z390 V1.0.07 3.0 GHZ P4 - 36,000 MACRO INSTR./SEC
*********************************************************************
* 10/21/05 RPI 108 ADD HASH KEY LOOKUP FOR MACRO LABELS
*********************************************************************
         TITLE 'BMKMAC1.MLC - BENCHMARK 8 QUEENS MACRO EXECUTION'
         MACRO
         M8Q
         GBLA  &N,&X(8)
         GBLB  &COL(8),&UP(15),&DOWN(15)
         LCLA  &H
&H       SETA  1
         AIF   (&N GT 0).LOOP
&N       SETA  1
.LOOP    ANOP
         AIF   (&COL(&H) NE 0).NEXT
         AIF   (&UP(&N-&H+8) NE 0).NEXT
         AIF   (&DOWN(&N+&H-1) NE 0).NEXT
&X(&N)        SETA &H
&COL(&H)      SETB 1
&UP(&N-&H+8)   SETB 1
&DOWN(&N+&H-1) SETB 1
&N            SETA &N+1
         AIF   (&N GT 8).PRINT
         M8Q
         AGO  .CONT
.PRINT   ANOP
 MNOTE 'SOLUTION &X(1) &X(2) &X(3) &X(4) &X(5) &X(6) &X(7) &X(8)'
.CONT    ANOP
&N             SETA  &N-1
&DOWN(&N+&H-1) SETB 0
&UP(&N-&H+8)   SETB 0
&COL(&H)       SETB 0
.NEXT    ANOP
&H       SETA  &H+1
         AIF   (&H LE 8).LOOP
         MEND
         M8Q
         END
