*********************************************************************
* Copyright 2005 Automated Software Tools Corporation               *
* This source code is part of z390 assembler/emulator package       *
* The z390 package is distributed under GNU general public license  *
* Author - Don Higgins                                              *
* Date   - 11/20/05                                                 *
*********************************************************************
* RUN BMKINS1 INSTRUCTION LOOP BENCHMARKL TO MEASURE EXECUTION
* SPEED - SEE STATISTICS GENERATED ON BMKINS.LOG.  PERFORAMCE LOG:
*   10/20/05 Z390 V1.0.07 3.0 GHZ P4
*********************************************************************
* 10/21/05 RPI 108 ADD KEY INDEX LOOKUP FOR MACRO LABELS
*********************************************************************
         MACRO
&INS     BMKINS
         MVC  DINSOP,=CL5'&INS' 
&OPS     SETC &SYSLIST(1)
&I       SETA 1
.OPLOOP  ANOP
&I       SETA &I+1
         AIF  (&I GT N'&SYSLIST).OPEND
&OPS     SETC '&OPS,&SYSLIST(&I)'
         AGO  .OPLOOP
.OPEND   ANOP
         BAL  R12,BMKINIT
&INS     &INS &OPS
         BCT  R11,&INS
         BAL  R12,BMKCALC
         MEND
BMKINS1  SUBENTRY
         WTO  'BMKINS1 CALC Z390 INSTRUCTION SPEEDS'
         L     R0,=A(REPS)
         CVD   R0,PWORK
         ED    DREPS,PWORK+4
         WTO   MF=(E,WTOREPS)
         BAL   R12,BMKINIT
BCT      BCT   R11,BCT            CALC BCT ONLY
         BAL   R12,BMKCALC
         MVC   BCT_NS,INS_NS      SAVE BCT MICS FOR OTHER CALCS
*
* INSTRUCTION TIMING MACROS
*
A        BMKINS R0,=F'1'
AG       BMKINS R0,=FL8'1'
AGR      BMKINS R0,R1
AH       BMKINS R0,=H'1'
AHI      BMKINS R0,1
AL       BMKINS R0,=F'1'
ALC      BMKINS R0,=F'1'
ALCG     BMKINS R0,=FL8'1'
ALCGR    BMKINS R0,R1
ALCR     BMKINS R0,R1
ALG      BMKINS R0,=FL8'1'
ALGR     BMKINS R0,R1
ALR      BMKINS R0,R1
AP       BMKINS PDATA,=P'1'
AR       BMKINS R0,R1
S        BMKINS R0,=F'1'
SH       BMKINS R0,=H'1'
SL       BMKINS R0,=F'1'
SLB      BMKINS R0,=F'1'
SLBG     BMKINS R0,=FL8'1'
SLBGR    BMKINS R0,R1
SLBR     BMKINS R0,R1
SLG      BMKINS R0,=FL8'1'
SLGR     BMKINS R0,R1
SLR      BMKINS R0,R1
SP       BMKINS PDATA,=P'1'
SR       BMKINS R0,R1
* CLC      BMKINS DATA1,DATA2
LR       BMKINS R0,R1
* MVC      BMKINS DATA1,DATA2
MVI      BMKINS DATA1,0
* MVN      BMKINS DATA1,DATA2
* MVZ      BMKINS DATA1,DATA2 
* OC       BMKINS DATA1,DATA2
* NC       BMKINS DATA1,DATA2
* XC       BMKINS DATA1,DATA2

*
* END OF MACROS
*
         WTO  'BMKINS1 ENDED OK'
         SUBEXIT
*
* INIT - SET START TIME AND COUNT
*
BMKINIT  EQU  *
         TIME NS,TOD_NS_START
         L    R11,=A(REPS)
         SR   R0,R0
         SPM  R0     SUPRESS OVERFLOW
         BR   R12
*
* CALC AND DISPLAY INSTRUCTION RATE
*
BMKCALC  EQU  *
         TIME NS,TOD_NS_END
         LG   R1,TOD_NS_END
         SG   R1,TOD_NS_START
         DSGF R0,=A(REPS)
         SG   R1,BCT_NS
         STG  R1,INS_NS
         CVDG R1,PWORK16
         MVC  DRATE,MASK
         ED   DRATE,PWORK16+13
         WTO  MF=(E,WTORATE)
         BR   R12
*
* COMMON DATA
*
         EQUREGS
*
* SET REPS TO MINIMUM VALUE REQUIRED TO GET WITHIN DESIRED ACCURACY
*
*   1.  Using original TIME MIC counter based on Java current time mics
*       reguired 10 M reps to get within 5% accuracy
*   2.  Using new TIME NS counter based on Java nanoTime only
*       requires 1 M reps to get within 5% accuracy
*
REPS     EQU  1000000
PWORK    DS   D  
PWORK16  DS   DL16
TOD_NS_START DS  D
TOD_NS_END   DS  D
INS_NS   DS  D
BCT_NS   DS  FL8'0'
WTOREPS  DC  AL2(REPEND-*,0)
         DC  C'INSTRUCTION REPITITIONS = '
DREPS    DC  X'4020202020202020'
REPEND   EQU *         
WTORATE  DC  AL2(RATEEND-*,0)
         DC  C'INS '
DINSOP   DC  CL5'BCT',C' ='
DRATE    DC  CL6' ZZZZN'
         DC  C' NANOSECONDS'
RATEEND  EQU *         
MASK     DC  X'402020202120'
DATA1    DC   CL256' '
DATA2    DC   CL256' ' 
PDATA    DC   PL8'0'
         END      
