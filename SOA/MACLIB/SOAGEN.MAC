        MACRO
&NAME   SOAGEN &MAIN=,      OPTIONAL GEN &MAIN.390 WITH STUBS          X
               &CLIENT=,    CLIENT SOA MESSAGE MANAGER NAME            X
               &SERVER=,    SERVER SOA MESSAGE MANAGER NAME            X
               &HOST=,      HOST SERVER URL                            X
               &PORT=,      HOST SERVER PORT NUMBER                    X
               &SERVICES=,  DEFINE NAME AND LENGTH OF SERVICE PARMS    X
               &MACDIR=,    DIRECTORY WITH SOA GEN MACROS              X
               &GENDIR=,    DIRECTORY TO PLACE GENERATED SOURCE CODE   X
               &GENBLD=,    NAME OF GENERATED BUILD BAT FILE           X
               &GENRUN=     NAME OF GENERATED RUN BAT                   
*********************************************************************
* Copyright 2006 Automated Software Tools Corporation               *
* This source code is part of z390 assembler/emulator package       *
* The z390 package is distributed under GNU general public license  *
* Author - Don Higgins                                              *
* Date   - 12/28/06                                                 *
.********************************************************************
.* MAINTENANCE: 
.* 12/28/06 RPI 524 ADD SUPPORT FOR SOA CLIENT SERVER APPLICATIONS 
.* 02/05/07 RPI 532 ADD DEMORUNS & DEMORUNC.BAT AND FIX PATH CASE
.********************************************************************
.*  SOAGEN GENERATES SERVICE ORIENTED ARCHITECTURE (SOA) CLIENT     
.*  SERVER SOURCE CODE FOR APPLICATION                                                  
.*                                           
.*  OUTPUT:
.*    &GENDIR\&CLIENT.MLC  - SOURCE CODE FOR SOA CLIENT MESSAGE MANAGER
.*    &GENDIR\&SERVER.MLC  - SOURCE CODE FOR SOA SERVER MESSAGE MANAGER
.*    &GENDIR\&SERVICE.MLC - GENERATED SOA CALL STUBS FOR EACH SERVICE 
.*                           LINKED WITH OTHER CLIENT APPLICATION PGMS
.*                           STUBS DYNAMICALLY CALL SOA CLIENT MSG MGR
.*    &GENDIR\&GENBLD.BAT  - BAT TO GENERATE SOA APPLICATION IN GENDIR
.*    &GENDIR\&GENRUN.BAT  - BAT TO START SERVER AND CLIENT THREADS
.*    &GENDIR\&GENRUNS.BAT - BAT TO RUN DEMOSMGR SERVER UNTIL SHUTDOWN
.*    &GENDIR\&GENRUNC.BAT - BAT TO RUN DEMOMAIN CLIENT
.*
.*  EXAMPLE:
.*    1.  RUN SOA\DEMO\DEMOSTD.BAT TO BUILD AND RUN STATICALLY LINKED 
.*        STANDARD DEMO APPLICATION LOCALLY.                        
.*    2.  SEE SOA\DEMO\DEMOSOA.MLC SOURCE DEFINITION OF SOA APPLICATION
.*        A.  TO RUN SOA SERVER ON YOUR SYSTEM, CHANGE HOST CPU NAME                              
.*    3.  RUN SOA\DEMO\DEMOSOA.BAT TO GENERATE, BUILD, AND RUN CLIENT
.*        SERVER SOA DEMO APPLICATION.  NOTE ON LINUX ONCE DEMOSMGR IS
.*        RUNNING IN CURRENT TERMINAL, START DEMORUNC IN ANOTHER
.*        TERMINAL WINDOW TO SEE SEPARATE CLIENT SERVER OUTPUT.
.*
.*      SOURCE CODE GENERATED IN SOA\DEMOSOA DIRECTORY
.*   
.*      1.  DEMOCMGR.MLC/390 - SOA CLIENT MSG MGR
.*      2.  DEMOSMGR.MLC/390 - SOA SERVER MSG MGR
.*      3.  DEMOSUB1.MLC/OBJ - SOA CLIENT STUB FOR DEMOSUB1 SERVICE
.*      4.  DEMOSUB1.390     - SOA SERVICE FOR LOADING BY SOASMGR
.*      5.  DEMOSUB2.MLC/OBJ - SOA CLIENT STUB FOR DEMOSUB2 SERVICE
.*      6.  DEMOSUB2.390     - SOA SERVICE FOR LOADING BY SOASMGR
.*      7.  DEMOBLD.BAT  - BATCH JOB TO BUILD SOA CLIENT SERVER PGMS
.*      8.  DEMORUN.BAT  - BATCH JOB TO RUN CLIENT SERVER APPLICATION
.*      9.  DEMORUNS.BAT - EXEC DEMOSMGR SERVER 
.*     10.  DEMORUNC.BAT - EXEC DEMORUNC CLIENT
.* NOTES:
.*   1. THE SOA CLIENT MSG MGR PASSES PARMS BY VALUE AND ALL FIELDS
.*      IN MEMORY ACCESSED BY SERVICE MUST BE PASSED AS PARMS.
.*   2. READ ONLY PARMS INDICATED BY NEGATIVE LENGTHS ARE NOT RETURNED
.*      BY SERVER.  ALL OTHER PARMS ARE RETURNED BY SERVER AND UPDATED
.*      IN MEMORY PRIOR TO RETURNING FROM CLIENT MESSAGE MANAGER.
.*   3. DYNAMIC MSG BUFFER SIZE = LARGEST SERVICE SEND MESSAGE        
.********************************************************************
&ZOPT    SETC  &SYSPARM
         AIF   (&ZOPT EQ '' OR '&ZOPT'(1,1) NE '''').SKP_FIX_ZOPT
&ZOPT    SETC  '&ZOPT'(2,K'&ZOPT-2).'%1 %2 %3 %4 %5'
.SKP_FIX_ZOPT ANOP
 PUNCH ' SOACMGR CLIENT=&CLIENT,HOST=&HOST,PORT=&PORT,SERVICES=&SERVICEX
               S',DSNAME=&GENDIR\&CLIENT..MLC,FORMAT
 PUNCH ' END',DSNAME=&GENDIR\&CLIENT..MLC,FORMAT
 PUNCH ' SOASMGR SERVER=&SERVER,HOST=&HOST,PORT=&PORT,SERVICES=&SERVICEX
               S',DSNAME=&GENDIR\&SERVER..MLC,FORMAT                                              
 PUNCH ' END',DSNAME=&GENDIR\&SERVER..MLC,FORMAT
&NS      SETA  0 
.GEN_SERVICE_STUB_MLC ANOP
&NS      SETA  &NS+1
         AIF   (&NS GT N'&SERVICES).END_GEN_SERVICE_STUB_MLC
 PUNCH ' SOASTUB SERVICE=&SERVICES(&NS,1),CLIENT=&CLIENT',             X
               DSNAME=&GENDIR\SOA_STUB_&SERVICES(&NS,1)..MLC,FORMAT
 PUNCH ' END',DSNAME=&GENDIR\SOA_STUB_&SERVICES(&NS,1)..MLC,FORMAT
         AGO   .GEN_SERVICE_STUB_MLC
.END_GEN_SERVICE_STUB_MLC ANOP
         AIF   (&GENBLD EQ '').SKIP_GEN_BAT
.GEN_BAT ANOP
 PUNCH 'REM GENERATE SOA APPLICAITON                 ',                X
               DSNAME=&GENDIR\&GENBLD..BAT
 PUNCH 'REM GEN SOA CLIENT MSG MGR                   ',                X
               DSNAME=&GENDIR\&GENBLD..BAT
 PUNCH 'CALL ASML &GENDIR\&CLIENT SYSMAC(&MACDIR+mac) &ZOPT',          X
               DSNAME=&GENDIR\&GENBLD..BAT
 PUNCH 'IF ERRORLEVEL 1 PAUSE ERROR IN ASML &CLIENT',                  X
               DSNAME=&GENDIR\&GENBLD..BAT
 PUNCH 'REM GEN SOA SERVER MSG MGR',                                   X
               DSNAME=&GENDIR\&GENBLD..BAT
 PUNCH 'CALL ASML &GENDIR\&SERVER SYSMAC(&MACDIR+mac) &ZOPT',          X
               DSNAME=&GENDIR\&GENBLD..BAT
 PUNCH 'IF ERRORLEVEL 1 PAUSE ERROR IN ASML &SERVER',                  X
               DSNAME=&GENDIR\&GENBLD..BAT
&NS      SETA  0
.GEN_SERVICE_STUB_OBJ ANOP
&NS      SETA  &NS+1
         AIF   (&NS GT N'&SERVICES).END_GEN_SERVICE_STUB_OBJ
 PUNCH 'REM GEN SOA SERVICE 390 FOR LOADING BY SERVER',                X
               DSNAME=&GENDIR\&GENBLD..BAT
 PUNCH 'CALL ASML &GENDIR\&SERVICES(&NS,1) SYSMAC(&MACDIR+mac) SYS390(&X
               GENDIR) &ZOPT',DSNAME=&GENDIR\&GENBLD..BAT
 PUNCH 'IF ERRORLEVEL 1 PAUSE ERROR IN ASML &SERVICES(&NS,1)',         X
               DSNAME=&GENDIR\&GENBLD..BAT
 PUNCH 'REM GEN SOA SERVICE STUB OBJ FOR LINKING WITH CLIENT APPL',    X
               DSNAME=&GENDIR\&GENBLD..BAT
 PUNCH 'CALL ASM  &GENDIR\SOA_STUB_&SERVICES(&NS,1) SYSMAC(&MACDIR+mac)X
                &ZOPT',DSNAME=&GENDIR\&GENBLD..BAT
 PUNCH 'IF ERRORLEVEL 1 PAUSE ERROR IN ASM STUB &SERVICES(&NS,1)',     X
               DSNAME=&GENDIR\&GENBLD..BAT
 PUNCH 'COPY &GENDIR\SOA_STUB_&SERVICES(&NS,1)..OBJ &GENDIR\&SERVICES(&X
               NS,1)..OBJ',DSNAME=&GENDIR\&GENBLD..BAT
 PUNCH 'IF ERRORLEVEL 1 PAUSE ERROR IN COPY STUB &SERVICES(&NS,1)',    X
               DSNAME=&GENDIR\&GENBLD..BAT
         AGO   .GEN_SERVICE_STUB_OBJ
.END_GEN_SERVICE_STUB_OBJ ANOP
         AIF   (&MAIN EQ '').SKIP_GEN_MAIN
 PUNCH 'REM GEN SOA MAIN CLIENT LINKED WITH SERVICE STUBS',            X
               DSNAME=&GENDIR\&GENBLD..BAT
 PUNCH 'CALL ASML &GENDIR\&MAIN SYSOBJ(&GENDIR) SYS390(&GENDIR)',      X
               DSNAME=&GENDIR\&GENBLD..BAT
.SKIP_GEN_MAIN ANOP
.SKIP_GEN_BAT  ANOP
         AIF   (&GENRUN EQ '').SKIP_GEN_RUN
.GEN_RUN ANOP
 PUNCH 'REM RUN SO CLIENT SERVER APPLICAITON',                         X
               DSNAME=&GENDIR\&GENRUN..BAT
 PUNCH 'IF EXIST /usr/local/lib/z390/z390.jar GOTO LINUX',             X
               DSNAME=&GENDIR\&GENRUN..BAT
 PUNCH 'REM START SOA APPLICATION SERVER AS SEPARATE PROCESS',         X
               DSNAME=&GENDIR\&GENRUN..BAT
 PUNCH 'START &GENDIR\&GENRUN.S &ZOPT',                                X
               DSNAME=&GENDIR\&GENRUN..BAT
 PUNCH 'PAUSE PRESS ENTER TO START CLIENT AFTER SERVER IS RUNNING',    X
               DSNAME=&GENDIR\&GENRUN..BAT
 PUNCH 'REM RUN SOA CLIENT APPLICATION',                               X
               DSNAME=&GENDIR\&GENRUN..BAT
 PUNCH 'CALL &GENDIR\&GENRUN.C &ZOPT',                                 X
               DSNAME=&GENDIR\&GENRUN..BAT
 PUNCH 'GOTO END',                                                     X
               DSNAME=&GENDIR\&GENRUN..BAT
 PUNCH ':LINUX',                                                       X
               DSNAME=&GENDIR\&GENRUN..BAT
 PUNCH 'REM RUN &GENRUN.S DEMOSMGR SERVER AND THEN',                   X
               DSNAME=&GENDIR\&GENRUN..BAT
 PUNCH 'REM RUN &GENRUN.C DEMOMAIN CLIENT IN 2ND TERMINAL WINDOW',     X
               DSNAME=&GENDIR\&GENRUN..BAT
 PUNCH ':END',                                                         X
               DSNAME=&GENDIR\&GENRUN..BAT
 PUNCH 'REM RUN SOA SERVER APPLICATION',                               X
               DSNAME=&GENDIR\&GENRUN.S.BAT
 PUNCH 'CALL EXEC &GENDIR\&SERVER &ZOPT',                              X
               DSNAME=&GENDIR\&GENRUN.S.BAT
 PUNCH 'REM RUN SOA CLIENT APPLICATION',                               X
               DSNAME=&GENDIR\&GENRUN.C.BAT
 PUNCH 'CALL EXEC &GENDIR\&MAIN &ZOPT',                                X
               DSNAME=&GENDIR\&GENRUN.C.BAT
.SKIP_GEN_RUN ANOP
    MEND
