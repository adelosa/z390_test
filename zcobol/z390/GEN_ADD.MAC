         MACRO
.*********************************************************************
.* Copyright 2008 Automated Software Tools Corporation               *
.* This source code is part of z390 zCOBOL/assembler/emulator package*
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 04/07/08                                                 *
.*********************************************************************
.* 10/30/08 initial coding for Z390 GEN_ADD using GEN_SUB as model    
.* 01/26/09 add TRUNC support for F,G,H - see zcobol\test\TESTTRC1.CBL            
.* 01/27/09 add ON SIZE and optimize TRUNC for pipeline and use LGFI                      
.* 02/25/09 RPI 1004 add support for types 1,2,E,D,L &ZC_FLOAT type    
.* 02/26/09 RPI 1012 move onsize end-add pending to add                
.* 04/28/09 RPI 1003 add support for temp vars with neg IX
.* 08409/28/09 RPI 1065 support literals starting with .      
.*********************************************************************
.* Notes:
.*  1.  ROUNDED not supported yet.
.*********************************************************************
         GEN_ADD &T_NAME,&T_IX,&S_NAME,&S_IX,&ROUNDED,&ONSIZE
         COPY  ZC_WS
         AIF (&T_IX GT 0)
             :&N_TARGET SETC '&T_NAME'
             :&L_TARGET SETA  &SYM_LEN(&T_IX)
             :&S_TARGET SETB  &SYM_PIC_SIGN(&T_IX)
             :&T_TARGET SETC '&SYM_PIC_TYPE(&T_IX)'
             :&P_TARGET SETC '&SYM_PIC(&T_IX)'
             AIF ('&T_TARGET' NE 'P')
                 :&LAB SETC '&N_TARGET'
                 ACALL RESET_LAB_LEN
                 :&N_TARGET SETC '&LAB'
             AEND
         AELSEIF (&T_IX LT 0)
             :&N_TARGET SETC '&TMP_NAME(-&T_IX)'
             :&L_TARGET SETA  &TMP_LEN(-&T_IX)
             :&S_TARGET SETB  &TMP_SIGN(-&T_IX)
             :&T_TARGET SETC '&TMP_TYPE(-&T_IX)'
             :&P_TARGET SETC ''
             AIF ('&T_TARGET' EQ 'P')
                 :&N_TARGET SETC '&N_TARGET.(&L_TARGET)'
             AEND
         AELSE
            MNOTE 8,'GEN_ADD MISSING TARGET INDEX FOR &T_NAME'
            MEXIT
         AEND
         :&N_SOURCE SETC '&S_NAME'
         AIF (&S_IX GT 0)
             :&L_SOURCE SETA  &SYM_LEN(&S_IX)
             :&S_SOURCE SETB  &SYM_PIC_SIGN(&S_IX)
             :&T_SOURCE SETC '&SYM_PIC_TYPE(&S_IX)'
         AELSEIF (&S_IX LT 0)
             :&N_SOURCE SETC '&TMP_NAME(-&S_IX)'
             :&L_SOURCE SETA  &TMP_LEN(-&S_IX)
             :&S_SOURCE SETB  &TMP_SIGN(-&S_IX)
             :&T_SOURCE SETC '&TMP_TYPE(-&S_IX)'
             AIF ('&T_SOURCE' EQ 'P')
                 :&N_SOURCE SETC '&N_SOURCE.(&L_SOURCE)'
             AEND
         AEND
         AIF   (&ROUNDED EQ 1)
               MNOTE 8,'GEN_ADD ROUNDED NOT SUPPORTED YET'
               MEXIT
         AEND
         AIF   (&S_IX NE 0)
               ACALL ADD_FLD
         AELSE
               :&LIT SETC '&N_SOURCE'
               ACALL ADD_LIT
         AEND
         MEXIT
.*
.* ADD LITERAL
.* 
         AENTRY ADD_LIT
         AIF  ('&LIT'(1,1) GE '0'                                      X
                OR '&LIT'(1,1) EQ '+'                                  X
                OR '&LIT'(1,1) EQ '-'                                  X
                OR '&LIT'(1,1) EQ '.')  RPI 1002, RPI 1065           
               :&NUM SETC '&LIT'
               ACALL ADD_NUM_LIT
         AELSEIF  ('&LIT' EQ 'ZERO'                                    X
                   OR '&LIT' EQ 'ZEROES')                          
               :&NUM SETC '0'
               ACALL ADD_NUM_LIT
         AELSE
               MNOTE 8,'GEN_ADD UNKNOWN LITERAL &LIT'
               MEXIT
         AEND
         AEND
.*
.* ADD NUMERIC LIT 
.*
      AENTRY ADD_NUM_LIT
      ACASE (C2A('&T_TARGET'))
      AWHEN C'1' EH
         LE    ZC_F0,&N_TARGET
         LARL  ZC_R14,=E'&NUM'
         AE    ZC_F0,0(ZC_R14)
         STE   ZC_F0,&N_TARGET
      AWHEN C'2' DH
         LD    ZC_F0,&N_TARGET
         LARL  ZC_R14,=D'&NUM'
         AD    ZC_F0,0(ZC_R14)
         STD   ZC_F0,&N_TARGET
      AWHEN C'3' LH
         LD    ZC_F0,&N_TARGET
         LD    ZC_F2,8+&N_TARGET
         LARL  ZC_R14,=LH'&NUM'
         LD    ZC_F1,0(ZC_R14)        
         LD    ZC_F3,8(ZC_R14)          
         AXR   ZC_F0,ZC_F1     
         STD   ZC_F0,&N_TARGET
         STD   ZC_F2,8+&N_TARGET
      AWHEN C'4' EB
         LE    ZC_F0,&N_TARGET
         LARL  ZC_R14,=EB'&NUM'
         AEB   ZC_F0,0(ZC_R14)
         STE   ZC_F0,&N_TARGET
      AWHEN C'5' DB
         LD    ZC_F0,&N_TARGET
         LARL  ZC_R14,=DB'&NUM'
         ADB   ZC_F0,0(ZC_R14)
         STD   ZC_F0,&N_TARGET
      AWHEN C'6' LB
         LD    ZC_F0,&N_TARGET
         LD    ZC_F2,8+&N_TARGET
         LARL  ZC_R14,=LB'&NUM'
         LD    ZC_F1,0(ZC_R14)        
         LD    ZC_F3,8(ZC_R14)          
         AXBR  ZC_F0,ZC_F1     
         STD   ZC_F0,&N_TARGET
         STD   ZC_F2,8+&N_TARGET
      AWHEN C'7' ED
         LE    ZC_F0,&N_TARGET
         LDETR ZC_F0,ZC_F0,0
         LARL  ZC_R14,=DD'&NUM'
         LD    ZC_F1,0(ZC_R14)
         ADTR  ZC_F0,ZC_F0,ZC_F1
         LEDTR ZC_F0,0,ZC_F0,0
         STE   ZC_F0,&N_TARGET
      AWHEN C'8' DD
         LD    ZC_F0,&N_TARGET
         LARL  ZC_R14,=DD'&NUM'
         LD    ZC_F1,0(ZC_R14)
         ADTR  ZC_F0,ZC_F0,ZC_F1
         STD   ZC_F0,&N_TARGET
      AWHEN C'9' LD
         LD    ZC_F0,&N_TARGET
         LD    ZC_F2,8+&N_TARGET
         LARL  ZC_R14,=LD'&NUM'
         LD    ZC_F1,0(ZC_R14)        
         LD    ZC_F3,8(ZC_R14)          
         AXTR  ZC_F0,ZC_F0,ZC_F1     
         STD   ZC_F0,&N_TARGET
         STD   ZC_F2,8+&N_TARGET
.*
.* ADD_NUM_LIT F A I
.*
      AWHEN C'F',C'A',C'I'
       AIF (&ONSIZE EQ 1 OR (&ZC_TRUNC AND '&P_TARGET' NE ''))
         SR    ZC_R0,ZC_R0
         L     ZC_R1,&N_TARGET
         ZC_PIC_DIGITS &T_IX
         :&TRUNC_DIV SETA ('1'.(&PIC_DIGITS)'0')
         LARL  ZC_R14,=F'&TRUNC_DIV'
         AFI   ZC_R1,&NUM
        AIF   (&ONSIZE EQ 1)
              :&STX SETC 'ST'
               ACALL SET_ON_SIZE_F32
        AELSE  
         D     ZC_R0,0(ZC_R14)
         ST    ZC_R0,&N_TARGET
        AEND 
       AELSE
        AIF (K'&NUM LE 2)
         ASI   &N_TARGET,&NUM
        AELSE
         L     ZC_R0,&N_TARGET
         AFI   ZC_R0,&NUM
         ST    ZC_R0,&N_TARGET
        AEND
       AEND
.*
.* ADD_NUM_LIT G
.*
      AWHEN C'G'
       AIF (&ONSIZE EQ 1 OR (&ZC_TRUNC AND '&P_TARGET' NE ''))
         ZC_PIC_DIGITS &T_IX
         :&TRUNC_DIV SETC '1'.(&PIC_DIGITS)'0'
        AIF (&ZC_R64)
         LG    ZC_R1,&N_TARGET
         LGRL  ZC_R0,=FL8'&TRUNC_DIV'
         AGFI  ZC_R1,&NUM
          AIF  (&ONSIZE EQ 1)
               :&STX SETC 'STG'
               ACALL SET_ON_SIZE_G64
          AELSE  
         DSGR  ZC_R0,ZC_R0
         STG   ZC_R0,&N_TARGET
          AEND
        AELSE
         LM    ZC_R0,ZC_R1,&N_TARGET
         LARL  ZC_R14,=FL8'&NUM'
         A     ZC_R1,4(ZC_R14)
         ALC   ZC_R0,0(ZC_R14)
         LARL  ZC_R14,=FL8'&TRUNC_DIV'
         LM    ZC_R2,ZC_R3,0(ZC_R14)
          AIF  (&ONSIZE EQ 1)
         L     ZC_R15,ZCVT_OSE64R32
         BASR  ZC_R14,ZC_R15
               ACALL SET_ON_SIZE_G64_R32
          AELSE
         L     ZC_R15,ZCVT_DIV64R32
         BASR  ZC_R14,ZC_R15
         STM   ZC_R0,ZC_R1,&N_TARGET
          AEND
        AEND  
       AELSE
        AIF (K'&NUM LE 2)
         AGSI  &N_TARGET,&NUM
        AELSE
          AIF  (&ZC_R64)
         LG    ZC_R0,&N_TARGET
         LARL  ZC_R14,=FL8'&NUM'
         AG    ZC_R0,0(ZC_R14)
         STG   ZC_R0,&N_TARGET
          AELSE
         LM    ZC_R0,ZC_R1,&N_TARGET
         LARL  ZC_R14,=FL8'&NUM'
         A     ZC_R1,4(ZC_R14)
         ALC   ZC_R0,0(ZC_R14)
         STM   ZC_R0,ZC_R1,&N_TARGET
          AEND
        AEND
       AEND
.*
.* ADD_NUM_LIT H
.*
      AWHEN C'H'
       AIF (&ONSIZE EQ 1 OR (&ZC_TRUNC AND '&P_TARGET' NE ''))
         SR    ZC_R0,ZC_R0
         LH    ZC_R1,&N_TARGET
         ZC_PIC_DIGITS &T_IX
         :&TRUNC_DIV SETA ('1'.(&PIC_DIGITS)'0')
         LARL  ZC_R14,=F'&TRUNC_DIV'
         AFI   ZC_R1,&NUM
        AIF   (&ONSIZE EQ 1)
              :&STX SETC 'STH'
               ACALL SET_ON_SIZE_F32
        AELSE  
         D     ZC_R0,0(ZC_R14)
         STH   ZC_R0,&N_TARGET
        AEND 
       AELSE
         LH    ZC_R0,&N_TARGET
         AHI   ZC_R0,&NUM
         STH   ZC_R0,&N_TARGET
       AEND
.*
.* ADD_NUM_LIT P
.*
      AWHEN C'P'
         :&LPNUM SETA (K'&NUM/2+3)/2*2 ROUNDED EVEN FOR LARL
        AIF (&LPNUM EQ 2)
         BRAS  ZC_R14,3 SET ZC_R1 TO A(INLINE LIT) AND SKIP OVER IT
         DC    PL2'&NUM'
        AELSE
         AIF (&LPNUM GT 16)
             :&LPNUM SETA 16
         AEND
         LARL  ZC_R14,=PL&LPNUM'&NUM'
        AEND
         AP    &N_TARGET,0(&LPNUM,ZC_R14)
.*
.* ADD_NUM_LIT Q
.*
      AWHEN C'Q'
         LMG  ZC_R0,ZC_R1,&N_TARGET
         LARL ZC_R14,=FL16'&NUM'
         ALG  ZC_R1,8(ZC_R14)
         ALCG ZC_R0,0(ZC_R14)
         STMG ZC_R0,ZC_R1,&N_TARGET
.*
.* ADD_NUM_LIT Z
.*
      AWHEN C'Z'
         :&PLEN SETA &L_TARGET/2+1
         ZC_PACK31  ZCVT_PWORK1,&PLEN,&N_TARGET,&L_TARGET
         :&LPNUM SETA (K'&NUM/2+3)/2*2 ROUNDED EVEN FOR LARL
         AIF (&LPNUM GT 16)
             :&LPNUM SETA 16
         AEND
         LARL  ZC_R14,=PL&LPNUM'&NUM'
         AP    ZCVT_PWORK1(&PLEN),0(&LPNUM,ZC_R14)
         :&ST SETB &S_TARGET
         ZC_UNPK31  &N_TARGET,&L_TARGET,&ST,ZCVT_PWORK1,&PLEN
      AELSE
         MNOTE 8,'GEN_ADD INVALID DATA TYPE FOR &N_TARGET'
      AEND
      AEND
.*
.* ADD_FLD
.*
         AENTRY ADD_FLD
         ACASE (C2A('&T_TARGET'))
.*
.*  ADD_FLD ?? TO EH 
.*
         AWHEN C'1'                     ADD ?? TO EH
               ACASE (C2A('&T_SOURCE'))
               AWHEN C'1'               ADD EH TO EH
         LE    ZC_F0,&N_SOURCE
               AWHEN C'2'               ADD DH TO EH
         ZC_CONV EH,DH,&N_SOURCE
               AWHEN C'3'               ADD LH TO EH
         ZC_CONV EH,LH,&N_SOURCE
               AWHEN C'4'               ADD EB TO EH
         ZC_CONV EH,EB,&N_SOURCE
               AWHEN C'5'               ADD DB TO EH
         ZC_CONV EH,DB,&N_SOURCE
               AWHEN C'6'               ADD LB TO EH
         ZC_CONV EH,LB,&N_SOURCE
               AWHEN C'7'               ADD ED TO EH
         ZC_CONV EH,ED,&N_SOURCE
               AWHEN C'8'               ADD DD TO EH
         ZC_CONV EH,DD,&N_SOURCE
               AWHEN C'9'               ADD LB TO EH
         ZC_CONV EH,LD,&N_SOURCE
               AWHEN C'F',C'A',C'I'
         L     ZC_R0,&N_SOURCE
         CEFR  ZC_F0,ZC_R0
               AWHEN C'G'
         LG    ZC_R0,&N_SOURCE
         CEGR  ZC_F0,ZC_R0
               AWHEN C'H'
         LH    ZC_R0,&N_SOURCE
         CEFR  ZC_F0,ZC_R0
               AWHEN C'P'
         ZAP   ZCVT_PWORK1,&N_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
         ZC_CONV EH,LD
               AWHEN C'Q'  RPI 1004
         ZC_CONV EH,Q,&N_SOURCE
               AWHEN C'Z'
                   ZC_PACK31  ZCVT_PWORK1,16,&N_SOURCE,&L_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
                   ZC_CONV EH,LD
               AELSE
                    MNOTE 8,'GEN_ADD INVALID SOURCE DATA TYPE &N_SOURCEX
               '         
                    MEXIT
               AEND 
         AE    ZC_F0,&N_TARGET
         STE   ZC_F0,&N_TARGET
.*
.*  ADD_FLD ?? TO DH
.*
         AWHEN C'2'
         LD    ZC_F1,&N_TARGET
               ACASE (C2A('&T_SOURCE'))
               AWHEN C'1'               ADD EH TO DH
         LE    ZC_F0,&N_SOURCE
         LDER  ZC_F0,ZC_F0
               AWHEN C'2'               ADD DH TO DH
         LD    ZC_F0,&N_SOURCE
               AWHEN C'3'               ADD LH TO DH
         LD    ZC_F0,&N_TARGET
         LD    ZC_F2,8+&N_TARGET
         LDXR  ZC_F0,ZC_F0
               AWHEN C'4'               ADD EB TO DH
         ZC_CONV DH,EB,&N_SOURCE
               AWHEN C'5'               ADD DB TO DH
         ZC_CONV DH,DB,&N_SOURCE
               AWHEN C'6'               ADD LB TO DH
         ZC_CONV DH,LB,&N_SOURCE
               AWHEN C'7'               ADD ED TO DH
         ZC_CONV DH,ED,&N_SOURCE
               AWHEN C'8'               ADD DD TO DH
         ZC_CONV DH,DD,&N_SOURCE
               AWHEN C'9'               ADD LB TO DH
         ZC_CONV DH,LD,&N_SOURCE
               AWHEN C'F',C'A',C'I'
         L     ZC_R0,&N_SOURCE
         CDFR  ZC_F0,ZC_R0
               AWHEN C'G'
         LG    ZC_R0,&N_SOURCE
         CDGR  ZC_F0,ZC_R0
               AWHEN C'H'
         LH    ZC_R0,&N_SOURCE
         CDFR  ZC_F0,ZC_R0
               AWHEN C'P'
         ZAP   ZCVT_PWORK1,&N_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
         ZC_CONV DH,LD
               AWHEN C'Q'  RPI 1004
         ZC_CONV DH,Q,&N_SOURCE
               AWHEN C'Z'
                   ZC_PACK31  ZCVT_PWORK1,16,&N_SOURCE,&L_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
                   ZC_CONV DH,LD
               AELSE
                    MNOTE 8,'GEN_ADD INVALID SOURCE DATA TYPE &N_SOURCEX
               '           
                    MEXIT
               AEND 
         ADR   ZC_F0,ZC_F1
         STD   ZC_F0,&N_TARGET
.*
.*  ADD_FLD ?? TO LH
.*
         AWHEN C'3'
         LD    ZC_F1,&N_TARGET
         LD    ZC_F3,8+&N_TARGET
               ACASE (C2A('&T_SOURCE'))
               AWHEN C'1'               ADD EH TO LH
         LE    ZC_F0,&N_SOURCE
         LXER  ZC_F0,ZC_F0
               AWHEN C'2'               ADD DH TO LH
         LD    ZC_F0,&N_SOURCE
         LXDR  ZC_F0,ZC_F0
               AWHEN C'3'               ADD LH TO LH
         LD    ZC_F0,&N_SOURCE
         LD    ZC_F2,8+&N_SOURCE
               AWHEN C'4'               ADD EB TO LH
         ZC_CONV LH,EB,&N_SOURCE
               AWHEN C'5'               ADD DB TO LH
         ZC_CONV LH,DB,&N_SOURCE
               AWHEN C'6'               ADD LB TO LH
         ZC_CONV LH,LB,&N_SOURCE
               AWHEN C'7'               ADD ED TO LH
         ZC_CONV LH,ED,&N_SOURCE
               AWHEN C'8'               ADD DD TO LH
         ZC_CONV LH,DD,&N_SOURCE
               AWHEN C'9'               ADD LB TO LH
         ZC_CONV LH,LD,&N_SOURCE
               AWHEN C'F',C'A',C'I'
         L     ZC_R0,&N_SOURCE
         CXFR  ZC_F0,ZC_R0
               AWHEN C'G'
         LG    ZC_R0,&N_SOURCE
         CXGR  ZC_F0,ZC_R0
               AWHEN C'H'
         LH    ZC_R0,&N_SOURCE
         CXFR  ZC_F0,ZC_R0
               AWHEN C'P'
         ZAP   ZCVT_PWORK1,&N_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
         ZC_CONV LH,LD
               AWHEN C'Q'  RPI 1004
         ZC_CONV LH,Q,&N_SOURCE
               AWHEN C'Z'
                   ZC_PACK31  ZCVT_PWORK1,16,&N_SOURCE,&L_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
                   ZC_CONV LH,LD
               AELSE
                    MNOTE 8,'GEN_ADD INVALID SOURCE DATA TYPE &N_SOURCEX
               '           
                    MEXIT
               AEND 
         AXR   ZC_F0,ZC_F1
         STD   ZC_F0,&N_TARGET
         STD   ZC_F2,8+&N_TARGET
.*
.*  ADD_FLD ?? TO EB 
.*
         AWHEN C'4'                     ADD ?? TO EB
               ACASE (C2A('&T_SOURCE'))
               AWHEN C'1'               ADD EH TO EB
         ZC_CONV EB,EH,&N_SOURCE
               AWHEN C'2'               ADD DH TO EB
         ZC_CONV EB,DH,&N_SOURCE
               AWHEN C'3'               ADD LH TO EB
         ZC_CONV EB,LH,&N_SOURCE
               AWHEN C'4'               ADD EB TO EB
         ZC_CONV EB,EB,&N_SOURCE
               AWHEN C'5'               ADD DB TO EB
         ZC_CONV EB,DB,&N_SOURCE
               AWHEN C'6'               ADD LB TO EB
         ZC_CONV EB,LB,&N_SOURCE
               AWHEN C'7'               ADD ED TO EB
         ZC_CONV EB,ED,&N_SOURCE
               AWHEN C'8'               ADD DD TO EB
         ZC_CONV EB,DD,&N_SOURCE
               AWHEN C'9'               ADD LB TO EB
         ZC_CONV EB,LD,&N_SOURCE
               AWHEN C'F',C'A',C'I'
         L     ZC_R0,&N_SOURCE
         CEFBR ZC_F0,ZC_R0
               AWHEN C'G'
         LG    ZC_R0,&N_SOURCE
         CEGBR ZC_F0,ZC_R0
               AWHEN C'H'
         LH    ZC_R0,&N_SOURCE
         CEFBR ZC_F0,ZC_R0
               AWHEN C'P'
         ZAP   ZCVT_PWORK1,&N_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
         ZC_CONV EB,LD
               AWHEN C'Q'  RPI 1004
         ZC_CONV EB,Q,&N_SOURCE
               AWHEN C'Z'
                   ZC_PACK31  ZCVT_PWORK1,16,&N_SOURCE,&L_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
                   ZC_CONV EB,LD
               AELSE
                    MNOTE 8,'GEN_ADD INVALID SOURCE DATA TYPE &N_SOURCEX
               '           
                    MEXIT
               AEND 
         AEB   ZC_F0,&N_TARGET
         STE   ZC_F0,&N_TARGET
.*
.*  ADD_FLD ?? TO DB
.*
         AWHEN C'5'
               ACASE (C2A('&T_SOURCE'))
               AWHEN C'1'               ADD EH TO DB
         ZC_CONV DB,EH,&N_SOURCE
               AWHEN C'2'               ADD DH TO DB
         ZC_CONV DB,DH,&N_SOURCE
               AWHEN C'3'               ADD LH TO DB
         ZC_CONV DB,LH,&N_SOURCE
               AWHEN C'4'               ADD EB TO DB
         ZC_CONV DB,EB,&N_SOURCE
               AWHEN C'5'               ADD DB TO DB
         ZC_CONV DB,DB,&N_SOURCE
               AWHEN C'6'               ADD LB TO DB
         ZC_CONV DB,LB,&N_SOURCE
               AWHEN C'7'               ADD ED TO DB
         ZC_CONV DB,ED,&N_SOURCE
               AWHEN C'8'               ADD DD TO DB
         ZC_CONV DB,DD,&N_SOURCE
               AWHEN C'9'               ADD LB TO DB
         ZC_CONV DB,LD,&N_SOURCE
               AWHEN C'F',C'A',C'I'
         L     ZC_R0,&N_SOURCE
         CDFBR ZC_F0,ZC_R0
               AWHEN C'G'
         LG    ZC_R0,&N_SOURCE
         CDGBR ZC_F0,ZC_R0
               AWHEN C'H'
         LH    ZC_R0,&N_SOURCE
         CDFBR ZC_F0,ZC_R0
               AWHEN C'P'
         ZAP   ZCVT_PWORK1,&N_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
         ZC_CONV DB,LD
               AWHEN C'Q'  RPI 1004
         ZC_CONV DB,Q,&N_SOURCE
               AWHEN C'Z'
                   ZC_PACK31  ZCVT_PWORK1,16,&N_SOURCE,&L_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
                   ZC_CONV DB,LD
               AELSE
                    MNOTE 8,'GEN_ADD INVALID SOURCE DATA TYPE &N_SOURCEX
               '           
                    MEXIT
               AEND 
         ADB   ZC_F0,&N_TARGET
         STD   ZC_F0,&N_TARGET
.*
.*  ADD_FLD ?? TO LB
.*
         AWHEN C'6'
         LD    ZC_F1,&N_TARGET
         LD    ZC_F3,8+&N_TARGET
               ACASE (C2A('&T_SOURCE'))
               AWHEN C'1'               ADD EH TO LB
         ZC_CONV LB,EH,&N_SOURCE
               AWHEN C'2'               ADD DH TO LB
         ZC_CONV LB,DH,&N_SOURCE
               AWHEN C'3'               ADD LH TO LB
         ZC_CONV LB,LH,&N_SOURCE
               AWHEN C'4'               ADD EB TO LB  
         ZC_CONV LB,EB,&N_SOURCE
               AWHEN C'5'               ADD DB TO LB
         ZC_CONV LB,DB,&N_SOURCE
               AWHEN C'6'               ADD LB TO LB
         ZC_CONV LB,LB,&N_SOURCE
               AWHEN C'7'               ADD ED TO LB
         ZC_CONV LB,ED,&N_SOURCE
               AWHEN C'8'               ADD DD TO LB
         ZC_CONV LB,DD,&N_SOURCE
               AWHEN C'9'               ADD LB TO LB
         ZC_CONV LB,LD,&N_SOURCE
               AWHEN C'F',C'A',C'I'
         L     ZC_R0,&N_SOURCE
         CXFBR ZC_F0,ZC_R0
               AWHEN C'G'
         LG    ZC_R0,&N_SOURCE
         CXGBR ZC_F0,ZC_R0
               AWHEN C'H'
         LH    ZC_R0,&N_SOURCE
         CXFBR ZC_F0,ZC_R0
               AWHEN C'P'
         ZAP   ZCVT_PWORK1,&N_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
         ZC_CONV LB,LD
               AWHEN C'Q'  RPI 1004
         ZC_CONV LH,Q,&N_SOURCE
               AWHEN C'Z'
                   ZC_PACK31  ZCVT_PWORK1,16,&N_SOURCE,&L_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
                   ZC_CONV LB,LD
               AELSE
                    MNOTE 8,'GEN_ADD INVALID SOURCE DATA TYPE &N_SOURCEX
               '           
                    MEXIT
               AEND 
         AXBR  ZC_F0,ZC_F1
         STD   ZC_F0,&N_TARGET
         STD   ZC_F2,8+&N_TARGET
.*
.*  ADD_FLD ?? TO ED (FIRST TO DD, THEN ADD DD, THEN CVT TO ED)
.*
         AWHEN C'7'                     ADD ?? TO ED
               ACASE (C2A('&T_SOURCE'))
               AWHEN C'1'               ADD EH TO DD
         ZC_CONV DD,EH,&N_SOURCE
               AWHEN C'2'               ADD DH TO DD
         ZC_CONV DD,DH,&N_SOURCE
               AWHEN C'3'               ADD LH TO DD
         ZC_CONV DD,LH,&N_SOURCE
               AWHEN C'4'               ADD EB TO DD
         ZC_CONV DD,EB,&N_SOURCE
               AWHEN C'5'               ADD DB TO DD
         ZC_CONV DD,DB,&N_SOURCE
               AWHEN C'6'               ADD LB TO DD
         ZC_CONV DD,LB,&N_SOURCE
               AWHEN C'7'               ADD ED TO DD
         ZC_CONV DD,ED,&N_SOURCE
               AWHEN C'8'               ADD DD TO DD
         ZC_CONV DD,DD,&N_SOURCE
               AWHEN C'9'               ADD LB TO DD
         ZC_CONV DD,LD,&N_SOURCE
               AWHEN C'F',C'A',C'I'
         LGF   ZC_R0,&N_SOURCE
         CDGTR ZC_F0,ZC_R0
               AWHEN C'G'
         LG    ZC_R0,&N_SOURCE
         CDGTR ZC_F0,ZC_R0
               AWHEN C'H'
         LGH   ZC_R0,&N_SOURCE
         CDGTR ZC_F0,ZC_R0
               AWHEN C'P'
         ZAP   ZCVT_PWORK1,&N_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
         ZC_CONV DD,LD
               AWHEN C'Q'  RPI 1004
         ZC_CONV DD,Q,&N_SOURCE
               AWHEN C'Z'
                   ZC_PACK31  ZCVT_PWORK1,16,&N_SOURCE,&L_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
                   ZC_CONV DD,LD
               AELSE
                    MNOTE 8,'GEN_ADD INVALID SOURCE DATA TYPE &N_SOURCEX
               '           
                    MEXIT
               AEND 
         LE    ZC_F1,&N_TARGET
         LDETR ZC_F1,ZC_F1,0
         ADTR  ZC_F0,ZC_F0,ZC_F1
         LEDTR ZC_F0,0,ZC_F0,0
         STE   ZC_F0,&N_TARGET
.*
.*  ADD_FLD ?? TO DD
.*
         AWHEN C'8'
               ACASE (C2A('&T_SOURCE'))
               AWHEN C'1'               ADD EH TO DD
         ZC_CONV DD,EH,&N_SOURCE
               AWHEN C'2'               ADD DH TO DD
         ZC_CONV DD,DH,&N_SOURCE
               AWHEN C'3'               ADD LH TO DD
         ZC_CONV DD,LH,&N_SOURCE
               AWHEN C'4'               ADD EB TO DD
         ZC_CONV DD,EB,&N_SOURCE
               AWHEN C'5'               ADD DB TO DD
         ZC_CONV DD,DB,&N_SOURCE
               AWHEN C'6'               ADD LB TO DD
         ZC_CONV DD,LB,&N_SOURCE
               AWHEN C'7'               ADD ED TO DD
         ZC_CONV DD,ED,&N_SOURCE
               AWHEN C'8'               ADD DD TO DD
         ZC_CONV DD,DD,&N_SOURCE
               AWHEN C'9'               ADD LB TO DD
         ZC_CONV DD,LD,&N_SOURCE
               AWHEN C'F',C'A',C'I'
         LGF   ZC_R0,&N_SOURCE
         CDGTR ZC_F0,ZC_R0
               AWHEN C'G'
         LG    ZC_R0,&N_SOURCE
         CDGTR ZC_F0,ZC_R0
               AWHEN C'H'
         LGH   ZC_R0,&N_SOURCE
         CDGTR ZC_F0,ZC_R0
               AWHEN C'P'
         ZAP   ZCVT_PWORK1,&N_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
         ZC_CONV DD,LD
               AWHEN C'Q'  RPI 1004
         ZC_CONV DD,Q,&N_SOURCE
               AWHEN C'Z'
                   ZC_PACK31  ZCVT_PWORK1,16,&N_SOURCE,&L_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
                   ZC_CONV DD,LD
               AELSE
                    MNOTE 8,'GEN_ADD INVALID SOURCE DATA TYPE &N_SOURCEX
               '           
                    MEXIT
               AEND 
         LD    ZC_F1,&N_TARGET
         ADTR  ZC_F0,ZC_F0,ZC_F1                
         STD   ZC_F0,&N_TARGET
.*
.*  ADD_FLD ?? TO LD
.*
         AWHEN C'9'
               ACASE (C2A('&T_SOURCE'))
               AWHEN C'1'               ADD EH TO LD
         ZC_CONV LD,EH,&N_SOURCE
               AWHEN C'2'               ADD DH TO LD
         ZC_CONV LD,DH,&N_SOURCE
               AWHEN C'3'               ADD LH TO LD
         ZC_CONV LD,LH,&N_SOURCE
               AWHEN C'4'               ADD EB TO LD
         ZC_CONV LD,EB,&N_SOURCE
               AWHEN C'5'               ADD DB TO LD
         ZC_CONV LD,DB,&N_SOURCE
               AWHEN C'6'               ADD LB TO LD
         ZC_CONV LD,LB,&N_SOURCE
               AWHEN C'7'               ADD ED TO LD
         ZC_CONV LD,ED,&N_SOURCE
               AWHEN C'8'               ADD DD TO LD
         ZC_CONV LD,DD,&N_SOURCE
               AWHEN C'9'               ADD LB TO LD
         ZC_CONV LD,LD,&N_SOURCE
               AWHEN C'F',C'A',C'I'
         LGF   ZC_R0,&N_SOURCE
         CXGTR ZC_F0,ZC_R0
               AWHEN C'G'
         LG    ZC_R0,&N_SOURCE
         CXGTR ZC_F0,ZC_R0
               AWHEN C'H'
         LGH   ZC_R0,&N_SOURCE
         CXGTR ZC_F0,ZC_R0
               AWHEN C'P'
         ZAP   ZCVT_PWORK1,&N_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F0,ZC_R0
               AWHEN C'Q'  RPI 1004
         ZC_CONV LD,Q,&N_SOURCE
               AWHEN C'Z'
                   ZC_PACK31  ZCVT_PWORK1,16,&N_SOURCE,&L_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F0,ZC_R0
               AELSE
                    MNOTE 8,'GEN_ADD INVALID SOURCE DATA TYPE &N_SOURCEX
               '           
                    MEXIT
               AEND 
         LD    ZC_F1,&N_TARGET
         LD    ZC_F3,8+&N_TARGET
         AXTR  ZC_F0,ZC_F0,ZC_F1
         STD   ZC_F0,&N_TARGET
         STD   ZC_F2,8+&N_TARGET
.*
.*  ADD_FLD ?? TO F/A/I
.*
         AWHEN C'F',C'A',C'I'
               ACASE (C2A('&T_SOURCE'))
               AWHEN C'1'               ADD EH TO DH
         LD    ZC_F0,&N_TARGET
         LE    ZC_F1,&N_SOURCE
         LDER  ZC_F1,ZC_F1
         ADR   ZC_F0,ZC_F1
               AWHEN C'2'               ADD DH TO DH
         LD    ZC_F0,&N_TARGET
         AD    ZC_F0,&N_SOURCE
               AWHEN C'3'               ADD LH TO DH
         LD    ZC_F1,&N_TARGET
         LD    ZC_F3,8+&N_TARGET
         LDXR  ZC_F1,ZC_F1
         ADR   ZC_F0,ZC_F1
               AWHEN C'4'               ADD EB TO DH
         LD    ZC_F1,&N_TARGET
         ZC_CONV DH,EB,&N_SOURCE
         ADR   ZC_F0,ZC_F1
               AWHEN C'5'               ADD DB TO DH
         LD    ZC_F1,&N_TARGET
         ZC_CONV DH,DB,&N_SOURCE
         ADR   ZC_F0,ZC_F1
               AWHEN C'6'               ADD LB TO DH
         LD    ZC_F1,&N_TARGET
         ZC_CONV DH,LB,&N_SOURCE
         ADR   ZC_F0,ZC_F1
               AWHEN C'7'               ADD ED TO DH
         LD    ZC_F1,&N_TARGET
         ZC_CONV DH,ED,&N_SOURCE
         ADR   ZC_F0,ZC_F1
               AWHEN C'8'               ADD DD TO DH
         LD    ZC_F1,&N_TARGET
         ZC_CONV DH,DD,&N_SOURCE
         ADR   ZC_F0,ZC_F1
               AWHEN C'9'               ADD LB TO DH
         LD    ZC_F1,&N_TARGET
         ZC_CONV DH,LD,&N_SOURCE
         ADR   ZC_F0,ZC_F1
               AWHEN C'2'
         L     ZC_R0,&N_TARGET
         LD    ZC_F1,&N_SOURCE
         CFDR  ZC_R3,0,ZC_F1
         AR    ZC_R0,ZC_R3
         ST    ZC_R0,&N_TARGET
               AWHEN C'1'
         L     ZC_R0,&N_TARGET
         LE    ZC_F1,&N_SOURCE
         CFER  ZC_R3,0,ZC_F1
         AR    ZC_R0,ZC_R3
         ST    ZC_R0,&N_TARGET
               AWHEN C'F',C'A',C'I'
         L     ZC_R0,&N_TARGET
         A     ZC_R0,&N_SOURCE
         ST    ZC_R0,&N_TARGET
               AWHEN C'G'
         L     ZC_R0,&N_TARGET
         A     ZC_R0,4+&N_SOURCE IGNORE HIGH 32 BITS
         ST    ZC_R0,&N_TARGET  
               AWHEN C'H'
         L     ZC_R0,&N_TARGET
         AH    ZC_R0,&N_SOURCE
         ST    ZC_R0,&N_TARGET
               AWHEN C'P'
         ZAP   ZCVT_PWORK1,&N_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
         ZC_CONV Q,LD
         A     ZC_R1,&N_TARGET IGNORE HIGH 96 BITS
         ST    ZC_R1,&N_TARGET
               AWHEN C'Q'
         L     ZC_R0,&N_TARGET
         A     ZC_R0,12+&N_SOURCE IGNORE HIGH 48 BITS
         ST    ZC_R0,&N_TARGET  
               AWHEN C'Z'
                   ZC_PACK31  ZCVT_PWORK1,16,&N_SOURCE,&L_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
                   ZC_CONV Q,LD
         A     ZC_R1,&N_TARGET IGNORE HIGH 96 BITS
         ST    ZC_R1,&N_TARGET 
               AELSE
                    MNOTE 8,'GEN_ADD INVALID SOURCE DATA TYPE &N_SOURCEX
               '           
                    MEXIT
               AEND 
.*
.*  ADD_FLD ?? TO G    
.*
         AWHEN C'G'
               ACASE (C2A('&T_SOURCE'))
               AWHEN C'2'
         LG    ZC_R0,&N_TARGET
         LD    ZC_F1,&N_SOURCE
         CGDR  ZC_R3,0,ZC_F1
         AGR   ZC_R0,ZC_R3
         STG   ZC_R0,&N_TARGET
               AWHEN C'1'
         LG    ZC_R0,&N_TARGET
         LE    ZC_F1,&N_SOURCE
         CGER  ZC_R3,0,ZC_F1
         AGR   ZC_R0,ZC_R3
         STG   ZC_R0,&N_TARGET
               AWHEN C'F',C'A',C'I'
         LG    ZC_R0,&N_TARGET
         AGF   ZC_R0,&N_SOURCE
         STG   ZC_R0,&N_TARGET
               AWHEN C'G'
         LG    ZC_R0,&N_TARGET
         AG    ZC_R0,&N_SOURCE
         STG   ZC_R0,&N_TARGET                          
               AWHEN C'H'
         LG    ZC_R0,&N_TARGET
         LH    ZC_R3,&N_SOURCE
         AGFR  ZC_R0,ZC_R3
         STG   ZC_R0,&N_TARGET
               AWHEN C'P'
         ZAP   ZCVT_PWORK1,&N_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
         ZC_CONV Q,LD
         AG    ZC_R1,&N_TARGET
         STG   ZC_R1,&N_TARGET
               AWHEN C'Q'
         LG    ZC_R0,&N_TARGET
         AG    ZC_R0,8+&N_SOURCE IGNORE HIGH 64 BITS
         STG   ZC_R0,&N_TARGET  
               AWHEN C'Z'
                   ZC_PACK31  ZCVT_PWORK1,16,&N_SOURCE,&L_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
                   ZC_CONV Q,LD
         AG    ZC_R1,&N_TARGET  IGNORE HIGH 64
         STG   ZC_R1,&N_TARGET
               AELSE
                    MNOTE 8,'GEN_ADD INVALID SOURCE DATA TYPE &N_SOURCEX
               '           
                    MEXIT
               AEND 
.*
.*  ADD_FLD ?? TO H    
.*
         AWHEN C'H'
               ACASE (C2A('&T_SOURCE'))
               AWHEN C'2'
         LH    ZC_R0,&N_TARGET
         LD    ZC_F3,&N_SOURCE
         CFDR  ZC_R1,0,ZC_F3
         AR    ZC_R0,ZC_R1
         STH   ZC_R0,&N_TARGET
               AWHEN C'1'
         LH    ZC_R0,&N_TARGET
         LE    ZC_F1,&N_SOURCE
         CFER  ZC_R3,0,ZC_F1
         AR    ZC_R0,ZC_R3
         STH   ZC_R0,&N_TARGET
               AWHEN C'F',C'A',C'I'
         LH    ZC_R0,&N_TARGET
         A     ZC_R0,&N_SOURCE
         STH   ZC_R0,&N_TARGET
               AWHEN C'G'
         LH    ZC_R0,&N_TARGET
         A     ZC_R0,&N_SOURCE+4 IGNORES HIGH 32
         STH   ZC_R0,&N_TARGET
               AWHEN C'H'
         LH    ZC_R0,&N_TARGET
         AH    ZC_R0,&N_SOURCE
         STH   ZC_R0,&N_TARGET
               AWHEN C'P'
         ZAP   ZCVT_PWORK1,&N_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
         ZC_CONV Q,LD
         AH    ZC_R1,&N_TARGET  IGNORE HIGH 96 BITS
         STH   ZC_R1,&N_TARGET
               AWHEN C'Z'
                   ZC_PACK31  ZCVT_PWORK1,16,&N_SOURCE,&L_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
                   ZC_CONV Q,LD
         AH    ZC_R1,&N_TARGET  IGNORE HIGH 96
         STH   ZC_R1,&N_TARGET  IGNORE HIGH 112
               AELSE
                    MNOTE 8,'GEN_ADD INVALID SOURCE DATA TYPE &N_SOURCEX
               '           
                    MEXIT
               AEND 
.*
.*  ADD_FLD ?? TO P    
.*
         AWHEN C'P'
               ACASE (C2A('&T_SOURCE'))
               AWHEN C'1'
         LE    ZC_F0,&N_SOURCE
         CFER  ZC_R0,0,ZC_F0
         CVD   ZC_R0,ZCVT_PWORK
         AP    &N_TARGET,ZCVT_PWORK
               AWHEN C'2'
         LD    ZC_F0,&N_SOURCE
         CFDR  ZC_R0,0,ZC_F0
         CVD   ZC_R0,ZCVT_PWORK
         AP    &N_TARGET,ZCVT_PWORK
               AWHEN C'3'               ADD LH TO DH
         ZC_CONV P,LH,&N_SOURCE
         STMG  ZC_R0,ZC_R1,ZCVT_PWORK2
         AP    &N_TARGET,ZCVT_PWORK2
               AWHEN C'4'               ADD EB TO DH
         ZC_CONV P,EB,&N_SOURCE
         STMG  ZC_R0,ZC_R1,ZCVT_PWORK2
         AP    &N_TARGET,ZCVT_PWORK2
               AWHEN C'5'               ADD DB TO DH
         ZC_CONV P,DB,&N_SOURCE
         STMG  ZC_R0,ZC_R1,ZCVT_PWORK2
         AP    &N_TARGET,ZCVT_PWORK2
               AWHEN C'6'               ADD LB TO DH
         ZC_CONV P,LB,&N_SOURCE
         STMG  ZC_R0,ZC_R1,ZCVT_PWORK2
         AP    &N_TARGET,ZCVT_PWORK2
               AWHEN C'7'               ADD ED TO DH
         ZC_CONV P,ED,&N_SOURCE
         STMG  ZC_R0,ZC_R1,ZCVT_PWORK2
         AP    &N_TARGET,ZCVT_PWORK2
               AWHEN C'8'               ADD DD TO DH
         ZC_CONV P,DD,&N_SOURCE
         STMG  ZC_R0,ZC_R1,ZCVT_PWORK2
         AP    &N_TARGET,ZCVT_PWORK2
               AWHEN C'9'               ADD LB TO DH
         ZC_CONV P,LD,&N_SOURCE
         STMG  ZC_R0,ZC_R1,ZCVT_PWORK2
         AP    &N_TARGET,ZCVT_PWORK2
               AWHEN C'F',C'A',C'I'
         L     ZC_R0,&N_SOURCE
         CVD   ZC_R0,ZCVT_PWORK
         AP    &N_TARGET,ZCVT_PWORK
               AWHEN C'G'
         LG    ZC_R0,&N_SOURCE
         CVDG  ZC_R0,ZCVT_PWORK1
         AP    &N_TARGET,ZCVT_PWORK1
               AWHEN C'H'
         LH    ZC_R0,&N_SOURCE
         CVD   ZC_R0,ZCVT_PWORK
         AP    &N_TARGET,ZCVT_PWORK
               AWHEN C'P'
         AP    &N_TARGET,&N_SOURCE
               AWHEN C'Q'
         ZC_CONV P,Q,&N_SOURCE
         STMG  ZC_R0,ZC_R1,ZCVT_PWORK1
         AP    &N_TARGET,ZCVT_PWORK1         
               AWHEN C'Z'
                   :&PLEN SETA &L_SOURCE/2+1
                   ZC_PACK31  ZCVT_PWORK1,&PLEN,&N_SOURCE,&L_SOURCE
         AP    &N_TARGET,ZCVT_PWORK1(&PLEN)
               AELSE
                    MNOTE 8,'GEN_ADD INVALID SOURCE DATA TYPE &N_SOURCEX
               '           
                    MEXIT
               AEND 
.*
.* ADD FLD ?? TO Q
.*
         AWHEN C'Q'
               ACASE (C2A('&T_SOURCE'))
               AWHEN C'1'
         LMG   ZC_R0,ZC_R1,&N_TARGET
         LE    ZC_F1,&N_SOURCE
         CGER  ZC_R3,0,ZC_F1
         AGR   ZC_R1,ZC_R3
         JNO   *+4+6
         AGI   ZC_R0,1
               AWHEN C'2'
         LMG   ZC_R0,ZC_R1,&N_TARGET
         LD    ZC_F1,&N_SOURCE
         CGDR  ZC_R3,0,ZC_F1
         AGR   ZC_R0,ZC_R3
               AWHEN C'F',C'A',C'I'
         LMG   ZC_R0,ZC_R1,&N_TARGET
         AGF   ZC_R1,&N_SOURCE
         JNO   *+4+6
         AGFI  ZC_R0,1
               AWHEN C'G'
         LMG   ZC_R0,ZC_R1,&N_TARGET
         AG    ZC_R1,&N_SOURCE
         JNO   *+4+6
         AGFI  ZC_R0,1
               AWHEN C'H'
         LMG   ZC_R0,ZC_R1,&N_TARGET
         LH    ZC_R3,&N_SOURCE
         AGFR  ZC_R1,ZC_R3
         JNO   *+4+6
         AGFI  ZC_R0,1
               AWHEN C'P'
         ZAP   ZCVT_PWORK1,&N_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
               ZC_CONV Q,LD
         ALG   ZC_R1,8+&N_TARGET
         ALCG  ZC_R0,&N_TARGET
               AWHEN C'Q'
         LMG   ZC_R0,ZC_R1,&N_TARGET
         ALG   ZC_R1,8+&N_SOURCE                    
         ALCG  ZC_R0,&N_SOURCE                     
               AWHEN C'Z'
                   ZC_PACK31  ZCVT_PWORK1,16,&N_SOURCE,&L_SOURCE
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK1
         CXSTR ZC_F4,ZC_R0
               ZC_CONV Q,LD
         ALG   ZC_R1,8+&N_TARGET
         ALCG  ZC_R0,&N_TARGET
               AELSE
                    MNOTE 8,'GEN_ADD INVALID SOURCE DATA TYPE &N_SOURCEX
               '           
                    MEXIT
               AEND 
         STMG  ZC_R0,ZC_R1,&N_TARGET
.*
.*  ADD_FLD Z    
.*
         AWHEN C'Z'
               ACASE (C2A('&T_SOURCE'))
               AWHEN C'2'
                     :&PLEN SETA &L_TARGET/2+1
                     ZC_PACK31 ZCVT_PWORK1,&PLEN,&N_TARGET,&L_TARGET
         LD    ZC_F0,&N_SOURCE
         CFDR  ZC_R0,0,ZC_F0
         CVD   ZC_R0,ZCVT_PWORK
         AP    ZCVT_PWORK1(&PLEN),ZCVT_PWORK
                     :&ST SETB &S_TARGET
                 ZC_UNPK31 &N_TARGET,&L_TARGET,&ST,ZCVT_PWORK1,&PLEN
               AWHEN C'1'
                     :&PLEN SETA &L_TARGET/2+1
                     ZC_PACK31 ZCVT_PWORK1,&PLEN,&N_TARGET,&L_TARGET
         LE    ZC_F0,&N_SOURCE
         CFER  ZC_R0,0,ZC_F0
         CVD   ZC_R0,ZCVT_PWORK
         AP    ZCVT_PWORK1(&PLEN),ZCVT_PWORK
                     :&ST SETB &S_TARGET
                 ZC_UNPK31 &N_TARGET,&L_TARGET,&ST,ZCVT_PWORK1,&PLEN
               AWHEN C'F',C'A',C'I'
                     :&PLEN SETA &L_TARGET/2+1
                     ZC_PACK31 ZCVT_PWORK1,&PLEN,&N_TARGET,&L_TARGET
         L     ZC_R0,&N_SOURCE
         CVD   ZC_R0,ZCVT_PWORK
         AP    ZCVT_PWORK1(&PLEN),ZCVT_PWORK
                     :&ST SETB &S_TARGET
                 ZC_UNPK31 &N_TARGET,&L_TARGET,&ST,ZCVT_PWORK1,&PLEN
               AWHEN C'G'
                     :&PLEN SETA &L_TARGET/2+1
                     ZC_PACK31 ZCVT_PWORK1,&PLEN,&N_TARGET,&L_TARGET
         LG    ZC_R0,&N_SOURCE
         CVDG  ZC_R0,ZCVT_PWORK2
         AP    ZCVT_PWORK1(&PLEN),ZCVT_PWORK
                     :&ST SETB &S_TARGET
                 ZC_UNPK31 &N_TARGET,&L_TARGET,&ST,ZCVT_PWORK1,&PLEN
               AWHEN C'H'
                     :&PLEN SETA &L_TARGET/2+1
                     ZC_PACK31 ZCVT_PWORK1,&PLEN,&N_TARGET,&L_TARGET
         LH    ZC_R0,&N_SOURCE
         CVD   ZC_R0,ZCVT_PWORK
         AP    ZCVT_PWORK1(&PLEN),ZCVT_PWORK
                     :&ST SETB &S_TARGET
                 ZC_UNPK31 &N_TARGET,&L_TARGET,&ST,ZCVT_PWORK1,&PLEN
               AWHEN C'P'
                     :&PLEN SETA &L_TARGET/2+1
                     ZC_PACK31 ZCVT_PWORK1,&PLEN,&N_TARGET,&L_TARGET
         AP    ZCVT_PWORK1(&PLEN),&N_SOURCE
                     :&ST SETB &S_TARGET
                 ZC_UNPK31 &N_TARGET,&L_TARGET,&ST,ZCVT_PWORK1,&PLEN
               AWHEN C'Q'
                     ZC_PACK31 ZCVT_PWORK1,16,&N_TARGET,&L_TARGET
         ZC_CONV P,Q,&N_SOURCE
         STMG  ZC_R0,ZC_R1,ZCVT_PWORK2
         AP    ZCVT_PWORK1,ZCVT_PWORK2
                     :&ST SETB &S_TARGET
                 ZC_UNPK31 &N_TARGET,&L_TARGET,&ST,ZCVT_PWORK1,16
               AWHEN C'Z'
                     :&PLEN1 SETA &L_TARGET/2+1
                     :&PLEN2 SETA &L_SOURCE/2+1
                   ZC_PACK31 ZCVT_PWORK1,&PLEN1,&N_TARGET,&L_TARGET
                   ZC_PACK31 ZCVT_PWORK2,&PLEN2,&N_SOURCE,&L_SOURCE
         AP    ZCVT_PWORK1(&PLEN1),ZCVT_PWORK2(&PLEN2)   
                     :&ST SETB &S_TARGET
                ZC_UNPK31 &N_TARGET,&L_TARGET,&ST,ZCVT_PWORK1,&PLEN1
               AELSE
                    MNOTE 8,'GEN_ADD INVALID SOURCE DATA TYPE &N_SOURCEX
               '           
                    MEXIT
               AEND 
         AELSE
                    MNOTE 8,'GEN_ADD INVALID SOURCE DATA TYPE &N_SOURCEX
               '           
               MEXIT
         AEND
         AEND
.*
.* SET ON SIZE ERROR USING G64 WITH PENDING END_ADD OR PERIOD
,*
         AENTRY SET_ON_SIZE_G64
         JO    PG_ADD_&ADD_LAB._ON_SIZE    ERROR IF OVERFLOW
         LPGR  ZC_R15,ZC_R1
         CGR   ZC_R15,ZC_R0                COMPARE ABS RESULT TO MAX
         JNL   PG_ADD_&ADD_LAB._ON_SIZE    ERROR IF TOO BIG
         &STX   ZC_R1,&N_TARGET            STORE RESULT
         AEND
.*
.* SET ON SIZE ERROR USING ZC_F32 WITH PENDING END_ADD OR PERIOD
,*
         AENTRY SET_ON_SIZE_F32
         JO    PG_ADD_&ADD_LAB._ON_SIZE    ERROR IF OVERFLOW
         LPR   ZC_R15,ZC_R1
         C     ZC_R15,0(ZC_R14)           COMPARE ABS RESULT TO MAX
         JNL   PG_ADD_&ADD_LAB._ON_SIZE   ERROR IF TOO BIG
         &STX  ZC_R1,&N_TARGET            STORE RESULT
         AEND
.*
.* SET ON SIZE ERROR USING G64_R32 WITH PENDING END_ADD OR PERIOD
,*
         AENTRY SET_ON_SIZE_G64_R32
         JO    PG_ADD_&ADD_LAB._ON_SIZE    ERROR IF OVERFLOW SET 
         STM   ZC_R0,ZC_R1,&N_TARGET
         AEND
         COPY ZCRSTLAB
         MEND
