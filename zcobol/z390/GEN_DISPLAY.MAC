         MACRO
.*********************************************************************
.* Copyright 2008 Automated Software Tools Corporation               *
.* This source code is part of z390 zCOBOL/assembler/emulator package*
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 04/07/08                                                 *
.*********************************************************************
.* 04/11/08 initial coding for z390 DISPLAY 'text' or label
.* 10/07/08 ZSTRMAC
.*********************************************************************
.* 11/19/08 add support for multiple field types using DISPLAY routine
.* 03/06/09 RPI 1004 add DEL DFP/BFP/HFP support                      
.* 03/10/09 RPI 1013 remove dependency on ZC_FLOAT using types 1-9        
.*********************************************************************
         GEN_DISPLAY 
         COPY  ZC_WS
         LA    R3,ZCVT_WORKAREA  R3=CVT LIST WORK AREA
         :&NP  SETA N'&SYSLIST
         :&PARM_IX SETA 1
         :&OFF     SETA 0
         AWHILE (&PARM_IX LE &NP)
               :&FIELD SETC '&SYSLIST(&PARM_IX)'
               AIF ('&FIELD'(1,1) EQ '''')  LITERAL TEXT
                   :&LEN   SETA K'&FIELD-2
                   :&LITL  SETA (&LEN+1)/2*2
         LARL  R0,=CL&LITL&FIELD PAD TO EVEN LEN LIT
         LA    R1,&LEN
         LA    R2,C'X'
         STM   R0,R2,&OFF.(R3)  SET DISPLAY LIST ENTRY              
               AELSE
                    :&FIELD_REG SETC 'R2'
                    :&FIELD_SS1 SETB 0
                    ACALL GET_PARM_FIELD
                    AIF (&FIELD_IX EQ 0)
                        MNOTE 8,'GEN_DISPLAY FIELD NOT FOUND &FIELD_NAMX
               E'
                        MEXIT
                    AEND
         LA    R0,&FIELD_NAME
         LA    R1,&SYM_LEN(&FIELD_IX)
         LA    R2,C'&SYM_PIC_TYPE(&FIELD_IX)'
         STM   R0,R2,&OFF.(R3)  SET DISPLAY LIST ENTRY              
               AEND
               :&PARM_IX SETA &PARM_IX+1
               :&OFF SETA &OFF+12
         AEND       
         OI    &OFF-4(R3),X'80' SET VL BIT IN LAST WORD OF PARM LIST
         LR    R1,R3
         L     R15,ZCVT_DISPLAY
         BASR  R14,R15
         MEXIT
         COPY  ZCGETFLD
         MEND
