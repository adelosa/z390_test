         TITLE 'RTGENBAT - REGRESSION TEST COMPARISON BAT GENERATOR'
*********************************************************************
* Copyright 2006 Automated Software Tools Corporation               *
* This source code is part of z390 assembler/emulator package       *
* The z390 package is distributed under GNU general public license  *
* Author - Don Higgins                                              *
* Date   - 08/12/06                                                 *
*********************************************************************
* 08/12/06 RPI 363 REPACKAGE MVS_MACLIB.ZIP AND Z390_RT.ZIP RT TESTS
*********************************************************************
*
*  RTGENBAT READS FILE &SYSPARM.DAT AND GENERATES &SYSPARM.PCH
*
*  The input txt file is sorted directory list of regression TEST
*  source and generated files directory plus SAVE directory.  There
*  should be 2 duplicate entries of each file to be compared.  Any
*  file which does not have match will generate bat pause error msg.
*  Matching files will generate fc %1file %2file > %3file. 
*
*********************************************************************
&LAST_FILE SETC '(FILE EMPTY)' PREVIOUS FILE
&BREAK   SETB  1 STARTING NEW MATCH
&TOT_COMP SETA 0
&TOT_ERR  SETA 0
         ACTR 1000000
.LOOP    ANOP
&NEXT_FILE AREAD DSNAME='&SYSPARM'  
         AIF (&NEXT_FILE NE &LAST_FILE).NEW_FILE 
         AIF (&BREAK EQ 0).OK1 OK TO COMPARE MATCHING FILES
&TOT_ERR SETA &TOT_ERR+1
         PUNCH 'PAUSE MORE THAN 2 FILES FOR %1\&NEXT_FILE',            X
               DSNAME='&SYSPARM'
         MNOTE 8,'RTGENBAT MORE THAN 2 DUP FILES FOR &NEXT_FILE'
         AGO   .LOOP
.OK1     ANOP
&TOT_COMP SETA &TOT_COMP+1
         PUNCH 'FC %1\&NEXT_FILE %2\&NEXT_FILE > %3\&NEXT_FILE',       X
               DSNAME='&SYSPARM'
&BREAK   SETB  1         
         AGO   .LOOP
.NEW_FILE ANOP
         AIF   (&BREAK EQ 1).OK2 OK TO CHECK FOR EOF OR NEW FILE
&TOT_ERR SETA &TOT_ERR+1
         PUNCH 'PAUSE MISSING FILE FOR %1 OR %2 &LAST_FILE',           X
               DSNAME='&SYSPARM'
         MNOTE 8,'RTGENBAT MISSING MATCHING FILE FOR &LAST_FILE'
.OK2     ANOP
         AIF   (&NEXT_FILE EQ '').EOF
&LAST_FILE SETC &NEXT_FILE       
&BREAK   SETB  0
         AGO   .LOOP
.EOF     ANOP
         PUNCH 'REM TOT COMPARES = &TOT_COMP  TOT ERRORS =&TOT_ERR',   X
               DSNAME='&SYSPARM'
         AIF   (&TOT_ERR EQ 0).EXIT
         MNOTE 8,'RTGENCOM FILE ERRORS FOUND - SEE RTCOMP4.BAT'
         PUNCH 'COPY RTGENCMP.BAT %3',                                 X
               DSNAME='&SYSPARM'
.EXIT    ANOP
         END
