         MACRO                                                          00001000
         IGZPRMOD &TYPE=ENTRY,                                         X00002000
               &DEVICE=,                                               X00003000
               &IOAREA2=,                                              X00004000
               &RECFORM=,                                              X00005000
               &CTLCHR=,                                               X00006000
               &FUNC=                                                   00007000
.********************************************************************** 00008000
.*                                                                      00009000
         GBLB  &STTFND,&ENTFND,&FINFND                                  00010000
         LCLB  &ERROR,&NOMOD,&NOIOA2                                    00011000
         LCLC  &DEVNUM,&RECFRM                                          00012000
         LCLC  &MOD4,&MOD5,&MOD6,&MOD7,&MOD8                            00013000
         LCLC  &WORK,&NAME                                              00014000
.*                                                                      00015000
.********************************************************************** 00016000
         AGO   .CHKTYP                                                  00017000
.* ******************************************************************** 00018000
.*                                                                    * 00019000
.*   5686-094  (C) COPYRIGHT IBM CORP. 1991, 1996.                    * 00020000
.*   ALL RIGHTS RESERVED                                              * 00021000
.*   US GOVERNMENT USERS RESTRICTED RIGHTS - USE,                     * 00022000
.*   DUPLICATION OR DISCLOSURE RESTRICTED BY GSA                      * 00023000
.*   ADP SCHEDULE CONTRACT WITH IBM CORP.                             * 00024000
.*                                                                    * 00025000
.* ******************************************************************** 00026000
.*                                                                      00027000
.*  MACRO NAME: IGZPRMOD                                                00028000
.*                                                                      00029000
.*  DESCRIPTIVE NAME: GENERATE VSE LIOCS PRINTER LOGIC MODULES          00030000
.*                                                                      00031000
.*  STATUS: RELEASE 1.0                                                 00032000
.*                                                                      00033000
.*                                                                      00034000
.*  FUNCTION: THIS MACRO GENERATES VSE LIOCS LOGIC MODULES FOR          00035000
.*            PRINTER DEVICES.  THE MACRO IS ASSEMBLED, IN THE          00036000
.*            CUSTOMER'S INSTALLATION-TIME PROCEDURE, TO BUILD HIS      00037000
.*            INSTALLATION REQUIRED RUN-TIME LIOCS LOGIC MODULES.       00038000
.*            THE RESULTANT OBJECT DECK IS LINK EDITED AND GIVEN        00039000
.*            THE PHASE NAME IGZEQP0.  THE PHASE CONTAINS, IN           00040000
.*            ADDITION TO THE LOGIC MODULES:                            00041000
.*                                                                      00042000
.*            -  A TABLE CONTAINING THE NAME AND ADDRESS OF EACH LOGIC  00043000
.*               MODULE (CSECT) GENERATED, AND                          00044000
.*            -  CODE TO SEARCH THE TABLE FOR A MATCH ON LOGIC MODULE   00045000
.*               NAME AND RETURN THE ADDRESS OF THE LOGIC MODULE.       00046000
.*                                                                      00047000
.*                                                                      00048000
.*            THE MACRO IS USED AS A 'FRONT-END' TO THE VSE LIOCS       00049000
.*            MACRO, PRMOD.  NOT ALL PARAMETERS USED BY PRMOD ARE       00050000
.*            USABLE BY IGZPRMOD.  THE PRMOD PROTOTYPE STATEMENT        00051000
.*            INDICATES THE IBM-SUPPLIED DEFAULTS FOR THESE             00052000
.*            PARAMETERS.  THEY MAY BE SELECTIVELY OVER-RIDDEN AT       00053000
.*            INSTALLATION TIME.                                        00054000
.*                                                                      00055000
.*            THE FORMAT OF THE MACRO IS:                               00056000
.*                                                                      00057000
.*            IGZPRMOD    TYPE={START|ENTRY|FINAL}                      00058000
.*                           <,CTLCHR={YES|ASA}>                        00059000
.*                           <,DEVICE=XXXXX>                            00060000
.*                           <,FUNC=XXX>                                00061000
.*                           <,IOAREA2=YES>                             00062000
.*                           <,RECFORM={FIXUNB|VARUNB|UNDEF}>           00063000
.*                                                                      00064000
.*            BEFORE THE VSE LIOCS MACRO, PRMOD, IS INVOKED             00065000
.*            THE FOLLOWING MACRO PARAMETER VALIDATION TAKES PLACE.     00066000
.*                                                                      00067000
.*            1. THE TYPE PARAMETER IS VALIDATED.  IF INVALID, A        00068000
.*               MESSAGE IS ISSUED AND MACRO GENERATION TERMINATED.     00069000
.*            2. TYPE=START MUST BE SPECIFIED ON THE FIRST MACRO        00070000
.*               AND MAY ONLY BE USED ONCE.  IF INVALID, A MESSAGE IS   00071000
.*               ISSUED AND MACRO GENERATION TERMINATED.                00072000
.*            2. TYPE=FINAL MUST BE SPECIFIED ON THE LAST MACRO         00073000
.*               AND MAY ONLY BE USED ONCE.  IF INVALID, A MESSAGE IS   00074000
.*               ISSUED AND MACRO GENERATION TERMINATED.                00075000
.*            3. TYPE=ENTRY MUST BE PRECEDED BY TYPE=START.  IF         00076000
.*               NO TYPE=START HAS BEEN SPECIFIED AN ERROR IS ISSUED    00077000
.*               AND MACRO GENERATION TERMINATED.                       00078000
.*            4. TYPE=FINAL MUST BE PRECEDED BY TYPE=START.  IF         00079000
.*               NO TYPE=START HAS BEEN SPECIFIED AN ERROR IS ISSUED    00080000
.*               AND MACRO GENERATION TERMINATED.                       00081000
.*                                                                      00082000
.*            HAVING VALIDATED THE TYPE PARAMETER, THE PRMOD            00083000
.*            PARAMETERS ARE CHECKED AND A LOGIC MODULE NAME GENERATED  00084000
.*            ACCORDINGLY.  AS PRMOD WILL VALIDATE THE PARAMETERS NO    00085000
.*            ERROR MESSAGES ARE PRODUCED IF INVALID PARAMETERS ARE     00086000
.*            DETECTED, BUT A VALID LOGIC MODULE NAME WILL NOT BE       00087000
.*            GENERATED.                                                00088000
.*                                                                      00089000
.*            A RESTRICTION WHICH IS NOT DETECTED BY PRMOD IS           00090000
.*            THAT IOAREA2=YES SHOULD NOT BE SPECIFIED FOR MULTI-       00091000
.*            FUNCTION CARD MACHINES (3525) WHEN A MULTI-FUNCTION       00092000
.*            FUNC PARAMETER IS SPECIFIED.  IF THIS PARAMETER           00093000
.*            COMBINATION IS DETECTED THE IOAREA2 PARAMETER IS          00094000
.*            IGNORED WHEN PRMOD IS INVOKED, AND AN INFORMATION         00095000
.*            MESSAGE ISSUED.                                           00096000
.*                                                                      00097000
.*            THE EXPANSION OF THE MACRO PROCEEDS IN THE FOLLOWING      00098000
.*            MANNER.                                                   00099000
.*                                                                      00100000
.*            IF TYPE=START IS SPECIFIED, THE CSECT IGZEQP0 IS          00101000
.*            GENERATED WITH CODE TO SEARCH THE TABLE OF LOGIC          00102000
.*            MODULE NAMES FOR A MATCH ON THE PARAMETER POINTED         00103000
.*            TO BY REGISTER 1.  IF FOUND, THE ADDRESS OF THE           00104000
.*            LOGIC MODULE IS RETURNED IS REGISTER 1.  IF NOT           00105000
.*            FOUND A NON-ZERO RETURN CODE IS RETURNED IN               00106000
.*            REGISTER 15.                                              00107000
.*                                                                      00108000
.*            IF TYPE=ENTRY IS SPECIFIED A C TYPE CONSTANT              00109000
.*            CONTAINING THE NAME OF THE LOGIC MODULE IS GENERATED.     00110000
.*            THE NAME OF THE LOGIC MODULE IS DETERMINED FROM THE       00111000
.*            VALUES AND COMBINATION OF THE PARAMETERS.  IF             00112000
.*            A VALID LOGIC MODULE NAME IS GENERATED A V TYPE           00113000
.*            CONSTANT IS GENERATED WHICH WILL CONTAIN THE ADDRESS      00114000
.*            OF THE LOGIC MODULE AFTER LINKAGE EDITING.                00115000
.*                                                                      00116000
.*            IF TYPE=FINAL IS SPECIFIED AN X TYPE CONSTANT             00117000
.*            CONTAINING HIGH VALUES IS GENERATED TO INDICATED THE      00118000
.*            END OF THE LOGIC MODULE TABLE.                            00119000
.*                                                                      00120000
.*                                                                      00121000
.*  NOTES:                                                              00122000
.*            DEPENDENCIES: NONE                                        00123000
.*                                                                      00124000
.*            RESTRICTIONS: NONE                                        00125000
.*                                                                      00126000
.*            REGISTER CONVENTIONS:                                     00127000
.*               THE CONVENTIONS OBSERVED BY THE CODE GENERATED         00128000
.*               BY THE MACRO ARE:                                      00129000
.*                                                                      00130000
.*               R0 IS A SAVE REGISTER FOR R14                          00131000
.*               R1 POINTS TO THE PARAMETER LIST ON ENTRY               00132000
.*               R1 CONTAINS THE LOGIC MODULE ADDRESS ON EXIT           00133000
.*               R14 IS THE LINK REGISTER ON ENTRY AND EXIT             00134000
.*               R14 POINTS TO THE LOGIC MODULE TABLE                   00135000
.*               R15 IS THE ADDRESS OF THIS MODULE ON ENTRY             00136000
.*               R15 CONTAINS THE RETURN CODE ON EXIT                   00137000
.*                                                                      00138000
.*  MODULE TYPE:                                                        00139000
.*            PROCESSOR: ASSEMBLER                                      00140000
.*                                                                      00141000
.*            MODULE SIZE: N/A                                          00142000
.*                                                                      00143000
.*                                                                      00144000
.*  INPUT:                                                              00145000
.*            SEE FUNCTION.                                             00146000
.*                                                                      00147000
.*  EXIT NORMAL:                                                        00148000
.*            RETURN CODE = 0                                           00149000
.*                                                                      00150000
.*  EXIT ERROR:                                                         00151000
.*            RETURN CODE = 8 (SEVERE PARAMETER ERROR)                  00152000
.*                                                                      00153000
.*  MACROS:                                                             00154000
.*            INTERNAL:                                                 00155000
.*               NONE                                                   00156000
.*                                                                      00157000
.*            EXTERNAL:                                                 00158000
.*               PRMOD                                                  00159000
.*                                                                      00160000
.CHKTYP  ANOP                                                           00161000
         AIF   ('&TYPE' EQ 'START').CHKSTT                              00162000
         AIF   ('&TYPE' EQ 'ENTRY').CHKENT                              00163000
         AIF   ('&TYPE' EQ 'FINAL').CHKFIN                              00164000
         MNOTE 8,'IGZPRM01 TYPE MUST BE EITHER START, ENTRY OR FINAL'   00165000
&ERROR   SETB  (1)                                                      00166000
         AGO   .ENDGEN                                                  00167000
.CHKSTT  ANOP                                                           00168000
         AIF   (NOT &STTFND).CHKSTT1                                    00169000
         MNOTE 8,'IGZPRM02 TYPE=START ALLOWED ONCE ONLY'                00170000
&ERROR   SETB  (1)                                                      00171000
         AGO   .ENDGEN                                                  00172000
.CHKSTT1 ANOP                                                           00173000
         AIF   (T'&DEVICE NE 'O').BADSTT                                00174000
         AIF   (T'&IOAREA2 NE 'O').BADSTT                               00175000
         AIF   (T'&RECFORM NE 'O').BADSTT                               00176000
         AIF   (T'&CTLCHR NE 'O').BADSTT                                00177000
         AIF   (T'&FUNC NE 'O').BADSTT                                  00178000
         AGO   .GENSTT                                                  00179000
.BADSTT  ANOP                                                           00180000
         MNOTE 0,'IGZPRM03 PRMOD PARAMETERS IGNORED FOR TYPE=START DEFIX00181000
               NITION'                                                  00182000
         AGO   .GENSTT                                                  00183000
.*                                                                      00184000
.CHKENT  ANOP                                                           00185000
         AIF   (NOT &FINFND).CHKENT1                                    00186000
         MNOTE 8,'IGZPRM06 TYPE=ENTRY NOT ALLOWED AFTER TYPE=FINAL'     00187000
&ERROR   SETB  (1)                                                      00188000
         AGO   .ENDGEN                                                  00189000
.CHKENT1 ANOP                                                           00190000
         AIF   (&STTFND).GENENT                                         00191000
         MNOTE 8,'IGZPRM04 TYPE=START REQUIRED BEFORE TYPE=ENTRY DEFINIX00192000
               TION'                                                    00193000
&ERROR   SETB  (1)                                                      00194000
         AGO   .ENDGEN                                                  00195000
.*                                                                      00196000
.CHKFIN  ANOP                                                           00197000
         AIF   (&STTFND).CHKFIN1                                        00198000
         MNOTE 8,'IGZPRM04 TYPE=START REQUIRED BEFORE TYPE=FINAL DEFINIX00199000
               TION'                                                    00200000
&ERROR   SETB  (1)                                                      00201000
         AGO   .ENDGEN                                                  00202000
.CHKFIN1 ANOP                                                           00203000
         AIF   (NOT &FINFND).CHKFIN2                                    00204000
         MNOTE 8,'IGZPRM02 TYPE=FINAL ALLOWED ONCE ONLY'                00205000
&ERROR   SETB  (1)                                                      00206000
         AGO   .ENDGEN                                                  00207000
.CHKFIN2 ANOP                                                           00208000
         AIF   (&ENTFND).CHKFIN3                                        00209000
         MNOTE 0,'IGZPRM05 NO TYPE=ENTRY DEFINITIONS FOUND'             00210000
.CHKFIN3 ANOP                                                           00211000
         AIF   (T'&DEVICE NE 'O').BADFIN                                00212000
         AIF   (T'&IOAREA2 NE 'O').BADFIN                               00213000
         AIF   (T'&RECFORM NE 'O').BADFIN                               00214000
         AIF   (T'&CTLCHR NE 'O').BADFIN                                00215000
         AIF   (T'&FUNC NE 'O').BADFIN                                  00216000
         AGO   .GENFIN                                                  00217000
.BADFIN  ANOP                                                           00218000
         MNOTE 0,'IGZPRM03 PRMOD PARAMETERS IGNORED FOR TYPE=FINAL DEFIX00219000
               NITION'                                                  00220000
         AGO   .GENFIN                                                  00221000
.*                                                                      00222000
.GENSTT  ANOP                                                           00223000
&STTFND  SETB  (1)                                                      00224000
IGZEQP0  CSECT                                                          00225000
         USING *,15                ESTABLISH ADDRESSABILITY             00226000
         B     IGZEQP00            BYPASS SIGNATURE                     00227000
         SPACE 1                                                        00228000
         DC    AL1(39)             LENGTH FOR SA HDR IN DUMP            00229000
         DC    CL1' '              ALIGN                                00230000
         DC    CL8'IGZEQP0'        SIGNATURE - ROUTINE NAME             00231000
         DC    B'01000010'                     MAJOR OPTIONS            00232000
         DC    B'01000010'                     OTHER OPTIONS            00233000
         DC    CL2'C2'                         COMPILER                 00234000
         DC    CL3'4.0'                        RELEASE                  00235000
         DC    CL8' '                          DATE (MVS ONLY)          00236000
         DC    CL1' '                                                   00237000
         DC    CL8' '                          TIME (MVS ONLY)          00238000
         DC    CL1'O'                          O.S. ESM                 00239000
         DC    XL5'00'             RESERVED                             00240000
         DS    0F                                                       00241000
         SPACE 1                                                        00242000
IGZEQP00 LR    0,14                SAVE LINK REGISTER                   00243000
         LTR   1,1                 PARAMETER PROVIDED ?                 00244000
         BZ    IGZEQP03                                                 00245000
         LA    14,IGZQP0MT         ADDRESS LOGIC MODULE TABLE           00246000
IGZEQP01 DS    0H                                                       00247000
         CLI   0(14),X'FF'         END OF TABLE ?                       00248000
         BE    IGZEQP03            YES ...                              00249000
         CLC   0(3,14),KIJD        VALID PHASE NAME IN TABLE ?          00250000
         BNE   IGZEQP04            NO ...                               00251000
         CLC   0(8,14),0(1)        LOGIC MODULE FOUND ?                 00252000
         BE    IGZEQP02            YES ...                              00253000
         LA    14,12(14)           NEXT TABLE ENTRY                     00254000
         B     IGZEQP01            LOOP                                 00255000
IGZEQP02 DS    0H                                                       00256000
         L     1,8(14)             LOAD ADDRESS OF LOGIC MODULE         00257000
         XR    14,14               RETURN CODE = OK                     00258000
         B     IGZEQP05                                                 00259000
IGZEQP03 DS    0H                                                       00260000
         XR    1,1                 CLEAR LOGIC MODULE ADDRESS           00261000
         LA    14,4                RC = LOGIC MODULE NOT FOUND          00262000
         B     IGZEQP05                                                 00263000
IGZEQP04 DS    0H                                                       00264000
         XR    1,1                 CLEAR LOGIC MODULE ADDRESS           00265000
         LA    14,8                RC = INVALID MODULE NAME IN TABLE    00266000
IGZEQP05 DS    0H                                                       00267000
         LR    15,14               SET RETURN CODE IN REGISTER 15       00268000
         LR    14,0                RESTORE LINK REGISTER                00269000
         BR    14                  RETURN TO CALLER                     00270000
KIJD     DC    CL3'IJD'            LOGIC MODULE NAME PREFIX             00271000
         EJECT                                                          00272000
IGZQP0MT DS    0H                  START OF LOGIC MODULE TABLE          00273000
         AGO   .ENDGEN                                                  00274000
.*                                                                      00275000
.GENENT  ANOP                                                           00276000
&ENTFND  SETB  (1)                                                      00277000
.*                                                                      00278000
&DEVNUM  SETC  '&DEVICE'                                                00279000
         AIF   (T'&DEVICE NE 'O').ENTDEV                                00280000
&DEVNUM  SETC  '1403'                                                   00281000
         AGO   .ENTREC                                                  00282000
.*                                                                      00283000
.ENTDEV  ANOP                                                           00284000
         AIF   ('&DEVNUM' EQ '1403' OR '&DEVNUM' EQ '1443' OR          X00285000
               '&DEVNUM' EQ '3525').ENTREC                              00286000
         AIF   ('&DEVNUM' EQ '3800' OR '&DEVNUM' EQ '3211' OR          X00287000
               '&DEVNUM' EQ 'PRT1').ENTSKP                              00288000
.*                                                                      00289000
         MNOTE 8,'IGZPRM07 DEVICE &DEVNUM NOT A VS COBOL II SUPPORTED DX00290000
               EVICE'                                                   00291000
&ERROR   SETB  (1)                                                      00292000
         AGO   .ENDGEN                                                  00293000
.*                                                                      00294000
.ENTSKP  ANOP                                                           00295000
         MNOTE 0,'IGZPRM08 LIOCS MODULE GENERATION NOT REQUIRED FOR DEVX00296000
               ICE &DEVNUM'                                             00297000
         AGO   .ENDGEN                                                  00298000
.*                                                                      00299000
.ENTREC  ANOP                                                           00300000
&RECFRM  SETC  '&RECFORM'                                               00301000
         AIF   (T'&RECFORM NE 'O').ENTREC1                              00302000
&RECFRM  SETC  'FIXUNB'                                                 00303000
.ENTREC1 ANOP                                                           00304000
&MOD4    SETC  'U'                                                      00305000
         AIF   ('&RECFRM' EQ 'UNDEF').ENTCTL                            00306000
&MOD4    SETC  'V'                                                      00307000
         AIF   ('&RECFRM' EQ 'VARUNB').ENTCTL                           00308000
&MOD4    SETC  'F'                                                      00309000
         AIF   ('&RECFRM' EQ 'FIXUNB').ENTCTL                           00310000
.*                                                                      00311000
.ENTCTL  ANOP                                                           00312000
&MOD5    SETC  'A'                                                      00313000
         AIF   ('&CTLCHR' EQ 'ASA').ENTFUN                              00314000
&MOD5    SETC  'Y'                                                      00315000
         AIF   ('&CTLCHR' EQ 'YES').ENTFUN                              00316000
         AIF   (T'&CTLCHR NE 'O').ENTFUN                                00317000
&MOD5    SETC  'Z'                                                      00318000
         AGO   .ENTFUN                                                  00319000
.*                                                                      00320000
.ENTFUN  ANOP                                                           00321000
&WORK    SETC  'ZORST'                                                  00322000
&MOD6    SETC  '&WORK'(1,1)                                             00323000
         AIF   ('&DEVNUM' EQ '3525').ENTFUN2                            00324000
         AGO   .ENTIOA                                                  00325000
.ENTFUN2 ANOP                                                           00326000
&MOD6    SETC  '&WORK'(2,1)                                             00327000
         AIF   (T'&FUNC EQ 'O').ENTIOA                                  00328000
         AIF   ('&FUNC' EQ 'W').ENTIOA                                  00329000
         AIF   ('&FUNC' EQ 'WT').ENTFUN3                                00330000
&MOD6    SETC  '&WORK'(3,1)                                             00331000
         AIF   ('&FUNC' EQ 'RW').ENTIOA                                 00332000
         AIF   ('&FUNC' EQ 'RWT').ENTFUN3                               00333000
&MOD6    SETC  '&WORK'(4,1)                                             00334000
         AIF   ('&FUNC' EQ 'PW').ENTIOA                                 00335000
         AIF   ('&FUNC' EQ 'PWT').ENTFUN3                               00336000
&MOD6    SETC  '&WORK'(5,1)                                             00337000
         AIF   ('&FUNC' EQ 'RPWT').ENTFUN3                              00338000
         AIF   ('&FUNC' EQ 'RPW').ENTIOA                                00339000
&MOD6    SETC  '?'                                                      00340000
&NOMOD   SETB  (1)                                                      00341000
         AGO   .ENTIOA                                                  00342000
.ENTFUN3 ANOP                                                           00343000
         AIF   ('&MOD5' NE 'Z').ENTIOA                                  00344000
&MOD5    SETC  'T'                                                      00345000
.*                                                                      00346000
.ENTIOA  ANOP                                                           00347000
         AIF   ('&IOAREA2' EQ '').ENTIOA2                               00348000
         AIF   ('&DEVNUM' NE '3525').ENTIOA2                            00349000
         AIF   ('&FUNC' NE 'RW' AND '&FUNC' NE 'RWT' AND               X00350000
               '&FUNC' NE 'PW' AND '&FUNC' NE 'PWT' AND                X00351000
               '&FUNC' NE 'RPW' AND '&FUNC' NE 'RPWT').ENTIOA2          00352000
&NOIOA2  SETB  (1)                                                      00353000
         MNOTE 0,'IGZPRM09 IOAREA2=YES IGNORED WITH FUNC &FUNC'         00354000
.ENTIOA2 ANOP                                                           00355000
&MOD7    SETC  'Z'                                                      00356000
         AIF   ('&IOAREA2' EQ '' OR &NOIOA2).ENTRDO                     00357000
&MOD7    SETC  'I'                                                      00358000
.*                                                                      00359000
.ENTRDO  ANOP                                                           00360000
&MOD8    SETC  'Y'                                                      00361000
&NAME    SETC  'IJD&MOD4&MOD5&MOD6&MOD7&MOD8'                           00362000
.*                                                                      00363000
.GENMOD  ANOP                                                           00364000
         DC    CL8'&NAME'          NAME OF LOGIC MODULE (IF KNOWN)      00365000
         AIF   (&NOMOD).GENMOD1                                         00366000
         DC    V(&NAME)            ADDRESS OF LOGIC MODULE              00367000
         AGO   .GENMOD2                                                 00368000
.GENMOD1 ANOP                                                           00369000
         DC    A(0)                LOGIC MODULE NOT GENERATED           00370000
.*                                                                      00371000
.********************************************************************** 00372000
.*                                                                      00373000
.GENMOD2 ANOP                                                           00374000
         AIF   (&NOIOA2).GENMOD3                                        00375000
         PRMOD DEVICE=&DEVNUM,                                         X00376000
               IOAREA2=&IOAREA2,                                       X00377000
               RECFORM=&RECFRM,                                        X00378000
               CTLCHR=&CTLCHR,                                         X00379000
               FUNC=&FUNC,                                             X00380000
               RDONLY=YES                                               00381000
.*                                                                      00382000
         AGO   .ENDGEN                                                  00383000
.GENMOD3 ANOP                                                           00384000
         PRMOD DEVICE=&DEVNUM,                                         X00385000
               RECFORM=&RECFRM,                                        X00386000
               CTLCHR=&CTLCHR,                                         X00387000
               FUNC=&FUNC,                                             X00388000
               RDONLY=YES                                               00389000
.*                                                                      00390000
         AGO   .ENDGEN                                                  00391000
.GENFIN  ANOP                                                           00392000
&FINFND  SETB  (1)                                                      00393000
         DC    X'FFFFFFFF'         END OF LOGIC MODULE TABLE            00394000
.ENDGEN  ANOP                                                           00395000
         AIF   (NOT &ERROR).ENDGEN1                                     00396000
         MNOTE 8,'IGZPRM10 MACRO GENERATION TERMINATED BECAUSE OF PREVIX00397000
               OUS ERRORS'                                              00398000
.ENDGEN1 ANOP                                                           00399000
         MEXIT                                                          00400000
         MEND                                                           00401000
