         MACRO                                                          00001000
&LABEL   CEEPPA &ID=15,         . member ID                            X00002000
               &SUBID=0,        . member subID                         X00003000
               &LIBRARY=NO,     . this is a library routine            X00004000
               &PPA2=,          . generate PPA2.                       X00005000
               &EXTPROC=YES,    . this is an external procedure        X00006000
               &TSTAMP=YES,     . generate timestamp                   X00007000
               &PEP=YES,        . primary entry point                  X00008000
               &INSTOP=NO,      . sampling attributed to program       X00009000
               &EXITDSA=NO,     . exit (cleanup) processing required   X00010000
               &OWNEXM=YES,     . use my own exception model           X00011000
               &EPNAME=,        . Entry Point name (default is CSECT nmX00012000
               &VER=1,          . Version Number                       X00013000
               &REL=4,          . Release number                @03C   X00014000
               &MOD=4,          . Modification.                 @04C   X00015000
               &DSA=YES         . procedure has DSA                     00016000
.********************************************************************** 00017000
.*                                                                    * 00018000
.*  Language Environment/VSE V1 R4 M4                                 * 00019000
.*                                                                    * 00020000
.*  CEEPPA  - LE/VSE Program Prologue Area (PPA)                      * 00021000
.*                                                                    * 00022000
.*  Licensed Materials - Property of IBM                              * 00023000
.*                                                                    * 00024000
.*  5686-CF7-32-81K (C) Copyright IBM Corporation 1991,  2004         * 00025000
.*  All rights reserved.                                              * 00026000
.*                                                                    * 00027000
.*                                                                    * 00028000
.*      US Government Users Restricted Rights - Use, duplication or   * 00029000
.*      disclosure restricted by GSA ADP Schedule Contract with IBM   * 00030000
.*      Corp.                                                         * 00031000
.*                                                                    * 00032000
.*                                                                    * 00033000
.*  STATUS - Release level of this macro                              * 00034000
.*         LE/VSE Version 1 Release 4 Modification 4                  * 00035000
.*                                                                    * 00036000
.*  FUNCTION:                                                         * 00037000
.*     This macro used to generate the LE/VSE Program Prologue Area.  * 00038000
.*     The Program Prologue Area defines the code and data at         * 00039000
.*     the entry point of an LE/VSE block.  It is generated at        * 00040000
.*     compilation time: one PPA is generated per entry point.        * 00041000
.*                                                                    * 00042000
.*                                                                    * 00043000
.*  ACCESS                                                            * 00044000
.*       Previous save area R15.                                      * 00045000
.*                                                                    * 00046000
.*  History - History of changes to this macro                        * 00047000
.*                                                                    * 00048000
.*      10/09/94 - (JL )  Add PLIST parameter                 @NX0178A* 00049000
.*  Version 1 Release 4 LE/VSE                                        * 00050000
.* $01=PQ24951           19990415 MJM:Year2000 compliant timestamp    * 00051000
.*                                                                    * 00052000
.*  Version 1 Release 4 Modification 1                                * 00053000
.* $02=LE141             20000112 GWH:update rel/mod settings         * 00054000
.*  Version 1 Release 4 Modification 3                                * 00055000
.* $03=LE143             20020521 GWH:update rel/mod settings         * 00056000
.*  Version 1 Release 4 Modification 3                                * 00055000
.* $04=LE144             20030619 GWH:update rel/mod settings         * 00056000
.*                                    Sysroute LE z/OS PQ74720        * 00056000
.********************************************************************** 00057000
.********************************************************************** 00058000
.*                                                                      00059000
.********************************************************************** 00060000
.*                                                                      00061000
.*  Global symbols set by master global macro                           00062000
.*                                                                      00063000
         GBLC  &CEEGVER           Product version number                00064000
         GBLC  &CEEGREL           Product release number                00065000
         GBLC  &CEEGMOD           Product modification level            00066000
         GBLB  &CEETSGEN       Indication Time Stamp generated.         00067000
.*                                                                      00068000
.*                                                                      00069000
.********************************************************************** 00070000
.*                                                                      00071000
.*                                                                      00072000
         GBLC   &CEENX            System index number retention         00073000
         GBLC   &CEEOEPVNX        Label of the CEEPROC macro            00074000
         GBLC   &CEEPPA2N         Name of PPA2                          00075000
         GBLC   &CEEENTRYN        Name on CEEENTRY macro.               00076000
         GBLB   &CEESEP           sec EP - LE/VSE flag                  00077000
         GBLB   &CEEPPA2X         PPA2 generated                        00078000
         GBLB   &CEEINPL          Set when main entry list generated.   00079000
         GBLB   &CEEEXOPS         Execops/Noexecops option              00080000
         GBLA   &CEEPLIST         Plist format                @NX0178A* 00081000
.*                                                                      00082000
         LCLA   &EPLN                    Length of procedure name       00083000
.*                                       LE/VSE flags                   00084000
         LCLB   &EXP                     external entry point           00085000
         LCLB   &HDS                     rtn has DSA                    00086000
         LCLB   &LIO                     library object                 00087000
         LCLB   &EDS                     exit DSA (exit processing rqd) 00088000
         LCLB   &OEM                     own exception model            00089000
         LCLB   &ISP                     RESERVED.                      00090000
         LCLB   &TIMEST                  TIME STAMP ASKED FOR.          00091000
         LCLB   &GPPA2            Generate PPA2                         00092000
.*                                                                      00093000
         LCLC  &DATE,&TIME,&YEAR,&MONTH,&DAY,&HOUR,&MIN,&SEC,&YRPREF    00094000
         LCLC  &VVER,&RREL,&MMOD                                        00095000
         LCLC  &EPN                     Full Entry Point Name           00096000
         LCLC  &NM                      last three digits of sysindex   00097000
         LCLC  &EP8                     first 8 chars of e. p. name     00098000
.*                                                                      00099000
.*   retain index from the first PPA1                                   00100000
         AIF   (('&CEENX' NE '') ).G1                                   00101000
&CEENX   SETC  '&SYSNDX'(2,3)           retain syst. index of first PPA 00102000
.G1      ANOP  ,                                                        00103000
&NM      SETC  '&SYSNDX'(2,3)           current sys index value         00104000
.*                                                                      00105000
.*======================== Yes No Parm checking =============           00106000
&TIMEST  SETB  0                   TIME STAMP OFF.                      00107000
         AIF   ('&TSTAMP'(1,1)  EQ 'N').DIAG1OK                         00108000
&TIMEST  SETB  1                    SET TIME STAMP ON.                  00109000
         AIF   ('&TSTAMP'(1,1)  EQ 'Y').DIAG1OK                         00110000
   MNOTE 4,'TSTAMP PARAMETER ''&TSTAMP'' SHOULD BE ''YES'' OR ''NO'''   00111000
   MNOTE *,'TSTAMP PARAMETER ''YES'' WILL BE USED'                      00112000
.DIAG1OK ANOP  ,                                                        00113000
.TSOK    ANOP  ,                                                        00114000
.*                                                                      00115000
&EXP     SETB   0                  internal procedure                   00116000
         AIF   ('&EXTPROC'(1,1) EQ 'N').A1                              00117000
&EXP     SETB  1                   external procedure                   00118000
         AIF   ('&EXTPROC'(1,1) EQ 'Y').A1                              00119000
   MNOTE 4,'EXTPROC PARAMETER ''&EXTPROC'' SHOULD BE ''YES'' OR ''NO''' 00120000
   MNOTE *,'EXTPROC PARAMETER ''YES'' WILL BE USED'                     00121000
.A1      ANOP  ,                                                        00122000
.*                                                                      00123000
.*   SEP IS INVERSE OF PEP.                                             00124000
&SEP     SETB   1                  primary entry point                  00125000
         AIF   ('&PEP'(1,1) EQ 'N').A2                                  00126000
&SEP     SETB  0                   secondary entry point                00127000
         AIF   ('&PEP'(1,1) EQ 'Y').A2                                  00128000
   MNOTE 4,'PEP PARAMETER ''&PEP'' SHOULD BE ''YES'' OR ''NO'''         00129000
   MNOTE *,'PEP PARAMETER ''YES'' WILL BE USED'                         00130000
.A2      ANOP  ,                                                        00131000
.*                                                                      00132000
&HDS     SETB  0                   this procedure does not have DSA     00133000
         AIF   ('&DSA'(1,1) EQ 'N').A3                                  00134000
&HDS     SETB  1                   this procedure has DSA               00135000
         AIF   ('&DSA'(1,1) EQ 'Y').A3                                  00136000
   MNOTE 4,'DSA PARAMETER ''&DSA'' SHOULD BE ''YES'' OR ''NO'''         00137000
   MNOTE *,'DSA PARAMETER ''YES'' WILL BE USED'                         00138000
.A3      ANOP  ,                                                        00139000
.*                                                                      00140000
&LIO     SETB  1                   this is a library object             00141000
         AIF   ('&LIBRARY'(1,1) EQ 'Y').A4                              00142000
&LIO     SETB  0                   this is not a library object         00143000
         AIF   ('&LIBRARY'(1,1) EQ 'N').A4                              00144000
   MNOTE 4,'LIBRARY PARAMETER ''&LIBRARY'' SHOULD BE ''YES'' OR ''NO''' 00145000
   MNOTE *,'LIBRARY PARAMETER ''NO'' WILL BE USED'                      00146000
.A4      ANOP  ,                                                        00147000
.*                                                                      00148000
&ISP     SETB  1                                                        00149000
         AIF   ('&INSTOP'(1,1) EQ 'Y').A5                               00150000
&ISP     SETB  0                                                        00151000
         AIF   ('&INSTOP'(1,1) EQ 'N').A5                               00152000
   MNOTE 4,'INSTOP PARAMETER ''&INSTOP'' SHOULD BE ''YES'' OR ''NO'''   00153000
   MNOTE *,'INSTOP PARAMETER ''NO'' WILL BE USED'                       00154000
.A5      ANOP  ,                                                        00155000
.*                                                                      00156000
&EDS     SETB  1                   exit processing rqd. for this DS     00157000
         AIF   ('&EXITDSA'(1,1) EQ 'Y').A6                              00158000
&EDS     SETB  0                 exit processing NOT rqd. for this DSA  00159000
         AIF   ('&EXITDSA'(1,1) EQ 'N').A6                              00160000
   MNOTE 4,'EXITDSA PARAMETER ''&EXITDSA'' SHOULD BE ''YES'' OR ''NO''' 00161000
   MNOTE *,'EXITDSA PARAMETER ''NO'' WILL BE USED'                      00162000
.A6      ANOP  ,                                                        00163000
.*                                                                      00164000
&OEM     SETB  1                   inherited (callers) exception model  00165000
         AIF   ('&OWNEXM'(1,1) EQ 'N').A6A                              00166000
&OEM     SETB  0                 own exception model                    00167000
         AIF   ('&OWNEXM'(1,1) EQ 'Y').A6A                              00168000
   MNOTE 4,'OWNEXM PARAMETER ''&OWNEXM'' SHOULD BE ''YES'' OR ''NO'''   00169000
   MNOTE *,'OWNEXM PARAMETER ''YES'' WILL BE USED'                      00170000
.A6A     ANOP  ,                                                        00171000
.*                                                                      00172000
         AIF  ('&PPA2' NE '').A8                                        00173000
&GPPA2   SETB 0                      SET OFF                            00174000
         AIF  (&CEEPPA2X EQ 1).A8Z   IF GENED LEAVE                     00175000
&GPPA2   SETB 1                      ELSE GENERATE                      00176000
         AGO  .A8M                                                      00177000
.A8      ANOP ,                                                         00178000
&GPPA2   SETB 0                                                         00179000
         AIF  ('&PPA2'(1,1) EQ 'N').A8F                                 00180000
&GPPA2   SETB 1                                                         00181000
         AIF  ('&PPA2'(1,1) EQ 'Y').A8A                                 00182000
   MNOTE 4,'PPA2 PARAMETER ''&PPA2'' SHOULD BE ''YES'' OR ''NO'''       00183000
   MNOTE *,'PPA2 PARAMETER ''YES'' WILL BE USED'                        00184000
.*                                                                      00185000
.*                      Parm = Yes                                      00186000
.A8A     ANOP  ,                                                        00187000
         AIF (&CEEPPA2X EQ 0).A8M                                       00188000
         AGO  .A8W                                                      00189000
.*                                                                      00190000
.*                      Parm = NO                                       00191000
.A8F     ANOP  ,                                                        00192000
     AIF (&CEEPPA2X EQ 1).A8Z                                           00193000
     MNOTE 0,'PPA2 PARAMETER ''NO'' ASSUME PPA2 GENERATED LATER'        00194000
&CEEPPA2N SETC 'PPA2&CEENX'                                             00195000
     AGO  .A8Z                                                          00196000
.A8M     ANOP ,                                                         00197000
&CEEPPA2X SETB 1                                                        00198000
&CEEPPA2N SETC 'PPA2&CEENX'                                             00199000
         AGO .A8Z        OK Get out.                                    00200000
.A8W     ANOP  ,                                                        00201000
&GPPA2   SETB 0                                                         00202000
     MNOTE 0,'PPA2 PARAMETER ''YES'' AND PPA2 ALREADY GENERATED'        00203000
.A8Z     ANOP  ,                                                        00204000
.*                                                                      00205000
.*=========================== Entry name ====================           00206000
.*                                                                      00207000
.*                                                                      00208000
         AIF   ('&CEENX' EQ '&NM').A7A                                  00209000
         AIF   ('&CEEENTRYN' EQ '').A7A                                 00210000
&EPN     SETC '&CEEENTRYN'                                              00211000
         AIF   ('&EPNAME' EQ '').A7C                                    00212000
&EPN     SETC  '&EPNAME'           Set entry point name                 00213000
         AGO   .A7B                                                     00214000
.*                                                                      00215000
.A7A     ANOP  ,                                                        00216000
&EPN     SETC  '&SYSECT'           Set entry point name                 00217000
.A7B     ANOP  ,                                                        00218000
         AIF   ('&EPNAME' EQ '').A7C                                    00219000
&EPN     SETC  '&EPNAME'           Set entry point name                 00220000
.A7C     ANOP  ,                                                        00221000
&EPLN    SETA   K'&EPN                   Length of entry point name     00222000
.*                                                                      00223000
&DD      SETC   '0'                RESERVED                             00224000
&EP8     SETC  ''                                                       00225000
         AIF   (('&CEENX' NE '&NM') AND ('&EPNAME' EQ '')).A10          00226000
         AGO   .A11                                                     00227000
.A10     ANOP  ,                                                        00228000
         MNOTE 0,'MISSING ENTRY POINT NAME ON SECONDARY ENTRY POINT'    00229000
.A11     ANOP  ,                                                        00230000
.*                                                                      00231000
.*                                                                      00232000
.*=========================== Get version mod ===============           00233000
.*                                                                      00234000
         AIF ('&CEEGVER' EQ '').NOGLOB                                  00235000
&VVER     SETC  '&CEEGVER'          Application version level           00236000
&RREL     SETC  '&CEEGREL'          Application release level           00237000
&MMOD     SETC  '&CEEGMOD'          Application modification level      00238000
         AGO   .NOSET                                                   00239000
.NOGLOB  ANOP  ,                                                        00240000
&VVER     SETC  '&VER'              Application version level           00241000
&RREL     SETC  '&REL'              Application release level           00242000
&MMOD     SETC  '&MOD'              Application modification level      00243000
.NOSET   ANOP  ,                                                        00244000
.*                                                                      00245000
&CDI     SETC   '0'                 initialize comp unit debug info     00246000
.*                                                                      00247000
.*                                                                      00248000
.*       Start generation of PPA blocks.                                00249000
.*                                                                      00250000
*                                                                       00251000
**********************************************************************  00252000
*        P R O G R A M  P R O L O G  A R E A 1   ( P P A 1)          *  00253000
**********************************************************************  00254000
*                                                                       00255000
.*                                                                      00256000
.*************start of PPA1 CSECT generation**************************  00257000
.*                                                                      00258000
         AIF   ('&EPN' EQ '&SYSECT').P1A                                00259000
&EP8     SETC  '&EPN        '(1,8)                                @04C          
*        ENTRY &EP8                                                     00261000
.P1A     ANOP  ,                                                        00262000
         AIF   ('&CEENX' NE '&NM').P1B                                  00263000
PPA1&CEENX  DS    0F                                                    00264000
.P1B     ANOP  ,                                                        00265000
         AIF   ('&LABEL' EQ '').P1C                                     00266000
&LABEL   DS    0F                                                       00267000
.P1C     ANOP  ,                                                        00268000
         DC    AL1(PPANL&NM-*)  Offset to the entry name length         00269000
         DC    X'CE'             E/370  Indicator.                      00270000
         DC    B'&EXP.&SEP.&HDS.&LIO.&ISP.&EDS.&OEM.0' . PPA flags      00271000
*                     Bit 0   0 = Internal Procedure                    00272000
*                             1 = External Procedure                    00273000
*                     Bit 1   0 = Primary Entry Point                   00274000
*                             1 = Secondary Entry Point                 00275000
*                     Bit 2   0 = Block doesn't have a DSA              00276000
*                             1 = Block has a DSA                       00277000
*                     Bit 3   0 = compiled object                       00278000
*                             1 = library object                        00279000
*                     Bit 4   0 = sampling interrupts to library        00280000
*                             1 = sampling interrupts to code           00281000
*                     Bit 5   0 = not an exit DSA                       00282000
*                             1 = Exit DSA                              00283000
*                     Bit 6   0 = own exception model                   00284000
*                             1 = inherited (callers) exception model   00285000
*                     Bit 7       Reserved                              00286000
         DC    X'00'            Member flags                            00287000
         DC    A(PPA2&CEENX)    Addr of Compile Unit Block (PPA2)       00288000
         DC    A(0)                                                     00289000
         DC    A(0)             Data Descriptors for this entry point   00290000
         DS    0H                                                       00291000
PPANL&NM DC    AL2(&EPLN)    .  Length of Entry Point Name              00292000
         DC    CL&EPLN'&EPN' .  Entry Point Name                        00293000
.*                                                                      00294000
.*       Generate CEEINPL Area for main routines.                       00295000
.*                                                                      00296000
     AIF (&CEEINPL).AA1                                                 00297000
&CEEINPL SETB 1                                                         00298000
         AIF   ('&CEEOEPVNX' EQ '').AA1                                 00299000
CEEINPL  DS    0D                                                       00300000
         DC    A(PPA2M&CEENX)                                           00301000
         DC    A(CEEINPLSTST-CEEINPL)                                   00302000
CEEINPLSTST DS 0F                                                       00303000
         DC    F'7'            Number of items.                         00304000
         DC    A(PPA2M&CEENX) . A of A(first entry point in comp unit)  00305000
         DC    V(CEESTART)   . A(Address of CEESTART)                   00306000
         DC    V(CEEBETBL)                                              00307000
         DC    A(&ID)        . Memeber id                               00308000
         DC    A(0)                                                     00309000
     AIF (&CEEEXOPS).EXOPS1                                             00310000
.EXOPS0  DC    XL4'000&CEEPLIST.0000' . EXECOPS(ON), PLIST              00311000
         AGO   .DONEXEC                                                 00312000
.EXOPS1  DC    XL4'100&CEEPLIST.0000' . EXECOPS(OFF), PLIST             00313000
.DONEXEC ANOP  ,                                                        00314000
.AA1     ANOP  ,                                                        00315000
         DS    0H                                                       00316000
.*                                                                      00317000
         AIF   ( NOT &SEP).P2CSECT                                      00318000
         MEXIT                                                          00319000
.*                                                                      00320000
.*************end of PPA1 CSECT generation****************************  00321000
.*                                                                      00322000
         AIF   ( NOT &SEP).P2CSECT                                      00323000
         MEXIT                                                          00324000
.P2CSECT ANOP  ,                                                        00325000
.*                                                                      00326000
.*************start of PPA2 CSECT generation**************************  00327000
.*                                                                      00328000
     AIF (&GPPA2 EQ 0).PPA2GENX                                         00329000
*                                                                       00330000
**********************************************************************  00331000
*        P R O G R A M  P R O L O G  A R E A 2   ( P P A 2)          *  00332000
**********************************************************************  00333000
*                                                                       00334000
         LCLC  &CSADDR                                                  00335000
&CSADDR  SETC  '0'                                                      00336000
         AIF   ('&LIBRARY' EQ 'YES').PPA2                               00337000
.*       AIF   (D'CEESTART).PPA2                                        00338000
         EXTRN CEESTART                                                 00339000
&CSADDR  SETC  'CEESTART'                                               00340000
.PPA2    ANOP  ,                                                        00341000
PPA2&CEENX  DS    0F                                                    00342000
         DC    AL1(&ID)         Member ID                               00343000
         DC    AL1(&SUBID)      Sub ID                                  00344000
         DC    AL1(0)           Member defined                          00345000
         DC    AL1(1)           Level of PPAx control blocks            00346000
PPA2S&CEENX DC    A(&CSADDR)       A(CEESTART for this load module)     00347000
         DC    A(&CDI)          A(Compile Debug Information (CDI) )     00348000
         AIF   (&TIMEST EQ 0).TSTPTRN                                   00349000
         DC    A(CEETIMES-PPA2&CEENX) A(Offset to time stamp)           00350000
         AGO   .TSTPTRE                                                 00351000
.TSTPTRN ANOP ,                                                         00352000
         DC    A(0)           . A(Offset to time stamp)                 00353000
.TSTPTRE ANOP ,                                                         00354000
         AIF   ('&CEEOEPVNX' EQ '').B1                                  00355000
PPA2M&CEENX DC    A(&CEEOEPVNX)  . A(first entry point in comp. unit)   00356000
         AGO   .B2                                                      00357000
.B1      ANOP  ,                                                        00358000
PPA2M&CEENX DC    A(&SYSECT)     . A(first entry point in comp. unit)   00359000
.B2      ANOP  ,                                                        00360000
.*                                                                      00361000
.*************end of PPA2 CSECT generation****************************  00362000
.*                                                                      00363000
.PPA2GENX ANOP ,                                                        00364000
         AIF   ( NOT &SEP).TSCSECT                                      00365000
         MEXIT                                                          00366000
.*                                                                      00367000
.TSCSECT ANOP  ,                                                        00368000
     AIF (&TIMEST EQ 0).TSGEN          Local request for time stamp     00369000
     AIF (&CEETSGEN).TSGEN             previously generated ?           00370000
&CEETSGEN SETB 1                       set global to indicate TS gened  00371000
*                                                                       00372000
**********************************************************************  00373000
*                 T I M E    S T A M P                               *  00374000
**********************************************************************  00375000
*                                                                       00376000
.*                                                                      00377000
.*************start of TIME STAMP CSECT generation********************  00378000
.*                                                                      00379000
.*                                                                      00380000
&DATE    SETC  '&SYSDATC'                Date of compilation     @01C   00381000
&YRPREF  SETC  '&DATE'(1,2)              Century of compilation  @01A   00382000
&YEAR    SETC  '&DATE'(3,2)              Year of compilation     @01C   00383000
&MONTH   SETC  '&DATE'(5,2)              Month of compilation    @01C   00384000
&DAY     SETC  '&DATE'(7,2)              Day of compilation      @01C   00385000
&TIME    SETC  '&SYSTIME'                Time of compilation            00386000
&HOUR    SETC  '&TIME'(1,2)              Hour of compilation            00387000
&MIN     SETC  '&TIME'(4,2)              Minutes of compilation         00388000
&SEC     SETC  '00'                      Macros do not work in seconds. 00389000
.*                                                                      00390000
.*                                                                      00391000
*        Time Stamp                                                     00392000
 MNOTE *,'Time Stamp = &YRPREF&YEAR/&MONTH/&DAY &HOUR:&MIN:&SEC' @01C   00393000
 MNOTE *,'Version &VER Release &REL Modification &MOD'                  00394000
CEETIMES DS    0F                                                       00395000
         DC    CL4'&YRPREF.&YEAR' Year                                  00396000
         DC    CL2'&MONTH'        Month                                 00397000
         DC    CL2'&DAY'          Day                                   00398000
         DC    CL2'&HOUR'         Hours                                 00399000
         DC    CL2'&MIN'          Minutes                               00400000
         DC    CL2'&SEC'          Seconds                               00401000
         DC    CL2'&VER'          Version                               00402000
         DC    CL2'&REL'          Release                               00403000
         DC    CL2'&MOD'          Modification                          00404000
         MEXIT                                                          00405000
.*                                                                      00406000
.*************end of TIME STAMP CSECT generation**********************  00407000
.*                                                                      00408000
.*                                                                      00409000
.*************The End                                                   00410000
.*                                                                      00411000
.TSGEN   ANOP  ,                                                        00412000
         MEND                                                           00413000
