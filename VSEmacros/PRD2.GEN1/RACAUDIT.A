********************************************************************    00050000
*                                                                       00100000
*  $MAC(RACAUDIT) COMP(SC1BN) PROD(RACF):                               00150000
*                                                                       00200000
*  DESCRIPTIVE NAME = INTERFACE TO RACF AUDITING FUNCTION               00250000
*                                                                       00300000
*  **PROPRIETARY_STATEMENT********************************************  00337500
*                                                                    *  00375000
*                                                                    *  00412500
*   LICENSED MATERIALS - PROPERTY OF IBM                             *  00450000
*   THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                      *  00487500
*   5655-068 (C) COPYRIGHT IBM CORP. 1989, 1993                      *  00525000
*   SEE COPYRIGHT INSTRUCTIONS                                       *  00562500
*                                                                    *  00600000
*   STATUS= HBB5510                                                  *  00637500
*                                                               @L2C *  00675000
*  **END_OF_PROPRIETARY_STATEMENT*************************************  00712500
*                                                                       00750000
*  FUNCTION:                                                            00800000
*    THE RACAUDIT MACRO PROVIDES A STANDARD INTERFACE TO THE RACF       00850000
*    AUDITING ROUTINE ICHAUD00                                          00900000
*                                                                       00950000
*  INVOCATION:                                                          01000000
*    THE RACF AUDIT ROUTINE IS INVOKED BY THE RACROUTE REQUEST=AUDIT    01050000
*    MACRO.                                                             01100000
*                                                                       01150000
*   REQUIREMENTS FOR USE - USER MUST BE APF AUTHORIZED, SUP STATE       01200000
*                          OR SYSTEM KEY (0-7)                          01250000
*                                                                       01300000
*  MODULE TYPE:                                                         01350000
*    MACRO                                                              01400000
*                                                                       01450000
*  CHANGE ACTIVITY:                                                     01500000
*   $L0= VTAMSLS RACF/190 880803 PDPWB1: VTAM SESSION SECURITY     @L0A 01550000
*   $P1= PGW0043 RACF/190 881102 PDPWB1: PGW0043                   @P1A 01600000
*   $P2= PGW0045 RACF/190 881102 PDPWB1: PGW0045                   @P2A 01650000
*   $P3= PGW0042 RACF/190 881102 PDPWB1: PGW0042                   @P3A 01700000
*   $P4= PGP0408 RACF/190 890721 PDPWB1: PGP0408                   @P4A 01750000
*   $D1= DCR0077 RACF/190 890814 PDJCD1: DCR0077                   @D1A 01800000
*   $P5= PGP0550 RACF/190 890915 PDB_S1: PGP0550                   @P5A 01850000
*   $P6= PI90009 RACF/190 891010 PDB_S1: PI90009                   @P6A 01900000
*   $L1= RELID   RACF/192 900904 PDAHW1: SUPPORT FOR RACF 1.9.2    @L1A 01950000
*   $P7= PJA0029 RACF/192 910524 PDPSB1: PJA0029                   @P7A 01975000
*   $L2= OW04015 RACF/210 930426 PDKAF1: SUPPORT FOR RACF 2.1      @L2A 02000000
*   $L3= OW10709 RACF/220 940509 PDJMV1: SUPPORT FOR RACF 2.2      @L3A 02025000
*                                                                       02050000
*  CHANGE DESCRIPTIONS:                                                 02100000
*   A000000-999999 ORIGINAL CODE                                   @L0A 02150000
*   A - START PLIST ON A WORD BOUNDARY                             @P1A 02200000
*   A - START BAL ON A WORD BOUNDARY                               @P2A 02250000
*   A - ASSEMBLER F COMPATIBILITY                                  @P3A 02300000
*   A - GENERALIZED AUDIT FUNCTION, LOGSTR & RESULT KEYWORDS ADDED @D1A 02350000
*   A - ALLOW PARENS AROUND RELEASE                                @P4A 02400000
*   D - DELETE REFERENCES TO ERTN070 FOR ASSEMBLER F COMPATIBILITY @P5A 02450000
*   C - CORRECT THE PARAMETER LIST RESERVED STORAGE                @P6A 02500000
*   C - ALLOW RACF RELEASE 1.9.2 TO BE SPECIFIED                   @L1A 02550000
*   C - STATUS TO CORRECTLY CORRESPOND TO MVS LEVEL                @P7A 02575000
*   C - ALLOW RACF RELEASE 2.1 TO BE SPECIFIED                     @L2A 02600000
*   C - ALLOW RACF RELEASE 2.2 TO BE SPECIFIED                     @L3A 02625000
*                                                                       02650000
********************************************************************    02700000
         MACRO                                                          02750000
&SYMBOL RACAUDIT &ACEE=,                                           @D1C*02800000
               &CLASS=,                                                *02850000
               &ENTITYX=,                                              *02900000
               &EVENT=,                                                *02950000
               &EVQUAL=,                                               *03000000
               &RELEASE=,                                              *03050000
               &RESULT=,                                               *03100000
               &LOGSTR=,                                               *03150000
               &MF=S                                                    03200000
.*  DEFINE LOCAL VARIABLES                                              03250000
         LCLA  &AUDVERS            USED TO CONTAIN VERSION NUMBER       03300000
         LCLA  &BIT0,&BIT1,&BIT2,&BIT3,&BIT4,&BIT5,&BIT6,&BIT7          03350000
         LCLA  &BITA0,&BITA1,&BITA2,&BITA3                              03400000
         LCLA  &BITA4,&BITA5,&BITA6,&BITA7                              03450000
         LCLC  &NX                                                 @P3A 03500000
         LCLC  &EVN000                                             @P3A 03550000
         LCLC  &CLS000                                             @P3A 03600000
.*  DEFINE GLOBAL VARIABLES                                             03650000
         GBLC  &ICHL               LABEL TO START RACAUDIT PLIST        03700000
         GBLC  &ICHMF              AN INDICATOR OF THE MACRO            03750000
.*                                 FORMAT - L, E, S OR M                03800000
         GBLA  &ICHCT              ICHCT - A COUNTER USED TO KEEP       03850000
.*                                 TRACK OF WHERE IN THE PARAMETER      03900000
.*                                 LIST THE PARAMETER IS TO BE          03950000
.*                                 PLACED. ICHCT IS INITIALIZED TO      04000000
.*                                 THE FIRST FULLWORD IN THE LIST       04050000
.*                                 TO BE FILLED IN BY ICHMM,            04100000
.*                                 AND INCREMENTED BY THE INNER         04150000
.*                                 MACRO (ICHMM).                       04200000
         GBLB  &ICHLIST            USED TO TELL THE ICHMM MACRO    @L0A 04250000
.*                                 WHETHER LIST OR EXECUTE PASS    @L0A 04300000
.*                                 IS BEING PROCESSED              @L0A 04350000
         GBLB  &ICHRAC             ICHRAC - A FLAG WHICH IS             04400000
.*                                 INITIALIZED IN THE RACROUTE MACRO    04450000
.*                                 TO COMMUNICATE TO RACXTRT THAT IT    04500000
.*                                 IS BEING INVOKED AS AN INNER MACRO   04550000
.*                                 TO THE RACROUTE MACRO.               04600000
.*                                 EXECUTE PASS.                        04650000
         GBLB  &ICHNTFY            ICHNTFY - A GLOBAL INDICATOR         04700000
.*                                 WHICH IS USED TO COMMUNICATE WITH    04750000
.*                                 ICHMM - THE INNER MACRO. IF ICHNTFY  04800000
.*                                 IS ON, THE INNER MACRO WILL NOT      04850000
.*                                 ISSUE ITS OWN MNOTE IF IT FINDS AN   04900000
.*                                 ERROR-IT WILL MERELY TURN ON THE     04950000
.*                                 ICHINER FLAG AS NOTIFICATION TO THE  05000000
.*                                 OUTER MACRO, WHICH WILL TAKE THE     05050000
.*                                 APPROPRIATE ACTION.                  05100000
.*                                 IF ICHNTFY IS OFF, THE INNER MACRO   05150000
.*                                 WILL TAKE ACTION ON ITS OWN,         05200000
.*                                 IE. ISSUE AN MNOTE WITH A SEVERITY   05250000
.*                                 CODE OF ITS CHOOSING.                05300000
         GBLB  &ICHINER            ICHINER - A GLOBAL INDICATOR         05350000
.*                                 WHICH IS USED BY ICHMM - AN INNER    05400000
.*                                 MACRO - TO TELL THE OUTER MACRO IF   05450000
.*                                 AN ERROR HAS BEEN DETECTED IN A      05500000
.*                                 PARAMETER.  IF ICHINER IS ON, AN     05550000
.*                                 ERROR HAS BEEN DETECTED.             05600000
.********************************************************************** 05650000
.*                     INITIALIZE VARIABLES                           * 05700000
.********************************************************************** 05750000
&NX      SETC  '&SYSNDX'                                                05800000
&ICHNTFY SETB  0                   WANT ICHMM TO ISSUE ERROR MSGS       05850000
&ICHINER SETB  0                   NO ERRORS YET FROM ICHMM             05900000
&AUDVERS SETA  0                   VERSION 0 OF AUDIT PLIST             05950000
.*                                 NOTE: THE MAXIMUM PLIST VERSION      06000000
.*                                   SHOULD BE BE INCREMENTED WHEN      06050000
.*                                   A NEW FIELD IS ADDED (I.E. AS      06100000
.*                                   NEW KEYWORDS ARE ADDED TO THE      06150000
.*                                   MACRO). THE VERSION OF THE PLIST   06200000
.*                                   WILL BE DETERMINED BY THE KEYWORDS 06250000
.*                                   SPECIFIED. FOR EXAMPLE IF THE      06300000
.*                                   MAXIMUM CURRENT VERSION OF THE     06350000
.*                                   PLIST IS 1 BUT NO VERSION 1        06400000
.*                                   KEYWORDS ARE CODED THEN A VERSION  06450000
.*                                   0 PLIST WILL BE GENERATED.         06500000
         AIF   (NOT &ICHRAC).ERR010 RACAUDIT MUST BE INVOKED VIA        06550000
.*                                   RACROUTE REQUEST=AUDIT             06600000
.ERRTN10 ANOP                      RETURN POINT FROM ERROR 10      @P3C 06650000
.********************************************************************** 06700000
.*  VALIDATE RELEASE AND MF                                           * 06750000
.********************************************************************** 06800000
.****  ERROR INVALID RELEASE                                       @L2C 06825000
         AIF   ('&RELEASE(1)' NE '1.9' AND '&RELEASE(1)' NE            X06850000
                '1.9.2' AND '&RELEASE(1)' NE '2.1'                     X06883300
                AND '&RELEASE(1)' NE '2.2').ERR015                 @L3C 06916600
.ERRTN15 ANOP                                                      @P3C 06950000
         AIF   ('&MF' NE 'L' AND '&MF' NE 'S' AND '&MF(1)' NE 'M' AND  X07000000
               '&MF(1)' NE 'E').ERR020      MF MUST BE S, L, M OR E     07050000
.*                                    NOTE: RACROUTE MACRO WILL CONVERT 07100000
.*                                          MF=M TO MF=E IN &ICHMF      07150000
.*                                          BEFORE CALLING RACAUDIT     07200000
.ERRTN20 ANOP                      RETURN POINT FROM ERROR 20      @P3C 07250000
         AIF   ('&ICHMF' EQ 'E' AND (N'&MF NE 2 OR                     X07300000
               '&MF(2)' EQ '')).ERR030   MF=E MUST HAVE PLIST ADDR PARM 07350000
.ERRTN30 ANOP                      RETURN POINT FROM ERROR 30      @P3C 07400000
         AIF   ('&ICHMF' NE 'S').SKIPCHK SKIP CHECK FOR REQUIRED   @P3C 07450000
.*                                      MF=S PARMS IF NOT MF=S          07500000
.********************************************************************** 07550000
.*        MAKE SURE ALL REQUIRED PARMS SUPPLIED FOR MF=S              * 07600000
.********************************************************************** 07650000
.*                                                                1@P5D 07700000
         AIF   ('&EVENT' EQ '').ERR050    EVENT REQUIRED                07750000
         AIF   ('&EVQUAL' EQ '').ERR060   EVQUAL REQUIRED               07800000
.*                                                                1@P5D 07850000
.ERRTN50 ANOP                             RETURN FROM ERROR 50     @P3C 07900000
.ERRTN60 ANOP                             RETURN FROM ERROR 60     @P3C 07950000
.*                                                                 @D1D 08000000
         CNOP  0,4                        ALIGN BAL TO WORD BNDY   @P2C 08050000
         BAL   1,ICH&NX.A                 BRANCH AROUND PLIST, SET R1   08100000
.SKIPCHK ANOP                                                      @P3C 08150000
.********************************************************************** 08200000
.*        SET UP FOR LITERALS IF ANY SUPPLIED                         * 08250000
.********************************************************************** 08300000
&EVN000  SETC  '&EVENT'                                                 08350000
         AIF   ('&EVENT' EQ '').SKIPLT1          SKIP IF NO EVENT  @P3C 08400000
         AIF   ('&EVENT'(1,1) NE '''').SKIPLT1   SKIP NOT LITERAL  @P3C 08450000
&EVN000  SETC  'ICH&NX.E'                 SUBSTITUTE LABEL OF LITERAL   08500000
.*                                         THAT WILL BE GENERATED       08550000
.SKIPLT1 ANOP                                                      @P3C 08600000
&CLS000  SETC  '&CLASS'                                                 08650000
         AIF   ('&CLASS' EQ '').SKIPLT2          SKIP IF NO CLASS  @P3C 08700000
         AIF   ('&CLASS'(1,1) NE '''').SKIPLT2   SKIP NOT LITERAL  @P3C 08750000
&CLS000  SETC  'ICH&NX.C'                 SUBSTITUTE LABEL OF LITERAL   08800000
.*                                         THAT WILL BE GENERATED       08850000
.SKIPLT2 ANOP                                                      @P3C 08900000
         AIF   ('&ICHMF' EQ 'E').MFECONS IF MF=E GO GENERATE            08950000
.*                                           LITERAL CONSTANTS          09000000
.********************************************************************** 09050000
.*                                                                    * 09100000
.*             GENERATE LIST PASS FOR MF=L AND MF=S                   * 09150000
.*                                                                    * 09200000
.********************************************************************** 09250000
&ICHLIST SETB  1                          INDICATE TO ICHMM THAT LIST   09300000
.*                                        PASS IS BEING PROCESSED       09350000
.*    ***************************************************************** 09400000
.*    **   SET VERSION AND LENGTH                                    ** 09450000
.*    ***************************************************************** 09500000
&ICHL    DS    0F                         START OF RACAUDIT PLIST  @P1C 09550000
ICH&NX.B DC    AL2(&AUDVERS)              VERSION                       09600000
         DC    AL2(ICH&NX.F-ICH&NX.B)     LENGTH                        09650000
.*    ***************************************************************** 09700000
.*    **   EVENT KEYWORD                                             ** 09750000
.*    ***************************************************************** 09800000
          ICHMM &EVN000                                                 09850000
.*    ***************************************************************** 09900000
.*    **   EVQUAL KEYWORD                                            ** 09950000
.*    ***************************************************************** 10000000
         AIF   ('&EVQUAL' NE '').CHKREG1  NO EVQUAL, SKIP DC(0)    @P3C 10050000
         DC    AL2(0)                     EVENT QUALIFIER               10100000
         AGO   .RESFLD                    GO DECLARE RESERVED FIELD     10150000
.CHKREG1 ANOP                             SEE IF REGISTER PASSED   @P3C 10200000
         AIF   ('&EVQUAL'(1,1) EQ '(').REGFRM1  IF REGISTER PASSED @P3C 10250000
.*                                        SKIP OVER LITERAL DC          10300000
         DC    AL2(&EVQUAL)               EVENT QUALIFIER               10350000
         AGO   .RESFLD                    GO DECLARE RESERVED FIELD     10400000
.REGFRM1 ANOP                             REGISTER FORM USED       @P3C 10450000
         AIF   ('&ICHMF' EQ 'L').ERR080   REGISTER ILLEGAL FOR MF=S     10500000
.ERRTN80 ANOP                             RETURN FROM ERROR 75     @P3C 10550000
         DC    AL2(0)                     EVENT QUALIFIER               10600000
.RESFLD  ANOP                                                           10650000
         DC    AL2(0)                     RESERVED                      10700000
.*    ***************************************************************** 10750000
.*    **   CLASS KEYWORD                                             ** 10800000
.*    ***************************************************************** 10850000
         ICHMM &CLS000                                                  10900000
.*    ***************************************************************** 10950000
.*    **   ENTITYX KEYWORD                                           ** 11000000
.*    ***************************************************************** 11050000
         ICHMM &ENTITYX                                                 11100000
.*    ***************************************************************** 11150000
.*    **   ACEE KEYWORD                                              ** 11200000
.*    ***************************************************************** 11250000
         ICHMM &ACEE                                                    11300000
.*    ***************************************************************** 11350000
.*    **   LOGSTR KEYWORD                                        @D1A** 11400000
.*    ***************************************************************** 11450000
         ICHMM &LOGSTR                                           @D1A   11500000
.*    ***************************************************************** 11550000
.*    **   VALIDATE AND GENERATE RESULT BYTE                     @D1A** 11600000
.*    ***************************************************************** 11650000
         AIF   ('&RESULT' EQ '' OR '&RESULT' EQ 'SUCCESS').NORES @D1A   11700000
         AIF   ('&RESULT' NE 'FAILURE').RESERR                   @D1A   11750000
.*                                                                      11800000
         DC    AL1(1)            RESULT OF FAILURE SPECIFIED     @D1A   11850000
         DC    AL3(0)            RESERVED                        @D1A   11900000
         AGO   .CONT1                                            @D1A   11950000
.RESERR  ANOP                                                    @D1A   12000000
         AGO   .ERR090                                           @D1A   12050000
.ERRTN90 ANOP                                                    @D1A   12100000
.NORES   ANOP                                                    @D1A   12150000
         DC    AL1(0)            RESULT BYTE                     @D1A   12200000
         DC    AL3(0)            RESERVED                        @D1A   12250000
         AGO   .CONT1                                            @D1A   12300000
.CONT1   ANOP                                                    @D1A   12350000
         DC    4F'0'                      RESERVED               @P6C   12400000
ICH&NX.F DS    0H                         END OF RACAUDIT PLIST         12450000
         AGO   .MFLCONS           GO GENERATE CONSTANTS FOR MF=L,S @P3C 12500000
.********************************************************************** 12550000
.*        GENERATE LITERAL CONSTANTS IF NEEDED                        * 12600000
.********************************************************************** 12650000
.MFECONS ANOP                        MF=E CONSTANT GENERATION           12700000
         B     ICH&NX.A                   BRANCH AROUND CONSTANTS       12750000
.MFLCONS ANOP                        MF=L,S CONSTANT GENERATION    @P3C 12800000
         AIF   ('&EVENT' EQ '').SKIPLT3          SKIP IF NO EVENT  @P3C 12850000
         AIF   ('&EVENT'(1,1) NE '''').SKIPLT3   SKIP NOT LITERAL  @P3C 12900000
ICH&NX.E DC    CL8&EVENT                  EVENT NAME                    12950000
.SKIPLT3 ANOP                                                      @P3C 13000000
         AIF   ('&CLASS' EQ '').SKIPLT4          SKIP IF NO CLASS  @P3C 13050000
         AIF   ('&CLASS'(1,1) NE '''').SKIPLT4   SKIP IF LITERAL   @P3C 13100000
ICH&NX.C DC    CL8&CLASS                  CLASS NAME                    13150000
.SKIPLT4 ANOP                                                      @P3C 13200000
         AIF   ('&ICHMF' EQ 'L').MEND     FINISHED IF IF MF=L           13250000
.********************************************************************** 13300000
.*                                                                    * 13350000
.*               GENERATE EXECUTE PASS FOR FOR MF=S,M,E               * 13400000
.*                                                                    * 13450000
.********************************************************************** 13500000
         AIF   ('&ICHMF' NE 'S').LOADADR IF MF NOT S GO LOAD ADDR  @P3C 13550000
.*                                       OF PLIST INTO R1               13600000
ICH&NX.A LA    1,0(1)                    CLEAR HIGH BYTE/BIT OF REG 1   13650000
         AGO   .SKPADDR                SKIP LOADING ADDR OF PLIST  @P3C 13700000
.LOADADR ANOP                                                      @P3C 13750000
ICH&NX.A A     1,36(1)                   ADDRESS OF AUDIT PLIST IN R1   13800000
.SKPADDR ANOP                                                      @P3C 13850000
&ICHLIST SETB  0                     INDICATE TO ICHMM THAT EXECUTE     13900000
.*                                   OR MODIFY IS BEING PROCESSED       13950000
&ICHCT   SETA  1                     INDICATE TO ICHMM THAT NEXT        14000000
.*                                   FIELD IS 1 WORD OFF OF R1          14050000
.*    ***************************************************************** 14100000
.*    **   EVENT KEYWORD                                             ** 14150000
.*    ***************************************************************** 14200000
         ICHMM &EVN000                                                  14250000
.*    ***************************************************************** 14300000
.*    **   EVQUAL KEYWORD                                            ** 14350000
.*    ***************************************************************** 14400000
         AIF   ('&EVQUAL' EQ '').CLSKEY   NO EVQUAL, TRY CLASS     @P3C 14450000
         AIF   ('&EVQUAL'(1,1) EQ '(').REGFRM2  IF REGISTER PASSED @P3C 14500000
.*                                        SKIP OVER LITERAL STORE       14550000
         LA    15,&EVQUAL                 EVENT QUALIFIER               14600000
         STH   15,8(1)                    EVENT QUALIFIER               14650000
         AGO   .CLSKEY                    GO PROCESS CLASS KEY     @P3C 14700000
.REGFRM2 ANOP                             REGISTER FORM USED       @P3C 14750000
         STH   &EVQUAL(1),8(1)            EVENT QUALIFIER               14800000
.CLSKEY  ANOP                                                      @P3C 14850000
&ICHCT   SETA  &ICHCT+1                  BUMP ICHMM TO NEXT WORD OFFSET 14900000
.*    ***************************************************************** 14950000
.*    **   CLASS KEYWORD                                             ** 15000000
.*    ***************************************************************** 15050000
         ICHMM &CLS000                                                  15100000
.*    ***************************************************************** 15150000
.*    **   ENTITYX KEYWORD                                           ** 15200000
.*    ***************************************************************** 15250000
         ICHMM &ENTITYX                                                 15300000
.*    ***************************************************************** 15350000
.*    **   ACEE KEYWORD                                              ** 15400000
.*    ***************************************************************** 15450000
         ICHMM &ACEE                                                    15500000
.*    ***************************************************************** 15550000
.*    **   LOGSTR KEYWORD                                        @D1A** 15600000
.*    ***************************************************************** 15650000
         ICHMM &LOGSTR                                                  15700000
.*                                                                      15750000
         AIF   ('&ICHMF' EQ 'S').MEND                            @D1A   15800000
.*                                                                      15850000
         AIF   ('&RESULT' EQ '').CONT1S                          @D1A   15900000
         AIF   ('&RESULT' EQ 'SUCCESS').NORES1                   @D1A   15950000
         AIF   ('&RESULT' NE 'FAILURE').RESERR1                  @D1A   16000000
.*                                                                      16050000
         LA    15,1              RESULT OF FAILURE SPECIFIED     @D1A   16100000
         STC   15,28(1)          RESERVED                        @D1A   16150000
         AGO   .CONT1S                                           @D1A   16200000
.RESERR1 ANOP                                                    @D1A   16250000
         AGO   .ERR095                                           @D1A   16300000
.ERRTN95 ANOP                                                    @D1A   16350000
.NORES1  ANOP                                                    @D1A   16400000
         LA    15,0              RESULT BYTE                     @D1A   16450000
         STC   15,28(1)          RESERVED                        @D1A   16500000
.CONT1S  ANOP                                                    @D1A   16550000
         AGO   .MEND                      FINISHED WITH EXECUTE PATH    16600000
.********************************************************************** 16650000
.*        ERROR PROCESSING                                            * 16700000
.********************************************************************** 16750000
.ERR010  ANOP                                                           16800000
         MNOTE 12,'*** RACAUDIT MUST BE INVOKED VIA RACROUTE'           16850000
         AGO   .ERRTN10                                            @P3C 16900000
.ERR015  ANOP                                                           16950000
         MNOTE 12,'*** INVALID RELEASE SPECIFIED FOR KEYWORDS'          17000000
         AGO   .ERRTN15                                            @P3C 17050000
.ERR020  ANOP                                                           17100000
         MNOTE 12,'*** MF MUST BE S, L, E OR M'                         17150000
         AGO   .MEND                                                    17200000
.ERR030  ANOP                                                           17250000
         MNOTE 12,'*** INVALID PLIST ADDR SUPPLIED FOR MF'              17300000
         AGO   .ERRTN30                                           3@P5D 17350000
.ERR050  ANOP                                                           17400000
         MNOTE 12,'*** EVENT REQUIRED ON MF=S'                          17450000
         AGO   .ERRTN50                                            @P3C 17500000
.ERR060  ANOP                                                           17550000
         MNOTE 12,'*** EVQUAL REQUIRED ON MF=S'                         17600000
         AGO   .ERRTN60                                           4@P5D 17650000
.ERR080  ANOP                                                           17700000
         MNOTE 12,'*** REGISTER NOT ALLOWED ON LIST FORM'               17750000
         AGO   .ERRTN80                                            @P3C 17800000
.ERR090  ANOP                                                      @D1A 17850000
         MNOTE 12,'*** IF RESULT IS SPECIFIED, VALID OPERANDS ARE SUCCEX17900000
               SS, OR FAILURE'                                     @D1A 17950000
         AGO   .ERRTN90                                            @D1A 18000000
.ERR095  ANOP                                                      @D1A 18050000
         MNOTE 12,'*** IF RESULT IS SPECIFIED, VALID OPERANDS ARE SUCCEX18100000
               SS, OR FAILURE'                                     @D1A 18150000
         AGO   .ERRTN95                                            @D1A 18200000
.MEND    MEND                               EL FIN                      18250000
