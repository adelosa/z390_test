         MACRO                                                          00001000
         IOINTER  &MOD=                                                 00002000
         GBLA  &AG1       NUMBER OF PUBS                       @D51ODGL 00003000
         GBLB  &BGDEBUG   DEBUGGING SERVICES                   @D34SDAP 00004000
         GBLB  &IOINTER   IOINTER PRINT CONTROL SWITCH         @D34SDAP 00005000
         GBLB  &VSE260    IOINTER RELEASE CONTROL SWITCH       @D34SDAP 00007000
         GBLB  &VSE270    IOINTER RELEASE CONTROL SWITCH       @D34SDAP 00008000
         GBLB  &VSE280    IOINTER RELEASE CONTROL SWITCH       @DSCSI   00009000
         GBLB  &VTAM31    VTAM-31 SWITCH                       @DY46396 00011380
         GBLB  &VSE410    IOINTER RELEASE CONTROL SWITCH       @D41ADAP 00011980
         AIF   ('&MOD' EQ 'IJBPVPR').YPVP010                            00014000
         ACTR  200        LOOP CONTROL                         @D35IDAP 00015000
***************************************************************@D319DAP 00016000
*                                                              @D319DAP 00017000
*        LICENSED MATERIALS-PROPERTY OF IBM                    @D319DAP 00018000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"           @D319DAP 00019000
         AIF   (&VSE410).Y410000                                        00020090
*        5686-CF7 (C) COPYRIGHT IBM CORP. 1977, 2005           @D67FDAP 00020180
         AGO   .N410000                                                 00020270
.Y410000 ANOP                                                           00020360
*        5686-CF8 (C) COPYRIGHT IBM CORP 1979, 2005            @D41ADAP 00020450
.N410000 ANOP                                                           00020540
*        SEE COPYRIGHT INSTRUCTIONS                            @D319DAP 00021000
*                                                              @D319DAP 00022000
***************************************************************@D319DAP 00023000
.* /* START OF SPECIFICATIONS ****                             @D31DCHR 00024000
.*01  MODULE-TYPE = MACRO                                      @D31DCHR 00025000
.*02    PROCESSOR = ASSEMBLER                                  @D31DCHR 00026000
.*      SOFT WAIT WHEN RAS RETRY SIO RECEIVES CC3              @DA30327 00027000
.*      OPERATOR CONSOLE LOCKED                                @DA33194 00028000
.*      SENSE DATA INCORRECT PROVIDED                          @DA33459 00029000
.*      LOOP ON MSG0E09I AFTER UNIT CHECK ON PRINTER (KKJ0183) @DA33589 00030000
.*      LOOP ON MSG0P46I AFTER CUE+BUSY FROM A TAPE CU         @DA33881 00031000
.*      SOFTWAIT, PUB QUEUED FOR RECOVERY AFTER ERP COMPLETE   @DA34415 00032000
.*      TP TERMINAL HANGS AFTER POWER-ON READY INTERRUPT       @DA34538 00033000
.*      CCB SHOULD BE UPDATED IF COMMAND CHAIN RETRY SPECIFIED @DA34431 00034000
.*      PUBX ERBLK OVERLAY DUE TO INCORRECT CLEAR LENGTH       @DA34728 00035000
.*      OLTEP DOES NOT GET CONTROL FOR UNSOLICITED INTERRUPTS  @DA35296 00036000
.*      SIO LOOP WITH CCB OF ZERO'S FOR D/T3540                @DA35139 00037000
.*      OLTEP ADDRESSABILITY SETUP FOR VAE                     @DA35736 00038000
.*      NEW DEVICE SUPPORT                                     @D21KDAP 00039000
.*      ECB TRAFFIC BIT NOT POSTED RUNNING IN VM/E MODE AND    @DA35862 00040000
.*      THE LOGICAL UNIT IS SYSLOG                             @DA35862 00041000
.*      SPE NEW FUNCTION, SUPPORT FOR D/T9370                  @D21ODAP 00042000
.*      DEVICE HANG AFTER SPE DY36233                          @DA36688 00043000
.*      WAITFFF DUE TO DAT BIT IS SET OF AND THE CCB HAS THE   @DA36623 00044000
.*      USER TRANSLATED BIT ON                                 @DA36623 00045000
.*      SYSTEM LOOP AFTER CU BUSY ON A SENSE-TASK I/O REQUEST  @DA36777 00046000
.*      RESTART OF A VTAM X.25 LINE AFTER ERP RECORDING        @DA36929 00047000
.*      BTAM TERMINAL HANG AFTER CHANNEL BUSY                  @DA37160 00048000
.*      DASD ERP NOT COMPLETED                                 @DA37004 00049000
.*      AVR FOR TAPE                                           @D21LDAP 00050000
.*      PROVIDE INDICATION FOR VTAM IN CASE MULTIPLE ERRORS    @DA37770 00051000
.*      MAINTAIN THE CORRECT ACTIVE OLD TIME STAMP FOR MIH     @DA37824 00052000
.*      IGNORE READY INTERRUPT FROM ALTERNATE PATH             @DA37952 00053000
.*      SUPPORT OF DISCONNECTED CONSOLE                        @D31YDAP 00054000
.*      31-BIT REAL AND ESA SUPPORT                            @D51GDAP 00055000
.*      ACCESS REGISTERS                                       @D51KDTP 00056000
.*      MORE DEVICES SUPPORT                                   @D51ODGL 00057000
.*      NEW CONSOLE SUPPORT                                    @D61BDAP 00058000
.*      IQDIO DEVICE SUPPORT                                   @D67FDAP 00059000
.*      MSG0E06I IF DEV STATUS IS X'14'  WAIT DURING IPL       @DA40072 00060000
.*      INVALID CUU AWAITING READY                             @DA40072 00061000
.*      ALLOW 'CANCEL CUU' INSTEAD OF 'RC' FOR AR              @DA40195 00062000
.*      INTERRUPTION SUBCLASS SET TO 3                         @DA40460 00063000
.*      INCORRECT CHANNEL ADDRESS IN MSG0PXX                   @DA40546 00064000
.*      HANDLING UNSOLICITED INTERRUPTS                        @DA40559 00065000
.*      SYSTEM CONSOLE HANG IN CASE OF UNSOLICITED I/O ERROR   @DA40897 00066000
.*      PROVIDE DEBUG ENTRY ON RETURN FROM CHANNEL END APPEND. @DA40900 00067000
.*      HARD WAIT FFA OPERAND EXCEPTION ON SSCH INSTRUCTION    @DA41224 00068000
.*      HARD WAIT FFB IN RAST10 DUE TO ERRQMSG NOT SET PROPERLY@DA41206 00069000
.*      PASS CONTROL TO OLTEP APPENDAGE IF DEFERRED INTERRUPT  @DA41234 00070000
.*      ENSURE MIH MESSAGE FOR LOST CHANNEL END                @DA41356 00071000
.*      ENSURE NOT READY-TO-READY INTERRUPT RECOGNITION        @DA41772 00072000
.*      ENSURE RECORDING FOR SIM RECORDS FOR CONVERTED CCW'S   @DA41879 00073000
.*      PREVENT H/W IN CASE OF UNSOLICITED CC=1 STATUS         @DA42095 00074000
.*      PREVENT STATUS ACCUMULATION FOR ALTERNATE I/O INTERRPT.@DA42158 00075000
.*      PREVENT MSG1T50A FOR OFFLINED DEVICES                  @DA42335 00076000
.*      LOST RECORDS AFTER X'24' DEVICE STATUS                 @DA42376 00077000
.*      SNS-TASK NOT POSTED DUE TO ERROR ENTRY IN USE BY RAS   @DA42411 00078000
.*      ENSURE PATH VERIFICATION WHEN DEVICES ARE ATTACHED     @DA42602 00079000
.*      PREVENT PATH VERIFICATION PROCESS FOR SINGLE PATHER    @DA42602 00080000
.*      HANDLE RESERVE/RELEASE WITH THE SAME PATH MASK         @DA42692 00081000
.*      MSG0P31A WITH SOB2 SET IN COMREG                       @DA42904 00082000
.*      ENSURE SINGLE PATH ACCESS WHILE DEVICE IS RESERVED     @DA42913 00083000
.*      PROVIDE CORRECT CHANNEL PATH /PREVENT H/W IN IOINTER   @DA42922 00084000
.*      PREVENT 401 ERROR FOR SYSLOG                           @DA42940 00085000
.*      PREVENT SYSLOG TO BE DISCONNECTED IF ONE PATH AVAILABLE@DA42940 00086000
.*      RECOGNIZE NOT OPERATIONAL TO OPERATIONAL CONDITION     @DA42963 00087000
.*      PREVENT LOOP IN CASE OF CHANNEL CHECK FOR VTAM DEVICES @DA43372 00088000
.*      RESET ERROR BITS IN CHANQ IF ERROR ENTRY BEING REUSED  @DA43415 00089000
.*      PREVENT PARTITION HANG IN MULTI-PROCESSOR ENVIRONMENT  @DA43510 00090000
.*      PREVENT DELAYED CRW PROCESSING                         @DA43697 00091000
.*      INDICATE DEVICE INACTIVE IN CASE OF PROGR./PROT. CHECK @DA43858 00092000
.*      PREVENT DEVICE/TASK HANG IN CASE OF MIH D-TYPE MESSAGE @DA43896 00093000
.*      PREVENT SNS-TASK LOOP IN CASE OF PERMANENT DEVICE BUSY @DA44035 00094000
.*      PREVENT HARD/WAIT IN CASE OF LOST CHANNEL END INTERRUPT@DA44201 00095000
.*      9348 IS NOT A CARTRIDGE DEVICE                         @DA44241 00096000
.*      RECOGNIZE AND PROCESS PENDING "READY" INTERRUPTS       @DA44277 00097000
.*      ENSURE BASE POINTERS TO BE RELEASED PROPERLY           @DA44277 00098000
.*      IMMEDIATELY RETRY POWER PRINT REQUESTS WHEN CHAN9/12   @DA44311 00099000
.*      ACCESSING IRB IN SNS-TASK CAUSES  ERR ON RECOVERY      @DA44311 00100000
.*      SIMULATE DEFERRED CC=3 FOR DETACHED DEVICES            @DA44364 00101000
.*      PREVENT SIMULATION OF CC=3 FOR DETACHED DEVICES PAST CE@DA44462 00102000
.*      PREVENT STSCH INSTRUCTION FOR SUBCHANNEL FFFF          @DA44471 00103000
.*      HARD WAIT DUE TO STORAGE OVERLAY WHEN RUNNING BTAM     @DA44498 00104000
.*      FORCE DOM PROCESSING FOR DEVICES THAT CAUSED AN MIH    @DA44547 00105000
.*            DECISION MSG REGARDLES OF THE INTERRUPT STATUS.  @DA44547 00106000
.*      PROPERLY RECOGNIZE THE OSA RESET CONDITION             @DA44616 00107000
.*      ENSURE SYS-TASK ERROR ENTRY TO BE USED FOR NOP-OP DEV. @DA44616 00108000
.*      PREVENT PATH VERIFICATION FOR OFFLINED PATHES          @DA44616 00109000
.*      PREVENT 0P31A FOR VIRTUAL FBA DEVICES                  @DA44634 00110000
.*      ENSURE INTERRUPT INFO TO BE SAVED INTO PUBX ENTRY      @DA44738 00111000
.*      PREVENT SYSTEM HANG IN CASE WHEN MIH REPLY OUTSTANDING @DA44841 00112000
.*      PROVIDE FOR VSE CONTROLLED I/O MEASURING DATA          @DA44841 00113000
.*      PREVENT MIH LOOP                                       @DA44993 00114000
.*      PREVENT SVT HANG WHEN SAME SUBCHANNEL IS BEING REUSED  @DY45062 00115000
.*      PREVENT SNS LOOP WHEN MIH DECISION MSG OUTSTANDING     @DY45062 00116000
.*      PREVENT DEVICE TO BE FLAGGED INTVRQ ON DELAYED RETRY   @DY45299 00117000
.*      PREVENT POWER PNET LOOP                                @DY45... 00118000
.*      SNS-TASK LOOP AFTER MIH REPLY WAS ENTERED              @DY45768 00119000
.*      PREVENT APPENDAGE TO BE ENTERED WHILE IN EOT PROCESS   @DY45817 00120000
.*      QEDERR BIT SET, DELAYED IO PROCESSING RESTARTS IO REQ  @DY46053 00121000
.*      VDISK WAIT, INTPARM SET WHEN SIMINTER IS ENTERED       @DY46053 00122000
.*      PSW 00EEEEEE: STACKED IO, NO INTPARM, FE SET AT X45    @DY46074 00123000
.*      PROVIDE FOR QDIO THIN INTERRUPT PROCESSING             @DY46075 00124000
.*      ENSURE LONG-BUSY-RETRY DURING MIH PROCESSING           @DY46139 00125000
.*      RCD AREA HAS BEEN INCREASED FROM 256 TO 512 BYTES      @DY46299 00125590
.*      ENSURE MIH TO RUN IN 24-BIT MODE                       @DY46337 00125680
.*      DO NOT WAIT FOR CRW PROCESSING FOR 'NOT OP' UNDER VM   @DY46413 00125780
.*      NO PATH VERIFY AFTER A NOT OPERATIONAL                 @DY46494 00125880
.*      DO NOT CALL VTAM APPENDAGE IF ALREADY COPY BLOCK BOUND @DY46580 00125940
.*      HARDWAIT IF INTPARM ADDRESS IS NOT ADDRESSABLE         @DY46585 00125970
.*                                                                    * 00126000
.**** END OF SPECIFICATIONS ***/                                        00127000
.********************************************************************** 00128000
.*                                                                    * 00129000
.*       GENERAL FUNCTIONS                                            * 00130000
.*                                                                    * 00131000
.*       ALL I/O INTERRUPTIONS ARE HANDLED BY THIS CODE.              * 00132000
.*       THE CHANNEL AND DEVICE STATUS ARE ANALYSED. IN CASE OF       * 00133000
.*       CHANNEL ERRORS RAS IS ACTIVATED. IN CASE OF DEVICE ERRORS    * 00134000
.*       AN ENTRY IS BUILT IN THE ERROR QUEUE AND ERP IS ACTIVATED    * 00135000
.*                                                                    * 00136000
.*       WHEN NO ERRORS ARE DETECTED (ANY MORE) THE WAIT BIT IN THE   * 00137000
.*       THE CCB IS POSTED AND THE REQUESTING TASK IS TAKEN OUT OF    * 00138000
.*       THE WAIT STATE, IF NECESSARY.                                * 00139000
.*                                                                    * 00140000
.*                                                                    * 00141000
.*       GENERAL REGISTER USAGE                                       * 00142000
.*                                                                    * 00143000
.*       R1   POINTER TO CCB                                          * 00144000
.*       R2   POINTER TO PUBX ENTRY                                   * 00145000
.*       R3   POINTER TO PUB                                          * 00146000
.*       R4   POINTER TO CHANNEL-QUEUE ENTRY                          * 00147000
.*       R5   WORK REGISTER                                           * 00148000
.*       R6   DISPATCHER BASE ADDRESS                                 * 00149000
.*       R7   LINK AND WORK REGISTER                                  * 00150000
.*       R8   LINK AND WORK REGISTER                                  * 00151000
.*       R9   IOINTER    BASE ADDRESS                                 * 00152000
.*       R10  POINTER TO PIB OF REQUESTING TASK                       * 00153000
.*       R11  POINTER TO TIB OF REQUESTING TASK                       * 00154000
.*       R12  SGSERI     BASE ADDRESS   /   RESERVED FOR MAINTENANCE  * 00155000
.*       R13  SGSCHED    BASE ADDRESS                                 * 00156000
.*       R14  WORK REGISTER                                           * 00157000
.*       R15  WORK REGISTER                                           * 00158000
.*                                                                    * 00159000
.********************************************************************** 00160000
         SPACE 3                                                        00161000
         AIF   (NOT &IOINTER).NPRT010                          @D14BDAP 00162000
         PRINT GEN                ENSURE PRINTING OF IOINTER   @D37DDAP 00163000
.NPRT010 ANOP                                                           00164000
         DC    CL8'IOINTER '                                   @D14BDAP 00165000
         DC    CL8'DY46585 '      CHANGE ACTIVITY CODE         @DY46585 00174720
         AIF   (&VSE280).Y280020                                        00175000
         TITLE 'VSE SUPERVISOR     IOINTER    INTERRUPT RESPONSE BLOCK' 00176000
         DC    CL8'IRB'           INTERRUPT RESPONSE BLOCK     @D51GDAP 00177000
         IRB   DSECT=NO           GENERATE IRB                 @D51GDAP 00178000
         TITLE 'VSE SUPERVISOR     IOINTER  SUBCHNL. INFORMATION BLOCK' 00179000
         DC    CL8'SCHIB'         INTERRUPT RESPONSE BLOCK     @D51GDAP 00180000
         SCHIB DSECT=NO           GENERATE SCHIB               @D51GDAP 00181000
.Y280020 ANOP                                                           00182000
         TITLE 'VSE SUPERVISOR     IOINTER  INTERRUPT SIMULATION RTN. ' 00183000
.*NOTE                                                                  00184000
.*       THIS PART OF CODE MUST BE EXECUTED IN 31-BIT MODE              00185000
.*       BECAUSE INPUT DATA (R1) COULD RESIDE IN 31-BIT STORAGE         00186000
.*                                                                      00187000
         USING SIMINTER,R7        SIMULATION BASE POINTER      @DY46139 00188000
SIMINTER DS    0H'0'              INTERRUPT SIMULATION ROUTINE @D52HDAP 00189000
         XC    INTPARM,INTPARM    RESET INTERRUPT PARAMETER    @DY46053 00190000
SIMINT10 DS    0H'0'              INTERRUPT AND INTPARM SET    @D52HDAP 00191000
         MVC   CSW(8),0(R1)       SET UP CSW                   @D52HDAP 00192000
*HERE WE MAY WANT TO SAVE THE PUB ADDRESS IN INTPARM           @D68ADAP 00193000
         STH   R2,CHNADR          SET CUU ADDRESS IN LOW CORE  @D52HDAP 00194000
         MVI   IOINF+1,XFF        INDICATE SIMULATED INTERRUPT @D52HDAP 00195000
         L     R9,AINTER          GET IOINTER BASE ADDRESS     @D52HDAP 00196000
         DROP  R7                 RELEASE SIMINTER BASE POINTER@DY46139 00197000
         BSM   0,R9               DO INTERRUPT PROCESSING      @D66ADAP 00198000
         SPACE 3                                               @D36IDAP 00199000
         AIF   (NOT &VSE280).N280000                           @D68ADAP 00200000
INTTAB1  DS    0D                 I/O SUBSYSTEM FACILITIES     @D68ADAP 00201000
CHSC#FLG DC    XL4'FF000000'      FLAG BYTE                    @D68ADAP 00202000
ACHSC#BA DC    A(0)               ADDRESS OF CHSC-BUFFER       @D28DR06 00203000
CHSC#GEN DC    XL32'00'           GENERAL INFORMATION          @D68ADAP 00204000
CHSC#CHS DC    XL32'00'           CHSC SPECIFIC DATA           @D68ADAP 00205000
         AGO   .YMDV000                                        @D68ADAP 00206000
.N280000 AIF   (&AG1 LE 254).NMDV000                           @D51ODGL 00207000
INTTAB1  DC    256XL2'FF00'       FIRST LEVEL SCAN TABLE       @D51ODGL 00208000
.NMDV000 AIF   (&AG1 GT 254).YMDV000                           @D51ODGL 00209000
INTTAB1  DC    256XL1'FF'         FIRST LEVEL SCAN TABLE       @D35IDAP 00210000
.YMDV000 ANOP                                                  @D51ODGL 00211000
         USING INTRTN,R9          INTERRUPT  BASE REGISTER     @D35IDAP 00271000
         USING DISP,R6            DISPATCHER BASE REGISTER     @D35IDAP 00272000
         SPACE 3                                               @D369DAP 00273000
INTRTN   B     INTFPUB            I/O INTERRUPT ENTRY POINT    @D35IDAP 00274000
         SPACE 1                                               @D369DAP 00275000
AINTTAB1 DC    A(INTTAB1)         1ST LEVEL SCAN TABLE ADDRESS @D35IDAP 00276000
.*                                                                      00277000
.*       IPL   COMMUNICATION INFORMATION                                00278000
.*                                                                      00279000
AINTTAB2 DC    2F'0'              2ND LEVEL SCAN TABLE ADDRESS @D35IDAP 00280000
         ORG   AINTTAB2+4                                      @D35IDAP 00281000
AINTTAB3 DC    F'0'               3RD LEVEL SCAN TABLE ADDRESS @D35IDAP 00282000
         ORG   ,                                               @D35IDAP 00283000
         TITLE 'VSE SUPERVISOR     IOINTER     INTERRUPT INITIATOR    ' 00288000
         AIF   (NOT &BGDEBUG).NTXT010                          @D35IDAP 00289000
***************************************************************@D35IDAP 00290000
*                                                              @D35IDAP 00291000
*  FUNCTION:                                                   @D35IDAP 00292000
*            FIND PUB ENTRY THAT IS ASSOCIATED TO              @D35IDAP 00293000
*            THE I/O INTERRUPT                                 @D35IDAP 00294000
*  ENTRY POINT:                                                @D35IDAP 00295000
*            1. ENTERED  VIA I/O INTERRUPT                     @D35IDAP 00296000
*            2. ENTERED  FROM DEQUEUE ROUTINE                  @D35IDAP 00297000
*  EXITS:                                         TO LABEL     @D35IDAP 00298000
*            I/O INITIATOR                           INTNOPUB  @D35IDAP 00299000
*            STATUS ANALYSER                         TSTCSW    @D35IDAP 00300000
*  INPUTS:                                                     @D35IDAP 00301000
*            R6= ADDRESS OF DISPATCHER                         @D35IDAP 00302000
*            R9= IOINTER BASE REGISTER                         @D35IDAP 00303000
*         IOINF= INTERRUPT ID                                  @D61NDAP 00304000
*       INTPARM= INTERRUPT PARAMETER                           @D61NDAP 00305000
*  OUTPUTS:                                                    @D35IDAP 00306000
*  REGISTERS:                                                  @D35IDAP 00307000
*            R0= UNPREDICTABLE                                 @D36IDAP 00308000
*            R1= ADDRESS OF CCB                                @D35IDAP 00309000
*            R2= ADDRESS OF PUBX                               @D35IDAP 00310000
*            R3= ADDRESS OF PUB                                @D35IDAP 00311000
*            R4= ADDRESS OF CHANQ ENTRY                        @D35IDAP 00312000
*            R5= UNPREDICTABLE                                 @D35IDAP 00313000
*            R7= UNPREDICTABLE                                 @D35IDAP 00314000
*            R8= UNPREDICTABLE                                 @D35IDAP 00315000
         CL    R3,APUBTAB         INTPARM BELOW PUBTAB?        @DY46585 00315200
         BL    INTFINDCUU         YES                          @DY46585 00315400
         CL    R3,APUBEND         INTPARM ABOVE PUBTAB?        @DY46585 00315600
*            RA= ADDRESS OF PIB                                @D35IDAP 00316000
*  SUBROUTINES:                                                @D35IDAP 00317000
*         CQDSP= SET UP I/O REGISTERS AND POINTERS IN LOW CORE @D35IDAP 00318000
*                                                              @D35IDAP 00319000
***************************************************************@D35IDAP 00320000
         SPACE 3                                               @D35IDAP 00321000
.NTXT010 ANOP                                                  @D35IDAP 00322000
INTFPUB  DS    0H'0'              I/O INTERRUPT ENTRY POINT    @D51GDAP 00323000
         SLR   R4,R4              CLEAR TEMP CCT POINTER       @D51GDAP 00324000
         L     R1,IOINF           GET SUBCHANNEL NUMBER        @D28ADAP 00325000
         L     R3,INTPARM         GET INTERRUPTION PARAMETER   @D51GDAP 00326000
         TM    INTPARM+3,ONDWB    PARM ON DOUBLE WORD BNDRY?   @DY46585 00327190
         BZ    INTFPUB4           YES, OK                      @DY46585 00327380
         SLR   R3,R3              INDICATE PARM IS INVALID     @DY46585 00327570
INTFPUB4 MVC   FLOGA+16(8),IOINF  SAVE FOR DEBUG               @D28ADAP 00327760
         ST    R4,INTPARM         CLEAR FOR NEXT PATH THROUGH  @D28ADAP 00328000
         CLI   IOINF+1,1          IS IT A REAL INTERRUPT       @D51GDAP 00329000
         BNE   INTSIMUL           NO, =======================> @D51GDAP 00330000
         XC    CSW(8),CSW         RESET CSW                    @DY46074 00354000
         ST    R1,XASUBSCH        SAVE SUBCHANNEL ID           @D51GDAP 00355000
         L     R2,IRB$            ADDRESS OF IRB               @D28ADAP 00356000
         USING IRB,R2             IRB BASE POINTER             @D28ADAP 00357000
         CL    R3,APUBTAB         INTPARM BELOW PUBTAB?        @DY46585 00358390
         BL    INTFINDCUU         YES                          @DY46585 00358780
         CL    R3,APUBEND         INTPARM ABOVE PUBTAB?        @DY46585 00359170
         BNL   INTFINDCUU         YES                          @DY46585 00359560
         USING PUBADR,R3          PUB BASE POINTER             @D51GDAP 00360000
         MVC   CHNADR(2),PUBCHANN SET CUU ADDRESS IN LOW CORE  @D51GDAP 00361000
INTFP000 DS    0H'0'              PROCEED REAL INTERRUPT PROC. @D51GDAP 00362000
         TSCH  IRB                IS INTERRUPT PENDING         @D51GDAP 00366000
         BNZ   IOINTEXT           NO,  ======================> @DAOM083 00367000
         LR    R4,R1              NON-ZERO = REAL INTERRUPT    @DA44738 00368000
         DROP  R3                 RELEASE PUB BASE             @D61NDAP 00369000
         MVC   CSW(1),IRBSTB0     SET KEY AND CC               @D51GDAP 00370000
         NI    CSW,XF0+IRBCC      RESET UNKNOWN BITS IF ANY    @D51GDAP 00371000
         MVC   CSW+1(7),IRBCCW+1  SET CCW ADDR.+STATUS+RES.CNT @D51GDAP 00372000
         TM    IRBSTB3,IRBSCAS+IRBSCIS+IRBSCPS+IRBSCSS VALID   @D51GDAP 00373000
         BNZ   INTFP020           YES,                         @D51GDAP 00374000
         XC    DEVSTA(2),DEVSTA   CLEAR UNPREDICTABLE INFO     @D51GDAP 00375000
INTFP020 TM    IRBSTB2,IRBFCS     IS THIS START FUNCTION       @DA40072 00376000
         BZ    INTFP040           NO,                          @DA40559 00377000
         TM    IRBSTB3,IRBSCIS+IRBSCPS+IRBSCSS SOLIC. INTERPT. @DA40559 00378000
         BNZ   INTMAIN            YES, ======================> @DA40559 00379000
         TM    IRBSTB2,IRBFCC+IRBFCH CLEAR OR HALT ISSUED      @D52VDAP 00380000
         BNZ   INTFP030           YES,                         @D52VDAP 00381000
         OI    DEVSTA,BUSY        INDICATE PENDING STATUS      @DA40559 00382000
INTFP030 NI    CSW,IRBCC          LEAVE CONDITION CODE ONLY    @DA40559 00383000
         B     INTFP050           CLEAR INVALID INFO IN CSW    @DA40559 00384000
INTFP040 XC    CSW(1),CSW         CLEAR FIRST BYTE IN CSW      @DA40559 00385000
INTFP050 XC    CSW+1(3),CSW+1     CLEAR CCW ADDRESS            @DA40559 00386000
         XC    CSW+6(2),CSW+6     CLEAR RESIDUAL COUNT         @D51GDAP 00387000
         B     INTMAIN                 ======================>          00388000
         DROP  R2                 RELEASE IRB BASE POINTER     @D51GDAP 00389000
.*                                                                      00390000
.*       VERIFY/FIND PUB ADDRESS FOR SIMULATED I/O INTERRUPTS           00391000
.*                                                                      00392000
INTSIMUL LTR   R3,R3              DO WE HAVE PUB ADDRESS       @DY46053 00393000
         BZ    INTSIM20           NO, WE NEED TO FIND IT       @DY46053 00394000
         LR    RE,R3              COPY ADDRESS                 @D28DR06 00395000
         N     RE,F7              RESET ALL EXCEPT LAST 3 BITS @D28DR06 00396000
         BNZ   INTSIM20           NOT DW-BOUNDARY              @D28DR06 00397000
         LH    RE,APNO-2          GET LENGTH OF PUB TABLE      @D28DR06 00398000
         SH    RE,H2              OFFS. LAST BYTE OF LAST ENTRY@D28DR06 00399000
         AH    RE,YPUBTAB         LAST BYTE OF PUBTAB          @D28DR06 00400000
         CLR   R3,RE              COULD THIS BE A PUB          @D28DR06 00401000
         BH    INTSIM20           NO, WE NEED TO FIND IT       @D28DR06 00402000
         CH    R3,YPUBTAB         SURE ITS A PUB ENTRY         @D28DR06 00403000
         BL    INTSIM20           NO,                          @DY46053 00404000
         CLC   PUBCHANN-PUBADR(2,R3),CHNADR IS THIS A MATCH    @DY46053 00405000
         BE    INTSIM40           YES,                         @DY46053 00406000
INTSIM20 L     RD,AINTALPU        ADDRESS OF FIND_PUB_ROUTINE  @D61BDAP 00407000
         BALR  RE,RD              FIND PUB ADDRESS     ======> @D61BDAP 00408000
INTSIM40 DS    0H'0'                                                    00409000
         EJECT ,                                                        00413000
         USING PUBADR,R3          ESTABLISH ADDRESSABILITY     @D35IDAP 00414000
INTMAIN  MVI   IOINF+1,ZERO       RESET INTERRUPT INDICATOR    @D52VDAP 00415000
         XR    R1,R1              CLEAR CCB POINTER            @D35IDAP 00416000
         BAL   RE,GETPUBX         GET PUB EXTENTION ADDRESS    @D62TPAP 00417000
*SGLINK  GETPUBX,INPUT=(R3,R9,RE),OUTPUT=R2                    @D61NDAP 00418000
         USING PBXADR,R2          PUBX BASE POINTER            @D61NDAP 00419000
         L     RE,IRB$            ADDRESS OF IRB               @D27ADAP 00420000
         USING IRB,RE             IRB BASE POINTER             @D27ADAP 00421000
         LTR   R4,R4              WAS THIS SIMULATED INTERRUPT @D61CDAP 00422000
         BZ    INTIRBPREP         YES, SIMULATE IRB DATA       @D27ADAP 00423000
INTMA010 MVC   PBXIRB(14),IRB     SAVE IRB IN PUBX             @DAOM055 00424390
         TM    IRBSTB0,IRBCC      SOME KIND OF DELAYED INTER.  @DAOM055 00424780
         BZ    INTMA015           NO, LEAVE LPUM IN PUBX       @DAOM055 00425170
         MVC   PBXIRB+IRBLPUM-IRB(1),PBXSSCHM USE SSCH LPM     @DAOM055 00425560
         DROP  RE                 RELEASE IRB BASE POINTER     @DAOM055 00425950
INTMA015 TM    SNSFLAG,SNSMSGRO+SNSMSGOP IS MIH ACTIVE         @D61CDAP 00426340
         BZ    INTMA020           NO,                          @D61CDAP 00427000
         L     RD,AMIHCHCK        SUBROUTINE BASE ADDRESS      @D14CDAP 00428000
         BALR  R7,RD              EXECUTE SUBROUTINE AND RETURN@D62TAAP 00429000
INTMA020 BAL   R7,CQDSP           SET UP I/O POINTERS/FIELDS   @D35IDAP 00430000
*SGLINK  CQDSP,INPUT=(R3,R6,R7),OUTPUT=(R1,R4,RA),WORK=R0      @D369DAP 00431000
         USING CCBADR,R1          CCB BASE POINTER             @D35IDAP 00432000
         USING CHQADR,R4          CHANQ BASE POINTER           @D35IDAP 00433000
         USING PIBADR,RA          PIB BASE POINTER             @D35IDAP 00434000
INTMADBG DEBUG IOFIELDS,XXDBGMC   SET UP DEBUG ENTRY           @DA44462 00435000
         LTR   R4,R4              IS CHANQ ADDRESS PRESENT     @DTIMING 00436000
         BZ    INTMA040           NO,                          @DTIMING 00437000
         LA    R4,0(,R4)          CLEAR CHAINING BYTE          @DAOM043 00438000
         TM    IJBMBADR,X'80'     IS MEASURING ACTIVE          @DTIMING 00439000
         BZ    INTMA040           NO,                          @DTIMING 00440000
         L     RD,AINTSMFU        SUBROUTINE BASE ADDRESS      @DTIMING 00441000
         BALR  RE,RD              UPDATE MEASUREMENT INFO      @DTIMING 00442000
*SGLINK  INTSMFUP,INPUT=(R2,R3,R4,R9,RD,RE)                    @DTIMING 00443000
         AGO   .SKIP020                                        @DELETE  00444000
INTMA040 B     INTMA060           SKIP LCK SPECIFIC CODE       @DELETE  00445000
         L     RD,ALCKPROC        SPECIAL LOCK PROCESSING      @DELETE  00446000
         BALR  R7,R0              DO SPECIAL LOCK PROCESSING   @DELETE  00447000
INTMA060 DS    0H'0'                                           @DELETE  00448000
.SKIP020 ANOP                                                           00449000
INTMA040 DS    0H'0'              PROVIDE FOR INTERCEPT        @D28DR06 00450000
         MC    $TRIO,$MCSDAID     PASS INFORMATION TO SDAID    @DA42913 00455000
         PRODEXIT ACTIVATE,ID=IOINT                            @D61EDMK 00456000
*        SGVSEPT PROBE=1,SAVE=NO                               @D51GDAP 00457000
         SGVSEPT PROBE=1,SAVE=NO                               @D51GDAP 00458000
         TM    IJBOLTEP,OLTAC     IS OLTEP ACTIVE              @D14NDAP 00459000
         BZ    GOLTAPP2           NO                           @D14NDAP 00460000
         CLC   OLTPIK(2),PIK      IS IT THE OLTEP PARTITION    @D14NDAP 00461000
         BE    GOLTAPP2           YES                          @DA35736 00462000
         LTR   R1,R1              IS CCB AVAILABLE             @DA35296 00463000
         BNZ   OLTRET2            YES,                         @DA41234 00464000
         LH    R5,OLTPIK          GET OLTEP PIK                @DA35736 00465000
         SRL   R5,2               GET PCB ADDRESS OFFSET       @D51IDAP 00466000
         A     R5,APCBATAB        ADDRESS OF PCB ADDRESS       @D51IDAP 00467000
         ICM   R5,15,0(R5)        PCB ADDRESS PRESENT          @D51IDAP 00468000
         BZ    OLTRET2            NO,                          @DA41234 00469000
         USING PCBADR,R5          PCB BASE POINTER             @DA35736 00470000
         SLR   RA,RA              CLEAR FOR INSERT             @DA35736 00471000
         IC    RA,TIDSTR          GET OLTEP TID                @DA35736 00472000
         BAL   R7,SETLCALL        ESTABLISH OLTEP ADDRESSES    @DA35736 00473000
*SGLINK  SETLCALL,INPUT=(R6,R7,RA),OUTPUT=(RA),WORK=R0         @D369DAP 00474000
         DROP  R5                 DROP PCB BASE POINTER        @DA35736 00475000
GOLTAPP2 L     R8,OLTAPP2         GET OLTEP APPENDAGE ADDRESS           00476000
         BALR  R5,R8              CALL OLTEP IF ACTIVE                  00477000
*SGLINK  OLTAPP2,INPUT=(R1,R5,R8)                              @D369DAP 00478000
OLTRET2  DS    0H'0'              RETURN HERE FROM OLTEP       @D35IDAP 00482000
         TM    CSW,DELAYINT       IS THIS A DELAYED INTERRUPT  @DA41234 00483000
         BZ    TSTCSW             NO,  ======================> @DA42913 00484000
         L     RD,APROCDLY        DELAYED INTERRUPT ROUTINE    @DA42913 00485000
         BR    RD                      =====================>> @DA42913 00486000
           SPACE 2                                                      00487000
.*                                                                      00519000
.*       FIND PUB ENTRY BELONGING TO THE GIVEN SUBCHANNEL               00520000
.*                                                                      00521000
           USING IRB,R2             IRB BASE POINTER           @D28ADAP 00522000
INTFINDCUU DS    0H'0'              FIND CUU PROCESSING        @D52VDAP 00523000
           L     RE,SCHIB$          ADDRESS OF SCHIB           @DA40072 00524000
           USING SCHIB,RE           SCHIB BASE POINTER         @D51GDAP 00525000
           STSCH SCHIB              GET SUBCHANNEL INFORMATION @D51GDAP 00526000
           BO    INTFC020           NOT OPERATIONAL            @D51GDAP 00527000
           TM    SCHFLGS,SCHV       IS DEVICE NUMBER VALID     @D51GDAP 00528000
           BNO   INTFC020           NO,                        @D51GDAP 00529000
           MVC   CHNADR(2),SCHDEVNO SET CUU INFO               @D51GDAP 00530000
           L     RD,AINTALPU        SUBROUTINE BASE ADDRESS    @D61BDAP 00531000
           BALR  RE,RD              FIND PUB ADDRESS   ======> @D61BDAP 00532000
           B     INTFP000                ====================> @D28ADAP 00533000
           DROP  RE                 RELEASE SCHIB BASE POINTER @D51GDAP 00534000
INTFC020   SLR   RE,RE              CLEAR REGISTER             @D61NDAP 00535000
           BCTR  RE,0               MAKE IT NEGATIVE           @D61NDAP 00536000
           STH   RE,CHNADR          INVALIDATE CUU ADDRESS     @D61NDAP 00537000
           MVC   CAW(4),IRBSTB0     PROVIDE INTERRUPT INFO     @D51GDAP 00538000
           MVC   CSW(8),IRBCCW      FOR DEBUG                  @D51GDAP 00539000
           DEBUG IOFIELDS,XXDBGMC   SET UP DEBUG ENTRY         @D61COAP 00540000
           NI    CSW,XF0            RESET UNKNOWN BITS IF ANY  @DA41224 00541000
           B     INTNOPUB                ====================> @D51GDAP 00542000
           DROP  R2                 RELEASE IRB BASE POINTER   @D28ADAP 00543000
           EJECT ,                                                      00544000
           USING IRB,RE             IRB BASE POINTER           @D27ADAP 00545000
           USING PBXADR,R2          PUBX BASE POINTER          @D61NDAP 00546000
INTIRBPREP DS    0H'0'              PROVIDE SIMULATED IRB DATA @D27ADAP 00547000
           XC    IRB(IRBLEN),IRB    CLEAR THE IRB              @D27ADAP 00548000
           MVC   IRBSTB0(1),CSW     PROVIDE PROTECTION KEY     @D28APAP 00549000
           MVI   IRBSTB3,IRBSCAS+IRBSCSP SIMULATE UNSOLICITED  @D27ADAP 00550000
           MVC   IRBCCW+1(7),CSW+1  PROVIDE CSW INFORMATION    @D27ADAP 00551000
           OC    IRBCCW(4),IRBCCW   IS IT PRIMARY STATUS       @DSCSI   00552000
           BZ    INTIRBP10          NO,                        @DSCSI   00553000
           OI    IRBSTB2,IRBFCS     SIMULATE START FUNCTION    @DSCSI   00554000
           OI    IRBSTB3,IRBSCPS+IRBSCSS SOLICITED INTERRUPT   @DSCSI   00555000
           TM    DEVSTA,UNITCK      WAS UNIT CHECK SIMULATED   @DSCSI   00556000
           BO    INTIRBP10          YES, ALERT STATUS IS OK    @DSCSI   00557000
           NI    IRBSTB3,XFF-IRBSCAS RESET ALERT STATUS        @DSCSI   00558000
INTIRBP10  MVC   IRBLPUM(1),PBXIRB+IRBLPUM-IRB PROVIDE LPUM    @D27ADAP 00559000
           CLI   IRBLPUM,ZERO       IS THIS A VALID LPUM       @D27ADAP 00560000
           BNE   INTMA010           YES, ====================> @D27ADAP 00561000
           MVI   IRBLPUM,X'80'      ASSUME PATH-0              @D27ADAP 00562000
           B     INTMA010                ====================> @D27ADAP 00563000
           DROP  RE                 RELEASE IRB BASE POINTER   @D27ADAP 00564000
           AIF   (NOT &VSE280).N280020                         @DAOM    00565000
DBGCONST   DEBUG IOFIELDS,XXDBGMC   DEBUG INTERFACE CODE       @DAOM    00566000
           ORG   DBGCONST           OVERLAY                    @DAOM    00567000
           DC    C'AXEL'                                       @DAOM    00568000
           ORG   ,                  RESTORE LOCATION COUNTER   @DAOM    00569000
.N280020   ANOP                                                @DAOM    00570000
         TITLE 'VSE SUPERVISOR     IOINTER     STATUS ANALYSER        ' 00571000
         AIF   (NOT &BGDEBUG).NTXT020                          @D35IDAP 00572000
***************************************************************@D35IDAP 00573000
*                                                              @D35IDAP 00574000
*  FUNCTION:                                                   @D35IDAP 00575000
*            ANALYSE CHANNEL AND DEVICE STATUS AND PASS        @D35IDAP 00576000
*            CONTROL TO THE APPROPRIATE ROUTINE OR PROCESS     @D35IDAP 00577000
*  ENTRY POINT:                                   AT LABEL     @D35IDAP 00578000
*            1. ENTERED  FROM INITIATOR              TSTCSW    @D35IDAP 00579000
*            2. ENTERED  FROM APPENDAGE PROCESSOR    TSTSTAT   @D35IDAP 00580000
*            3. ENTERED  FROM ATTENTION PROCESSOR    TSTUCERR  @D61BDAP 00581000
*            4. ENTERED  FROM ERROR IGNORE ROUTINE   TSTPCI    @D35IDAP 00582000
*            5. ENTERED  FROM I/O ERROR PROCESSOR    TSTPCI    @D35IDAP 00583000
*            6. ENTERED  FROM PCI PROCESSOR          TSTCSWC   @D369DAP 00584000
*  EXITS:                                         TO LABEL     @D35IDAP 00585000
*            CHANNEL CHECK HANDLER                   CCENTRY1  @D35IDAP 00586000
*                                                    CCENTRY2  @D35IDAP 00587000
*            APPENDAGE PROCESSOR                     PROCAPP   @D35IDAP 00588000
*            PRIMARY STATUS PROCESSOR                CHNDRT    @D35IDAP 00589000
*            FINAL STATUS PROCESSOR                  CEDETST   @D35IDAP 00590000
*                                                    NOPUREDE  @D35IDAP 00591000
*            SIO-INITIATOR                           INITRG    @D35IDAP 00592000
*            I/O ERROR PROCESSOR  (UNIT CHECK)       INTERRUC  @D35IDAP 00593000
*                                 (CHANNEL ERROR)    INTERRCC  @D35IDAP 00594000
*            ATTENTION PROCESSOR                     ATTNPROC  @D35IDAP 00595000
*            PCI PROCESSOR                           PROCPCI   @D35IDAP 00596000
*  INPUTS:                                                     @D35IDAP 00597000
*            R1= ADDRESS OF CCB                                @D35IDAP 00598000
*            R2= ADDRESS OF PUBX                               @D35IDAP 00599000
*            R3= ADDRESS OF PUB                                @D35IDAP 00600000
*            R4= ADDRESS OF CHANQ ENTRY                        @D35IDAP 00601000
*            R6= ADDRESS OF DISPATCHER                         @D35IDAP 00602000
*            R9= IOINTER BASE REGISTER                         @D35IDAP 00603000
*            RA= ADDRESS OF PIB                                @D35IDAP 00604000
*        CHNADR= CHANNEL+DEVICE ADDRESS                        @D36IDAP 00605000
*           CSW= INTERRUPT INFORMATION                         @D35IDAP 00606000
*      RASFLAGS= RAS I/O CONTROL BITS                          @D36IDAP 00607000
*  OUTPUTS:                                                    @D35IDAP 00608000
*  REGISTERS:                                                  @D35IDAP 00609000
*            RF= UNPREDICTABLE                                 @D35IDAP 00610000
*           CSW= INTERRUPT INFORMATION (MODIFIED BY APPEN)     @D35IDAP 00611000
*  SUBROUTINES:                                                @D35IDAP 00612000
*                                                              @D35IDAP 00613000
***************************************************************@D35IDAP 00614000
         SPACE 3                                               @D35IDAP 00615000
.NTXT020 ANOP                                                  @D35IDAP 00616000
TSTCSW   EQU   *                  ANALYZE STATUS INFORMATION   @D35IDAP 00617000
         AIF   (NOT &VSE280).N280040                           @DAOM    00618000
         CLC   DBGCONST+4(18),INTMADBG+4 CODE OVERLAY          @DAOM    00619000
         BNE   TSTHWAIT           NO,                          @DAOM    00620000
         CLC   INTMADBG+1(3),DBGCONST+19 ORIGINAL CODE         @DAOM    00621000
         BE    TSTCSW10           SEEMS TO BE OK               @DAOM    00622000
TSTHWAIT BAL   R5,SYSERROR        FORCE DISABLED WAIT          @DAOM    00623000
TFCPID   DC    AL1(TQDIO),AL1(PUBOPFCP) FCP DEVICE ID          @DSCSI   00624000
.N280040 ANOP                                                  @DAOM    00625000
* TCSW10 OI    SYSFLAG2,AFTIOINT  IOINTER PROCESSING           @D28DR06 00626000
TSTCSW10 LTR   R4,R4              DO WE HAVE CHANQ ENTRY       @D31YDAP 00627000
         BZ    TSTCHNCK           NO, SKIP CHANNEL END APPEND. @D37BDAP 00628000
         TM    CHQGRP,CHQGRINT    CAW SUPPLIED BY REQUESTOR    @D21CDAP 00629000
         BNO   TSTOCCSW           NO,                          @D52VDAP 00630000
         OC    CSW+1(3),CSW+1     PRIMARY INTERRUPT            @D21CDOW 00631000
         BZ    TSTCHNCK           NO, DONT OVERWRITE CAW       @D21CDAP 00632000
         NI    CHQGRP,XFF-CHQGRINT CAW IS NOT NEEDED ANYMORE   @D21CDOW 00633000
*HERE CCW-31                                                            00633500
         XC    CHQCSW+1(7),CHQCSW+1 CLEAR CCW ADDRESS          @D21CDOW 00634000
TSTOCCSW OC    CHQCSW+1(7),CSW+1  SAVE CSW IN CHANNEL QUEUE    @D52VDAP 00635000
TSTCHNCK TM    CHNSTA,CHNCHK      CHANNEL FAILURE              @D35IDAP 00636000
         BNZ   RASPROC0           YES, ======================> @D35IDAP 00637000
         LTR   R4,R4              IS CHANQ ENTRY PRESENT       @D61BDAP 00638000
         BZ    TSTFCPAP           NO,                          @DSCSI   00639000
         TM    CHQGRP,CHQGRRAS    IS IT A RAS RETRY REQUEST    @D61NDAP 00640000
         BO    RASPROC6           YES, LET RAS DO ITS POSTING  @D14BDAP 00641000
TSTAPPND TM    CHQCCBB3,APPEN     IS APPENDAGE TO BE ENTERED   @D35IDAP 00642000
         BZ    TSTSTAT            NO, SKIP APPENDAGE           @DSCSI   00643000
         B     PROCAPP            YES, ======================> @D35IDAP 00644000
TSTFCPAP DS    0H'0'                                           @DSCSI   00645000
         AIF   (NOT &VSE280).N280060                           @DSCSI   00646000
         CLC   PUBDEVTY(2),TFCPID IS THIS THE FCP ADAPTER      @DSCSI   00647000
         BE    PROCAPP            YES, ======================> @DSCSI   00648000
.N280060 ANOP                                                  @DSCSI   00649000
TSTSTAT  TM    CHNSTA,XFF-PCI-IL  ANY UNUSUAL CHANNEL STATUS   @D35IDAP 00650000
         BNZ   TSTATTN            YES, ANALYZE MORE DETAILED   @D35IDAP 00651000
         CLI   DEVSTA,CHANEND+DEVEND PURE CHANNEL+DEVICE END   @DM03715 00652000
         BE    CHNDRT             YES, ======================> @DM03715 00653000
         CLI   DEVSTA,CHANEND     PURE CHANNEL END             @DM03715 00654000
         BE    CHNDRT             YES, ======================>          00655000
         CLI   DEVSTA,DEVEND      PURE DEVICE END              @DM03715 00656000
         BE    CEDETST            YES, ======================> @DM03715 00657000
TSTATTN  TM    DEVSTA,ATTENT      IS THIS ATTENTION INTERRUPT  @D61BDAP 00658000
         BZ    TSTUCERR           NO                           @D28ADAP 00659000
*HEREPIO                                                                00660000
         L     RC,AATNPROC        GET SUBROUTINE BASE          @D28ADAP 00661000
         USING ATTNPROC,RC        ATTENTION PROCESSING BASE    @D28ADAP 00662000
         B     ATTNPROC           CHECK FOR AOM PROCESSING     @D61BDAP 00663000
         DROP  RC                 RELEASE ATTNPROC BASE        @D28ADAP 00664000
TSTUCERR TM    DEVSTA,UNITCK      STATUS WITH UNIT CHECK                00665000
         BO    INTERRUC           YES, ======================> @D34SDAP 00666000
         TM    CHNSTA,PRGPRT+CHDTA+CHCHN ANY CHANNEL ERROR     @D14BDAP 00667000
         BNZ   INTERRCC           YES, ======================> @D34SDAP 00668000
         CLI   DEVSTA,ATTENT+DEVEND+UNITEXCP NEW READY INTERR  @D37ADAP 00669000
         BE    PURERDY            YES, PROCESS READY INTERRUPT @D37ADAP 00670000
TSTPCI   CLI   DEVSTA,ZERO        ANY DEVICE STATUS            @D35IDAP 00671000
         BE    PROCPCI            NO,  ======================> @D35IDAP 00672000
TSTBUSY  TM    DEVSTA,BUSY        DEVICE OR CONTROL UNIT BUSY  @D35IDAP 00673000
         BO    NOPUREDE           YES, ======================> @D35IDAP 00674000
TSTCSWC  OC    CCWDRS(L3),CCWDRS  IS THIS A PRIMARY INTERRUPT  @D369DAP 00675000
         BZ    NOPUREDE           NO,  ======================> @D35IDAP 00676000
*        B     CHNDRT             PROCESS PRIMARY INTERRUPT    @D149DAP 00677000
         TITLE 'VSE SUPERVISOR     IOINTER    PRIMARY STATUS PROCESSOR' 00678000
         AIF   (NOT &BGDEBUG).NTXT030                          @D35IDAP 00679000
***************************************************************@D35IDAP 00680000
*                                                              @D35IDAP 00681000
*  FUNCTION:                                                   @D35IDAP 00682000
*            PROCESS PRIMARY INTERRUPT                         @D35IDAP 00683000
*  ENTRY POINT:                                   AT LABEL     @D35IDAP 00684000
*            1. ENTERED  FROM STATUS ANALYSER        CHNDRT    @D35IDAP 00685000
*            2. ENTERED  FROM ATTENTION PROCESSOR    CHNDP010  @D37BDAP 00686000
*  EXITS:                                         TO LABEL     @D35IDAP 00687000
*            SIO-INITIATOR                           INITRG    @D35IDAP 00688000
*            POST ROUTINE                            PSTRESET  @D35IDAP 00689000
*                                                    PSTPUB    @D35IDAP 00690000
*  INPUTS:                                                     @D35IDAP 00691000
*            R1= ADDRESS OF CCB                                @D35IDAP 00692000
*            R2= ADDRESS OF PUBX                               @D35IDAP 00693000
*            R3= ADDRESS OF PUB                                @D35IDAP 00694000
*            R4= ADDRESS OF CHANQ ENTRY                        @D35IDAP 00695000
*            R6= ADDRESS OF DISPATCHER                         @D35IDAP 00696000
*            R9= IOINTER BASE REGISTER                         @D35IDAP 00697000
*            RA= ADDRESS OF PIB                                @D35IDAP 00698000
*        CHNADR= CHANNEL+DEVICE ADDRESS                        @D36IDAP 00699000
*           CSW= INTERRUPT INFORMATION                         @D35IDAP 00700000
*        INSIZE= SYSIN     PROCESSING FLAGS                    @D36IDAP 00701000
*  OUTPUTS:                                                    @D35IDAP 00702000
*  REGISTERS:                                                  @D35IDAP 00703000
*            R0= UNPREDICTABLE                                 @D35IDAP 00704000
*            R5= UNPREDICTABLE                                 @D35IDAP 00705000
*            R7= UNPREDICTABLE                                 @D35IDAP 00706000
*            R8= UNPREDICTABLE                                 @D35IDAP 00707000
*            RD= UNPREDICTABLE                                 @D35IDAP 00708000
*            RF= UNPREDICTABLE                                 @D35IDAP 00709000
*  SUBROUTINES:                                                @D35IDAP 00710000
*            DIBENTRY - RETURN DIB ENTRY ADDRESS               @D35IDAP 00711000
*            GETDADR  - VALIDATE I/O AREA ADDRESS              @D35IDAP 00712000
*                                                              @D35IDAP 00713000
***************************************************************@D35IDAP 00714000
         SPACE 3                                               @D35IDAP 00715000
.NTXT030 ANOP                                                  @D35IDAP 00716000
CHNDRT   LTR   R1,R1              IS CCB AVAILABLE                      00717000
         BZ    GOENABLE           NO,  ======================> @DSCSI   00718000
         TM    CHQCCSIO,CHQCCACT+CHQCCNOP DID WE START DEVICE  @D37BDAP 00719000
         BZ    INITRG             NO,  ======================> @D37BDAP 00720000
CHNDP010 L     R5,CRADDR          ADDRESS OF COMREG            @D36IDAP 00721000
         USING COMREG,R5          ESTABLISH ADDRESSABILITY     @D36IDAP 00722000
         CLI   CHQSLUB,SYSLST     IS IT A SYSTEM FILE          @D36JDAP 00723000
         BH    CHNDPST            NO,                          @D369DAP 00724000
         L     RC,APROCSYS        ADDRESS OF SYSFIL PROCESSING @D31YDAP 00725000
         BR    RC                 EXECUTE SYSFIL PROCESSING    @D31YDAP 00726000
CHNDPST  TM    DEVSTA,DEVEND      IS THIS THE ENDING STATUS    @D35IDAP 00727000
         BO    PSTRESET           YES, ======================> @D35IDAP 00728000
         TM    DEVSTA,CHANEND     IS THIS INITIAL SELECTION    @D35IDAP 00729000
         BZ    PSTRESET           YES, ======================> @D35IDAP 00730000
         TM    CHQCCBB1,POSTDE    TO BE POSTED AT DEVICE END   @D35IDAP 00731000
         BZ    PSTPUB             NO,  ======================> @D35IDAP 00732000
         AIF   (NOT &VSE410).N410020                                    00732100
         TM    CHQCCBB3,APPEN     CAN WE UPDATE CCBCSW         @D61NDAP 00732200
         BO    GOENABLE           NO,  ======================> @D61NDAP 00732300
*        BO    INITRG             NO,  ======================> @D61NDAP 00732400
         AGO   .Y410020                                                 00732500
.N410020 ANOP                                                           00732600
         TM    CHQCCBB3,APPEN+OLTP CAN WE UPDATE CCBCSW        @D61NDAP 00733000
         BNZ   GOENABLE           NO,  ======================> @DSCSI   00734000
*        BNZ   INITRG             NO,  ======================> @D61NDAP 00735000
.Y410020 ANOP                                                           00735500
         MVC   13(3,R1),CHQCSW+1  SAVE CCW INFO FOR SDAID      @D61NDAP 00736000
         B     GOENABLE           NO,  ======================> @DSCSI   00737000
*        B     INITRG                  ======================> @D35IDAP 00738000
         DROP  R5                 RELEASE COMREG BASE          @DA44993 00739000
         TITLE 'VSE SUPERVISOR     IOINTER     POST ROUTINE           ' 00740000
         AIF   (NOT &BGDEBUG).NTXT040                          @D35IDAP 00741000
***************************************************************@D35IDAP 00742000
*                                                              @D35IDAP 00743000
*  FUNCTION:                                                   @D35IDAP 00744000
*            RESET PUB FLAG BITS AND COUNTERS                  @D35IDAP 00745000
*            POST ECB (DIRECTLY OR DELAYED)                    @D35IDAP 00746000
*            SET UP PIB FLAG AND FORCE SYSTEM TASK DISPATCHING @D35IDAP 00747000
*            POST ALL FIELDS IN USER CCB/IORB                  @D35IDAP 00748000
*  ENTRY POINT:                                   AT LABEL     @D35IDAP 00749000
*            1. ENTERED  FROM PRIM. STATUS PROCESSOR PSTRESET  @D35IDAP 00750000
*                                                    PSTPUB    @D35IDAP 00751000
*            2. ENTERED  FROM I/O ERROR PROCESSOR    PSTRESET  @D35IDAP 00752000
*            3. ENTERED  FROM FINAL STATUS PROCESSOR PSTRESET  @D35IDAP 00753000
*  EXITS:                                         TO LABEL     @D35IDAP 00754000
*            RESOURCE RELEASER                       INTRELSE  @D35IDAP 00755000
*            I/O ERROR PROCESSOR                     INTTRANS  @D35IDAP 00756000
*            DISPATCHER                              DISP      @D35IDAP 00757000
*  INPUTS:                                                     @D35IDAP 00758000
*            R1= ADDRESS OF CCB                                @D35IDAP 00759000
*            R2= ADDRESS OF PUBX                               @D35IDAP 00760000
*            R3= ADDRESS OF PUB                                @D35IDAP 00761000
*            R4= ADDRESS OF CHANQ ENTRY                        @D35IDAP 00762000
*            R6= ADDRESS OF DISPATCHER                         @D35IDAP 00763000
*            R9= IOINTER BASE REGISTER                         @D35IDAP 00764000
*            RA= ADDRESS OF PIB                                @D35IDAP 00765000
*  OUTPUTS:                                                    @D35IDAP 00766000
*  REGISTERS:                                                  @D35IDAP 00767000
*            R5= UNPREDICTABLE                                 @D36IDAP 00768000
*            R7= UNPREDICTABLE                                 @D35IDAP 00769000
*            R8= UNPREDICTABLE                                 @D35IDAP 00770000
*            RB= ADDRESS OF TIB                                @D36IDAP 00771000
*            RE= UNPREDICTABLE                                 @D35IDAP 00772000
*            RF= UNPREDICTABLE                                 @D35IDAP 00773000
*  SUBROUTINES:                                                @D35IDAP 00774000
*       IOPOST1= SET TASK READY IF IT IS SVC 7-BOUND           @D35IDAP 00775000
*                                                              @D35IDAP 00776000
***************************************************************@D35IDAP 00777000
         SPACE 3                                               @D35IDAP 00778000
.NTXT040 ANOP                                                  @D35IDAP 00779000
PSTRESET NI    PUBCSFLG,XFF-DEVBSY RESET PUB FLAG BITS         @D62TAAP 00780000
*        NI    PUBCSFLG,XFF-QEDERR RESET PUB FLAG BITS         @D62TAAP 00781000
PSTPUB   L     RB,TIBPTR          ADDRESS OF TIB               @D36IDAP 00782000
         USING TIBADR,RB          TIB BASE POINTER             @D36IDAP 00783000
         TM    CHQDEV,CHQTAPE     IS DEVICE TAPE               @D35IDAP 00784000
         BO    PSTTAPE            YES, =====================>> @D37BDAP 00785000
         CLI   PUBDEVTY,PRINTER   IS DEVICE A PRINTER          @DA44201 00786000
         BL    PSTPUB10           NO,                          @DA44201 00787000
         CLI   PUBDEVTY,TTAPE     SURE THIS IS A PRINTER       @DA44201 00788000
         BNL   PSTPUB10           NO,                          @DA44201 00789000
         TM    DEVSTA,UNITEXCP    IS THIS CHANNEL 12 OVERFLOW  @DA44201 00790000
         BZ    PSTPUB10           NO,                          @DA44201 00791000
         L     RD,AINTPOWC        GET ROUTINE ADDRESS          @DA44201 00792000
         BALR  RE,RD              CHECK FOR CHAN9/12 IGNORE    @DA44201 00793000
         B     PSTPUB10          0 POST =====================> @DA44201 00794000
         B     INITRG            4      =====================> @DA44201 00795000
PSTPUB10 DS    0H'0'                                           @D61NDAP 00796000
         AIF   (&AG1 GT 254).YMDV010                           @D51ODGL 00797000
         TM    CHQCCBB3,ERRTIN    IS ERP OPERATING             @D35IDAP 00798000
         BO    PSTIOCMP           YES, DONT RESET ERROR COUNT  @D35IDAP 00799000
         MVI   PUBERR,ZERO        RESET RETRY COUNTER                   00800000
.YMDV010 ANOP                                                  @D51ODGL 00801000
PSTIOCMP CLC   CHQTID,IORQ4AR     REQUEST FROM SYSTEM TASK     @D66ADAP 00802000
         BNH   PSTCCB             YES, SKIP SPECIAL PROCESSING @D36IDAP 00803000
         TM    CHQGRP,CHQGRVTM    IS THIS A VTAM ENTRY         @D14CDFG 00804000
         BO    DEQUNCON           IF YES, SKIP POSTING         @D14CDFG 00805000
PSTCCB   DC    0H'0'              POST USERS CCB               @D41ADAP 00806090
         AIF   (NOT &VSE410).N410040                                    00806180
         TM    CHQCCBB3,APPEN     APPENDAGE ROUTINE PRESENT    @D61NDAP 00806270
         BO    PSTRCNT            YES, WE NEED TO SKIP UPDATE  @D61NDAP 00806360
         AGO   .Y410040                                                 00806450
.N410040 ANOP                                                           00806540
         TM    CHQCCBB3,APPEN+OLTP APPENDAGE ROUTINE PRESENT   @D35IDAP 00806630
         BNZ   PSTRCNT            YES, DONT UPDATE CCW ADDRESS @D35IDAP 00807000
.Y410040 ANOP                                                           00807500
         L     R7,CHQCSW          GET NEXT CCW ADDRESS         @D35IDAP 00808000
         LA    R7,ZERO(R7)        CLEAR HIGH ORDER BYTE        @D35IDAP 00809000
         C     R7,DASDCPT         IS IT A SUPVR-CHAINED CCW    @D14BDAP 00810000
         BL    PSTUCSWW           NO, THEN IT IS CORRECT       @D35IDAP 00811000
         C     R7,DASDCPTA        IS IT A SUPVR-CHAINED CCW    @D14BDAP 00812000
         BH    PSTUCSWW           NO, THEN IT IS CORRECT       @D35IDAP 00813000
PSTSCSWW L     R7,CCBCCW          GET ADDRESS OF FIRST CCW     @D35IDAP 00814000
PSTPCK10 EQU   *                  PROGRAM CHECK MAY OCCUR HERE @D35IDAP 00815000
         LA    R7,8(R7)           INCREMENT WITH CCWLENGTH     @D35IDAP 00816000
PSTUCSWW STCM  R7,7,CCBCSWW       LAST CCW ADDRESS+8           @D35IDAP 00817000
PSTRCNT  LH    R7,CHQCSW+D6       PICK UP RESIDUAL COUNT       @D35IDAP 00818000
         STH   R7,CCBCNT          AND STORE IT IN CCB          @D35IDAP 00819000
PSTSTAT  MVC   CCBSTA(L2),CHQCSW+4  COPY STATUS TO USER CCB    @D35IDAP 00820000
         NI    CHQCCBB1,XFF-POSTDE RESET POST AT DE BIT        @D51GDAP 00821000
         OC    CCBCOM1(2),CHQCCBB1 COPY COMMUNICATION BYTES    @D35IDAP 00822000
         OC    CHQCCBB1(1),CCBCOM1 RESTORE OLD SETTING         @D51GDAP 00823000
         TM    CHQPROC,CHQSFFBA   SYSTEM FILE ON FBA DEVICE    @D35DDAP 00824000
         BZ    INTRELSE           NO,  ======================> @D37BDAP 00825000
         OI    TIBFLAG,TIBDELMV   INDICATE DELAYED POSTING     @D52HDAP 00826000
         OI    TIBDMFLG,TIBSFLEX  FORCE RETURN TO SVC103       @D14NDAP 00827000
         B     INTRELSE                ======================> @D36IDAP 00828000
         SPACE 3                                               @D35IDAP 00829000
PSTTAPE  DC    0H'0'              SPECIAL TAPE PROCESSING      @DA42376 00830000
         SLR   RD,RD              CLEAR REGISTER               @DA42376 00831000
         ICM   RD,7,CSW+1         IS THIS PRIMARY STATUS       @DA42376 00832000
         BZ    PSTTAP30           NO,                          @DA42376 00833000
         SH    RD,H8              GET FAILING CCW ADDRESS      @DA42376 00834000
         TM    IJBFLG08,IJBSAENV+IJBSVALC SA ACTIVE / COMPLETE @D61DDAP 00835000
         BNM   PSTTAP00           YES,                         @D61DDAP 00836000
         C     R3,PUBRES          IS THIS IPL DEVICE           @D61DDAP 00837000
         BNE   PSTTAP00           NO,                          @D61DDAP 00838000
         CLC   CHQTID,IORQ4BG     IS THIS A USER REQUEST       @D66DDAP 00839000
         BNE   PSTTAP00           NO,                          @D61DDAP 00840000
         TM    CHQCCBB3,ERRTIN    IS THIS AN ERP REQUEST       @D61DDAP 00841000
         BO    PSTTAP00           YES,                         @D61DDAP 00842000
         L     R7,0(RD)           GET LAST I/O AREA ADDRESS    @D61DDAP 00843000
         LA    R7,0(,R7)          CLEAR COMMAND CODE           @D61DDAP 00844000
         MVC   IJBSABLK(4),4(R7)  SAVE BLOCK NUMBER            @D61DDAP 00845000
PSTTAP00 CLI   DEVSTA,CUEND+DEVEND DID CU TERMINATE CCW CHAIN  @DA42376 00846000
         BNE   PSTTAP30           NO,                          @DA42376 00847000
         STNSM PSWMASK,XFF-SMDAT  SET DAT OFF                  @DA42376 00848000
         TM    CCWCHAIN-CCWADR(RD),CC COMMAND CHAINING         @DA42376 00849000
         BNO   PSTTAP20           NO,                          @DA42376 00850000
         TM    CCWCODE-CCWADR(RD),TIC IS THIS A TIC COMMAND    @DA42376 00851000
         BNO   PSTTAP10           NO,                          @DA42376 00852000
         TM    CCWCODE-CCWADR(RD),X'0F'-TIC IS IT REALLY A TIC @DA42376 00853000
         BNZ   PSTTAP10           NO,                          @DA42376 00854000
         MVC   CHQCAW+1(3),1(RD)  DONT START WITH A TIC        @DA42376 00855000
PSTTAP10 OI    CHQGRP,CHQGRINT    INDICATE CAW PROVIDED        @DA42376 00856000
         STOSM PSWMASK,SMDAT      FORCE DAT ON                 @DA42376 00857000
         B     INITRG                  ======================> @DA42376 00858000
PSTTAP20 STOSM PSWMASK,SMDAT      FORCE DAT ON                 @DA42376 00859000
PSTTAP30 CLC   CHQTID,IORQ4SVT    SYSTEM TASK REQUEST          @D66ADAP 00860000
         BNH   PSTPUB10           YES, ======================> @D21LDAP 00861000
.*                                                                      00862000
.*       THIS ROUTINE INTERCEPTS TAPE I/O OPERATIONS TO:                00863000
.*                                                                      00864000
.*           1.)   MERGE CHANNEL AND DEVICE END IN CASE                 00865000
.*                 THE LAST CCW WAS A WRITE COMMAND.                    00866000
.*           2.)   DETERMINE WETHER ERP REQUESTED SPECIAL               00867000
.*                 CCW'S TO BE EXECUTED.                                00868000
.*                                                                      00869000
         L     RD,ATAPERTN        ADDRESS OF I/O ERROR ROUTINES@D21IDAP 00870000
         BALR  RE,RD              DO SPECIAL PROCESSING        @D21IDAP 00871000
         L     RB,TIBPTR          RESTORE TIB POINTER          @D51VDAP 00872000
         BAL   RE,RMSGETP2        GET ADDRESS OF PUB 2         @D35IDAP 00873000
*SGLINK  RMSGETP2,INPUT=(R3,R6,RE),OUTPUT=R8                   @D51ODAP 00874000
         USING PUB2ADR,R8         SET BASE                              00875000
         TM    P2TFLG1,P2TERP     IS THE ERP IN CONTROL                 00876000
         BZ    PSTPUB10           NO,  ======================> @D35IDAP 00877000
.* PROBLEM     WHEN HQ HAS SUPERCEEDED THE ERP-RETRY REQUEST   @D28ADAP 00878000
.*             IT CAUSES A LOOP IN CASE OF I/O ERROR ON HQ     @D28ADAP 00879000
         TM    CHQFLG1,CHQHQA     IS THIS A HEAD QUEUE REQUEST @D28ADAP 00880000
         BZ    INTTRANS           NO,  ======================> @D28ADAP 00881000
         TM    CHQCCBB1,DISERR    DID THIS ONE REQUIRE ERP     @D28ADAP 00882000
         BO    PSTPUB10           NO, PROCESS INTERRUPT        @D28ADAP 00883000
         B     INTTRANS                ======================> @D37BDAP 00884000
         DROP  R8                 RELEASE PUB2 POINTER         @D35IDAP 00885000
         SPACE 3                                                        00886000
         DROP  RB                 RELEASE TIB POINTER          @D36IDAP 00887000
         SPACE 3                                               @D35IDAP 00888000
         TITLE 'VSE SUPERVISOR     IOINTER     RESOURCE RELEASER      ' 00889000
         AIF   (NOT &BGDEBUG).NTXT050                          @D35IDAP 00890000
***************************************************************@D35IDAP 00891000
*                                                              @D35IDAP 00892000
*  FUNCTION:                                                   @D35IDAP 00893000
*            FREE ALL RESOURCES THAT HAVE BEEN ALLOCATED       @D35IDAP 00894000
*            FOR THE I/O OPERATION                             @D35IDAP 00895000
*  ENTRY POINT:                                   AT LABEL     @D35IDAP 00896000
*            1. ENTERED  FROM POST ROUTINE           INTRELSE  @D35IDAP 00897000
*  EXITS:                                         TO LABEL     @D35IDAP 00898000
*            DEQUEUE ROUTINE                         DEQUNCON  @D35IDAP 00899000
*            SIO-INITIATOR                           INITRG    @D35IDAP 00900000
*  INPUTS:                                                     @D35IDAP 00901000
*            R1= ADDRESS OF CCB                                @D35IDAP 00902000
*            R2= ADDRESS OF PUBX                               @D35IDAP 00903000
*            R3= ADDRESS OF PUB                                @D35IDAP 00904000
*            R4= ADDRESS OF CHANQ ENTRY                        @D35IDAP 00905000
*            R6= ADDRESS OF DISPATCHER                         @D35IDAP 00906000
*            R9= IOINTER BASE REGISTER                         @D35IDAP 00907000
*            RA= ADDRESS OF PIB                                @D35IDAP 00908000
*  OUTPUTS:                                                    @D35IDAP 00909000
*  REGISTERS:                                                  @D35IDAP 00910000
*            R1= ADDRESS OF ORIGINAL USERS CCB                 @D369DAP 00911000
*            R7= UNPREDICTABLE                                 @D369DAP 00912000
*            R8= UNPREDICTABLE                                 @D369DAP 00913000
*            R9= IOINTER BASE ADDRESS                          @D369DAP 00914000
*            RB= UNPREDICTABLE                                 @D369DAP 00915000
*            RC= UNPREDICTABLE                                 @D369DAP 00916000
*            RD= UNPREDICTABLE                                 @D369DAP 00917000
*            RF= UNPREDICTABLE                                 @D369DAP 00918000
*  SUBROUTINES:                                                @D35IDAP 00919000
*       CCWFREE= FREE ALL ALLOCATED I/O AREAS                  @D35IDAP 00920000
*      CSWTRANS= EXECUTE CCW RETRANSLATION                     @D35IDAP 00921000
*       JAEXIT3= UPDATE JOB ACCOUNT INFORMATION                @D35IDAP 00922000
*                                                              @D35IDAP 00923000
***************************************************************@D35IDAP 00924000
         SPACE 3                                               @D35IDAP 00925000
.NTXT050 ANOP                                                  @D35IDAP 00926000
INTRELSE EQU   *                  RELEASE OCCUPIED RESOURCES   @D35IDAP 00927000
         TM    CCBCLS,CCBIORB     IS IT AN IORB                @DA35862 00928000
         BNO   INTREL20           NO, THEN NO ECB TO BE POSTED @D36IDAP 00929000
         TM    CCBCLS,CCBCOPY     TRANSLATED REQUEST           @D36IDAP 00930000
         BO    INTREL20           YES, CSWTRANS WILL DO IT     @D36IDAP 00931000
         TM    CHQCCBB3,IORBEXT   DOES IORB EXTENTION EXIST    @D35IDAP 00932000
         BZ    INTREL20           NO, THEN DONT POST ECB       @D35IDAP 00933000
         LA    R8,IORBEID         POINT R8 TO IORB EXTENTION   @D35IDAP 00934000
INTREL10 L     R7,0(R8)           GET EXTENTION ADDRESS        @D35IDAP 00935000
         TM    0(R8),X'7F'        IS THIS AN ECB EXTENTION     @D35IDAP 00936000
         BNZ   INTREL20           NO,                          @D35IDAP 00937000
         OI    2(R7),TRABIT       POST ECB                     @D35IDAP 00938000
         LTR   R7,R7              WAS THIS LAST EXTENTION      @D35IDAP 00939000
         BM    INTREL20           YES,                         @D35IDAP 00940000
         LA    R8,4(,R8)          POINT TO NEXT EXTENTION      @D35IDAP 00941000
         B     INTREL10                                        @D35IDAP 00942000
INTREL20 OI    CCBCOM1,TRABIT     POST TRAFFIC BIT             @D61NDAP 00943000
         TM    CCBCLS,CCBCOPY     TRANSL.I/O REQUEST           @D51VDAP 00944000
         BZ    INTREL30           NO,                                   00945000
         L     R9,ACCWT           SET UP BASE FOR CCWT MOD              00946000
         USING CCWTADR,R9                                               00947000
         BAS   R8,CSWTRANS        CCW RETRANSLATION            @D67.... 00948000
*SGLINK  CSWTRANS,INPUT=(R1,R8,R9,RA),OUTPUT=R1                @D369DAP 00949000
.*       THIS ROUTINE RETRANSLATES CCW ADDRESS IN CSW, IT      @D35IDAP 00950000
.*       RELEASES THE COPYBLOCKS USED FOR THE CHANNEL PROGRAM  @D35IDAP 00951000
.*       AND ITS ASSOCIATED IDALS. IT TFREE'S THE TFIXED I/O   @D35IDAP 00952000
.*       AREAS AND MOVES THE INFORMATION INTO USER'S CCB.      @D35IDAP 00953000
         L     R9,AINTER          IOINTER BASE ADDRESS         @D35IDAP 00954000
         USING INTRTN,R9          IOINTER BASE POINTER         @DM03030 00955000
INTREL30 L     RB,TIBPTR          LOAD TIB POINTER             @D61NDAP 00956000
         USING TIBADR,RB          TIB BASE POINTER             @D61NDAP 00957000
         LR    R8,RB              COPY TIB POINTER             @D61NDAP 00958000
         BAL   RF,IOPOST1         POST I/O COMPLETE IF I/O BND @D61NDAP 00959000
*SGLINK  IOPOST1,INPUT=(R6,R8,RF),WORK=RE                      @D61NDAP 00960000
         TM    TIBFLAG2,PWRMTASK  IS THIS THE POWER MAIN TASK  @D61NDAP 00961000
         BZ    INTREL40           NO,                          @D61NDAP 00962000
         ICM   RF,15,APOWTB       ADDRESS POWER TABLE          @D61NDAP 00963000
         BZ    INTREL40           POWER NOT ACTIVE             @D61NDAP 00964000
         USING PADS,RF            ADDRESSABILITY POWER TABLE   @D35IDAP 00965000
         OI    PAEB+2,TRABIT      TURN ON POWER/VS MASTER ECB  @D30EULR 00966000
         DROP  RF                 DROP POWER TABLE BASE        @D35IDAP 00967000
INTREL40 TM    CHQPROC,CHQSFFBA   SYSTEM FILE ON FBA DEVICE    @DA43510 00968000
         BZ    INTREL60           NO,                          @DA43510 00969000
         ICM   R5,15,PBXSIMAD     SIMULATION PROGRAM ACTIVE    @DA43510 00970000
         BZ    INTREL50           NO, POST USER TASK           @DSCSI   00971000
         AIF   (NOT &VSE280).N280080                           @DSCSI   00972000
         TM    PBXFLAG,PBXDASD+PBXSCSI IS THIS A SCSI DEVICE   @DSCSI   00973000
         BO    INTREL50           YES, POST USER               @DSCSI   00974000
.N280080 ANOP                                                  @DSCSI   00975000
         B     INTREL60           NO, FBAV WILL TAKE CARE      @DA43510 00976000
INTREL50 BAL   RF,POST            MAKE TASK READY              @DA43510 00977000
*SGLINK  POST,INPUT=(R6,R8,RF),WORK=(RE)                       @DA43510 00978000
INTREL60 TM    TIBFLAG2,OVHIND    OVERHEAD RUNNING             @D36IDAP 00979000
         BO    INTREL70           YES, NO NEED TO UPDATE       @DA35862 00980000
         DROP  RB                 RELEASE TIB POINTER          @D14BDAP 00981000
         L     RB,PCBPTR          ADDRESS OF PCB               @D51IDAP 00982000
         USING PCBADR,RB          PCB BASE POINTER             @D36IDAP 00983000
         L     RB,PCBJAPTR        ADDRESS OF PCB TO BE CHARGED @D14BDAP 00984000
         C     RB,ASYSPCB         IS IT THE SYSTEM PCB         @D14BDAP 00985000
         BE    INTREL70           YES, NO NEED TO UPDATE       @DA35862 00986000
.* NOTE:                                                       @D149DAP 00987000
.*       SGSETUP DEPENDS ON WR1 TO CONTAIN PCBJAPTR            @D149DAP 00988000
.*                                                             @D149DAP 00989000
         SGSETUP UPDTIME,WR1=RB,WR2=R7,WR3=R8,OPTION=IOINTER   @D51GDAP 00990000
         DROP  RB                 RELEASE PCB BASE POINTER     @D14BDAP 00991000
INTREL70 XR    R7,R7              CLEAR WORK REGISTER          @DA35862 00992000
         STCM  R7,M7,CHQCCBB1     CLEAR USERS COMM. BYTES      @D35IDAP 00993000
         STCM  R7,M7,CHQCCBAD+1   SAVE IT IN CHANNEL QUEUE     @D35IDAP 00994000
         TM    CHQPROC,CHQDQUNC   IS REQUEST TO BE DEQUEUED    @D35IDAP 00995000
         BO    DEQUNCON           YES, ======================> @D35IDAP 00996000
.*                                                                      00997000
.*       DEQUEUING WILL BE ENFORCED FOR:                       @D35IDAP 00998000
.*                 1. SUCCESSFULLY RETRIED RAS REQUEST         @D35IDAP 00999000
.*                                                                      01000000
         TM    DEVSTA,DEVEND      IS THIS FINAL STATUS         @D35IDAP 01001000
         BO    DEQUNCON           YES, ======================> @D35IDAP 01002000
         TM    DEVSTA,CHANEND     IS THIS INITIAL SELECTION    @D35IDAP 01003000
         AIF   (&VSE280).Y280040                                        01004000
         BOR   R6                 NO,  ====================>>> @D35IADP 01005000
         AGO   .N280100                                                 01006000
.Y280040 ANOP                                                           01007000
         BO    GOENABLE           NO,  ======================> @DSCSI   01008000
.N280100 ANOP                                                           01009000
         TITLE 'VSE SUPERVISOR     IOINTER     DEQUEUE ROUTINE        ' 01010000
         AIF   (NOT &BGDEBUG).NTXT060                          @D35IDAP 01011000
***************************************************************@D35IDAP 01012000
*                                                              @D35IDAP 01013000
*  FUNCTION:                                                   @D35IDAP 01014000
*            DEQUEUE THE CHANNEL QUEUE ENTRY FROM CHANNEL      @D35IDAP 01015000
*            QUEUE AND REQUEUE IT TO FREE LIST CHAIN IF        @D35IDAP 01016000
*            IT WAS NOT A RESERVED CHANQ ENTRY                 @D35IDAP 01017000
*  ENTRY POINT:                                   AT LABEL     @D35IDAP 01018000
*            1. ENTERED  FROM RESOURCE RELEASER      DEQUNCON  @D35IDAP 01019000
*  EXITS:                                         TO LABEL     @D35IDAP 01020000
*            INITIATOR                               INTRTN    @D35IDAP 01021000
*            SIO-INITIATOR                           INITRG    @D35IDAP 01022000
*  INPUTS:                                                     @D35IDAP 01023000
*            R1= ADDRESS OF CCB                                @D35IDAP 01024000
*            R2= ADDRESS OF PUBX                               @D35IDAP 01025000
*            R3= ADDRESS OF PUB                                @D35IDAP 01026000
*            R4= ADDRESS OF CHANQ ENTRY                        @D35IDAP 01027000
*            R6= ADDRESS OF DISPATCHER                         @D35IDAP 01028000
*            R9= IOINTER BASE REGISTER                         @D35IDAP 01029000
*            RA= ADDRESS OF PIB                                @D35IDAP 01030000
*  OUTPUTS:                                                    @D35IDAP 01031000
*  REGISTERS:                                                  @D35IDAP 01032000
*            R1= ADDRESS OF CCB                                @D35IDAP 01033000
*            R2= ADDRESS OF PUBX                               @D35IDAP 01034000
*            R3= ADDRESS OF PUB                                @D35IDAP 01035000
*            R4= ADDRESS OF CHANQ ENTRY                        @D35IDAP 01036000
*            R6= ADDRESS OF DISPATCHER                         @D35IDAP 01037000
*            R7= UNPREDICTABLE                                 @D35IDAP 01038000
*            R8= UNPREDICTABLE                                 @D35IDAP 01039000
*            RF= UNPREDICTABLE                                 @D35IDAP 01040000
*  SUBROUTINES:                                                @D35IDAP 01041000
*                                                              @D35IDAP 01042000
***************************************************************@D35IDAP 01043000
         SPACE 3                                               @D35IDAP 01044000
.NTXT060 ANOP                                                  @D35IDAP 01045000
DEQUNCON DS    0H                                              @D51ODGL 01046000
         SLR   R8,R8              CLEAR FOR INSERT             @D51ODGL 01047000
         IC    R8,PUBCHQPT        SAVE CHAIN POINTER FROM PUB  @D35IDAP 01048000
         AIF   (&AG1 LE 254).NMDV020                           @D51ODGL 01049000
         ICM   R8,2,PUBCHQPH      INCLUDING HIGH BYTE          @D51ODGL 01050000
.NMDV020 ANOP                                                  @D51ODGL 01051000
         SLR   R7,R7              CLEAR REGISTER               @D35IDAP 01052000
         IC    R7,CHQCHNLO        MAKE NEXT REQUEST            @D51ODGL 01053000
         AIF   (&AG1 LE 254).NMDV030                           @D51ODGL 01054000
         ICM   R7,2,CHQCHNHI      INCLUDING HIGH ORDER BYTE    @D51ODGL 01055000
         STCM  R7,2,PUBCHQPH      AND INSERT IT AS FIRST,      @D51ODGL 01056000
.NMDV030 ANOP                                                  @D51ODGL 01057000
         STC   R7,PUBCHQPT        FIRST ON QUEUE               @D35IDAP 01058000
DEQUNC10 NI    CHQCCSIO,XFF-CHQCCACT-CHQCCLTE RESET ACTIVE BIT @D37BDAP 01059000
         CLI   CHQCHNLO,XFF       IS THIS THE ONLY CHAIN ENTRY @D51ODGL 01060000
         BE    DEQUNC30           YES, LEAVE PUB FLAG CLEARED  @D35IDEP 01061000
         SLL   R7,CHQSLEN         MULTIPLY WITH ENTRY LENGTH   @D35IDAP 01062000
         A     R7,ACHANQ          ABSOLUTE ENTRY ADDRESS       @D35IDAP 01063000
         ICM   R7,8,H0            CLEAR CHAINING BYTE          @D68REEF 01064000
         DROP  R4                 NEXT ENTRY IN SEQUENCE       @D35IDEP 01065000
         USING CHQADR,R7          MAKE IT ADDRESSABLE          @D35IDAP 01066000
         TM    CHQFLG1,CHQCSBSY   WAS DEVICE BUSY              @D14CDAP 01067000
         BZ    DEQUNC20           NO, CHECK FOR QEDERR         @D35IDAP 01068000
         OI    PUBCSFLG,DEVBSY    KEEP DEVICE BUSY             @D35IDAP 01069000
DEQUNC20 TM    CHQFLG1,CHQCSQED   WAS DEVICE QUEUED IN ERROR   @D35IDAP 01070000
         BZ    DEQUNC25           DEVICE WAS BUSY 'ONLY'       @DA37004 01071000
         OI    PUBCSFLG,QEDERR    KEEP DEVICE QUEUED IN ERROR  @D35IDEP 01072000
DEQUNC25 NI    CHQFLG1,XFF-CHQCSQED-CHQCSBSY RESET GATE BITS   @DA37004 01073000
         DROP  R7                 RELEASE TEMP CHANQ BASE      @D35IDAP 01074000
         USING CHQADR,R4          CHANQ BASE POINTER           @D35IDEP 01075000
DEQUNC30 CLM   R4,7,AHQRESCH+1    IS IT RESERVED CHANQ ENTRY   @D35IDAP 01076000
         BL    DEQUNC40           NO, MUST QUEUE IT ON FLPTR   @DA42913 01077000
         CLC   CHQTID,IORQ4CST    DID CST TASK COMPLETE        @D66ADAP 01078000
         BNE   DEQUNC35           NO,                          @DY45629 01079000
         L     RC,APOSTAR         ADDRESS OF POST ROUTINE      @DY45629 01080000
         BALR  RE,RC              POST AR IF REQUIRED          @DY46053 01081000
*SGLINK  POSTARPR INPUT=(R6,RC,RE),WORK=(R8,RF)                @DY46053 01082000
DEQUNC35 XR    R8,R8              CLEAR WORK REG               @D61CDAP 01083000
         ICM   R8,3,CHQTID        GET SYSTEM TASK ID           @D66ADAP 01084000
         L     RF,IOSBASE         SGIOS BASE ADDRESS           @D21ODAP 01088000
         USING SGIOS,RF           SGIOS BASE POINTER           @D21ODAP 01089000
         IC    R8,HQUPRI(R8)      GET INDEX TO CONTROL TABLE   @D35IDAP 01090000
         LA    R8,HQUCNTL(R8)     GET ADDR OF CONTROL FLAG     @D35IDAP 01091000
         DROP  RF                 RELEASE SGIOS BASE           @D21ODAP 01092000
         USING HQUCNTLD,R8                                     @D35IDAP 01093000
         OI    HQUCNTLF,HQURES    FREE RESERVED ENTRY          @D35IDEP 01094000
         B     DEQUNC50           JOIN MAIN LINE               @D62TAAP 01095000
         DROP  R8                                              @D35IDAP 01096000
DEQUNC40 TM    CHQGRP,CHQGRVTM    VTAM ATTENTION ENTRY         @D14CDFG 01097000
         BO    DEQVTM10           YES, SPECIAL PROCESSING      @D14CDAP 01098000
         AIF   (&AG1 LE 254).NMDV040                           @D51ODGL 01099000
         LH    RF,CHQFLPTR        AND ADD THIS ENTRY           @D51ODGL 01100000
         STC   RF,CHQCHNLO        TO THE FREE LIST CHAIN       @D51ODGL 01101000
         STCM  RF,2,CHQCHNHI      AT HEAD OF FREE LIST         @D51ODGL 01102000
.NMDV040 AIF   (&AG1 GT 254).YMDV040                           @D51ODGL 01103000
         IC    RF,FLPTR           AND ADD THIS ENTRY           @D14CDFG 01104000
         STC   RF,CHQCHAIN        TO THE FREE LIST CHAIN       @D35IDAP 01105000
         STC   R8,FLPTR           AT HEAD OF FREE LIST         @D35IDEP 01106000
.YMDV040 ANOP                                                  @D51ODGL 01107000
         STH   R8,CHQFLPTR        UPDATE FREELIST POINTER      @D51ODGL 01108000
         L     RF,IOSBASE         GET SGIOS BASE ADDRESS       @D61NDAP 01109000
         USING SGIOS,RF           SGIOS BASE POINTER           @D61NDAP 01110000
         ICM   R5,15,CQEINUSE     GET IN USE COUNTER           @D61NDAP 01111000
         BZ    DEQUNC50           BRANCH IF ALREADY 0          @D61NDAP 01112000
         BCTR  R5,0               SUBTRACT 1                   @D61NDAP 01113000
         ST    R5,CQEINUSE        SAVE UPDATED VALUE           @D61NDAP 01114000
         DROP  RF                 RELEASE SGIOS BASE           @D61NDAP 01115000
DEQUNC50 L     R5,ASRQTAB         RESOURCE QUEUE BASE ADDRESS  @D61MPAP 01116000
         LA    R5,SRQCHQ-SRQTAB(R5) CHANQ WAIT QUEUE           @D61MPAP 01117000
         BAL   RF,RPOST           POST NEXT TASK (IF ANY)      @D36IDAP 01118000
*SGLINK  RPOST,INPUT=(R5,R6,RF),WORK=(R5,RE)                   @D369DAP 01119000
DEQUNC60 TM    CHQGRP,CHQGRROK    RAS RETRY SUCCESSFUL         @D149DAP 01120000
         BO    INTMAIN            YES, ======================> @D35IDAP 01121000
         CLI   PUBCHQPT,NOTQUED   IS THERE ANOTHER I/O REQUEST @D51ODGL 01122000
         AIF   (&VSE280).Y280060                                        01123000
         BER   R6                 NO,  ======================> @D51IDAP 01124000
         AGO   .N280120                                                 01125000
.Y280060 ANOP                                                           01126000
         BE    GOENABLE           NO,  ======================> @DSCSI   01127000
.N280120 ANOP                                                           01128000
         DROP  R4                 OLD CHANQ ENTRY BASE         @D35IDAP 01129000
         USING CHQADR,R7          NEW CHANQ ENTRY POINTER      @D35IDAP 01130000
         TM    CHQPROC,CHQDOINT   IS DELAYED PROCESSING NEEDED @D35IDAP 01131000
         BZ    INITRG             NO,  ======================> @D35IDAP 01132000
         NI    CHQPROC,XFF-CHQDOINT RESET DELAYED PROCESS BIT  @D35IDAP 01133000
         MVC   CSW(8),CHQCSW      RESTORE CSW                  @D35IDAP 01134000
DEQUNC70 CLI   CHQIOINF,ERPXMAX   IS ERP RETURN CODE VALID     @D149DAP 01135000
         BNL   DEQUNC80           NO,  SYSTEM ERROR            @D149DAP 01136000
         SLR   R8,R8              CLEAR FOR INSERT             @D51ODGL 01137000
         IC    R8,CHQIOINF        GET ERP RETURN CODE          @D35IDAP 01138000
         DROP  R7                 RELEASE NEXT CHANQ ENTRY BASE@D35IDAP 01139000
         BAL   R7,CQDSP           SET UP I/O POINTERS/FIELDS   @D62NDAP 01140000
*SGLINK  CQDSP,INPUT=(R3,R6,R7),OUTPUT=(R1,R4,RA),WORK=R0      @D369DAP 01141000
         USING CHQADR,R4          OLD CHANQ ENTRY POINTER      @D35IDAP 01142000
         LA    R4,0(,R4)          CLEAR CHAINING BYTE          @DAOM043 01143000
         LA    RF,ERPXAD1         ADDRESS OF ERP EXIT TABLE    @D35IDAP 01144000
         AR    R8,RF              ADDRESS OF WANTED EXIT       @D35IDAP 01145000
         L     RF,ZERO(R8)        GET RETURN ADDRESS           @D35IDAP 01146000
         B     INTERTRN                ======================> @D35IDAP 01147000
DEQUNC80 BAL   R5,SYSERROR             ====================>>> @D149DAP 01148000
         SPACE 3                                                        01149000
DEQVTM10 CLI   PUBCHQPT,NOTQUED   IS ANOTHER REQUEST QUEUED    @D14CDFG 01150000
         BNE   DEQVTM20           YES, SKIP                    @D14CDFG 01151000
         OI    PUBCSFLG,DEVBSY    PREVENT RESTART              @D14CDFG 01152000
         STC   R8,PUBCHQPT        RE-ENQUEUE THE ENTRY AGAIN   @D14CDFG 01153000
         NI    CHQPROC,XFF-CHQDQUNC RESET DEQUEUE UNCONDITIONAL@DA43372 01154000
         AIF   (&AG1 LE 254).NMDV060                           @D51ODGL 01155000
         STCM  R8,2,PUBCHQPH      ..INCLUDING HIGH BYTE        @D51ODGL 01156000
.NMDV060 ANOP                                                  @D51ODGL 01157000
         LR    R7,R4              PROVIDE NEW CHANQ POINTER    @DA44841 01158000
         B     DEQUNC60           RESUME COMMON PATH           @D14CDFG 01159000
         SPACE 3                                                        01160000
DEQVTM20 MVI   CHQCHNLO,XFF       MAKE CURRENT LAST            @D14CDFG 01161000
         AIF   (&AG1 LE 254).NMDV070                           @D51ODGL 01162000
         MVI   CHQCHNHI,XFF                                    @D51ODGL 01163000
.NMDV070 ANOP                                                  @D51ODGL 01164000
         OI    CHQFLG1,CHQCSBSY   REMEMBER TO SET BUSY         @D14CDFG 01165000
         DROP  R4                 RELEASE CHANQ POINTER        @D14CDFG 01166000
         LR    RF,R7              POINT TO NEXT ENTRY          @D14CDFG 01167000
         USING CHQADR,RF                                       @D14CDFG 01168000
DEQVTM30 CLI   CHQCHNLO,XFF       ANY FURTHER REQUEST          @D14CDFG 01169000
         BE    DEQVTM40           NO, END OF CHAIN             @D14CDFG 01170000
         SLR   R0,R0              CLEAR FOR INSERT             @D14CDFG 01171000
         IC    R0,CHQCHNLO        GET INDEX OF NEXT ENTRY      @D51ODGL 01172000
         AIF   (&AG1 LE 254).NMDV080                           @D51ODGL 01173000
         ICM   R0,2,CHQCHNHI                                   @D51ODGL 01174000
.NMDV080 ANOP                                                  @D51ODGL 01175000
         SLL   R0,CHQSLEN         MULTIPLY WITH ENTRY LENGTH   @D14CDFG 01176000
         A     R0,ACHANQ          ABSOLUTE ENTRY ADDRESS       @D14CDFG 01177000
         ICM   R0,8,H0            CLEAR CHAINING BYTE          @D68REEF 01178000
         LR    RF,R0              IN RIGHT REGISTER            @D14CDFG 01179000
         B     DEQVTM30           CHECK FOR NEXT               @D14CDFG 01180000
DEQVTM40 STC   R8,CHQCHNLO        ENQUEUE AT END OF CHAIN      @D14CDFG 01181000
         AIF   (&AG1 LE 254).NMDV090                           @D51ODGL 01182000
         STCM  R8,2,CHQCHNHI                                   @D51ODGL 01183000
.NMDV090 ANOP                                                  @D51ODGL 01184000
         B     DEQUNC60           RESUME COMMON PATH           @D14CDFG 01185000
         DROP  RF                                              @D14CDFG 01186000
         USING CHQADR,R4                                       @D14CDFG 01187000
         TITLE 'VSE SUPERVISOR     IOINTER     CONSTANTS FOR ERP      ' 01188000
ERPXAD1  DC    A(DISP)            WANT TO RETURN TO DISPATCHER @D35IDAP 01189000
ERPXAD2  DC    A(INITRG)          WANT TO RETURN TO SCHEDULER  @D35IDAP 01190000
ERPXAD3  DC    A(INTRTN)          WANT TO RETURN TO INTRTN     @D35IDAP 01191000
ERPXAD4  DC    A(IGNORE)          WANT TO RETURN TO IGNORE     @D35IDAP 01192000
ERPXAD5  DC    A(ERR1A)           WANT TO RETURN TO CANCEL     @D35IDAP 01193000
ERPXAD6  DC    A(ERR31)           WANT TO RETURN TO CANCEL     @D35IDAP 01194000
ERPXAD7  DC    A(ERR1B)           WANT TO RETURN TO CANCEL     @D52VDAP 01195000
ERPXAD8  DC    A(TSTPCI)          WANT TO RETURN TO TSTPCI     @D62NDAP 01196000
ERPXRDLY DC    A(INITDLAY)        WANT DELAYED RETRY           @D31..AP 01197000
ERPXMWA1 DC    A(DEQUNCON)        WANT CHANQ ENTRY DEQUEUED    @D37BDAP 01198000
ERPXMWA2 DC    A(PSTRESET)        WANT UNCONDITIONAL POSTING   @D37BDAP 01199000
ERPXMWRT DC    A(EMWINTRQ)        WANT EMERGENCY MESSAGE       @D37BDAP 01200000
ERPXEND  EQU   *                                               @D35IDAP 01201000
         SPACE 3                                               @D35IDAP 01202000
ERPXDISP EQU   ERPXAD1-ERPXAD1                                 @D35IDAP 01203000
ERPXINIT EQU   ERPXAD2-ERPXAD1                                 @D35IDAP 01204000
ERPXINTR EQU   ERPXAD3-ERPXAD1                                 @D35IDAP 01205000
ERPXIGNR EQU   ERPXAD4-ERPXAD1                                 @D35IDAP 01206000
ERPXER1A EQU   ERPXAD5-ERPXAD1                                 @D35IDAP 01207000
ERPXER31 EQU   ERPXAD6-ERPXAD1                                 @D35IDAP 01208000
ERPXER1B EQU   ERPXAD7-ERPXAD1                                 @D35IDAP 01209000
ERPXPCI  EQU   ERPXAD8-ERPXAD1                                 @D62NDAP 01210000
ERPXDEQ  EQU   ERPXMWA1-ERPXAD1                                @D37BDAP 01211000
ERPXPSTR EQU   ERPXMWA2-ERPXAD1                                @D37BDAP 01212000
ERPXMAX  EQU   ERPXEND-ERPXAD1                                 @D35IDAP 01213000
         TITLE 'VSE SUPERVISOR     IOINTER     FINAL STATUS PROCESSOR ' 01214000
         AIF   (NOT &BGDEBUG).NTXT070                          @D35IDAP 01215000
***************************************************************@D35IDAP 01216000
*                                                              @D35IDAP 01217000
*  FUNCTION:                                                   @D35IDAP 01218000
*            PROCESS FINAL STATUS INFORMATION                  @D35IDAP 01219000
*            PROCESS UNSOLICITED DEVICE END INTERRUPTS         @D35IDAP 01220000
*  ENTRY POINT:                                   AT LABEL     @D35IDAP 01221000
*            1. ENTERED  FROM STATUS ANALIZER        CEDETST   @D35IDAP 01222000
*  EXITS:                                         TO LABEL     @D35IDAP 01223000
*            POST ROUTINE                            PSTRESET  @D35IDAP 01224000
*            SIO-INITIATOR                           INITRG    @D35IDAP 01225000
*            I/O ERROR PROCESSOR                     INTTRANS  @D35IDAP 01226000
*            DISPATCHER                              DISP      @D35IDAP 01227000
*  INPUTS:                                                     @D35IDAP 01228000
*            R1= ADDRESS OF CCB                                @D35IDAP 01229000
*            R2= ADDRESS OF PUBX                               @D35IDAP 01230000
*            R3= ADDRESS OF PUB                                @D35IDAP 01231000
*            R4= ADDRESS OF CHANQ ENTRY                        @D35IDAP 01232000
*            R6= ADDRESS OF DISPATCHER                         @D35IDAP 01233000
*            R9= IOINTER BASE REGISTER                         @D35IDAP 01234000
*            RA= ADDRESS OF PIB                                @D35IDAP 01235000
*            RB= SUPERVISOR BASE REGISTER                      @D35IDAP 01236000
*           CSW= INTERRUPT INFORMATION                         @D35IDAP 01237000
*  OUTPUTS:                                                    @D35IDAP 01238000
*  REGISTERS:                                                  @D35IDAP 01239000
*            R1= ADDRESS OF CCB                                @D35IDAP 01240000
*            R2= ADDRESS OF PUBX                               @D35IDAP 01241000
*            R3= ADDRESS OF PUB                                @D35IDAP 01242000
*            R4= ADDRESS OF CHANQ ENTRY                        @D35IDAP 01243000
*            R6= ADDRESS OF DISPATCHER                         @D35IDAP 01244000
*            R9= IOINTER BASE REGISTER                         @D35IDAP 01245000
*            RA= ADDRESS OF PIB                                @D35IDAP 01246000
*            RB= SUPERVISOR BASE REGISTER                      @D35IDAP 01247000
*            RC= UNPREDICTABLE                                 @D35IDAP 01248000
*            RE= UNPREDICTABLE                                 @D35IDAP 01249000
*            RF= UNPREDICTABLE                                 @D35IDAP 01250000
*  SUBROUTINES:                                                @D35IDAP 01251000
*            SERADD   - ACTIVATE SERVICE TASK                  @D35IDAP 01252000
*            HR00     - EXECUTE POWER HOT READER ROUTINE       @D35IDAP 01253000
*                                                              @D35IDAP 01254000
***************************************************************@D35IDAP 01255000
         SPACE 3                                               @D35IDAP 01256000
.NTXT070 ANOP                                                  @D35IDAP 01257000
CEDETST  CLC   DEVSTA(L2),DEVCEND IS IT A PURE DEVICE END      @D35IDAP 01258000
         BNE   NOPUREDE           NO, ITS NOT                  @D35IDAP 01259000
PURERDY  NI    PUBCSFLG,XFF-OPINTV-PUBLOBSY RESET FLAG BITS    @DBOC    01260000
         CLI   PUBDEVTY,TFBA      INTERRUPT FROM FBA DEVICE    @D35DDAP 01261000
         BE    PUREDE10           YES, CHECK FOR READY INT     @D35DDAP 01262000
         CLI   PUBDEVTY,T3540     TEST FOR 3540 DEVICE         @D14CDOW 01263000
         BE    PUREDE10           YES, CHECK FOR READY INT     @D14CDOW 01264000
         TM    PBXFLAG,PBXDASD+PBXTAPE IS THIS DASD OR TAPE    @D62TAAP 01265000
         BZ    PUREDE40           NO, CHECK OTHER DEVICES      @D62TAAP 01266000
PUREDE10 TM    DEVSTA,ATTENT+DEVEND+UNITEXCP NEW READY         @DA44277 01267000
         BO    PUREDE20           YES, DEVICE BECAME READY     @DA44277 01268000
         TM    PUBCSFLG,QEDERR    QUEUED FOR RECOVERY          @DA35139 01269000
         BO    DEVISBSY           YES, SKIP SERVICE TASK       @DA35139 01270000
         TM    PUBCSFLG,DEVBSY    IS DEVICE BUSY               @DA06549 01271000
         BO    DEVISBSY           YES, CANT BE PACK CHANGE     @DA06549 01272000
         CLI   PUBCHQPT,NOTQUED   IS A REQUEST QUEUED          @D35IDAP 01273000
         BNE   DEVISBSY           YES, SKIP SERVICE TASK       @D35IDAP 01274000
PUREDE20 DS    0H'0'              SERVICE TASK ACTIVATION      @D28ADAP 01275000
         AIF   (NOT &VSE280).N280140                           @D28ADAP 01276000
         TM    PBXFLAG3,PBXF3VOL  VOLUME PROCESSING ONGOING    @D28ADAP 01277000
         BO    DEVISBSY           YES, NO NEED TO DO IT AGAIN  @D28ADAP 01278000
         TM    PBXFLAG2,PBXREADY  READY PROCESSING ONGOING     @D28ADAP 01279000
         BO    DEVISBSY           YES, NO NEED TO DO IT AGAIN  @D28ADAP 01280000
.N280140 ANOP                                                  @D28ADAP 01281000
         L     RC,BASESERI        SGSERI BASE ADDRESS          @D35CDFG 01282000
         BAL   RE,SERADD-SGSERI(RC) ACTIVATE SERVICE TASK      @D35CDFG 01283000
*SGLINK  SERADD,INPUT=(R3,R6,RC,RE),WORK=(R7,R8,RF)            @D52VDAP 01284000
         TM    PBXFLAG,PBXTAPE    IS IT A TAPE INTERRUPT       @D62TAAP 01285000
         BNO   DEVISBSY           NO,                          @DA44277 01286000
         OI    PBXFLAG2,PBXREADY  FORCE READY PROCESSING       @D21LDAP 01287000
         TM    DEVSTA,ATTENT+DEVEND+UNITEXCP NEW READY INTERR  @DA44277 01288000
         BO    TAPREADY           YES, PROCESS READY INTERRUPT @DA44277 01289000
         B     DEVISBSY                                        @D21LDAP 01290000
PUREDE40 ICM   RF,M15,APOWTB      ADDRESS POWER TABLE          @D3CADAP 01291000
         BZ    DEVISBSY           POWER NOT ACTIVE             @D35IDAP 01292000
         L     RE,APWRINTA        POWER INTERRUPT APPENDAGE    @D51GDAP 01293000
         BR    RE                      ======================> @D51GDAP 01294000
         SPACE 3                                               @D35IDAP 01295000
NOPUREDE CLI   DEVSTA,ATTENT+DEVEND+UNITEXCP+BUSY DEVICE READY @DA44277 01296000
         BE    PURERDY            YES, PROCESS READY INTERRUPT @DA44277 01297000
         CLI   DEVSTA,DEVEND+BUSY DEVICE READY                 @DA44277 01298000
         BE    PURERDY            YES, PROCESS READY INTERRUPT @DA44277 01299000
         TM    DEVSTA,DEVEND      DEVICE END                   @D35IDAP 01300000
         BO    DEVISBSY           YES, DO FINAL STATUS PROCESS @D35IDAP 01301000
         TM    DEVSTA,BUSY        SOME BUSY CONDITION          @D35IDAP 01302000
         BZ    DEVISBSY           NO, DO FINAL STATUS PROCESS  @D35IDAP 01303000
         TM    DEVSTA,XFF-STATMOD-BUSY  DEV.OR CONTR.UNIT BUSY @D35IDAP 01304000
         BNZ   NOPUR010           NO, TEST OTHER ENDING STATUS @D35IDAP 01305000
         OC    CCWDRS(3),CCWDRS   IS THIS PRIMARY INTERRUPT    @DA24650 01306000
         BZ    IOINTEXT           NO,                          @D31YDAP 01307000
         B     RSETEXIT           YES, RETRY LATER             @DA24650 01308000
NOPUR010 CLI   DEVSTA,CUEND+BUSY  ALTERNATE INTERRUPT PENDING  @DA00386 01309000
         BNE   RSETINIT           NO, GET NEXT I/O STARTED     @D35IDAP 01310000
         OC    CCWDRS(3),CCWDRS   IS THIS PRIMARY INTERRUPT    @DA33881 01311000
         BNZ   NOPUR015           YES, CHECK IF DEV WAS STARTED@DA33881 01312000
         B     ERR1A              NO, THIS IS A HARDWARE ERROR @DA33881 01313000
NOPUR015 TM    PUBCSFLG,DEVBSY    HAS DEVICE BEEN STARTED      @DA33881 01314000
         BO    RSETEXIT           YES, ALTERN.INTERR.PENDING   @D35IDAP 01315000
RSETINIT NI    PUBCSFLG,XFF-DEVBSY-OPINTV RESET PUB FLAG       @D35IDAP 01316000
         B     INITRG                  ======================> @D35IDAP 01317000
RSETEXIT NI    PUBCSFLG,XFF-DEVBSY-OPINTV RESET PUB FLAG       @D35IDAP 01318000
         LTR   R4,R4              IS CHANQ ENTRY PRESENT       @DA24650 01319000
         BZ    IOINTEXT           NO,                          @D31YDAP 01320000
         MVI   CHQCCSIO,ZERO      CLEAR SIO CONDITION BITS     @DA24650 01321000
         B     IOINTEXT           EXIT                         @D31YDAP 01322000
         SPACE 3                                               @D3CADAP 01323000
DEVISBSY TM    DEVSTA,BUSY        WAS THIS PENDING STATUS      @DA44277 01324000
         BO    RSETINIT           YES,                         @DA44277 01325000
         CLI   PUBCHQPT,NOTQUED   UNSOLICITED INTERRUPT        @DA44277 01326000
         BE    RSETINIT           YES                          @D35IDAP 01327000
         TM    PUBCSFLG,QEDERR    QUEUED FOR RECOVERY          @DA27502 01328000
         BNZ   RSETINIT           YES, DONT ALLOW DEQUEUING    @D35IDAP 01329000
         TM    CHQCCSIO,CHQCCACT+CHQCCLTE DID WE START IT      @D37BDAP 01330000
         BZ    RSETINIT           NO, THEN JUST IGNORE         @D35IDAP 01331000
         NI    PUBCSFLG,XFF-DEVBSY RESET PUB FLAG              @DA27502 01332000
         TM    CHQCCBB1,POSTDE    DEVICE END POSTING DESIRED   @D35IDAP 01333000
         BZ    DEVISB20           NO,                          @DA41356 01334000
         OC    CHQCSW+1(3),CHQCSW+1 DID WE RECEIVE PRIM. STATUS@DA41356 01335000
         BNZ   PSTRESET           YES, ======================> @DA41356 01336000
DEVISB20 DS    0H'0'                                           @DA41356 01337000
         L     RB,TIBPTR          LOAD TIB POINTER             @D36IDAP 01338000
         LTR   R1,R1              IS CCB AVAILABLE             @D35IDAP 01339000
         BZ    INTREL60           NO, THEN ITS OK              @DA43510 01340000
         OI    CHQCCSIO,CHQCCLTE  INDICATE LONG TIME ENTRY     @DA44201 01341000
         B     GOENABLE                ======================> @DSCSI   01342000
         EJECT                                                 @D21LDAP 01343000
TAPREADY DS    0H'0'                                           @D21LDAP 01344000
         TM    PBXFLAG3,PBXTERP1  IS THIS RESIDENT ERP         @DA44241 01345000
         BO    DEVISBSY           YES, IT RECOGNIZES VOL.CHNAGE@D62TAAP 01346000
         BAL   RE,RMSGETP2        GO AND GET PUB2 ADDRESS      @D3CADAP 01347000
*SGLINK  RMSGETP2,INPUT=(R3,R6,RE),OUTPUT=R8                   @D51ODAP 01348000
         USING PUB2ADR,R8         PUB2 BASE POINTER            @D3CADAP 01349000
         TM    P2FLAGS,P2OPEN     IS A FILE OPENED ON VOLUME   @D3CADAP 01350000
         BZ    DEVISBSY           NO, NO NEED TO ISSUE MESSAGE @D3CADAP 01351000
         OI    P2TFLG1,P2TUNSOL+P2TECPT INDICATE VOLUME CHANGE @D3CADAP 01352000
         MVC   DEVSTA(2),NOTOP    INDICATE EXEPTIONAL CONDITION@D21CDOW 01353000
         B     INTTRANS                ======================> @D21CDOW 01354000
         DROP  R8                 RELEASE PUB2 BASE POINTER    @D3CADAP 01355000
         TITLE 'VSE SUPERVISOR     IOINTER     PCI PROCESSOR          ' 01356000
         AIF   (NOT &BGDEBUG).NTXT080                          @D35IDAP 01357000
***************************************************************@D35IDAP 01358000
*                                                              @D35IDAP 01359000
*  FUNCTION:                                                   @D35IDAP 01360000
*            PROCESS PROGRAM CONTROLED INTERRUPTS              @D35IDAP 01361000
*  ENTRY POINT:                                   AT LABEL     @D35IDAP 01362000
*            1. ENTERED  FROM STATUS ANALIZER        PROCPCI   @D35IDAP 01363000
*  EXITS:                                         TO LABEL     @D35IDAP 01364000
*            SIO-INITIATOR                           INITRG    @D35IDAP 01365000
*            STATUS ANALYSER                         TSTBUSY   @D61BDAP 01366000
*  INPUTS:                                                     @D35IDAP 01367000
*            R1= ADDRESS OF CCB                                @D35IDAP 01368000
*            R2= ADDRESS OF PUBX                               @D35IDAP 01369000
*            R3= ADDRESS OF PUB                                @D35IDAP 01370000
*            R4= ADDRESS OF CHANQ ENTRY                        @D35IDAP 01371000
*            R6= ADDRESS OF DISPATCHER                         @D35IDAP 01372000
*            R9= IOINTER BASE REGISTER                         @D35IDAP 01373000
*            RA= ADDRESS OF PIB                                @D35IDAP 01374000
*            RB= SUPERVISOR BASE REGISTER                      @D35IDAP 01375000
*           CSW= INTERRUPT INFORMATION                         @D35IDAP 01376000
*  OUTPUTS:                                                    @D35IDAP 01377000
*  REGISTERS:                                                  @D35IDAP 01378000
*            R1= ADDRESS OF CCB                                @D35IDAP 01379000
*            R2= ADDRESS OF PUBX                               @D35IDAP 01380000
*            R3= ADDRESS OF PUB                                @D35IDAP 01381000
*            R4= ADDRESS OF CHANQ ENTRY                        @D35IDAP 01382000
*            R6= ADDRESS OF DISPATCHER                         @D35IDAP 01383000
*            R9= IOINTER BASE REGISTER                         @D35IDAP 01384000
*            RA= ADDRESS OF PIB                                @D35IDAP 01385000
*            RB= SUPERVISOR BASE REGISTER                      @D35IDAP 01386000
*  SUBROUTINES:                                                @D35IDAP 01387000
*                                                              @D35IDAP 01388000
***************************************************************@D35IDAP 01389000
.********************************************************************** 01390000
.*       WHEN THE PCI BIT IS ON WITH ZERO UNIT STATUS, EXIT TO TASK   * 01391000
.*       SELECTION IS REQUESTED WITH NO DEQUEUING OF CCB, NOR         * 01392000
.*       RESCHEDULING OF CHANNEL.                                     * 01393000
.********************************************************************** 01394000
         SPACE 3                                               @D35IDAP 01395000
.NTXT080 ANOP                                                  @D35IDAP 01396000
PROCPCI  TM    CHNSTA,PCI         PROGRAM CONTROLLED INTERRUPT @D35IDAP 01397000
         BZ    TSTBUSY            NO,  ======================> @D35IDAP 01398000
         CLI   CHNSTA,X'FE'       DEVICE NOT OPERATIONAL       @D21CDOW 01399000
         BE    TSTBUSY            YES, ======================> @D21CDOW 01400000
         LTR   R1,R1              IS CCB STILL AVAILABLE       @D35IDAP 01401000
         BZ    INITRG             NO   ======================> @D35IDAP 01402000
         XC    CHQCSW+1(7),CHQCSW+1 CLEAR CSW IN CHANQ         @DA22775 01403000
         L     R8,TIBPTR          SET UP TIB POINTER           @D36IDAP 01404000
         USING TIBADR,R8          TIB BASE POINTER             @D14BDAP 01405000
         BAL   RF,IOPOST1         POST I/O COMPLETE IF I/O BND @D36IDAP 01406000
*SGLINK  IOPOST1,INPUT=(R6,R8,RF),WORK=RE                      @D369DAP 01407000
         B     INITRG             GO   ======================> @DA37160 01408000
         DROP  R8                 RELEASE TIB BASE POINTER     @D14BDAP 01409000
         TITLE 'VSE SUPERVISOR     IOINTER     APPENDAGE PROCESSING   ' 01410000
         AIF   (NOT &BGDEBUG).NTXT090                          @D35IDAP 01411000
***************************************************************@D35IDAP 01412000
*                                                              @D35IDAP 01413000
*  FUNCTION:                                                   @D35IDAP 01414000
*            EXECUTE USERS APPENDAGE ROUTINE                   @D35IDAP 01415000
*  ENTRY POINT:                                   AT LABEL     @D35IDAP 01416000
*            1. ENTERED  FROM STATUS ANALIZER        PROCAPP   @D35IDAP 01417000
*  EXITS:                                         TO LABEL     @D35IDAP 01418000
*            STATUS ANALYZER                         TSTSTAT   @D35IDAP 01419000
*            POST ROUTINE                            PSTRESET  @D35IDAP 01420000
*            SIO-INITIATOR                           INITRG    @D35IDAP 01421000
*  INPUTS:                                                     @D35IDAP 01422000
*            R1= ADDRESS OF CCB                                @D35IDAP 01423000
*            R2= ADDRESS OF PUBX                               @D35IDAP 01424000
*            R3= ADDRESS OF PUB                                @D35IDAP 01425000
*            R4= ADDRESS OF CHANQ ENTRY                        @D35IDAP 01426000
*            R6= ADDRESS OF DISPATCHER                         @D35IDAP 01427000
*            R9= IOINTER BASE REGISTER                         @D35IDAP 01428000
*            RA= ADDRESS OF PIB                                @D35IDAP 01429000
*            RB= SUPERVISOR BASE REGISTER                      @D35IDAP 01430000
*           CSW= INTERRUPT INFORMATION                         @D35IDAP 01431000
*  OUTPUTS:                                                    @D35IDAP 01432000
*  REGISTERS:                                                  @D35IDAP 01433000
*            R0= UNPREDICTABLE                                 @D35IDAP 01434000
*            R1= ADDRESS OF CCB                                @D35IDAP 01435000
*            R2= ADDRESS OF PUBX                               @D35IDAP 01436000
*            R3= ADDRESS OF PUB                                @D35IDAP 01437000
*            R4= ADDRESS OF CHANQ ENTRY                        @D35IDAP 01438000
*            R6= ADDRESS OF DISPATCHER                         @D35IDAP 01439000
*            R7= UNPREDICTABLE                                 @D35IDAP 01440000
*            R8= UNPREDICTABLE                                 @D35IDAP 01441000
*            R9= IOINTER BASE REGISTER                         @D35IDAP 01442000
*            RA= ADDRESS OF PIB                                @D35IDAP 01443000
*            RB= SUPERVISOR BASE REGISTER                      @D35IDAP 01444000
*            RD= UNPREDICTABLE                                 @D35IDAP 01445000
*            RE= UNPREDICTABLE                                 @D35IDAP 01446000
*            RF= UNPREDICTABLE                                 @D35IDAP 01447000
*  SUBROUTINES:                                                @D35IDAP 01448000
*            CSWTRBTM      -RETRANSLATE BTAM CCW-ADDRESS       @D35IDAP 01449000
*            ........      -USER SUPPLIED APPENDAGE ROUTINE    @D35IDAP 01450000
*                                                              @D35IDAP 01451000
***************************************************************@D35IDAP 01452000
         SPACE 3                                               @D35IDAP 01453000
.NTXT090 ANOP                                                  @D35IDAP 01454000
PROCAPP  EQU   *                                               @D35IDAP 01455000
         CLI   PUBDEVTY,TQDIO     IS THIS THE OSA-EXPRESS DEV. @D66ADAP 01471000
         BNE   INTAPP00           NO, PROCEED MAIN PATH        @D66ADAP 01472000
         L     RC,ANEWAPP         ADDRESS OF SPECIAL ROUTINE   @D66ADAP 01473000
         OC    CSW(8),CSW         IS THIS ZERO STATUS (CSCH)   @DY45817 01474000
         BNZR  RC                 NO,  ====================>>> @DSCSI   01475000
         TM    PUBJCFLG,PUBPPD    WAS DEVICE OFFLINED          @DSCSI   01476000
         BZ    TSTSTAT            NO,                          @DSCSI   01477000
         MVI   CSW+3,2            INDICATE DEVICE OFFLINED     @DSCSI   01478000
         BR    RC                      ====================>>> @D66ADAP 01479000
INTAPP00 DS    0H'0'                                           @D66ADAP 01480000
         L     R0,CSW             GET CCW ADDR. FROM CSW                01481590
         STM   R0,R1,BTSAV        SAVE COPIED DATA             @DY46396 01482570
         TM    CCBCLS,BTTR        TRANSLATED BTAM REQUEST               01483000
         BNO   INTAPPND           NO, ITS NOT BTAM                      01484990
         L     R9,ACCWT           SET BASE FOR CCW-T MOD                01487000
         USING CCWTADR,R9                                               01488000
         BAS   R7,CSWTRBTM        RETRANSL.CCW-ADDR.           @D67..AV 01489000
*SGLINK  CSWTRBTM,INPUT=(R1,R7,R9),OUTPUT=RF                   @D369DAP 01491000
         L     R9,AINTER          RELOAD IOINTER BASE REGISTER @D35IDAP 01492000
         USING INTRTN,R9          RESET STANDARD ADDRESSING             01493000
         STCM  RF,7,CCWDRS        STORE VIRT. CCW ADDR.IN CSW  @DM02325 01494490
         L     R1,CCBVA           LOAD POINTER TO VIRT.CCB              01495000
.N370X50 ANOP                                                  @D35EDAP 01496000
         L     RE,APMGMDAT        PAGE MANAGER DATA AREA BASE  @D35EDAP 01497000
         USING PMGMDATA,RE                                     @D35EDAP 01498000
         L     R5,ASVPFREE        GET ADDR. OF PFREE ROUTINE   @D35EDAP 01499000
         DROP  RE                                                       01500000
INTAPPND SLR   R0,R0              ENTERED BY INTERRUPT HANDLER @D14CDFG 01501000
         L     RD,AGOAPPND        APPENDAGE ROUTINE ADDRESS    @D62OSAP 01502000
         BALR  RF,RD              EXECUTE USERS APPENDAGE      @D62OSAP 01503000
*SGLINK  GOAPPND,INPUT=(R0,R1,R3,R4,R5,R9,RD,RF),WORK=(R5,R7,R8) 62OSAP 01504000
         LM    R0,R1,BTSAV        RESTORE PNTR. TO COPIED DATA @DY46396 01505480
         ST    R0,CSW             RESTORE COPIED CCW ADDRESS   @DY46396 01505670
         TM    APPFLAG,APPDOIGN   IS REQUEST TO BE IGNORED     @DY46508 01505770
         BO    GOENABLE           YES,                         @DY46508 01505870
         NI    CHQFLG1,XFF-CHQDIDJA NEXT I/O IS TO BE ACCOUNTED@DA27131 01506000
         TM    APPFLAG,APPDORTY   IS REQUEST TO BE RETRIED     @D35IDAP 01507000
         BO    INTAPP60           YES,                         @DY46396 01508980
         CLC   APPCSW+4(4),CSW+4  DID APPENDAGE CHANGE STATUS  @DY46396 01509370
         BNE   INTAPP10           YES                          @DA31144 01510000
         MVC   CHQCCSIO,APPCCSIO  SET CHQCCSIO FLAGS           @DA31144 01511000
         B     INTAPP20           LEAVE SIO FLAGS UNCHANGED    @DA31144 01512000
INTAPP10 TM    DEVSTA,DEVEND+UNITCK USER FORCED ENDING STATUS  @DA31144 01513000
         BZ    INTAPP20           NO, LEAVE CHANQ UNCHANGED    @D149DAP 01514000
         OI    CHQCCSIO,CHQCCACT  INDICATE ENTRY IS ACTIVE     @D37BDAP 01515000
         MVC   CHQCSW+4(2),DEVSTA OVERRULE CSW INFO IN CHANQ   @D37BDAP 01516000
INTAPP20 TM    APPFLAG,APPDODEQ   IS INTERRUPT TO BE PROCESSED @DY46396 01520980
         BO    TSTSTAT            YES, ======================> @D35IDAP 01523000
         L     RD,AINTEQS         ADDRESS OF I/O ERROR ROUTINES@D3CADAP 01524000
         USING INTEQSET,RD        I/O ERROR ROUTINE BASE       @D3CADAP 01525000
         TM    APPFLAG,APPDOSDR   SDR RECORDING REQUESTED      @D14CDFG 01526000
         BO    INTAPP30           YES,                         @D14CDFG 01527490
         BAL   R7,INTEQSET        SET UP ERROR ENTRY           @D14CDFG 01528000
*SGLINK  INTEQSET,INPUT=(R1,R3,R4,R6,R7,RD),WORK=(R8,RE),OUTPUT=(R2,R5) 01529000
         DROP  R2                 RELEASE PUBX    BASE POINTER @D62TAAP 01530000
         B     INTEEXIT                ======================> @D14CDFG 01531000
         USING PBXADR,R2          PUBX BASE POINTER            @D62TAAP 01532000
INTAPP30 L     R5,ASRQTAB         RESOURCE QUEUE BASE ADDRESS  @D62TAAP 01533000
         LA    R5,SRQERQ-SRQTAB(,R5) ERROR ENTRY WAIT QUEUE    @D62TAAP 01534000
         USING RQADR,R5           RESOURCE DESCRIPTOR BASE     @D62TAAP 01535000
         TM    RSCDESC,FREEBIT    IS ERBLOC IN USE             @D66ADAP 01536000
         BZ    INTAPP50           YES, IGNORE RECORDING        @D66ADAP 01537000
         MVC   RSCDESC,TID        SET ERBLOC IN USE            @D66ADAP 01538000
         L     R5,AERBLOC         ADDRESS OF ERBLOC            @D62TAAP 01539000
         USING ERBLOC,R5          ERROR BLOCK BASE POINTER     @D62TAAP 01540000
         XC    ERQCSW1(ERQRFRL1-ERQCSW1),ERQCSW1 CLEAR ERBLOC  @D52VDAP 01541000
         LA    R5,ERQ1-ERBLOC(R5) POINT TO ERBLOC ALTERN. ENTRY@D62TAAP 01542000
         USING ERQAEADR,R5        ALTERNATE ERROR ENTRY BASE   @D21WDAP 01543000
         OI    ERQAEFLG,OCCUP     INDICATE ENTRY IS IN USE     @D21WDAP 01544000
         MVI   ERQAEMSG,RMSAE     POST ALTERNATE ENTRY         @D37DDAP 01545000
         MVI   ERQAEIND,XFF       INDICATE NOT AN ERROR RECORD @DA34415 01546000
         STH   R3,ERQAEPUB        SAVE PUBADDR                 @D37DDAP 01547000
         MVC   ERQAEADR(8),0(R8)  SAVE RECORD DATA             @D37DDAP 01548000
         MVI   ERQAETYP,X'34'     INDICATE BTAM TYPE RECORD    @D37DDAP 01549000
         L     R8,TIBPTR          GET TIB POINTER OF REQUESTOR @D36IDAP 01550000
         ST    R8,ERQAETIB        SAVE IT IN ERROR ENTRY       @D37DDAP 01551000
         L     R8,AERPTIB         ADDRESS OF ERP TIB           @D21WDAP 01552000
         CLI   TIBRQID-TIBADR(R8),WAITBND ERP TASK WAIT-BOUND  @D61NDAP 01553000
         BE    INTAPP40           YES, MAKE TASK READY         @D61NDAP 01554000
         CLI   TIBRQID-TIBADR(R8),SYSBND ERP ALREADY ACTIVE    @D21YDOW 01555000
         BNE   INTAPP50           YES, SKIP ACTIVATION         @D21YDOW 01556000
INTAPP40 L     RF,ERPBASE         GET ERP BASE ADDRESS         @D61NDAP 01557000
         USING SGERP,RF           SGERP BASE POINTER           @D61NDAP 01558000
         OI    SGERPECB+2,TRABIT  POST ERP MASTER ECB          @D61NDAP 01559000
         DROP  RF                 RELEASE SGERP BASE POINTER   @D61NDAP 01560000
         BAL   RF,POST            ACTIVATE ERP TASK            @D61NDAP 01561000
*SGLINK  POST,INPUT=(R6,R8,RF),WORK=RE                         @D21WDAP 01562000
INTAPP50 B     TSTSTAT                 ======================> @D35IDAP 01563000
         DROP  R5                 RELEASE ERBLOC ENTRY BASE    @D62TAAP 01565000
INTAPP60   DS    0H'0'            APPENDAGE ASKED FOR RETRY    @DY46396 01565040
           TM    CHQGRP,CHQGRVTM  IS IT A VTAM REQUEST         @DY46396 01565050
           BZ    RSETINIT         NO,  ======================> @DY46396 01565060
           AIF   (NOT &VTAM31).NV31020                         @DY46396 01565070
           TM    CCBCLS,CCBCOPY   IS THIS A COPIED CCB         @DY46396 01565080
           BZ    RSETINIT         NO,  ======================> @DY46396 01565090
           L     RE,CCBVA         GET VTAM CCB ADDRESS         @DY46396 01565100
           L     R7,CCBCCW-CCBADR(RE) VTAM REAL CCW ADDRESS    @DY46396 01565110
           L     R9,ACCWT         SET BASE REGISTER FOR CCWT RT@DY46396 01565120
           USING CCWTADR,R9       CCWTADR BASE                 @DY46396 01565130
           BAS   R8,CCWVFREE      FREE OLD CCW-COPY BLOCKS     @DY46396 01565140
*SGLINK    CCWVFREE,INPUT=(R1,R3,R8,R9),WORK=R9                @DY46396 01565150
           DROP  R9               RELEASE CCWTADR BASE         @DY46396 01565160
           USING *,R8             TEMP BASE                    @DY46396 01565170
           L     R9,ACCWT         BASE REGISTER FOR CCWVTRAN   @DY46396 01565180
           USING CCWTADR,R9       CCWTADR BASE                 @DY46396 01565190
           MVC   CCBBY3(1),CCBBY3-CCBADR(RE) RESTORE CCBBY3    @DY46396 01565200
           ST    R7,CCBCCW        STORE NEW CCW ADDRESS        @DY46396 01565210
           DROP  R8               RELEASE TEMP BASE            @DY46396 01565220
           BAS   R8,CCWVTRAN      TRANSLATE THE NEW CCW CHAIN  @DY46396 01565230
*SGLINK    CCWVTRAN,INPUT=(R1,R3,R8,R9)                        @DY46396 01565240
           L     R9,AINTER        RELOAD IOINTER BASE ADDRESS  @DY46396 01565250
           USING INTRTN,R9        IOINTER BASE POINTER         @DY46396 01565260
.NV31020   ANOP                                                @DY46396 01565270
           B     RSETINIT         YES, ======================> @DY46396 01565280
           DROP  RD               RELEASE SUBROUTINE BASE      @D3CADAP 01565780
         SPACE 3                                               @D35IDAP 01566000
ATTXCPT  DC    A(ATTOCRTN)        INTERCEPT ROUTINE ADDDRESS   @D21CDAP 01567000
APPRETAD DC    F'0'               RETURN ADDRESS SAVE AREA     @D35IDAP 01568000
APPCSW   DC    2F'0'              CSW SAVED AREA               @DA31144 01569000
VTM31SAV DC    2F'0'              VTAM CSWCCW+CCB-ADDRESS      @DY46396 01569500
APPCCSIO DC    X'00'              FLAG BYTE SAVE AREA          @DA31144 01570000
APPFLAG  DC    X'00'              FLAG BYTE USED FOR APPENDAGE @D35IDAP 01570100
APPDORTY EQU   X'80'              RETRY I/O REQUEST            @D35IDAP 01570200
APPDODEQ EQU   X'40'              DEQUEUE I/O REQUEST          @D35IDAP 01570300
APPDOSDR EQU   X'20'              SDR PROCESSING REQUESTED     @D35IDAP 01570400
APPDOERP EQU   X'10'              ERP INVOCATION REQUESTED     @D14CDAP 01570500
APPDOIGN EQU   X'08'              IGNORE I/O INTERRUPT         @DY46580 01570550
INTPRFLG DC    X'00'              RESERVED FLAG BYTE           @D61CDAP 01570600
INTPRATT EQU   X'80'              ACTIVATE ATTENTION           @D61CDAP 01570700
         TITLE 'VSE SUPERVISOR     IOINTER     INITIATE NEXT I/O      ' 01571000
         AIF   (NOT &BGDEBUG).NTXT120                          @D35IDAP 01572000
***************************************************************@D35IDAP 01573000
*                                                              @D35IDAP 01574000
*  FUNCTION:                                                   @D35IDAP 01575000
*            MAKE PROVISIONS TO START ANOTHER I/O              @D35IDAP 01576000
*            OPERATION                                         @D35IDAP 01577000
*  ENTRY POINT:                                   AT LABEL     @D35IDAP 01578000
*            ENTERED  FROM SEARCH PUB ROUTINE        INTNOPUB  @D35IDAP 01579000
*  EXITS:                                         TO LABEL     @D35IDAP 01580000
*            CHANNEL CHECK HANDLER                   CCENTRY1  @D35IDAP 01581000
*            CHANNEL SCHEDULER                       SGSCHED   @D35IDAP 01582000
*  INPUTS:                                                     @D35IDAP 01583000
*            R2= ADDRESS OF CCT                                @D35IDAP 01584000
*            R3= ADDRESS OF PUB                                @D35IDAP 01585000
*            R6= ADDRESS OF DISPATCHER                         @D35IDAP 01586000
*  OUTPUTS:                                                    @D35IDAP 01587000
*            R2= ADDRESS OF CCT                                @D35IDAP 01588000
*            R3= ADDRESS OF PUB                                @D35IDAP 01589000
*            R6= ADDRESS OF DISPATCHER                         @D35IDAP 01590000
*            RD= ADDRESS OF CHANNEL SCHEDULER (SGSCHED)        @D35IDAP 01591000
*  SUBROUTINES:                                                @D35IDAP 01592000
*                                                              @D35IDAP 01593000
***************************************************************@D35IDAP 01594000
         SPACE 3                                               @D35IDAP 01595000
.NTXT120 ANOP                                                  @D35IDAP 01596000
         DROP  R2                 RELEASE PUBX   BASE POINTER  @D37BDAP 01597000
INTNOPUB SLR   R3,R3              CLEAR PUB POINTER            @D31YDAP 01598000
         MC    $TRIO,$MCSDAID     PASS INFORMATION TO SDAID    @D52VDAP 01599000
         TM    CHNSTA,CHNCHK      CHANNEL FAILURE              @D35IDAP 01600000
         BNZ   RASPROC1           YES, ======================> @D31YDAP 01601000
INITLAST DS    0H'0'              ENTRY FROM MCRAS             @DAOM083 01602000
IOINTEXT MVI   IOINF+1,ZERO       RESET INTERRUPT INDICATOR    @D31YDAP 01603000
INITRG   L     RD,INITBASE        ADDRESS OF SGSCHED PREPARE   @D28ADAP 01607000
         BR    RD                 PREPARE FOR SCHEDULER        @D28ADAP 01608000
         SPACE 1                                                        01609000
         AIF   (&VSE280).Y280080                                        01610000
GOENABLE B     INITRG             TRY TO RESTART DEVICE        @DSCSI   01611000
         AGO   .N280160                                                 01612000
.Y280080 ANOP                                                           01613000
GOENABLE L     RD,INITBASE        ADDRESS OF SGSCHED           @DSCSI   01614000
         USING SGSCHED,RD         SGSCHED BASE POINTER         @DSCSI   01615000
         B     INITENAB           DONE =====================>> @DSCSI   01616000
         DROP  RD                 RELEASE SGSCHED BASE         @DSCSI   01617000
.N280160 ANOP                                                           01618000
         SPACE 2                                               @D31YDAP 01619000
RASPROC0 NI    PUBCSFLG,XFF-OPINTV-PUBLOBSY RESET FLAG BYTES   @DBOC    01620000
.*             IN CASE A "LONG BUSY"  WAS REPORTED PREVIOUSLY  @DYAOM   01621000
         CLI   PUBDEVTY,TQDIO     IS THIS OSA-EXPRESS DEVICE   @DY45817 01623000
         BNE   RASPROC1           NO,                          @DY45817 01624000
         LTR   R4,R4              CHANQ ADDRESS PRESENT        @DY45817 01625000
         BZ    RASPROC1           NO, LET MCRAS PROCESS IT     @DY45817 01626000
         USING CHQADR,R4          CHANQ BASE POINTER           @DY45817 01627000
         TM    CHQCCBB3,APPEN     IS APPENDAGE PRESENT         @DY45817 01628000
         BO    PROCAPP            YES, ======================> @DY45817 01629000
         DROP  R4                 RELEASE CHANQ BASE           @DY45817 01630000
         DROP  R3                 RELEASE PUB BASE POINTER     @D37BDAP 01632000
         SPACE 3                                               @D35IDAP 01633000
RASPROC1 DS    0H'0'              INITIALIZE RAS PROCESSING    @D35IDAP 01634000
         MVI   IOINF+1,ZERO       RESET INTERRUPT INDICATOR    @DAOM083 01635000
         L     RB,ACCHBASE        GET BASE FOR CCH ROUTINES    @D35IDAP 01636000
         USING CCENTRY1,RB        CCH   BASE POINTER           @D35IDAP 01637000
         LA    RE,CCENTRY1        LOAD EXIT ADDRESS                     01638000
GO2RASCC DS    0H'0'              PREPARE FOR RAS TO PROCESS            01639000
         BR    RE                 RAS, ====================>>> @D26DR06 01644000
*                                 CCENTRY1 FOR CHANNEL CHECK   @D28DR06 01645000
*                                 CCENTRY2 FOR RAS RETRY COMPL.@D28DR07 01646000
         DROP  RB                 RELEASE CCH BASE REGISTER    @D52VDAP 01647000
         SPACE 2                                                        01648000
         USING CHQADR,R4                                       @D14BDFG 01649000
RASPROC6 TM    CHQGRP,CHQGRRAS    IS THIS A RAS RETRY          @D52VDAP 01650000
         BZ    RASRETRN           NO, RESTORE PUBX POINTER     @D14BDAP 01651000
         L     RB,ACCHBASE        GET BASE ADDRESS FOR RAS     @D52VDAP 01652000
         USING CCENTRY1,RB        CCH   BASE POINTER           @D52VDAP 01653000
         LA    RE,CCENTRY2        RETRY COMPLETE EXIT ADDRESS  @D35IDAP 01654000
         B     GO2RASCC                                                 01655000
RASRETRN BAL   RE,GETPUBX         GET PUBX ADDRESS             @D62TPAP 01656000
*SGLINK  GETPUBX,INPUT=(R3,R9,RE),OUTPUT=R2                    @D61NDAP 01657000
         B     TSTAPPND                ======================> @D62TAAP 01658000
         SPACE 1                                                        01659000
ACCHBASE DC    A(CCENTRY1)        CHANNEL CHECK BASE ADDRESS   @D52VDAP 01660000
         DROP  R4                                              @D14BDFG 01661000
         DROP  RB                 RELEASE CCH BASE REGISTER    @D52VDAP 01662000
         TITLE 'VSE SUPERVISOR     IOINTER     INTERNAL USED CONSTANTS' 01663000
***************************************************************@D35IDAP 01664000
*                                                              @D35IDAP 01665000
*        CONSTANTS AND FIELDS USED BY IOINTER                  @D35IDAP 01666000
*                                                              @D35IDAP 01667000
***************************************************************@D35IDAP 01668000
         SPACE 2                                               @D35IDAP 01669000
EAXCR8   DC    F'0'               ORIGINAL CONTENTS OF CR8     @D51KDTP 01670000
EAXCR8W  DC    F'0'               WORK AREA FOR CR8            @D51KDTP 01671000
         AGO   .SKIP040                                                 01672000
ALCKPROC DC    A(LCKPROC)         LCK TASK SPECIAL CODE        @DA42913 01673000
.SKIP040 ANOP                                                           01674000
AINTPOWC DC    A(INTPOWC9)        CKECK CHAN9/12 CONDITION     @DA44172 01675000
AGOAPPND DC    A(GOAPPND)         CEA ROUTINE ADDRESS          @D62OSAP 01676000
AINTALPU DC    A(INTALPUB)        PUB ALLOCATION ROUTINE       @D61BDAP 01677000
AINTEQS  DC    A(INTEQSET)        ERROR SUBROUTINE BASE ADDRESS@D3CADAP 01678000
ATAPERTN DC    A(TTAPERTN)        TAPE SUBROUTINE BASE ADDRESS @D21IDAP 01679000
APROCDLY DC    A(PROCDLY)         DELAYED INTERRUPT PROCESSING @D21IDAP 01680000
AMIHCHCK DC    A(MIHCHECK)        MIH FRONT END PROCESSING     @D21IDAP 01681000
AINTSMFU DC    A(INTSMFUP)        MEASUREMENT UPDATE ROUTINE   @DTIMING 01682000
APUBISVT DC    A(PUBISVTM)        PUB OWNERSHIP SUBROUTINE     @D31YDAP 01683000
APROCSYS DC    A(PROCSYSF)        SYSFIL PROCESSING ROUTINE    @D31YDAP 01684000
APWRINTA DC    A(PWRINTAP)        POWER INTERRUPT APPENDAGE    @D51GDAP 01685000
AINTGUSE DC    A(INTGUSER)        USER WANTS SENSE ROUTINE     @D52VDAP 01686000
APOSTAR  DC    A(POSTARPR)        POST PROCESSING FOR AR-TASK  @DY45629 01687000
AINTVMNO DC    A(INTVMNOP)        NOT OPERATIONAL PROCESSIND   @D66CDAP 01688000
AATNPROC DC    A(ATTNPROC)        ATTENTION PROCESSING BASE    @D28ADAP 01689000
ANEWAPP  DC    A(NEWAPPND)        NEW APPENDAGE PROCESSING     @D66ADAP 01691000
APUBTAB  DC    A(PUBTAB)          PUB AREA START ADDRESS       @DY46585 01691600
APUBEND  DC    A(PUBEND)          PUB AREA END ADDRESS         @DY46585 01692200
CENDDEND DC    X'0C00'            PURE CHANNEL+DEVICE END      @D35IDAP 01693000
CHANNEND DC    X'0800'            PURE CHANNEL END             @D35IDAP 01694000
DEVCEND  DC    X'0400'            PURE DEVICE END INTERRUPT    @D35IDAP 01695000
DELAYINT EQU   X'03'              DELAYED I/O INTERRUPT        @D14BDAP 01696000
         TITLE 'VSE SUPERVISOR     IOINTER      I/O ERROR  PROCESSING ' 01705000
         AIF   (NOT &BGDEBUG).NTXT130                          @D35IDAP 01706000
***************************************************************@D34SDAP 01707000
*                                                              @D34SDAP 01708000
*   FUNCTION      THE MAIN FUNCTION OF THE I/O ERROR PROCESSOR @D34SDAP 01709000
*                 IS TO RETRIEVE ALL THE INFORMATION WHICH IS  @D34SDAP 01710000
*                 NECESSARY FOR THE RESIDENT DISK ERROR RECVRY @D34SDAP 01711000
*                 OR THE TRANSIENT ERROR RECOVERY PROCEDURES   @D34SDAP 01712000
*                 TO SUCCESSFULLY RECOVER FROM THE ERROR.      @D34SDAP 01713000
*                 ALL THIS INFORMATION IS SAVED IN A SO CALLED @D34SDAP 01714000
*                 ERROR QUEUE ENTRY WHICH IS PART OF THE ERROR @D34SDAP 01715000
*                 BLOCK.                                       @D34SDAP 01716000
*                 RESIDENT DISK ERROR QUEUE ENTRIES ARE        @D35IDAP 01717000
*                 SEPERATED FROM TRANSIENT ERROR QUEUE ENTRIES @D35IDAP 01718000
*                 WITHIN THIS ERROR BLOCK.                     @D34SDAP 01719000
*                 THE I/O ERROR PROCESSOR IS SUBDIVIDED INTO   @D34SDAP 01720000
*                 THREE MAJOR PARTS:                           @D34SDAP 01721000
*                    1.   DEVICE NOT OPERATIONAL     PROCESSOR @D34SDAP 01722000
*                    2.   ABNORMAL CHANNEL STATUS    PROCESSOR @D34SDAP 01723000
*                    3.   UNIT CHECK                 PROCESSOR @D34SDAP 01724000
*                    IN ADDITION TO THESE MAJOR PARTS A SET OF @D34SDAP 01725000
*                    SUBROUTINES HAS BEEN ESTABLISHED.         @D34SDAP 01726000
*                       1.   ALLOCATE FREE ERROR QUEUE ENTRY   @D34SDAP 01727000
*                       2.   SET UP AN ERROR QUEUE ENTRY       @D34SDAP 01728000
*                       3.   FREE ALLOCATED ERROR QUEUE ENTRY  @D34SDAP 01729000
*                                                              @D34SDAP 01730000
*   ENTRY POINTS                                               @D34SDAP 01731000
*                                                              @D34SDAP 01732000
*                 INTENOP     DEVICE NOT OPERATIONAL           @D34SDAP 01733000
*                 INTERRCC    ABNORMAL CHANNEL STATUS          @D34SDAP 01734000
*                 INTERRUC    UNIT CHECK                       @D34SDAP 01735000
*                                                              @D34SDAP 01736000
*   REGISTER INPUT                                             @D34SDAP 01737000
*                                                              @D34SDAP 01738000
*                 REGISTER  1     POINTER TO USERS CCB         @D34SDAP 01739000
*                 REGISTER  3     POINTER TO PUB               @D34SDAP 01740000
*                 REGISTER  4     POINTER TO CHANQ ENTRY       @D34SDAP 01741000
*                 REGISTER  6     DISPATCHER BASE ADDRESS      @D34SDAP 01742000
*                 REGISTER  9     BASE REGISTER                @D34SDAP 01743000
*                 REGISTER 11     SUPERVISOR BASE REGISTER     @D34SDAP 01744000
*                                                              @D34SDAP 01745000
*   REGISTER OUTPUT                                            @D34SDAP 01746000
*                                                              @D34SDAP 01747000
*                 REGISTER  0     UNPREDICTABLE                @D34SDAP 01748000
*                 REGISTER  2     POINTER TO ERROR QUEUE ENTRY @D34SDAP 01749000
*                 REGISTER  5     POINTER TO ERROR BLOCK       @D34SDAP 01750000
*                 REGISTER  7     UNPREDICTABLE                @D34SDAP 01751000
*                 REGISTER  8     UNPREDICTABLE                @D34SDAP 01752000
*                 REGISTER 14     UNPREDICTABLE                @D34SDAP 01753000
*                 REGISTER 15     UNPREDICTABLE                @D34SDAP 01754000
*                                                              @D34SDAP 01755000
***************************************************************@D34SDAP 01756000
         SPACE 2                                               @D35IDAP 01757000
         AGO   .YTXT130                                        @D35IDAP 01758000
.NTXT130 ANOP                                                  @D35IDAP 01759000
***************************************************************@D34SDAP 01760000
*                                                              @D34SDAP 01761000
*        DEVICE NOT OPERATIONAL PROCESSOR                      @D34SDAP 01762000
*                                                              @D34SDAP 01763000
***************************************************************@D34SDAP 01764000
         SPACE 2                                               @D34SDAP 01765000
.YTXT130 ANOP                                                  @D35IDAP 01766000
         USING CCBADR,R1          CCB BASE POINTER             @D37BDAP 01767000
         USING PUBADR,R3          PUB BASE POINTER             @D37BDAP 01768000
         USING CHQADR,R4          CHANQ BASE POINTER           @D37BDAP 01769000
INTENOP  DS    0H'0'              NOT OPERATIONAL PROCESSING   @DA33314 01770000
         TM    PUBJCFLG,PUBPPD    WAS DEVICE OFFLINED          @D51GDAP 01771000
         BO    INTENOPN           YES, LEAVE IT AS IT IS       @D66CDAP 01772000
         L     RC,AINTVMNO        INTVMNOP BASE ADDRESS        @D66CDAP 01773000
         BR    RC                 CHECK IF DEVICE BECAME READY @D66CDAP 01774000
INTENOPN CLC   PUBCHANN(2),SYSOCDEV IS IT OPERATOR CONSOLE     @D51GDAP 01775000
         BNE   INTENOP0           NO,                          @D51GDAP 01776000
         CLC   CHQTID,IORQ4SNS    IS THIS PATH VERIFICATION    @D66ADAP 01777000
         BE    INTENOP0           YES, DONT DISCONNECT         @DA42940 01778000
         OI    IJBOPMOD,IJBOPDIS+IJBOPEXT FORCE DISCONNECT     @D51GDAP 01779000
         L     RD,INITBASE        SGSCHED BASE ADDRESS         @D51GDAP 01780000
         USING SGSCHED,RD         SGSCHED BASE POINTER         @D51GDAP 01781000
         B     SIOXRETN           DO I/O AGAIN ==============> @D61CDAP 01782000
         DROP  RD                 RELEASE SCHEDULER BASE       @D51GDAP 01783000
INTENOP0 LA    RF,RASPROC6        CHNL ERROR CONTINUATION ADDR @D62TAAP 01784000
         TM    CHQGRP,CHQGRRAS    RAS DASD RETRY ACTIVE        @D14BDAP 01785000
         BO    INTEN010           YES, GO TO MCRAS             @DA30327 01786000
INTENOP5 BAL   RF,INTEN010        SET CHQCSW AND INDICATORS    @D3AADAP 01787000
         CLC   CHQTID,IORQ4AR     IS THIS A SYSTEM TASK REQUEST@D66ADAP 01788000
         BNH   INTEN020           YES, LEAVE SOB2 UNCHANGED    @D14CDAP 01789000
         CLI   PUBDEVTY,TQDIO     IS THIS THE OSA-EXPRESS DEV. @D66ADAP 01791000
         BE    INTEN000           YES, TEST FOR APPENDAGE      @D66ADAP 01792000
         TM    CCBCLS,BTAM        IS THIS A BTAM REQUEST       @D21IDAP 01794000
         BZ    INTEN005           NO                           @D21IDAP 01795000
INTEN000 TM    CHQCCBB3,APPEN     IS APPENDAGE PRESENT         @D21IDAP 01796000
         BZ    INTEN005           NO                           @DA36731 01797000
         BAL   RF,INTERTRN        SET UP I/O POINTERS          @DA36731 01798000
         B     PROCAPP            LET APPENDAGE HANDLE         @DA36731 01799000
INTEN005 L     R5,CRADDR          GET COMREG ADDRESS           @D35IDAP 01800000
         USING COMREG,R5          COMREG BASE POINTER          @D35IDAP 01801000
         TM    SOB2,DUMDVC        CAN CONDITION BE IGNORED     @D35IDAP 01802000
         BZ    INTTRANS           NO, PROCESS AS NORMAL ERROR  @D3CADAP 01803000
         NI    SOB2,X'FF'-DUMDVC  INDICATE NOT OPERATIONAL     @D35IDAP 01804000
         LA    RF,PSTRESET        INITIALIZE RETURN REGISTER   @D35IDAP 01805000
         B     INTERTRN           SET UP I/O POINTERS          @D35IDAP 01806000
         DROP  R5                 STOP COMREG ADDRESSABILITY   @D35IDAP 01807000
INTEN010 STH   R2,CHNADR          SET UP DEVICE ADDRESS        @D3CADAP 01808000
         STH   R2,CSW+6           SAVE DEV ADDR INTO CSW       @DA33314 01809000
         OI    CHQCCSIO,CHQCCNOP  INDICATE NOT OPERATIONAL     @D35IDAP 01810000
         MVC   DEVSTA(2),NOTOP    SET DEVICE NOT OPERATIONAL   @D34SDAP 01811000
         L     R7,CAW             SET UP PROPER CSW AS         @D35IDAP 01812000
         AH    R7,H8              CHANNEL WOULD NORMALLY DO    @D35IDAP 01813000
         ST    R7,CSW             STORE FAILING CCW ADDRESS    @D35IDAP 01814000
         MVC   CHQCSW+1(7),CSW+1  SAVE CSW IN CHANQ            @D21CDOW 01815000
         BR    RF                 CONTINUE                     @D3CADAP 01816000
INTEN020 TM    CHQCCBB1,ACCUNRIO  DOES TASK EXPECT ERROR       @D14CDAP 01817000
         BO    INTTRANS           YES,                         @D14CDAP 01818000
         CLC   CHQTID,IORQ4AR     IS THIS A SYSTEM TASK REQUEST@D66ADAP 01819000
         BNL   INTTRANS           NO, NORMAL ERROR MESSAGE     @DA44201 01820000
         L     RF,ERPXMWRT        ADDRESS OF EMW ROUTINE       @D14CDAP 01821000
         BR    RF                 GIVE EMERGENCY MESSAGE       @D14CDAP 01822000
         EJECT ,                                               @D51GDAP 01823000
***************************************************************@D34SDAP 01824000
*                                                              @D34SDAP 01825000
*        ABNORMAL CHANNEL STATUS PROCESSOR                     @D34SDAP 01826000
*                                                              @D34SDAP 01827000
***************************************************************@D34SDAP 01828000
         SPACE 2                                               @D34SDAP 01829000
INTERRCC EQU   *                                               @D34SDAP 01830000
         L     RD,AINTEQS         I/O ERROR ROUTINE ADDRESS    @D21LDAP 01831000
         USING INTEQSET,RD        I/O ERROR ROUTINE BASE       @D21LDAP 01832000
         TM    CHNSTA,CHCHN+CHDTA CHAN. CHAINING-OR DATA CHECK @D35IDAP 01833000
         BZ    INTTRANS           NO,                          @D21LDAP 01834000
         CLI   PUBDEVTY,TTPA      IS THIS A TPA DEVICE         @D62TAAP 01835000
         BE    RASPROC1           YES,                         @D62TAAP 01836000
         TM    CHQDEV,CHQDASD+CHQFBA DASD DEVICE               @D35IDAP 01837000
         BZ    INTTRANS           NO, DONT RETRY RIGHT AWAY    @D21LDAP 01838000
         IC    R7,CHQERRCT        GET NUMBER OF RETRIES        @D35IDAP 01839000
         LA    R7,ONE(R7)         INCREASE IT BY ONE           @D35IDAP 01840000
         STC   R7,CHQERRCT        STORE NEW VALUE              @D35IDAP 01841000
         CLI   CHQERRCT,ONE       IS THIS FIRST OCCURENCE      @D35IDAP 01842000
         BNH   RSETINIT           YES, ======================> @D35IDAP 01843000
         TM    CHNSTA,CHDTA       CHANNEL DATA CHECK           @D35IDAP 01844000
         BO    INTECC10           YES, SET UP ERROR ENTRY      @D35IDAP 01845000
         CLI   CHQERRCT,TEN       UPPER RETY LIMIT REACHED     @D35IDAP 01846000
         BNH   RSETINIT           NO,  ======================> @D35IDAP 01847000
INTECC10 MVI   CHQERRCT,ZERO      CLEAR RETRY COUNTER          @D35IDAP 01848000
         DROP  RD                 RELEASE I/O ERROR BASE       @D21LDAP 01849000
INTTRANS L     RD,AINTEQS         I/O ERROR ROUTINE ADDRESS    @D3CADAP 01850000
         USING INTEQSET,RD        I/O ERROR ROUTINE BASE       @D3CADAP 01851000
         LTR   R1,R1              CHANQ ADDRESS PRESENT        @DA41385 01852000
         BZ    INTTRA10           NO,                          @DA41385 01853000
         CLC   CHQTID,IORQ4SNS    IS THIS A SENSE TASK REQUEST @D66ADAP 01854000
         BE    INTPSNS5           YES, POST SNSTASK            @D35ADAP 01855000
INTTRA10 BAL   R7,INTEQSET        GET AND FILL ERROR ENTRY     @D37BDFG 01856000
*SGLINK  INTEQSET,INPUT=(R1,R3,R4,R6,R7,RD),WORK=(R8,RE),OUTPUT=(R2,R5) 01857000
         B     INTEEXIT                ======================> @D35IDAP 01858000
         DROP  RD                 RELEASE I/O ERROR BASE       @D3CADAP 01859000
         EJECT                                                          01860000
***************************************************************@D34SDAP 01861000
*                                                              @D34SDAP 01862000
*        UNIT CHECK PROCESSOR                                  @D34SDAP 01863000
*                                                              @D34SDAP 01864000
***************************************************************@D34SDAP 01865000
         SPACE 2                                               @D34SDAP 01866000
INTERRUC EQU   *                  ENTRY POINT FOR UNIT CHECK   @D34SDAP 01867000
         L     RD,AINTEQS         ADDRESS OF I/O ERROR ROUTINES@D3CADAP 01868000
         USING INTEQSET,RD        I/O ERROR ROUTINE BASE       @D3CADAP 01869000
         LTR   R1,R1              IS A CCB AVAILABLE           @D34SDAP 01870000
         BZ    INTEUC10           NO                           @D34SDAP 01871000
         CLC   CHQTID,IORQ4SNS    IS IT A SENSE TASK REQUEST   @D66ADAP 01872000
         BE    INTPSNS            YES POST SNSTASK             @D34SDAP 01873000
         TM    CCBCLS,BTAM        IS THIS A BTAM REQUEST       @D34SDAP 01874000
         BO    INTTPERR           YES, SPECIAL PROCESSING      @D34SDAP 01875000
INTEUC10 BAL   R7,INTEQSET        SET UP ERROR QUEUE ENTRY     @D61CDAP 01876000
*SGLINK  INTEQSET,INPUT=(R1,R3,R4,R6,R7,RD),WORK=(R8,RE),OUTPUT=(R2,R5) 01877000
         USING ERBLKADR,R2        ERROR ENTRY BASE POINTER     @D37BDFG 01878000
         USING ERBLOC,R5          ERROR BLOCK BASE POINTER     @D37BDFG 01879000
         CLI   SNSSDAID,XFF       DID SDAID PROVIDE SENSE      @DA37004 01880000
         BNE   INTSDAID           YES, DONT INVOKE SENSE TASK  @DA37004 01881000
         OI    ERBLKFL1,NEEDSNS   INDICATE SENSE OUTSTANDING   @DA37004 01882000
         L     R8,ASNSTIB         ADDRESS OF SENSE TASK TIB    @DA28632 01883000
         CLI   TIBRQID-TIBADR(R8),SYSBND IS SNS-TASK ACTIVE    @DA28632 01884000
         BE    INTACSNS           NO, GET SNS-TASK ACTIVATED   @DA30525 01885000
         BAL   R7,INTEQPST        POST SNS-TASK                @D14ZDAP 01886000
*SGLINK  INTEQPST,INPUT=(R2,R7,RD),WORK=(R5,R8,RE)             @D149DAP 01887000
         B     GOENABLE           EXIT ======================> @DSCSI   01888000
INTACSNS SGSETUP ACTIVATE,STASK=SNS,WR1=R7,WR2=R8,OPTION=SYSOWNER 30525 01889000
         BAL   R7,INTEQPST        INSERT ERROR ENTRY IN CHAIN  @D14ADAP 01890000
*SGLINK  INTEQPST,INPUT=(R2,R7,RD),WORK=(R5,R8,RE)             @D149DAP 01891000
         DROP  RD                 RELEASE SUBROUTINE BASE      @D3CADAP 01892000
         L     RD,ASNSINIT        SNS-TASK BASE ADDRESS        @DA30525 01893000
         BR    RD                      =====================>> @DA30525 01894000
         SPACE 1                                               @D37BDFG 01895000
         DROP  R1                 RELEASE CCB BASE             @D62NDAP 01896000
         DROP  R2                 RELEASE ERBLKADR BASE        @D62NDAP 01897000
         DROP  R3                 RELEASE PUB BASE             @D62NDAP 01898000
         DROP  R4                 RELEASE CHANQ BASE           @D62NDAP 01899000
         DROP  R5                 RELEASE ERBLOC BASE          @D62NDAP 01900000
         EJECT ,                                               @D62NDAP 01901000
         USING ERBLKADR,R2        ERROR ENTRY BASE             @D62NDAP 01902000
         USING PUBADR,R3          PUB BASE POINTER             @D62NDAP 01903000
         USING ERBLOC,R5          ERBLOC BASE POINTER          @D62NDAP 01904000
         USING SNSTASK,RD         SNS-TASK BASE POINTER        @D37BDFG 01905000
         USING PBXADR,RE          PUBX BASE POINTER            @D62TAAP 01906000
INTRSNS  EQU   *                  SENSE TASK RETURNS HERE      @D37BDFG 01907000
         SLR   R1,R1              CLEAR CCB POINTER            @D14BDAP 01908000
         ICM   R1,7,ERRQCCB+1     RESTORE CCB ADDRESS          @D62NDAP 01909000
         USING CCBADR,R1          CCB BASE POINTER             @D62NDAP 01910000
         L     R4,ERRQCHQA        RESTORE CHANQ ADDRESS        @D62NDAP 01911000
         USING CHQADR,R4          CHANQ BASE POINTER           @D62NDAP 01912000
         LTR   R4,R4              IS CHANQ ENTRY PRESENT       @D62NDAP 01913000
         BZ    INTRS000           NO,                          @D62NDAP 01914000
         SLR   RA,RA              CLEAR REGISTER               @D62NDAP 01915000
         ICM   RA,3,CHQTID        GET TASK ID                  @D66ADAP 01916000
         BAL   R7,SETLCALL        SWITCH BACK TO SERVICE OWNER @D62NDAP 01920000
*SGLINK  SETLCALL,INPUT=(R6,R7,RA),OUTPUT=(RA),WORK=R0         @D62NDAP 01921000
         AGO   .SKIP060           COM                          @D62NDAP 01922000
         SLR   R4,R4              CLEAR CHANQ POINTER          @D14BDAP 01923000
         AIF   (&AG1 LE 254).NMDV100                           @D51ODGL 01924000
         CLI   PUBCHQPT,XFF       IF THERE IS ANY REQUEST      @D51ODGL 01925000
         BE    INTEUC50           NO,                          @D51ODGL 01926000
         IC    R4,PUBCHQPT        CHANQ INDEX LOW  BYTE        @D51ODGL 01927000
         ICM   R4,2,PUBCHQPH      CHANQ INDEX HIGH BYTE        @D51ODGL 01928000
         SLL   R4,CHQSLEN         MULTIPLY WITH ENTRY LENGTH   @D51ODGL 01929000
         A     R4,ACHANQ          CHANQ ENTRY ADDRESS          @D51ODGL 01930000
         ICM   R4,8,H0            CLEAR CHAINING BYTE          @D68REEF 01931000
         CLM   R4,7,ERRQCHQA+1    SET CONDITION CODE           @D51ODGL 01932000
         LA    R4,0               AND RESET REGISTER           @D51ODGL 01933000
.NMDV100 AIF   (&AG1 GT 254).YMDV100                           @D51ODGL 01934000
         CLC   ERRQCQPT,PUBCHQPT  IS OUR REQUEST QUEUED TO PUB @D14BDAP 01935000
.YMDV100 ANOP                                                  @D51ODGL 01936000
         BNE   INTEUC50           NO,                          @D14BDAP 01937000
         BAL   R7,CQDSP           RESTORE ASSOCIATED POINTERS  @D34SDAP 01938000
*SGLINK  CQDSP,INPUT=(R3,R6,R7),OUTPUT=(R1,R4,RA),WORK=R0      @D369DAP 01939000
         LA    R4,0(,R4)          CLEAR CHAINING BYTE          @DAOM043 01940000
INTEUC50 TM    ERBLKFLG,ERSNSDAV  SENSE DATA ALREADY AVAILABLE @D37BDFG 01941000
.SKIP060 ANOP                                                  @D62NDAP 01942000
INTRS000 TM    ERBLKFLG,ERSNSDAV  SENSE DATA ALREADY AVAILABLE @D62NDAP 01943000
         BO    INTTOERP           YES, ======================> @D37BDFG 01944000
         IC    R7,ERBLKSNL        GET LENGTH OF SENSE DATA     @D37BDFG 01945000
         BCTR  R7,0               MINUS ONE                    @D37BDFG 01946000
         EX    R7,INTSMVER        SAVE SENSE IN ERROR ENTRY    @D37BDFG 01947000
.*                                                                      01948000
.*       SENSE DATA IS NOW AVAILABLE FROM SNS-TASK                      01949000
.*                                                                      01950000
         DROP  RD                 RELEASE SNS-TASK BASE POINTER@D34SDAP 01951000
INTGSNS  OI    ERBLKFLG,ERSNSDAV  INDICATE SENSE DATA AVAIL.   @D37BDFG 01952000
         XC    INTFSTFL,INTFSTFL  CLEAR FAST PROCESSING FLAG   @D52VDAP 01953000
         TM    PBXTREC,PBXTCTG    IS THIS A CARTRIDGE DEVICE   @D62TAAP 01954000
         BZ    INTGS010           NO,                          @D52VDAP 01955000
         CLI   ERRQSNS+3,X'4D'    IS IT RESETTING EVENT        @D52VDAP 01956000
         BE    INTPVTRT           YES, NEED PATH VERIFICATION  @D52VDAP 01957000
INTGS010 TM    PBXFLAG,PBXDASD    IS THIS A DASD DEVICE        @D62TAAP 01958000
         BO    INTGDERF           YES, TRY FAST RECOVERY       @D62TAAP 01959000
INTGS020 LTR   R1,R1              USER ALREADY POSTED          @D37BDFG 01960000
         BZ    INTGS050           YES,                         @D34SDAP 01961000
         TM    CHQFLG1,CHQECKD    CONVERTED CHANNEL PROGRAM    @D51GDAP 01962000
         BZ    INTGS030           NO,                          @D62TAAP 01963000
         OI    ERRQFLG,RECONLY    REQUEST RECORDING ONLY       @D62TAAP 01964000
         DROP  RE                 RELEASE PUBX BASE            @D62NDAP 01965000
         BAL   RE,INTRSQED        RESET QUEUED IN ERROR        @D62NDAP 01966000
*SGLINK  INTRSQED,INPUT=(R3,R4,RE),WORK=R0                     @D62NDAP 01967000
         NOP   0                0 DELAY PROCESSING RETURN      @D62NDAP 01968000
         B     INTTOERP         4      ======================> @D62NDAP 01969000
         USING PBXADR,RE          PUBX BASE POINTER            @D62TAAP 01970000
INTGS030 TM    CHQCCBB3,USENSE    USER WANTS SENSE DATA        @D35IDAP 01971000
         BZ    INTGS040           NO                           @D35IDAP 01972000
         L     RD,AINTGUSE        SUBROUTINE BASE ADDRESS      @D52VDAP 01973000
         TM    CCBCLS,CCBIORB     IS THIS AN IORB              @D35IDAP 01974000
         BZR   RD                 NO,  ======================> @D52VDAP 01975000
.*                                                             @D61NDAP 01976000
.*       USER OWN ERROR ROUTINE PROCESSING                     @D61NDAP 01977000
.*                                                             @D61NDAP 01978000
INTGS040 TM    CHQCCBB1,OWNUCRT   HAS USER OWN UC-ROUTINE      @D35IDAP 01979000
         BO    INTEEXIT           YES, ======================> @D62TAAP 01980000
INTGS050 CLI   ERRQMSG,RCVMSG     ERROR ON RECOVERY            @D62TAAP 01981000
         BE    INTEEXIT           YES, ======================> @D62TAAP 01982000
         CLI   INTFSTFL,X'E0'     IS RECOVERY ALREADY COMPLETE @D52VDAP 01983000
         BE    INTNOREC           YES, ======================> @D52VDAP 01984000
         TM    PBXFLAG,PBXDASD    IS THIS A DASD DEVICE        @D62TAAP 01985000
         BO    INTEEXIT           YES, ======================> @D62TAAP 01986000
         CLI   PUBDEVTY,PRINTER   IS DEVICE A PRINTER          @D62TAAP 01987000
         BL    INTEEXIT           NO,  ======================> @D62TAAP 01988000
         CLI   PUBDEVTY,TTAPE     SURE THIS IS A PRINTER       @D62TAAP 01989000
         BNL   INTEEXIT           NO,  ======================> @D34SDAP 01990000
         TM    ERRQSNS,CHANN9     CHANNEL 9 OVERFLOW           @D34SDAP 01991000
         BZ    INTEEXIT           NO,  ======================> @D34SDAP 01992000
         LTR   R1,R1              IS THERE A CCB               @D34SDAP 01993000
         BZ    INTGS070           NO, THEN IGNORE IT           @D34SDAP 01994000
         OI    CHQCCBB2,CHAN9     INDICATE CHANNEL 9 OVERFLOW  @D35IDAP 01995000
INTGS070 CLI   ERRQSNS,CHANN9     IS THAT THE ONLY 'ERROR'     @D34SDAP 01996000
         BE    INTGS080           YES,                         @DA44172 01997000
         CLI   PUBDEVTY,T1443     IS THIS A 1443 PRINTER       @DA11295 01998000
         BNE   INTEEXIT           NO,  ======================> @DA11295 01999000
         TM    ERRQSNS,X'F2'      IS THIS REALY AN ERROR       @DA11295 02000000
         BNZ   INTEEXIT           YES, ======================> @DA44172 02001000
INTGS080 LTR   R1,R1              IS THERE A CCB               @DA44172 02002000
         BZ    INTNOREC           NO,  ======================> @DA44172 02003000
         L     RD,AINTPOWC        GET ROUTINE ADDRESS          @DA44172 02004000
         BALR  RE,RD              CHECK FOR CHAN9/12           @DA44172 02005000
         B     INTNOREC          0 POST =====================> @DA44172 02006000
         XC    ERBLKPTR,ERBLKPTR 4 CLEAR ERROR CHAIN POINTER   @DA44172 02007000
         NI    PUBCSFLG,XFF-DEVBSY-QEDERR RESET PUB FLAG BITS  @DA44172 02008000
         NI    ERBLKFL1,NEEDTASK-NEEDERP RESET FLAG BITS       @DA44172 02009000
         NI    ERBLKFLG,XFF-ERACTIVE  FREE ERROR ENTRY         @DA44172 02010000
         B     RSETINIT           RETRY======================> @DA44172 02011000
         SPACE 1                                               @D62TAAP 02012000
         USING SNSTASK,RD         SNS-TASK BASE POINTER        @D62TAAP 02013000
INTSMVER MVC   ERRQSNS(0),SNSAREA MOVE SENSE TO ERROR ENTRY    @D37BDFG 02014000
         DROP  RD                 RELEASE SNSTASK BASE         @D62TAAP 02015000
.**************************************************************@D52VDAP 02016000
.*                                                             @D52VDAP 02017000
.*  INTERFACE DESCRIPTION:                                     @D52VDAP 02018000
.*  RF = BASE ADDRESS OF DERFAST                               @D52VDAP 02019000
.*  RE = LINK REGISTER                                         @D52VDAP 02020000
.*                                                             @D52VDAP 02021000
.*  RETURN IS TO RE+0 ERROR HAS BEEN PROCESSED.                @D52VDAP 02022000
.*               RE+4 DO PATH VERIFICATION / PATH GROUPING     @D52VDAP 02023000
.*               RE+8 DO NORMAL ERROR PROCESSING               @D52VDAP 02024000
.*                                                             @D52VDAP 02025000
.**************************************************************@D52VDAP 02026000
INTGDERF LR    R8,RE              SAVE PUBX BASE ADDRESS       @D62TAAP 02027000
         L     RF,ADSKFAST        SUBROUTINE BASE              @D52VDAP 02028000
         USING DERFAST,RF         DERFAST BASE POINTER         @D52VDAP 02029000
         BALR  RE,RF              FAST ERROR ANALYSATION       @D52VDAP 02030000
         OI    INTFSTFL,X'80'   0 ERROR RECOVERY COMPLETE      @D52VDAP 02031000
         OI    INTFSTFL,X'40'   4 DO PATH VERIFICATION         @D52VDAP 02032000
         OI    INTFSTFL,X'20'   8 NORMAL RECOVERY REQUIRED     @D52VDAP 02033000
         DEBUG USERDATA,XXDBGMC,INTFASTE                       @D61COAP 02034000
         LR    RE,R8              RESTORE PUBX BASE ADDRESS    @D62TAAP 02035000
         CLI   INTFSTFL,X'60'     PATH VERIFICATION REQUIRED   @D52VDAP 02036000
         BNE   INTGS020           NO,  ======================> @D52VDAP 02037000
         DROP  RF                 RELEASE DERFAST BASE         @D62NDAP 02038000
.*                                                                      02039000
.*       INITIATE PATH VERIFICATION PROCESSING                          02040000
.*                                                                      02041000
INTPVTRT L     R8,IRB$            ADDRESS OF IRB               @D51GDAP 02042000
         USING IRB,R8             IRB BASE POINTER             @D51GDAP 02043000
         OC    PBXPVPEN(1),PBXPIM INSPECT ALL PATHES           @D52VDAP 02044000
         OI    PBXFLAG3,PBXF3PVT  INDICATE PATH VERIFICATION   @D52VDAP 02045000
         OI    SNSFLAG,SNSPVWRK   DO PATH VERIFICATION         @D52VDAP 02046000
*        CLI   PBXLPM(1),IRBLPUM  WAS THIS THE ONLY PATH       @DYAOM   02047000
*        BNE   INTNOREC           NO, WE ARE DONE              @DYAOM   02048000
         OC    PBXLPM,IRBLPUM     FORCE LPM BIT ON             @DYAOM   02049000
         XC    PBXLPM,IRBLPUM     TO GET IT TURNED OFF         @DYAOM   02050000
         B     INTGS020           JOIN MAIN PATH               @D52VDAP 02051000
         DROP  R8                 RELEASE IRB BASE             @D52VDAP 02052000
         SPACE 2                                               @D62NDAP 02053000
ADSKFAST DC    A(DERFAST)         FAST SENSE INSPECT ROUTINE   @D52VDAP 02054000
INTFASTE DC    CL14'DERFAST    RC='                            @D62NDAP 02055000
INTFSTFL DC    XL2'00'            FLAG BYTES                   @D52VDAP 02056000
         SPACE 3                  COM                          @D62NDAP 02057000
*SGLINK  INTRSQED,INPUT=(R3,R4,RE),WORK=R0                     @D62NDAP 02058000
INTRSQED LTR   R4,R4              IS CHANQ ENTRY PRESENT       @DA44311 02059000
         BZ    INTRSQ10           NO,                          @DA44311 02060000
         NI    CHQFLG1,XFF-CHQCSQED-CHQCSBSY RESET GATING BITS @DA37004 02061000
INTRSQ10 CLI   PUBCHQPT,NOTQUED   IS REQUEST QUEUED TO PUB     @DA44311 02062000
         BE    INTRSQ20           NO,                          @DA44311 02063000
         AIF   (&AG1 LE 254).NMDV200                           @D62TAAP 02064000
         SLR   R0,R0              CLEAR REGISTER               @D62NDAP 02065000
         IC    R0,PUBCHQPT        CHANQ INDEX LOW  BYTE        @D62TAAP 02066000
         ICM   R0,2,PUBCHQPH      CHANQ INDEX HIGH BYTE        @D62TAAP 02067000
         SLL   R0,CHQSLEN         MULTIPLY WITH ENTRY LENGTH   @D62TAAP 02068000
         A     R0,ACHANQ          CHANQ ENTRY ADDRESS          @D62TAAP 02069000
         ICM   R0,8,H0            CLEAR CHAINING BYTE          @DTIMING 02070000
         CLM   R0,7,ERRQCHQA+1    SET CONDITION CODE           @D62TAAP 02071000
.NMDV200 AIF   (&AG1 GT 254).YMDV200                           @D62TAAP 02072000
         CLC   ERRQCQPT,PUBCHQPT  IS OUR REQUEST STILL FIRST   @D62TAAP 02073000
.YMDV200 ANOP                                                  @D62TAAP 02074000
         BNE   0(,RE)             NO,  ======================> @D62NDAP 02075000
INTRSQ20 NI    PUBCSFLG,XFF-DEVBSY-QEDERR RESET PUB FLAG BITS  @DA44311 02076000
         B     4(,RE)                  ======================> @D62TAAP 02077000
         SPACE 2                                               @D62NDAP 02078000
         DROP  R5                 RELEASE ERROR BLOCK POINTER  @D35ADAP 02079000
         DROP  R1                 RELEASE CCB BASE POINTER     @D369DAP 02080000
         EJECT                                                          02081000
***************************************************************@D34SDAP 02082000
*                                                              @D34SDAP 02083000
*        GENERAL EXIT ROUTINE                                  @D34SDAP 02084000
*                                                              @D34SDAP 02085000
***************************************************************@D34SDAP 02086000
         SPACE 2                                               @D34SDAP 02087000
INTEEXIT LTR   R4,R4              REQUEST STILL QUEUED         @D62TAAP 02088000
         BZ    INTTOERP           NO,                          @D62TAAP 02089000
         CLC   CHQTID,IORQ4DSK    IS THIS A DSK REQUEST        @D66ADAP 02090000
         BE    INTEE020           YES,                         @D62TAAP 02091000
         CLC   CHQTID,IORQ4ERP    IS THIS AN ERP REQUEST       @D66ADAP 02092000
         BE    INTEE020           YES,                         @D62TAAP 02093000
         TM    ERRQFLG,RECONLY    REQUEST RECORDING ONLY       @D62TAAP 02094000
         BZ    INTIOERR           NO, REGULAR ERROR            @D62TAAP 02095000
         TM    SYSFLAG2,IPLBIT    IS IPL IN PROCESS            @D62TAAP 02096000
         BO    INTEE020           YES,                         @D62TAAP 02097000
         CLI   PUBDEVTY,TTPA      IS THIS A TPA DEVICE         @D62TAAP 02098000
         BE    INTEE010           YES, ENSURE RECORDING        @D62TAAP 02099000
         CLI   PUBDEVTY,T3480     IS THIS A 3480-TYPE DEVICE   @D62TAAP 02100000
         BE    INTEE010           YES, ENSURE RECORDING        @D62TAAP 02101000
         TM    ERBLKFL1,NEEDDSK   ERROR TO BE PASSED TO DSK    @D14BDAP 02102000
         BZ    INTEE020           NO, HERE AND NOW IS END      @D62TAAP 02103000
         TM    CHQDEV,CHQDASD+CHQFBA IS THIS A DASD DEVICE     @D62TAAP 02104000
         BZ    INTEE020           NO,                          @D62TAAP 02105000
INTEE010 OI    DEVSTA,CHANEND+DEVEND FORCE DEQUEUING OF ENTRY  @D62TAAP 02106000
         OI    CHQCCBB1,DISERR    POST DISASTER IN CCB         @D62TAAP 02107000
         B     INTDOREC           ENSURE RECORDING             @D62TAAP 02108000
INTEE020 OI    DEVSTA,CHANEND+DEVEND FORCE DEQUEUING OF ENTRY  @D62TAAP 02109000
         OI    CHQCCBB1,DISERR    POST DISASTER IN CCB         @D62TAAP 02110000
         DROP  RE                 RELEASE PUBX BASE            @D62TAAP 02111000
INTNOREC DS    0H'0'              ENTRY POINT                  @DA44311 02112000
         NI    ERBLKFL1,XFF-NEEDDSK-NEEDERP FORCE DEACTIVATION @D62TAAP 02113000
INTDOREC LA    RF,TSTPCI          INITIALIZE RETURN REGISTER   @D62TAAP 02114000
         BAL   RE,INTRSQED        RESET QUEUED IN ERROR        @D62NDAP 02115000
*SGLINK  INTRSQED,INPUT=(R3,R4,RE),WORK=R0                     @D62NDAP 02116000
         B     INTDODLY         0 DELAY PROCESSING RETURN      @D62NDAP 02117000
INTEEEXT L     RD,AINTEQS       4 ADDRESS OF ERROR SUBROUTINES @D3CADAP 02118000
         USING INTEQSET,RD        SUBROUTINE BASE POINTER      @D3CADAP 02119000
         BAL   R7,INTEQPST        POST APPROPRIATE TASK        @D37BDFG 02120000
*SGLINK  INTEQPST,INPUT=(R2,R7,RD),WORK=(R5,R8,RE)             @D149DAP 02121000
         DROP  RD                 RELEASE SUBROUTINE BASE      @D3CADAP 02122000
         B     INTERTRN           TAKE GENERAL EXIT            @D62TAAP 02123000
         SPACE 1                                               @D62NDAP 02124000
INTDODLY MVC   CHQCSW(8),CSW      SAVE CSW INFO                @D62NDAP 02125000
         MVI   CHQIOINF,ERPXPCI   INDICATE TSTPCI EXIT WANTED  @D62NDAP 02126000
         OI    CHQPROC,CHQDOINT   REQUEST DELAYED PROCESSING   @D62NDAP 02127000
         LR    RF,R6              FORCE DISPATCHER EXIT        @D62TAAP 02128000
         B     INTEEEXT           JOIN MAIN PATH               @D62TAAP 02129000
*                                                                       02130000
*        REGULAR I/O ERROR                                              02131000
*                                                                       02132000
INTIOERR CLC   CHQTID,IORQ4AR     IS THIS A SYSTEM TASK REQUEST@D66ADAP 02133000
         BH    INTTOERP           NO,                          @D62TAAP 02134000
         TM    CHQCCBB1,ACCUNRIO  DOES TASK ACCEPT ERRORS      @D62TAAP 02135000
         BZ    INTESYST           NO, PROCEED NORMAL           @D62TAAP 02136000
         TM    CHQCCSIO,CHQCCNOP  WAS DEVICE NOT OPERATIONAL   @D62TAAP 02137000
         BO    INTEE020           YES, DONT PASS IT ANY FURTHER@D62TAAP 02138000
INTESYST CLC   CHQTID,IORQ4AR     IS THIS THE AR TASK          @D66ADAP 02139000
         BE    INTTOERP           YES,                         @D62TAAP 02140000
         CLI   PUBDEVTY,TTPA      IS THIS A TPA DEVICE         @D62TAAP 02141000
         BE    INTTOERP           YES, INITIATE RECOVERY       @D62TAAP 02142000
         CLI   PUBDEVTY,T3480     IS THIS A 3480-TYPE DEVICE   @D62TAAP 02143000
         BE    INTTOERP           YES, INITIATE RECOVERY       @D62TAAP 02144000
         TM    CHQDEV,CHQDASD+CHQFBA IS THIS A DASD DEVICE     @D62TAAP 02145000
         BZ    INTEE020           NO, DONT PASS IT ALONG       @D62TAAP 02146000
INTTOERP DS    0H'0'              ENTRY POINT                  @DA44311 02147000
         LA    RF,INITRG          INITIALIZE RETURN REGISTER   @D37BDFG 02148000
         L     RD,AINTEQS         ADDRESS OF ERROR SUBROUTINES @D3CADAP 02149000
         USING INTEQSET,RD        SUBROUTINE BASE POINTER      @D3CADAP 02150000
         BAL   R7,INTEQPST        POST APPROPRIATE TASK        @D37BDFG 02151000
*SGLINK  INTEQPST,INPUT=(R2,R7,RD),WORK=(R5,R8,RE)             @D149DAP 02152000
         DROP  RD                 RELEASE SUBROUTINE BASE      @D3CADAP 02153000
         TM    ERBLKFLG,ERACTIVE  HAS ERP BEEN ACTIVATED       @D37BDFG 02154000
         BO    INTERTRN           YES, GENERAL EXIT            @D37BDFG 02155000
         BAL   RE,INTRSQED        RESET QUEUED IN ERROR        @D62NDAP 02156000
*SGLINK  INTRSQED,INPUT=(R3,R4,RE),WORK=R0                     @D62NDAP 02157000
         NOP   0                0 DELAY PROCESSING RETURN      @D62NDAP 02158000
INTERTRN DS    0H'0'            4 ENTRY POINT                  @D62TAAP 02159000
         LR    R5,RE              SAVE REGISTER-14             @D62TAAP 02160000
         DROP  R2                 DROP ERROR ENTRY BASE POINTER@D37BDAP 02161000
         BAL   RE,GETPUBX         GET PUBX ADDRESS             @D62TAAP 02162000
*SGLINK  GETPUBX,INPUT=(R3,R9,RE),OUTPUT=R2                    @D62TAAP 02163000
         LR    RE,R5              RESTORE REGISTER-14          @D62TAAP 02164000
         MVC   INTERPUB(8),PUBCHANN                            @D62NDAP 02165000
         ST    RF,INTERRET        SAVE EXIT ADDRESS            @D62NDAP 02166000
         MVC   INTERCHQ(32),CHQADR SAVE CHANQ ENTRY            @D62NDAP 02167000
         DEBUG USERDATA,XXDBGMC,INTERDBG,INTERCHQ,INTERCHQ+16  @D62NDAP 02168000
         L     R5,CRADDR          RESTORE COMREG POINTER       @D35IDAP 02169000
         BR    RF                      ====================>>> @D35IDAP 02170000
         DS    0F                                              @D62NDAP 02171000
INTERDBG DC    CL4'ERRX'                                       @D62NDAP 02172000
INTERRET DC    XL4'00'            EXIT ADDRESS                 @D62NDAP 02173000
INTERPUB DC    XL8'00'            PUB                          @D62NDAP 02174000
INTERCHQ DC    XL32'00'           COM                          @D62NDAP 02175000
         DROP  R4                 DROP CHANQ BASE POOINTER     @D37BDFG 02176000
         TITLE 'VSE SUPERVISOR     IOINTER     GET PUBX ENTRY         ' 02177000
         AIF   (NOT &BGDEBUG).NTXT160                          @D37BDFG 02178000
***************************************************************@D379DFG 02179000
*                                                              @D379DFG 02180000
*   FUNCTION      THIS ROUTINE RETURNS THE ADDRESS OF THE      @D379DFG 02181000
*                 PUBX ENTRY FOR A GIVEN PUB POINTER.          @D379DFG 02182000
*                                                              @D379DFG 02183000
*   CALLING SEQUENCE                                           @D379DFG 02184000
*                                                              @D379DFG 02185000
*                 BAL   RE,GETPUBX                             @D379DFG 02186000
*                                                              @D379DFG 02187000
*   REGISTER USAGE                                             @D379DFG 02188000
*                                                              @D379DFG 02189000
*                 REGISTER  2     PUBX POINTER                 @D379DFG 02190000
*                 REGISTER  3     PUB POINTER                  @D379DFG 02191000
*                 REGISTER  9     BASE REGISTER                @D379DFG 02192000
*                 REGISTER 14     LINK REGISTER                @D379DFG 02193000
*                                                              @D379DFG 02194000
*   ENTERED FROM                                               @D379DFG 02195000
*                                                              @D379DFG 02196000
*                 PROCAPP         EXIT FROM BTAM APPENDAGE     @D379DFG 02197000
*                 INTEQSET        GET DEVICE ERROR ENTRY       @D379DFG 02198000
*                                                              @D379DFG 02199000
***************************************************************@D379DFG 02200000
         SPACE 3                                               @D35IDAP 02201000
         AGO   .YTXT160                                        @D37BDFG 02202000
.NTXT160 ANOP                                                  @D37BDFG 02203000
***************************************************************@D379DFG 02204000
*                                                              @D379DFG 02205000
*        GET PUBX POINTER FOR GIVEN PUB POINTER                @D379DFG 02206000
*                                                              @D379DFG 02207000
***************************************************************@D379DFG 02208000
         SPACE 2                                               @D37BDFG 02209000
.YTXT160 ANOP                                                  @D37BDFG 02210000
*SGLINK  GETPUBX,ENTRY,INPUT=(R3,R9,RE),OUTPUT=R2              @D379DAP 02211000
GETPUBX  LR    R2,R3              PUB POINTER                  @D37BDFG 02212000
         SH    R2,YPUBTAB         OFFSET IN PUB TABLE          @D37BDFG 02213000
         SRL   R2,1               DEVIDED BY TWO               @D37BDFG 02214000
         A     R2,APBXAREA        ADDRESS OF PUB EXTENTION     @D37BDFG 02215000
         L     R2,0(R2)           GET PUBX POINTER             @D37BDFG 02216000
         BR    RE                      ======================> @D37BDFG 02217000
         EJECT ,                                                        02218000
*                                                              @D35IDAP 02219000
*        SUBROUTINES OR CODE WHICH NEED ITS OWN BASE POINTER   @D35IDAP 02220000
*                                                              @D35IDAP 02221000
         TITLE 'VSE SUPERVISOR     IOINTER     ATTENTION PROCESSING   ' 02222000
         AIF   (NOT &BGDEBUG).NTXT100                          @D35IDAP 02223000
***************************************************************@D35IDAP 02224000
*                                                              @D35IDAP 02225000
*  FUNCTION:                                                   @D35IDAP 02226000
*            PROCESS ATTENTION INTERRUPTS FROM THE SYSTEM      @D35IDAP 02227000
*            COMMUNICATION DEVICE                              @D35IDAP 02228000
*  ENTRY POINT:                                   AT LABEL     @D35IDAP 02229000
*            1. ENTERED  FROM STATUS ANALIZER        ATTNPROC  @D35IDAP 02230000
*  EXITS:                                         TO LABEL     @D35IDAP 02231000
*            STATUS ANALYZER                         TSTUCERR  @D35IDAP 02232000
*            SIO-INITIATOR                           INITRG    @D35IDAP 02233000
*            PRIMARY STATUS PROCESSOR                CHNDP010  @D37BDAP 02234000
*            ENDING  STATUS PROCESSOR                RSETINIT  @D37CDAP 02235000
*            DISPATCHER                              DISP      @D35IDAP 02236000
*  INPUTS:                                                     @D35IDAP 02237000
*            R1= ADDRESS OF CCB                                @D35IDAP 02238000
*            R2= ADDRESS OF PUBX                               @D35IDAP 02239000
*            R3= ADDRESS OF PUB                                @D35IDAP 02240000
*            R4= ADDRESS OF CHANQ ENTRY                        @D35IDAP 02241000
*            R5= ADDRESS OF COMREG                             @D35IDAP 02242000
*            R6= ADDRESS OF DISPATCHER                         @D35IDAP 02243000
*            R9= IOINTER BASE REGISTER                         @D35IDAP 02244000
*            RA= ADDRESS OF PIB                                @D35IDAP 02245000
*            RB= SUPERVISOR BASE REGISTER                      @D35IDAP 02246000
*           CSW= INTERRUPT INFORMATION                         @D35IDAP 02247000
*  OUTPUTS:                                                    @D35IDAP 02248000
*  REGISTERS:                                                  @D35IDAP 02249000
*            R1= ADDRESS OF CCB                                @D35IDAP 02250000
*            R2= ADDRESS OF PUBX                               @D35IDAP 02251000
*            R3= ADDRESS OF PUB                                @D35IDAP 02252000
*            R4= ADDRESS OF CHANQ ENTRY                        @D35IDAP 02253000
*            R5= ADDRESS OF COMREG                             @D35IDAP 02254000
*            R6= ADDRESS OF DISPATCHER                         @D35IDAP 02255000
*            R7= UNPREDICTABLE                                 @D35IDAP 02256000
*            R9= IOINTER BASE REGISTER                         @D35IDAP 02257000
*            RA= ADDRESS OF PIB                                @D35IDAP 02258000
*            RB= SUPERVISOR BASE REGISTER                      @D35IDAP 02259000
*            RE= UNPREDICTABLE                                 @D35IDAP 02260000
*  SUBROUTINES:                                                @D35IDAP 02261000
*                                                              @D35IDAP 02262000
***************************************************************@D35IDAP 02263000
         SPACE 3                                               @D35IDAP 02264000
.NTXT100 ANOP                                                  @D35IDAP 02265000
         USING PBXADR,R2          PUBX BASE ADDRESS            @D28ADAP 02266000
         USING CHQADR,R4          CHANQ BASE POINTER           @D28ADAP 02267000
         USING ATTNPROC,RC        ATTNPROC BASE POINTER        @D28ADAP 02268000
ATTNPROC TM    PBXFLAG,PBXTAPE+PBXDASD  DASD OR TAPE           @DAOM    02269000
         BNZ   AOMPROC            YES REAL AOM                 @DAOM    02270000
         CLC   PUBCHANN(2),SYSOCDEV IS IT OPERATOR CONSOLE     @D14BDAP 02271000
         BE    ATTOCDEV           YES,                         @D34EDAP 02272000
         CLC   PUBCHANN(2),IJBOWSAV IS IT THE REAL CONSOLE     @D31YDAP 02273000
         BE    ATTOCNAT           YES,                         @D31YDAP 02274000
         CLI   PUBDEVTY,TCTCA     IS THIS CTCA                 @DA44498 02275000
         BNE   ATTNPR10           NO,                          @DA44498 02276000
         CLI   DEVSTA,ATTENT+BUSY IS THIS THE WRONG COMMAND    @DA44498 02277000
         BE    CHNDPST            YES, GET REQUESTOR POSTED    @DA44498 02278000
ATTNPR10 CLI   PUBDEVTY,T3277     IS THIS A 3277 DEVICE        @DIPL    02279000
         BNE   ATTNPR20           NO,                          @DIPL    02280000
         ICM   R8,15,PBXATTIB     LOAD CONTROLLING TIB POINTER @DIPL    02281000
         BNZ   OSAPROC            ONE DOES EXIST               @DIPL    02282000
ATTNPR20 CLI   PUBDEVTY,TOSA      IS THIS THE OSA/FCP ADAPTER  @D62NDAP 02283000
         BNE   ATTRETRN           NO,                          @DAOM    02284000
         CLI   PUBOPTN,PUBOPOFE   IS IT THE FE CONTROLLER PORT @D62NDAP 02285000
         BE    OSAPROC            YES,                         @D62NDAP 02286000
ATTRETRN TM    DEVSTA,UNITCK      IS THIS ATTENTION-UNIT CHECK @D34EDAP 02287000
         BO    TSTUCERR           YES, LEAVE STATUS UNCHANGED  @D61CDAP 02288000
         OI    DEVSTA,DEVEND      FORCE DEQUEUING BY DEVEND    @D34EDAP 02289000
         B     TSTUCERR                ======================> @D61BDAP 02290000
.*                                                             @D31YCAP 02291000
.*   ATTENTION FROM THE REAL CONSOLE CAN ONLY OCCUR IF:        @D31YCAP 02292000
.*    1. USER SWITCHED OFF->ON THE REAL DEVICE TO INDICATE     @D31YCAP 02293000
.*       THAT VSE IS TO TAKE OVER THE REAL DEVICE              @D31YCAP 02294000
.*    2. TP ACCESS METHOD HAS RELEASED THE CHANQ ENTRY AND     @D31YCAP 02295000
.*       THE OPERATOR HAS NOW CAUSED AN ATTENTION INTERRUPT.   @D31YCAP 02296000
.*                                                             @D31YCAP 02297000
ATTOCNAT OI    IJBOPMOD,IJBOPCON  INDICATE AR SERVICE NEEDED   @D31YDAP 02298000
         SLR   R0,R0              INDICATE RECONNECT REQUEST   @D36IDAP 02299000
         L     RD,ATINTADR        ADDR OF AR INITIATOR         @D61NDAP 02300000
         BALR  R8,RD              ACTIVATE ATTENTION TASK      @D61NDAP 02301000
*SGLINK  ATTNINT,INPUT=(R0,R6,R8),OUTPUT=R0                    @D369DAP 02302000
         B     ATTNONLY                                                 02303000
ATTOCDEV TM    IJBOPMOD,IJBOPEXT  DID WE FORCE DISCONNECT      @D51GDAP 02304000
         BZ    ATTOCD10           NO,                          @D51GDAP 02305000
         NI    IJBOPMOD,XFF-IJBOPDIS-IJBOPEXT RECONNECT CONSOLE@D51GDAP 02306000
ATTOCD10 L     R8,ATTXCPT         SYSOCDEV INTERCEPT ADDRESS   @D21CDAP 02307000
         BALR  R7,R8              GO AND PROCESS INTERRUPT     @D21CDAP 02308000
ATTOCRTN L     RD,DOCBASE         SGDOC BASE ADDRESS           @D61CDAP 02309000
         USING SGDOC,RD           SGDOC BASE POINTER           @D61CDAP 02310000
         BAL   RE,CSTATTN         INDICATE READ REQUIRED       @D61CDAP 02311000
*SGLINK  CSTATTN,INPUT=(R6,RD,RE),WORK=(R7,R8,RF)              @D61CDAP 02312000
         DROP  RD                 RELEASE SGDOC BASE           @D61CDAP 02313000
ATTNONLY TM    DEVSTA,X'6F'       ANY OTHER EXCEPT ATTENTION   @D61CDAP 02314000
         BNZ   TSTUCERR           YES  ======================> @D35IDAP 02315000
         B     INITRG             NO,  ======================> @D35IDAP 02316000
         SPACE 3                                                      ' 02317000
.*                                                                      02318000
.*       HANDLE SOME KIND OF ATTENTION (ENTERED FOR DASD/TAPES ONLY)    02319000
.*                                                                      02320000
AOMPROC  TM    DEVSTA,XFF-ATTENT-BUSY-UNITCK ATTN OR ATTN-SNS  @DBOC    02321000
         BZ    AOMPRATT           YES, (80,82,90 AND 92 STATUS)@DBOC    02322000
         CLI   DEVSTA,ATTENT+DEVEND+UNITEXCP NEW READY INTERR  @DAOM    02323000
         BE    AOMPR020           YES,                         @DBOC    02324000
         CLI   DEVSTA,ATTENT+DEVEND+UNITEXCP+BUSY NEW READY    @DBOC    02325000
         BNE   ATTRETRN           NO,                          @DAOM    02326000
AOMPR020 NI    PUBCSFLG,XFF-OPINTV-PUBLOBSY RESET BITS         @DY46037 02327000
         LTR   R4,R4              IS CHANQ ADDRESS PRESENT              02328000
         BZ    AOMPR040           NO,                                   02329000
         TM    CHQCCSIO,CHQCCACT  HAS REQUEST BEEN STARTED              02330000
         BO    AOMPR040           YES,                                  02331000
         NI    PUBCSFLG,XFF-DEVBSY RESET BUSY BIT IN PUB                02332000
AOMPR040 TM    PBXFLAG,PBXTAPE    IS THIS A TAPE DEVICE        @DY46037 02333000
         BZ    ATTRETRN           NO, RETURN                   @DGA270  02334000
         ICM   RF,15,PBXAOMMS     GET AOM-CONTROL BLOCK        @DAOM    02335000
         N     RF,BIT0OFF         TURN ATTENTION BIT OFF       @DAOM    02336000
         LTR   RF,RF              ARE WE EXPECTING A "READY"   @DAOM    02337000
         BZ    ATTRETRN           NO,                          @DAOM123 02338000
*        B     AOMPR050           YES, WE ARE                  @DAOM123 02339000
         SPACE 1                                                        02340000
AOMPRATT DS    0H'0'              ATTENTION OR ATTENTION-SENSE @DAOM    02341000
         OI    PBXAOMMS,ATTENT    INDICATE UNSOLICITED ATTN.   @DAOM    02342000
AOMPR050 L     RF,IRB$            GET ADDRESS OF IRB           @DAOM    02343000
         USING IRB,RF             IRB BASE POINTER             @DAOM    02344000
         OC    PBXATTNR(1),IRBLPUM SAVE LPUM                   @DAOM    02345000
         DROP  RF                 RELEASE IRB BASE             @DAOM    02346000
         AIF   (NOT &VSE270).N270020                           @DAOM    02347000
         L     RD,AAOMACTI        AOM ACTIVATION ROUTINE ADDR. @DAOM    02348000
         BASSM RE,RD              ACTIVATE AOM TASK            @DAOM    02349000
*SGLINK  AOMACTIV INPUT=(R2,R3,R6,RD,RE)                       @DAOM    02350000
         TM    DEVSTA,UNITCK      IS IT A GLOBAL INTERCEPT     @DAOM    02351000
         BO    TSTUCERR           YES, CLEAR UNIT CHECK        @DAOM    02352000
         B     INITRG             NO,  ======================= @DY46037 02353000
         SPACE 1                                               @DAOM    02354000
AAOMACTI DC    A(AOMACTIV+X'80000000') ADDR OF AOM ACTIV. RTN. @DAOM    02355000
         AGO   .Y270020                                        @DAOM    02356000
.N270020 ANOP                                                  @DAOM    02357000
         CLI   DEVSTA,ATTENT+DEVEND+UNITEXCP NEW READY INTERR  @DAOM    02358000
         BE    ATTRETRN           YES, SKIP CUIR PROCESSING    @DAOM    02359000
AOMCUIR  DS    0H'0'              CHECK FOR RECONFIGURATION    @DAOM    02360000
         L     RC,BASESERI        SGSERI BASE POINTER          @D61BDGL 02361000
         USING SGSERI,RC          SGSERI BASE POINTER          @D61BDAP 02362000
         BAL   RE,SERADDQR        ACTIVATE CUIR PROCESSING     @D61BDAP 02363000
*SGLINK  SERADD,INPUT=(R3,R6,RC,RE),WORK=(R7,R8,RF)            @D61BDGL 02364000
         L     RC,AATNPROC        GET ATTENTION ROUTINE BASE            02365000
         USING ATTNPROC,RC        ATTENTION PROCESSING BASE             02366000
         B     ATTRETRN           RETURN                       @D61BDAP 02367000
.Y270020 ANOP                                                  @DAOM    02368000
         SPACE 3                                                        02369000
OSAPROC  DC    0H'0'              OSA SPECIAL PROCESSING       @DOSA    02370000
         ICM   R8,15,PBXATTIB     LOAD CONTROLLING TIB POINTER @DOSA    02371000
         BZ    ATTRETRN           NONE EXISTING                @DOSA    02372000
         USING TIBADR,R8          TIB BASE POINTER             @DOSA    02373000
         ICM   RF,15,PBXATECB     GET ECB ADDRESS              @DOSA    02374000
         BZ    ATTRETRN           NONE EXIUSTING               @DOSA    02375000
         LH    RA,TIBRTID         GET TASK ID                  @DOSA    02376000
         BAL   R7,SETLCALL        ESTABLISH OSA ADDRESSES      @DOSA    02377000
*SGLINK  SETLCALL,INPUT=(R6,R7,RA),OUTPUT=RA,WORK=R0           @DOSA    02378000
         OI    2(RF),TRABIT       POST THE PROPER ECB          @DOSA    02379000
         BAL   RF,IOPOST1         POST I/O COMPLETE IF I/O BND @DOSA    02380000
*SGLINK  IOPOST1,INPUT=(R6,R8,RF),WORK=RE                      @DOSA    02381000
         B     ATTNONLY           RETURN                       @DOSA    02382000
         DROP  R8                 RELEASE TIB BASE             @D62NDAP 02383000
         DROP  R2                 RELEASE PUBX BASE            @D28ADAP 02384000
         DROP  R4                 RELEASE CHANQ BASE           @D28ADAP 02385000
         DROP  RC                 RELEASE ATTNPROC BASE        @D28ADAP 02386000
         TITLE 'VSE SUPERVISOR     IOINTER     DEVICE NOT OPERATIONAL ' 02387000
.**************************************************************@D51GDAP 02388000
.*       A VSE MODE=ESA SYSTEM RUNNING UNDER VM/XA DEVICES MAY @D51GDAP 02389000
.*       BE ATTACHED OR RE-ATTACHED AFTER IPL. THESE DEVICES   @D51GDAP 02390000
.*       WILL BE ENSURED TO BE PROPERLY (RE)ENABLED.           @D51GDAP 02391000
.**************************************************************@D51GDAP 02392000
         USING CCBADR,R1          CCB BASE POINTER             @D66CDAP 02393000
         USING CHQADR,R4          CHANQ BASE POINTER           @D66CDAP 02394000
         USING INTVMNOP,RC        SUBROUTINE BASE              @D66CDAP 02395000
INTVMNOP L     RD,INITBASE        SGSCHED BASE ADDRESS         @D51GDAP 02396000
         USING SGSCHED,RD         SGSCHED BASE POINTER         @D51GDAP 02397000
         STM   R1,R3,INTNOPSV     SAVE I/O REGISTER            @D51GDAP 02398000
         BAL   RE,GETPUBX         GET PUBX ADDRESS             @D51GDAP 02399000
*SGLINK  GETPUBX,INPUT=(R3,R9,RE),OUTPUT=R2                    @D51GDAP 02400000
         USING PBXADR,R2          PUBX BASE POINTER            @D51GDAP 02401000
         ICM   RE,15,PBXSIMAD     SIMULATION PROGRAM ACTIVE    @D66ADAP 02402000
         BZ    INTVMN00           NO, PROCEED                  @D66ADAP 02403000
         AIF   (NOT &VSE280).N280180                           @SCSI    02404000
         TM    PBXFLAG,PBXDASD+PBXSCSI IS IT SCSI DEVICE       @SCSI    02405000
         BO    INTVMNOX           YES, REAL NOT OPERATIONAL    @SCSI    02406000
.N280180 ANOP                                                  @SCSI    02407000
         B     INTVMRTY           RETRY THE VIRTUAL I/O        @D66ADAP 02408090
INTVMN00 DS    0H'0'              NOT OPERATIONAL PROCESSING   @DAOM055 02408180
         AIF   (NOT &VSE280).NREF120                           @DAOM055 02408270
*HERENEW                                                       @DAOM055 02408360
         SLR   R1,R1              CLEAR REGISTER               @DAOM055 02408540
         ICM   R1,1,PBXLPNP       GET NON-PREFERRED PATHES     @DAOM055 02408630
         BZ    INTVMN10           THERE IS NONE                @DY46413 02408750
         EX    R1,INTVMXTM        DID WE ALREADY SWITCH        @DAOM055 02408810
         BNZ   INTVMN10           YES, SEARCH SUBCHANNELS      @DY46413 02408980
         OC    PBXLPM(1),PBXSSCHM LAST PATH USED INFO          @DAOM055 02409060
         XC    PBXLPM(1),PBXSSCHM RESET THOSE BITS             @DAOM055 02409170
         LM    R1,R3,INTNOPSV     RESTORE REGS 1-3             @DY46413 02409280
         B     INITRG             RETRY ON NON-PREFERRED ====> @D68REEF 02409300
         SPACE 1                                               @DAOM055 02409350
INTVMXTM TM    PBXLPM,0           IS THIS A NON-PREFERRED PATH @DAOM055 02409440
.NREF120 ANOP                                                           02409620
INTVMN10 LA    RE,SNSTID          SNS-TASK ID                  @DY46413 02409800
         CH    RE,TID             IS THIS THE SNS TASK         @D66ADAP 02410000
         BE    INTVMNOX           YES, REAL NOT OPERATIONAL    @DA42963 02411000
         L     RE,ASNSTIB         ADDRESS OF SNS-TASK TIB      @DA42963 02412000
         USING TIBADR,RE          SNS TIB BASE POINTER         @DA42963 02413000
         CLI   TIBRQID,READY      IS SNS TASK READY            @DA42963 02414000
         BNE   INTVMN20           NO,                          @DA42963 02415000
         DROP  RE                 RELEASE SNS-TASK TIB BASE    @DA42963 02416000
         L     RE,AHQRESCH        GET SNS-TASK CHANQ ENTRY     @DA42963 02417000
         CLC   CHQPUBHI-CHQADR(2,RE),CHQPUBHI SAME DEVICE      @DA42963 02418000
         BE    INTVMNOX           YES, REAL NOT OPERATIONAL    @DA42963 02419000
INTVMN20 TM    CHQGRP,X'01'       DID WE DO VERIFICATION       @DA42963 02420000
         BO    INTVMNOX           YES, DONT DO IT AGAIN        @DA42963 02421000
         BAL   RF,INTVMSTS        TRY AND FIND NEW SUBCHANNEL  @D51ODAP 02422000
         CLM   R1,3,XFFFF         DID WE FIND A NEW SUBCHANNEL @DA42963 02423000
         BNE   INTVMN40           YES, INVALIDATE AN EXISTING  @D51ODAP 02424000
         MVC   PBXSCH(2),XFFFF    INVALIDATE SUBCHANNEL        @D51GDAP 02425000
         MVI   PBXLPM,ZERO        FORCE LPM TO ZERO            @D52VDAP 02426000
         B     INTVMNOX           REAL NOT OPERATIONAL         @D52VDAP 02427000
INTVMN40 LA    R3,NEXTPUB         POINT TO NEXT PUB ENTRY      @D52VDAP 02428000
INTVMN60 C     R3,INTNOPSV+8      IS THIS THE STARTING PUB     @D52VDAP 02429000
         BE    INTVMRTY           YES, ALL WORK IS DONE        @D52VDAP 02430000
         CLI   PUBCHANN,XFF       WAS THIS LAST PUB            @D52VDAP 02431000
         BE    INTVMN80           YES, START WITH FIRST PUB    @D52VDAP 02432000
         BAL   RE,GETPUBX         GET ASSOCIATED PUBX          @D51GDAP 02433000
*SGLINK  GETPUBX,INPUT=(R3,R9,RE),OUTPUT=R2                    @D51GDAP 02434000
         CLM   R1,3,PBXSCH        IS THIS THE SAME SUBCHANNEL  @D52VDAP 02435000
         BNE   INTVMN40           NO,                          @D52VDAP 02436000
         MVC   PBXSCH(2),XFFFF    INVALIDATE SUBCHANNEL        @D51GDAP 02437000
         OI    PBXFLAG3,PBXF3INS  INDICATE PATH VERIFICATION   @DY45062 02438000
         B     INTVMRTY           RETRY FAILING OPERATION NOW  @D51ODAP 02439000
INTVMN80 LH    R3,YPUBTAB         BEGIN OF PUB TABLE           @D52VDAP 02440000
         B     INTVMN60                                        @DA42963 02441000
         DROP  R2                 RELEASE PUBX BASE            @D66CDAP 02442000
         SPACE 1                                               @D52VDAP 02443000
INTVMNOX NI    CHQGRP,XFF-X'01'   RESET PVT WAS DONE BIT       @DA42963 02444000
         LM    R1,R3,INTNOPSV     RESTORE REGS 1-3             @D51GDAP 02445000
         B     INTENOPN                =====================>> @D51GDAP 02446000
INTVMRTY LM    R1,R3,INTNOPSV     RESTORE REGS 1-3             @D51GDAP 02447000
         B     GOENABLE                ======================> @DSCSI   02448680
         SPACE 1                                               @D52VDAP 02449000
INTNOPSV DC    3F'0'              SAVE AREA FOR I/O REGISTER   @D51GDAP 02450000
         DROP  R1                 RELEASE CCB BASE             @D66CDAP 02451000
         SPACE 3                                               @D51GDAP 02452000
         USING PBXADR,R2          PUBX BASE POINTER            @D51GDAP 02453000
INTVMSTS L     RE,SCHIB$          ADDRESS OF SCHIB             @D51GDAP 02454000
         USING SCHIB,RE           SCHIB BASE POINTER           @D51GDAP 02455000
         SLR   R1,R1              INITIALIZE SUBCHANNEL ID     @D51GDAP 02456000
         ICM   R1,3,XFFFF         SET TO 0000FFFF              @D51GDAP 02457000
INTVMS10 LA    R1,1(,R1)          FIRST/NEXT SUBCHANNEL NUMBER @D51GDAP 02458000
         STSCH SCHIB              GET SUBCHANNEL INFORMATION   @D51GDAP 02459000
         BNO   INTVMS20           SUBCHANNEL DOES EXIST        @D51GDAP 02460000
         ICM   R1,3,XFFFF         SET TO 0000FFFF              @D51GDAP 02461000
         STCM  R1,8,PBXLPM        SET LPM TO ZERO              @DA42602 02462000
         B     INTVMS40           INVALIDATE SUBCHANNEL NUMBER @D51GDAP 02463000
INTVMS20 TM    SCHFLGS,SCHV       IS CUU VALID                 @D51GDAP 02464000
         BZ    INTVMS10           NO, PROCESS NEXT SUBCHANNEL  @D51GDAP 02465000
         CLC   SCHDEVNO,PUBCHANN  IS THIS OUR DEVICE           @D51GDAP 02466000
         BNE   INTVMS10           NO, PROCESS NEXT SUBCHANNEL  @D51GDAP 02467000
         TM    SCHFLGS,SCHE       ARE INTERRUPTS ENABLED       @D51GDAP 02468000
         BO    INTVMS30           YES                          @DY46494 02469990
         OI    SCHFLGS,SCHE       FORCE ENABLE BIT ON          @D51GDAP 02470980
         OI    SCHISC,X'18'       SUBCLASS 3                   @DA40460 02474000
         MSCH  SCHIB              MODIFY THE SUBCHANNEL        @D51GDAP 02475000
INTVMS30 ICM   R1,8,SCHPIM        PHYSICALLY INSTALLED MASK    @D51GDAP 02476000
         STCM  R1,8,PBXPVPEN      PROVIDE FOR PATH VERIFICATION@DA42602 02477000
         MVC   PBXIRB(12),SCHSTB0 SAVE CURR. SUBCHANNEL STATUS @D61NDAP 02478000
         MVC   PBXIRB+IRBLPUM-IRB(1),PBXSSCHM LAST PATH ATTEMPT@DAOM055 02479490
         OI    CHQGRP,X'01'       INDICATE PVT FOR THIS REQUEST@DA42963 02480000
         OI    PBXFLAG3,PBXF3INS  INDICATE PATH VERIFICATION   @DA42602 02481000
         L     RD,ASNSINIT        LOAD BASE REGISTER           @DA42602 02482000
         BAL   R7,SNSACT-SNSINIT(RD) ACTIVATE SNS TASK         @DA42602 02483000
         L     RD,INITBASE        RESTORE SGSCHED BASE         @D51GDAP 02484000
INTVMS40 STCM  R1,3,PBXSCH        SAVE SUBCHANNEL IN PUBX      @D51GDAP 02485000
         B     0(RF)                   ======================> @DA42963 02486000
         DROP  R2                 RELEASE PUBX BASE POINTER    @D51GDAP 02487000
         DROP  RE                 RELEASE SCHIB BASE POINTER   @D51GDAP 02489000
         SPACE 1                                               @D51GDAP 02490000
         DROP  R4                 RELEASE CHANQ BASE           @D66CDAP 02490600
         DROP  RD                 RELEASE SGSCHED BASE         @D51GDAP 02491000
         DROP  RC                 RELEASE INTVMNOP BASE        @D66CDAP 02492000
         USING PBXADR,R2          PUBX BASE POINTER            @D66ADAP 02494000
         USING NEWAPPND,RC        NEWAPPND BASE POINTER        @D66ADAP 02495000
NEWAPPND DC    0H'0'              NEW I/O APENDAGE INTERFACE   @D66ADAP 02496000
         ICM   RF,15,PBXCEAPP     IS APPENDAGE PRESENT         @D66ADAP 02497000
         BZ    TSTSTAT            NO,                          @D66ADAP 02498000
NEWAPP10 L     RB,TIBPTR          ADDRESS OF TIB               @DY45817 02499000
         USING TIBADR,RB          TIB BASE POINTER             @DY45817 02500000
         TM    TIBFLAG1,EOTACT    EOT PROCESSING ACTIVE        @DY45817 02501000
         BO    TSTSTAT            YES,                         @DY45817 02502000
         STCM  R3,7,IOOPSW+5      SAVE PUB-PTR. IF PROGR.CHECK @DSCSI   02503000
         NI    PUBCSFLG,XFF-OPINTV RESET INTERVENTION REQUIRED @D66ADAP 02504000
         MVI   RID+1,APPENDID     SET ROUTINE-ID TO APPENDAGE  @D66ADAP 02505000
         BASSM RE,RF              ENTER I/O APPENDAGE          @D66ADAP 02506000
         MVI   RID+1,SYSTEMID     SET ROUTINE-ID TO SYSTEM     @D66ADAP 02507000
         DEBUG ALLREGS,XXDBGMC    SAVE RETURN DATA             @D66ADAP 02508000
         CH    RF,NEWRCMAX        VALID RETURN CODE            @D66ADAP 02509000
         BH    ERR1A              NO,  ====================>>> @D66ADAP 02510000
         SLL   RF,2               MULTIPLY WITH FOUR           @D66ADAP 02511000
         B     NEWAPRET(RF)       TAKE WANTED EXIT             @D66ADAP 02512000
NEWAPRET B     RSETINIT         0 RETRY RETURN                 @D66APAP 02513000
         B     TSTSTAT          4 PROCESS INTERRUPT RETURN     @D66APAP 02514000
         B     GOENABLE         8 IGNORE INTERRUPT PROCESSING  @DSCSI   02515000
         BAL   R5,SYSERROR     12 RESERVED                     @DSCSI   02516000
         AIF   (NOT &VSE280).N280200                           @DSCSI   02517000
         NOP   0               16 PROVIDE FOR INTERRUPT PROCESS@DSCSI   02518000
         AIF   (NOT &BGDEBUG).NDBG040                          @DSCSI   02519000
         CLC   PUBDEVTY(2),TFCPID IS THIS THE FCP ADAPTER      @DSCSI   02520000
         BNE   NAPPERR            NO,                          @DSCSI   02521000
         NI    CHNSTA,XFF-PCI     RESET PCI INDICATION         @DSCSI   02522000
         OC    DEVSTA(2),DEVSTA   IS IT ALL ZERO NOW           @DSCSI   02523000
         BZ    NAPPISOK           YES, ITHEN ITS OK            @DSCSI   02524000
NAPPERR  BAL   R5,SYSERROR        IMPROPER USAGE OF THIS EXIT  @DSCSI   02525000
NAPPISOK OI    CHNSTA,PCI         TURN THE BIT ON AGAIN        @DSCSI   02526000
.NDBG040 ANOP                                                  @DSCSI   02527000
         OI    SYSFLAG2,SCSIACT   (RE)-ACTIVATE SCSI PROCESSING@DSCSI   02528000
.N280200 ANOP                                                  @DSCSI   02529000
         B     GOENABLE                                        @DSCSI   02530000
         DROP  RC                 RELEASE NEWAPPND BASE        @D66ADAP 02531000
         DROP  RB                 RELEASE TIB BASE             @DY45817 02532000
         DROP  R2                 RELEASE PUBX BASE            @D66ADAP 02533000
NEWRCMAX DC    AL2((NEWRCMAX-NEWAPRET)/4-1) MAX. RETURN CODE   @D66APAP 02534000
         SPACE 3                                                        02536000
         USING POSTARPR,RC        SUBROUTINE BASE              @DY45629 02537000
*SGLINK  POSTARPR INPUT=(R6,RC,RE),WORK=(R8,RF)                @DY46053 02538000
POSTARPR L     R8,AARTIB          ATTENTION TIB BASE           @DY45629 02539000
         USING TIBADR,R8          TIB BASE POINTER             @DY45629 02540000
         L     RF,AARBUFAR        ATTENTION BUFFER BASE ADDRESS@DY45629 02541000
         USING ARBUFFER,RF        AR BUFFER BASE POINTER       @DY45629 02542000
         TM    ARCMDFLG,CMDAVAIL  IS COMMAND AVAILABLE         @DY45629 02543000
         BZR   RE                 NO,                          @DY45629 02544000
         OI    ARECBATT+2,TRABIT  POST ATTENTION ECB           @DY45629 02545000
         CLI   TIBRQID,WAITBND    IS AR-TASK WAIT BOUND        @DY45629 02546000
         BNER  RE                 NO, WE ARE DONE              @DY45629 02547000
         ST    RE,POSTARSV        SAVE LINK REGISTER           @DY46053 02548000
         BAL   RF,POST            MAKE ATTENTION READY         @DY45629 02549000
*SGLINK  POST,INPUT=(R6,R8,RF),WORK=RE                         @DY45629 02550000
         L     RE,POSTARSV        RESTORE RE                   @DY46053 02551000
         BR    RE                 RETURN                       @DY45629 02552000
POSTARSV DC    F'0'               RETURN REGISTER              @DY46053 02553000
         DROP  RF                 RELEASE ATTENTION BUFFER BASE@DY45629 02554000
         DROP  R8                 RELEASE ATTENTION TIB BASE   @DY45629 02555000
         DROP  RC                 RELEASE POSTARPR BASE        @DY45629 02556000
         SPACE 3                                                        02557000
         USING CCBADR,R1          CCB BASE POINTER             @DA44172 02558000
         USING INTPOWC9,RD        INTPOWC9 BASE POINTER        @DA44201 02559000
INTPOWC9 DS    0H'0'                                           @DA44311 02560000
         L     RF,TIBPTR          LOAD TIB POINTER             @DA44172 02561000
         USING TIBADR,RF          TIB BASE POINTER             @DA44172 02562000
         TM    TIBFLAG2,PWRMTASK  IS THIS THE POWER MAIN TASK  @DA44172 02563000
         BZ    0(,RE)             NO,  ======================> @DA44172 02564000
         ICM   RF,15,APOWTB       IS POWER ACTIVE              @DA44172 02565000
         BZ    0(,RE)             NO,  ======================> @DA44172 02566000
         USING PADS,RF            POWER ADDRESS TABLE BASE     @DA44172 02567000
         TM    CATF2,CATF2NC9     CHAN-9 SUPPRESS              @DA44172 02568000
         BZ    0(,RE)             NO,  ======================> @DA44172 02569000
         DROP  RF                 DROP POWER TABLE BASE        @DA44172 02570000
         TM    CCBCLS,XCPR        IS THIS AN EXCP-REAL REQUEST @DA44172 02571000
         BZ    0(,RE)             NO,  ======================> @DA44172 02572000
         SLR   RF,RF              CLEAR REGISTER               @DA44172 02573000
         ICM   RF,7,CSW+1         GET CCW ADDRESS              @DA44172 02574000
         S     RF,F8              POINT TO FAILING CCW         @DA44172 02575000
         STNSM PSWMASK,XFF-SMDAT  SET DAT OFF                  @DA44172 02576000
         TM    4(RF),CC           COMMAND CHAINING             @DA44172 02577000
         STOSM PSWMASK,SMDAT      FORCE DAT ON                 @DA44172 02578000
         BZ    0(,RE)             NO,  ======================> @DA44172 02579000
         MVC   CCBCCW+1(3),CSW+1  UPDATE CCW ADDRESS           @DA44172 02580000
         B     4(,RE)             RETRY======================> @DA44172 02581000
         DROP  R1                 RELEASE CCB BASE             @DA44172 02582000
         DROP  RD                 RELEASE INTPOWC9 BASE        @DA44201 02583000
         EJECT ,                                                        02584000
           USING CCBADR,R1        CCB BASE POINTER             @D35IDAP 02585690
           USING CHQADR,R4        CHANQ BASE POINTER           @D35IDAP 02586380
           USING GOAPPND,RD       GOAPPND BASE POINTER         @D62OSAP 02587070
*SGLINK    GOAPPND,INPUT=(R0,R1,R3,R4,R5,R9,RD,RF),WORK=(R5,R7,R8) OSAP 02587760
GOAPPND    ST    RF,APPRETAD      SAVE RETURN ADDRESS          @D35IDAP 02588450
           MVI   APPFLAG,ZERO     CLEAR APPENDAGE FLAG BYTE    @DA43510 02589140
           STCM  R3,7,IOOPSW+5    SAVE PUB-PTR. IF PROGR.CHECK @D35IDAP 02589830
           NI    PUBCSFLG,XFF-OPINTV RESET INTERVENTION REQ.   @D14CDAP 02590520
           BAL   RE,RMSGETP2      GET PUB2 ADDRESS             @DA44462 02591210
*SGLINK    RMSGETP2,INPUT=(R3,R6,RE),OUTPUT=R8                 @DA44462 02591900
           L     RE,APMGMDAT      RESTORE PGMDATA BASE POINTER @DA44498 02592590
           LR    RF,R8            BTAM REQUIRES PUB2 IN RF     @DA44462 02593280
           MVC   APPCSW(8),CSW    SAVE CSW AT ENTRY            @DA31144 02593970
           MVC   APPCCSIO,CHQCCSIO SAVE FLAGS AT ENTRY         @DA31144 02594660
           TM    PUBCSFLG,DEVBSY  HAS DEVICE BEEN STARTED      @DA36688 02595350
           BZ    GOAPP000         NO,                          @DA36688 02596040
           OI    APPCCSIO,CHQCCACT ENSURE PROPER INDICATION    @DA36688 02596730
GOAPP000   TM    CHNSTA,PRGPRT    PROGRAM OR PROTECTION CHECK  @DA43858 02597420
           BNZ   GOAPP010         YES, ASSUME ENDING STATUS    @DA43858 02598110
           CLI   DEVSTA,ZERO      ANY DEVICE STATUS            @D14CDAP 02598800
           BE    GOAPP020         NO, ASSUME PCI               @D14CDAP 02599490
           TM    DEVSTA,DEVEND    IS THIS ENDING STATUS        @D14CDAP 02600180
           BO    GOAPP090         YES,                         @D31YDAP 02600870
           TM    DEVSTA,CHANEND   IS THIS PRIMARY STATUS       @D14CDAP 02601560
           BO    GOAPP020         YES,                         @D14CDAP 02602250
GOAPP010   NI    CHQCCSIO,XFF-CHQCCACT-CHQCCLTE INDICATE INACT.@DA24568 02602940
GOAPP020   TM    CHQGRP,CHQGRVTM  IS IT VTAM REQUEST           @D14CDFG 02603630
           BZ    GOAPP060         NO, SKIP                     @D14CDFG 02604320
           TM    IJBTCAVT,IJBTCACT VTAM STILL ACTIVE           @DA43510 02605010
           BZ    GOAPP070         NO, DONT INVOKE APPENDAGE    @DA43510 02605700
           LR    R5,R8            SAVE PUB2 ADDRESS IN R5      @DY46396 02606780
.*       VTAM  3.4 REQUIRES PUB2 ADDRESS IN GENERAL REGISTER 5 @DY46396 02607170
           AIF   (NOT &VSE270).N270040                                  02607770
           LR    RC,R0            SAVE CALLED-FROM INDICATION  @DY46396 02608750
           AIF   (NOT &VTAM31).NV31040                                  02609150
           TM    CCBCLS,CCBCOPY   IS THIS A COPIED CCW         @DY46396 02610430
           BZ    GOAPP060         NO, PROCEED NORMAL           @DY46396 02611020
           L     R8,TIBPTR        LOAD TIB PTR                 @DY46580 02611640
           USING TIBADR,R8                                     @DY46580 02611670
           CLI   TIBRQID,BUFBND   COPY BLOCK BOUND?            @DY46580 02611700
           BNE   GOAPP025         NO,                          @DY46580 02611730
.* TASK WAS ALREADY SET INTO BOUND CONDITION FOR THE LAST INTERRUPT     02611760
.* PASSED TO/FROM THE APPENDAGE. NO FURTHER I/O REQUESTS CAN BE BE      02611790
.* HONOURED. ( --> DUPLICATE 'COPY BLOCK BOUND'-UNPOSTING )             02611820
           OI    APPFLAG,APPDOIGN RETURN FOR IGNORE            @DY46580 02611850
           B     GOAPPRET         SKIP APPENDAGE               @DY46580 02611880
           DROP  R8               DROP TIBADR                  @DY46580 02611910
GOAPP025   SLR   R0,R0            CLEAR REGISTER               @DY46396 02611940
           ICM   R0,7,CSW+1       IS CCW ADDRESS PRESENT       @DY46396 02612200
           BZ    GOAPP030         NO, UNSOLICITED STATUS       @DY46396 02612790
           L     R9,ACCWT         SET BASE REGISTER FOR CCWT RT@DY46396 02613380
           USING CCWTADR,R9       CCWTADR BASE                 @DY46396 02613970
           BAS   R8,CCWVRETR      RETRANSLATE TO USERS CCW-ADDR@DY46396 02614560
*SGLINK    CCWVRETR,INPUT=(R0,R1,R3,R8,R9),OUTPUT=(R0,R1)      @DY46396 02615150
.*         VTAM  USED CCW ADDRESS IS RETURNED IN R0            @DY46396 02616440
.*         VTAM  USED CCB ADDRESS IS RETURNED IN R1            @DY46396 02616830
           L     R9,AINTER        RELOAD IOINTER BASE ADDRESS  @DY46396 02617720
           USING INTRTN,R9        IOINTER BASE POINTER         @DY46396 02618010
           STM   R0,R1,VTM31SAV   SAVE VTAM KNOWN ADDRESSES    @DY46396 02618420
           ST    R0,CSW           VTAM NEEDS VIRTUAL CCW ADDR. @DY46396 02619300
           B     GOAPP040         PROCEED MAIN PATH            @DY46396 02619790
GOAPP030   L     R1,CCBVA         LOAD VTAM CCB ADDRESS        @DY46396 02620280
GOAPP040   LR    R0,RC            RESTORE ENTRY CODE           @DY46396 02620770
           AGO   .Y270040         PREPARE APPENDAGE CALL       @DY46396 02621260
.N270040   ANOP                                                         02622260
.NV31040   ANOP                                                @DY46396 02623240
GOAPP050   MVN   CSW(1),CHQCCSIO  PASS SIO CONDITION CODE      @D14CDFG 02623640
.Y270040   ANOP                                                         02624330
GOAPP060   MVI   RID+1,APPENDID   SET ROUTINE-ID TO APPENDAGE  @D35IDAP 02625020
           SLR   R8,R8            CLEAR REGISTER               @DYAOM   02625710
           ICM   R8,7,CCBCSWW     APPENDAGE ROUTINE ADDRESS    @DYAOM   02626400
           BALR  R7,R8            EXECUTE IT, RETURN+0,+4,+8,+C         02627090
*SGLINK    USERAPP,INPUT=(R5,R7,R8,RE),WORK=RF                 @DA44498 02627780
           OI    APPFLAG,APPDORTY RETURN FOR RETRY             @D35IDAP 02628470
GOAPP070   OI    APPFLAG,APPDODEQ RETURN FOR INTERRUPT PROCESS @D35IDAP 02629160
           OI    APPFLAG,APPDOSDR RETURN FOR SDR RECORDING     @D35IDAP 02629850
           OI    APPFLAG,APPDOERP RETURN FOR ERP INVOCATION    @D14CDFG 02630540
           MVI   RID+1,SYSTEMID   SET ROUTINE-ID TO SYSTEM     @D35IDAP 02631230
           DEBUG USERDATA,XXDBGMC,CSW,APPRETAD SAVE DEBUG INFO @DY46396 02632210
           LH    RF,DEVSTA        GET MODIFIED STATUS          @D14CDFG 02632610
           LTR   RF,RF            IS STATUS ZERO               @D37BDAP 02633300
           BNZ   GOAPPRET         NO, DONT MODIFY IT           @D37BDAP 02633990
           OI    CHNSTA,PCI       MAKE IT A PCI INTERRUPT      @D37BDAP 02634680
GOAPPRET   L     RF,APPRETAD      RESTORE RETURN ADDRESS       @D35IDAP 02635370
           BR    RF                    ======================> @D35IDAP 02636060
GOAPP090   CLC   PUBCHANN(2),IJBOWSAV IS IT OUR REAL DEVICE    @D31YDAP 02636750
           BNE   GOAPP010         NO,                          @D31YDAP 02637440
           CLI   DEVSTA,DEVEND    IS THIS A READY INTERRUPT    @D31YDAP 02638130
           BNE   GOAPP010         NO,                          @D31YDAP 02638820
           TM    CHQCCSIO,CHQCCACT UNSOLICITED INTERRUPT       @D31YDAP 02639510
           BO    GOAPP010         NO,                          @D31YDAP 02640200
.*                                                             @D31YDAP 02644000
.*         VSE WILL NOW INITIATE VSE CONSOLE TAKE-OVER         @D31YDAP 02645490
.*                                                             @D31YDAP 02646000
           MVI   DEVSTA,ATTENT    SIMULATE ATTENTION INTERRUPT @D31YDAP 02647690
           ST    R3,INTPARM       PROVIDE INTERRUPT PARAMETER  @DAOM085 02648380
           MVI   IOINF+1,XFF      INDICATE SIMULATED INTERRUPT @D31YDAP 02649070
           B     TSTSTAT               ======================> @D31YDAP 02649760
           DROP  RD               RELEASE GOAPPND BASE         @D62OSAP 02650450
           DROP  R3               RELEASE PUB BASE POINTER     @D52VDAP 02651140
           DROP  R4               RELEASE CHANQ BASE POINTER   @DA42692 02651830
           DROP  R1               RELEASE CCB BASE POINTER     @DA42692 02652520
         TITLE 'VSE SUPERVISOR     IOINTER     ALLOCATE PUB ENTRY     ' 02654000
         USING INTALPUB,RD        SUBROUTINE BASE POINTER      @D61BDAP 02655000
INTALPUB DS    0H'0'                                                    02656000
         LH    R3,YPUBTAB         ADDRESS OF PUB-TABLE         @D28ADAP 02657000
         USING PUBADR,R3          PUB BASE POINTER             @D28ADAP 02658000
INTAL020 CLI   PUBCHANN,XFF       IS THIS PUB-TABLE END        @D28ADAP 02659000
         BE    INTAL040           YES,                         @D28ADAP 02660000
         CLC   PUBCHANN(2),CHNADR IS THIS THE RIGHT PUB        @D28ADAP 02661000
         BER   RE                 YES, ======================> @D28ADAP 02662000
         LA    R3,NEXTPUB         POINT TO NEXT PUB            @D28ADAP 02663000
         B     INTAL020           CHECK NEXT DEVICE            @D28ADAP 02664000
         DROP  R3                 RELEASE PUB BASE POINTER     @D28ADAP 02665000
INTAL040 SLR   R3,R3              INDICATE NO PUB FOR DEBUG    @D28ADAP 02666000
         DEBUG IOFIELDS,XXDBGMC   SET UP DEBUG ENTRY           @D28ADAP 02667000
         B     INTNOPUB           NO-PUB=====================> @D28ADAP 02668000
         AGO   .SKIP080           SKIP OLD CODE                @D28ADAP 02669000
INTALPUB CLI   CHNADR,X'10'       IS CHANNEL NO. VALID         @D35JDAP 02670000
         BNL   INTAL030           NO,                          @DA44462 02671000
         IC    R8,CHNADR          INTERRUPTING CHANNEL ID      @D62TAAP 02672000
         L     R8,AINTTAB1        GET ADDRESS OF MAIN TABLE    @D35IDAP 02673000
         IC    R1,DEVADR          INTERRUPTING DEVICE ID       @D35IDAP 02674000
         AIF   (&AG1 LE 254).NMDV110                           @D51ODGL 02675000
         LA    R3,0(R1,R1)        MULTIPLY FOR ENTRY           @D51ODGL 02676000
         LH    R3,0(R8,R3)        GET ENTRY NUMBER             @D51ODGL 02677000
.NMDV110 AIF   (&AG1 GT 254).YMDV110                           @D51ODGL 02678000
         IC    R3,0(R8,R1)        TAKE ONE BYTE ENTRY          @D51ODGL 02679000
.YMDV110 ANOP                                                  @D51ODGL 02680000
         LTR   R3,R3              IS THE DEVICE ADDRESS UNIQUE @D35IDAP 02681000
         BP    INTAL020           YES                          @D51ODAP 02682000
         AIF   (&AG1 LE 254).NMDV120                           @D51ODAP 02683000
         BM    INTAL030           NO,                          @DA44462 02684000
.NMDV120 ANOP                                                  @D51ODAP 02685000
         C     R3,AINTTAB2        DOES SECOND LEVEL TAB EXIST  @D35IDAP 02686000
         BE    INTAL030           NO,                          @DA44462 02687000
         A     R1,AINTTAB2        SECOND LEVEL TABLE ADDRESS   @D35IDAP 02688000
         IC    R3,0(R1)           GET LINE ENTRY NUMBER        @D35IDAP 02689000
         LTR   R3,R3              IS THERE A LINE ENTRY        @D35IDAP 02690000
         AIF   (&AG1 LE 254).NMDV130                           @D51ODAP 02691000
         BNZ   INTAL010           YES,                         @D51ODAP 02692000
         LA    R3,256             GET ROW NUMBER               @D51GDAP 02693000
         AGO   .YMDV130                                        @D51ODAP 02694000
.NMDV130 ANOP                                                           02695000
         BZ    INTAL030           YES,                         @DA44462 02696000
.YMDV130 ANOP                                                  @D51ODAP 02697000
INTAL010 BCTR  R3,R0              ABSOLUTE LINE ENTRY NUMBER   @D35IDAP 02698000
         LR    R1,R3              COPY LINE ENTRY NUMBER       @D35IDAP 02699000
         IC    R3,CHNADR          INTERRUPTING CHANNEL ID      @D35IDAP 02700000
         AIF   (&AG1 LE 254).NMDV140                           @D51ODGL 02701000
         AR    R3,R3              ..TIMES LENGTH OF ENTRY      @D51ODGL 02702000
         SLL   R1,5               MULTIPLY WITH LINE ENTRY LEN @D51ODGL 02703000
         A     R1,AINTTAB3        ADD TAB3 BEGIN ADDRESS       @D35IDAP 02704000
         LH    R3,0(R3,R1)        PUB ENTRY NUMBER             @D51ODGL 02705000
.NMDV140 AIF   (&AG1 GT 254).YMDV140                           @D51ODGL 02706000
         SLL   R1,4               MULTIPLY WITH LINE ENTRY LEN @D51ODGL 02707000
         A     R1,AINTTAB3        ADD TAB3 BEGIN ADDRESS       @D35IDAP 02708000
         IC    R3,0(R3,R1)        PUB ENTRY NUMBER             @D51ODGL 02709000
.YMDV140 ANOP                                                  @D51ODGL 02710000
         LTR   R3,R3              IS THERE AN ENTRY NO. AT ALL @D35IDAP 02711000
         BZ    INTAL030           NO,                          @DA44462 02712000
INTAL020 BCTR  R3,R0              ABSOLUTE ENTRY NUMBER        @D35IDAP 02713000
         SLL   R3,PUBSLEN         MULTIPLY WITH PUB LENGTH     @D51ODGL 02714000
         AH    R3,YPUBTAB         ABSOLUTE PUB ENTRY ADDRESS   @D35IDAP 02715000
         CLC   0(2,R3),CHNADR     IS THIS THE CORRECT PUB      @D61ADAP 02716000
         BER   RE                 YES, ======================> @D61ADAP 02717000
INTAL030 DEBUG IOFIELDS,XXDBGMC   SET UP DEBUG ENTRY           @DA44462 02718000
         B     INTNOPUB                ======================> @D61NDAP 02719000
         DROP  RD                 RELEASE SUBROUTINE BASE      @D61BDAP 02720000
         TITLE 'VSE SUPERVISOR     IOINTER   LCK TASK SPECIAL PROCESS ' 02721000
         USING LCKPROC,RD         ROUTINE BASE POINTER         @DA42913 02722000
         USING PUBADR,R3          PUB BASE POINTER             @DA42913 02723000
         USING PBXADR,R2          PUBX BASE POINTER            @D62TAAP 02724000
         USING CHQADR,R4          CHANQ BASE POINTER           @DA42913 02725000
LCKPROC  LTR   R1,R1              CCB AVAILABLE                @DA42913 02726000
         BZ    LCKEXIT            NO,                          @DA42913 02727000
         CLC   CHQTID,IORQ4LCK    IS THIS A LOCK REQUEST       @D66ADAP 02728000
         BNE   LCKEXIT            NO,                          @DA42913 02729000
         TM    CHQDEV,CHQDASD+CHQFBA IS THIS A DASD REQUEST    @DA42913 02730000
         BZ    LCKEXIT            NO,                          @DA42913 02731000
         L     RE,IRB$            GET IRB ADDRESS              @DA42913 02732000
         USING IRB,RE             IRB BASE POINTER             @DA42913 02733000
         CLC   PBXPIM(1),IRBLPUM  SINGLE PATH ONLY             @DA42913 02734000
         BNE   LCKPR010           NO,                          @DA42913 02735000
         LA    RE,INTMA060        LOAD                         @DA42913 02736000
         ST    RE,ALCKPROC        PREVENT FURTHER ACCESS       @DA42913 02737000
LCKEXIT  BR    R7                      ======================> @DA42913 02738000
         SPACE 1                                                        02739000
LCKPR010 CLI   PBXLPMSV,ZERO      IS PATH ALREADY RESTRICTED   @DYAOM   02740000
         BNE   LCKPR020           YES,                         @DYAOM   02741000
         MVC   PBXLPMSV(1),PBXLPM SAVE LOGICAL PATH MASK       @D52BDAP 02742000
         MVC   PBXLPM(1),IRBLPUM  RESTRICT TO A SINGLE PATH    @DA42913 02743000
         B     LCKEXIT                                         @DA42913 02744000
         DROP  RE                 RELEASE IRB BASE POINTER     @DA42913 02745000
LCKPR020 SLR   RE,RE              CLEAR REGISTER               @DA42913 02746000
         ICM   RE,7,CSW+1         GET CCW+8 ADDRESS            @DA42913 02747000
         BZ    LCKEXIT            SECONDARY STATUS             @DA42913 02748000
         SH    RE,H8              POINT TO LAST CCW            @DA42913 02749000
         CLI   0(RE),REL          IS THIS A RELEASE CCW        @DA42913 02750000
         BNE   LCKEXIT            NO,                          @DA42913 02751000
         OC    PBXLPM(1),PBXLPMSV RESTORE LOGICAL PATH MASK    @D52BDAP 02752000
         MVI   PBXLPMSV,ZERO      RESET SAVED LPM              @DYAOM   02753000
         B     LCKEXIT                                         @D61SAAP 02754000
         DROP  R3                 RELEASE PUB BASE POINTER     @D52VDAP 02755000
         DROP  R2                 RELEASE PUBX BASE POINTER    @D62TAAP 02756000
         DROP  RD                 RELEASE SUBROUTINE BASE      @D52VDAP 02757000
.SKIP080 ANOP                                                           02758000
         TITLE 'VSE SUPERVISOR     IOINTER   PROVIDE USER SENSE DATA  ' 02759000
         USING INTGUSER,RD        INTGUSER BASE POINTER        @D52VDAP 02760000
         USING CHQADR,R4          CHANQ BASE POINTER           @D52VDAP 02761000
         USING PUBADR,R3          PUB   BASE POINTER           @D61NDAP 02762000
         USING ERBLKADR,R2        ERROR ENTRY BASE POINTER     @D52VDAP 02763000
         USING CCBADR,R1          CCB BASE POINTER             @D52VDAP 02764000
INTGUSER LA    R8,CCBSENS         USERS SENSE CCW ADDRESS      @D61NDAP 02765000
         USING CCWADR,R8          CCW BASE POINTER             @D34SDAP 02766000
         LA    R0,1               SET INDICATOR FOR INTGUVAD   @D28ADAP 02767000
         BAL   RE,INTGUVAD        VIRTUAL ADDRESS OF LAST BYTE @D28ADAP 02768000
*SGLINK  GETGUVAD,INPUT=(R0,R1,R6,R8,RE),OUTPUT=RF,WORK=R7     @D28ADAP 02769000
         B     INTGUS50         0 NO DATA TO MOVE              @D28ADAP 02770000
         XR    R0,R0            4 CLEAR DISPLACEMENT POINTER   @D34SDAP 02771000
         BAL   RE,INTGUVAD        GET VIRTUAL DATA ADDRESS     @D28ADAP 02772000
*SGLINK  GETGUVAD,INPUT=(R0,R1,R6,R8,RE),OUTPUT=RF,WORK=R7     @D28ADAP 02773000
         B     INTGUS50         0 NO DATA TO MOVE              @D28ADAP 02774000
         L     R7,AINTGU40      4 31-BIT CONT. ADDRRESS        @D28ADAP 02775000
         BSM   0,R7                                                     02776000
AINTGU40 DC    A(INTGUS40+X'80000000') 31-BIT MODE CONT. ADDR. @D28ADAP 02777000
INTGUS40 L     R8,INTGUVL1        GET BYTE COUNT FROM CCW      @D28ADAP 02778000
         EX    R8,INTSMVUS        MOVE SENSE BYTES TO USER AREA@D37BDFG 02779000
         LA    R7,INTGUS50        GET 24-BIT CONT. ADDRESS     @D28ADAP 02780000
         BSM   0,R7               GET BACK INTO 24-BIT MODE    @D28ADAP 02781000
INTGUS50 CLI   INTFSTFL,X'E0'     WAS THIS DASD FAST RECOVERY  @D62TAAP 02782000
         BE    INTNOREC           YES,  =====================> @D52VDAP 02783000
         B     INTEEXIT           NO,   =====================> @D61NDAP 02784000
         SPACE 3                                                        02785000
*SGLINK  GETGUVAD,INPUT=(R0,R1,R6,R8,RE),OUTPUT=RF,WORK=R7     @D28ADAP 02786000
INTGUVAD DS    0H'0'              GET VIRTUAL ADDRESS          @D28ADAP 02787000
         AIF   (NOT &VSE280).N280220                           @D28ADAP 02788000
         TM    CCBBY3,CCBCCWF1    IS IT A FORMAT1 CCW          @D28ADAP 02789000
         BZ    INTGUV40           NO,                          @D28ADAP 02790000
         TM    CCWFLAG1,SKIP      IS SENSE DATA NEEDED         @D28ADAP 02791000
         BO    INTGUVX0           NO, TAKE PROPER EXIT         @D28ADAP 02792000
         LTR   R0,R0              OFFSET INDICATOR SET         @D28ADAP 02793000
         BZ    INTGUV20           NO,                          @D28ADAP 02794000
         ICM   R0,1,CCWLNG1+1     GET BYTE COUNT FROM CCW      @D28ADAP 02795000
         BZ    INTGUVX0           BYTE COUNT IS ZERO/INVALID   @D28ADAP 02796000
         BCTR  R0,ZERO            MINUS ONE                    @D28ADAP 02797000
         ST    R0,INTGUVL1        SAVE LENGTH COUNT            @D28ADAP 02798000
INTGUV20 BAL   R7,GETDADRF1       GET VIRTUAL DATA ADDRESS     @D28ADAP 02799000
*SGLINK  GETDADRF1,INPUT=(R0,R6,R7,R8),OUTPUT=RF               @D28ADAP 02800000
         B     INTGUVX4           RETURN TO CALLER             @D28ADAP 02801000
.N280220 ANOP                                                           02802000
INTGUV40 TM    CCWCHAIN,SKIP      IS SENSE DATA NEEDED         @D28ADAP 02803000
         BO    INTGUVX0           NO, TAKE PROPER EXIT         @D28ADAP 02804000
         LTR   R0,R0              OFFSET INDICATOR SET         @D28ADAP 02805000
         BZ    INTGUV60           NO,                          @D28ADAP 02806000
         ICM   R0,1,CCWLNG+1      GET BYTE COUNT FROM CCW      @D28ADAP 02807000
         BZ    INTGUVX0           BYTE COUNT IS ZERO/INVALID   @D28ADAP 02808000
         BCTR  R0,ZERO            MINUS ONE                    @D28ADAP 02809000
         ST    R0,INTGUVL1        SAVE LENGTH COUNT            @D28ADAP 02810000
INTGUV60 BAL   R7,GETDADR         GET VIRTUAL DATA ADDRESS     @D28ADAP 02811000
*SGLINK  GETDADR,INPUT=(R0,R6,R7,R8),OUTPUT=RF                 @D28ADAP 02812000
INTGUVX4 LTR   RF,RF              TRANSLATION SUCCESSFULL      @D34SDAP 02813000
         BZ    INTERR25           NO,   ====================>> @DA32237 02814000
         B     4(,RE)             DONE ======================> @D28ADAP 02815000
INTGUVX0 B     0(,RE)             SKIP ======================> @D62TAAP 02816000
INTGUVL1 DC    A(0)               LENGTH-1   OF SENSE DATA     @D28ADAP 02817000
         DROP  R8                 RELEASE CCW BASE POINTER     @D35IDAP 02818000
         DROP  R4                 RELEASE CHANQ BASE POINTER   @D52VDAP 02819000
         DROP  R3                 RELEASE PUB   BASE POINTER   @D61NDAP 02820000
         DROP  R1                 RELEASE CCB BASE POINTER     @D52VDAP 02821000
INTERR25 L     RD,AINTEQS         GET BASE FOR                 @DA32237 02822000
         USING INTEQSET,RD        ... ERROR ENTRY SUBROUTINE   @DA32237 02823000
         BAL   R7,INTEQPST        POST APPROPRIATE ERROR TASK  @DA32237 02824000
         LA    RB,X'25'           ERROR CODE FOR DISPATCHER    @DA32237 02825000
         B     ERRIOGO            CANCEL USER =============>>> @DA32237 02826000
INTSMVUS MVC   ZERO(ONE,RF),ERRQSNS MOVE SENSE TO USER         @D28ADAP 02827000
         DROP  R2                 RELEASE ERROR ENTRY BASE     @D52VDAP 02828000
         DROP  RD                 RELEASE INTGUSER BASE        @D34SDAP 02829000
         TITLE 'VSE SUPERVISOR     IOINTER   POWER INTERRUPT APPENDAGE' 02830000
         USING PADS,RF            COMMON ADDRESS BLOCK BASE    @D51KDAP 02831000
         USING PWRINTAP,RE        SUBROUTINE BASE POINTER      @D51KDAP 02832000
         USING PUBADR,R3          PUB BASE POINTER             @D51KDAP 02833000
PWRINTAP CLI   PUBDEVTY,TPRT      IS THIS A UNIT RECORD DEVICE @D51KDAP 02834000
         BNL   DEVISBSY           NO,  ======================> @D51KDAP 02835000
         TM    PUBCSFLG,DEVBSY+QEDERR UNSOLICITED INTERRUPT    @D51KDAP 02836000
         BNZ   DEVISBSY           NO,  ======================> @D51KDAP 02837000
         SGSETUP SETLCTL,NEXTSCB=CURRENT,OPTION=POWER,WR1=R7,WR2=R8     02838000
         L     RF,CAHR            ADDRESS HOT READER ROUTINE   @D30EULR 02839000
         DROP  RF                 RELEASE COMMON ADDRESS BLOCK @DF00901 02840000
         BALR  R8,RF              BRANCH TO HOT RDR RTN        @D30EULR 02841000
*SGLINK  CAHR,INPUT=(R8,RF)                                    @D369DAP 02842000
         SGSETUP SETLCTL,NEXTSCB=CURRENT,OPTION=OWNER,WR1=R7,WR2=R8     02843000
         B     RSETINIT                ======================> @D35IDAP 02844000
         DROP  R3                 RELEASE PUB BASE POINTER     @D51GDAP 02845000
         DROP  RE                 RELEASE SUBROUTINE BASE      @D51GDAP 02846000
         TITLE 'VSE SUPERVISOR     IOINTER     DELAYED SIO PROCESSING ' 02847000
         AIF   (NOT &BGDEBUG).NTXT115                          @D31..AP 02848000
***************************************************************@D31..AP 02849000
*                                                              @D31..AP 02850000
*  FUNCTION:                                                   @D31..AP 02851000
*            ENSURE THAT THE APPROPRIATE DEVICE IS NOT         @D31..AP 02852000
*            RETRIED BEFORE A DEFINED TIME INTERVAL HAS ELAPSED@D31..AP 02853000
*  ENTRY POINT:                                   AT LABEL     @D31..AP 02854000
*            1. ENTERED  FROM ERPEXIT IN (SGERP)     INITDLAY  @D31..AP 02855000
*  EXITS:                                         TO LABEL     @D31..AP 02856000
*            I/O INITIATOR                           INITRG    @D31..AP 02857000
*  INPUTS:                                                     @D31..AP 02858000
*            R2= ADDRESS OF PUBX                               @D31..AP 02859000
*            R3= ADDRESS OF PUB                                @D31..AP 02860000
*            R6= ADDRESS OF DISPATCHER                         @D31..AP 02861000
*            R9= IOINTER BASE REGISTER                         @D31..AP 02862000
*  OUTPUTS:                                                    @D31..AP 02863000
*  REGISTERS:                                                  @D31..AP 02864000
*  SUBROUTINES:                                                @D31..AP 02865000
*                                                              @D31..AP 02866000
***************************************************************@D31..AP 02867000
         SPACE 3                                               @D31..AP 02868000
.NTXT115 ANOP                                                  @D31..AP 02869000
         USING PBXADR,R2          PUBX BASE POINTER            @D62TAAP 02870000
         USING PUBADR,R3          PUB BASE POINTER             @D31..AP 02871000
INITDLAY EQU   *                                               @D31..AP 02872000
         BALR  RD,0               DELAYED RETRY  BASE ADDRESS  @D31..AP 02873000
         USING INITDLYB,RD        DELAYED RETRY  BASE POINTER  @D31..AP 02874000
INITDLYB BAL   R7,CQDSP           SET UP REGISTERS             @D31..AP 02875000
*SGLINK  CQDSP,INPUT=(R3,R6,R7),OUTPUT=(R1,R4,RA),WORK=R0      @D31..AP 02876000
         LTR   R4,R4              IS ENTRY STILL AVAILABLE     @D31..AP 02877000
         BZ    INITRG             NO   ======================> @D31..AP 02878000
         USING CHQADR,R4          CHANQ BASE POINTER           @D31..AP 02879000
         USING CCBADR,R1          CCB   BASE POINTER           @D31..AP 02880000
         LA    R4,0(,R4)          CLEAR CHAINING BYTE          @DAOM043 02881000
         TM    CHQCSW+4,ATTENT+DEVEND+UNITEXCP READY ALREADY   @DY45299 02882000
         BO    INITRG             YES, ======================> @DY45299 02883000
         OI    PUBCSFLG,OPINTV    HAVE TO WAIT FOR READY       @D31..AP 02884000
         OI    PUBCSFLG,PUBLOBSY  INDICATE LONG BUSY           @DBOC    02885000
         TM    PBXFLAG,PBXTAPE    IS THIS A TAPE DEVICE        @DBOC    02886000
         BZ    INITDL10           NO, WAIT FOR COMPLETION      @DBOC    02887000
         CLC   CHQTID,IORQ4SVT    SERVICE TASK REQUEST         @DBOC    02888000
         BE    IGNORE             YES, DON'T LET SVT TASK WAIT @DBOC    02889000
INITDL10 DS    0H'0'              PROVIDE FOR DELAYED RETRY    @DY46139 02890000
         AIF   (NOT &VSE270).N270060                           @DY46139 02891490
         L     RE,AMISINTA        SGMIH BASE ADDRESS           @DY46139 02892000
         USING MISINTAT,RE        SGMIH BASE POINTER           @DY46139 02893000
         OI    EMWFLAG1,EMWF1RTY  PROVIDE FOR DELAYED RETRY    @DY46139 02894000
         DROP  RE                 RELEASE MISINTAT BASE        @DY46139 02895000
.N270060 ANOP                                                  @DY46139 02896490
         LTR   R1,R1              HAS USER BEEN POSTED         @D67ADAP 02897000
         BZ    INITRG             YES, ====================>>> @DSCSI   02898000
         B     INITRG             NO,  ======================> @D31..AP 02899000
         DROP  R4                 RELEASE CHANQ BASE POINTER   @D31..AP 02900000
         DROP  R2                 RELEASE PUBX BASE POINTER    @D62TAAP 02901000
         DROP  R1                 RELEASE CCB BASE POINTER     @D31..AP 02902000
         DROP  RD                 RELEASE INITDLYB POINTER     @D31..AP 02903000
***************************************************************@D31YDAP 02904000
*                                                              @D31YDAP 02905000
*        GET PUB OWNER INFORMATION                             @D31YDAP 02906000
*                                                              @D31YDAP 02907000
***************************************************************@D31YDAP 02908000
         SPACE 2                                               @D31YDAP 02909000
         USING PUBISVTM,R8        SUBROUTINE BASE POINTER      @D31YDAP 02910000
*SGLINK  PUBISVTM INPUT=(R3,R7,R8,R9),OUTPUT=RF                @D31YDAP 02911000
PUBISVTM LR    RF,R3              COPY PUB POINTER             @D31YDAP 02912000
         SH    RF,YPUBTAB         PUB DISPLACEMENT             @D31YDAP 02913000
         SRL   RF,2               PUB OWNERSHIP DISPLACEMENT   @D31YDAP 02914000
         A     RF,PBOWNPTR        POINT TO ACTUAL ENTRY        @D31YDAP 02915000
         TM    0(RF),ISTPBOWN     OWNED BY VTAM                @D31YDAP 02916000
         BR    R7                 PASS CONDITION CODE BACK     @D31YDAP 02917000
         DROP  R3                 RELEASE PUB BASE POINTER     @D21IDAP 02918000
         DROP  R8                 RELEASE PUBISVTM BASE POINTER@D21IDAP 02919000
         SPACE 3                                                        02920000
         USING INTSMFUP,RD        INTSMFUP BASE POINTER        @DTIMING 02921000
         USING PBXADR,R2          PUBX     BASE POINTER        @DAOM043 02922000
         USING PUBADR,R3          PUB      BASE POINTER        @DTIMING 02923000
         USING CHQADR,R4          CHANQ    BASE POINTER        @DTIMING 02924000
*SGLINK  INTSMFUP,INPUT=(R3,R4,R9,RD,RE)                       @DTIMING 02925000
INTSMFUP DS    0H'0'              MEASUREMENT UPDATE ROUTINE   @DTIMING 02926000
         STM   RE,R3,INTSMFSV     SAVE REGISTERS               @DTIMING 02927000
         L     RE,IRB$            ADDRESS OF IRB               @DTIMING 02928000
         USING IRB,RE             IRB BASE POINTER             @DTIMING 02929000
         STCK  X'D8'              GET TIME STAMP               @DTIMING 02930000
         L     RF,AINTSMF0        CONTINUATION ADDRESS         @DAOM043 02931000
         BSM   0,RF               GET INTO 31-BIT MODE         @DAOM043 02932000
AINTSMF0 DC    A(INTSMF00+X'80000000') CONTINUATION ADDRESS    @DAOM043 02933000
INTSMF00 LR    RF,R3              COPY PUB ADDRESS             @DAOM043 02934000
         SH    RF,YPUBTAB         OFFSET WITHIN PUB TABLE      @DTIMING 02935000
         LA    RF,8(,RF)          ADJUST FOR SKIP MBI INDEX 0  @DTIMING 02936000
         SLL   RF,2               MEASUREMENT BLOCK OFFSET     @DTIMING 02937000
         A     RF,IJBMBADR        MEASUREMENT BLOCK ADDRESS    @DTIMING 02938000
         L     R1,IJBVSEMB        GET VSE ENTRY OFFSET         @DA44841 02939000
         LA    R1,0(RF,R1)        GET VSE ENTRY ADDRESS        @DA44841 02940000
         TM    IRBSTB2,IRBFCS     IS THIS START FUNCTION       @DTIMING 02941000
         BZ    INTSMF10           NO,                          @DTIMING 02942000
         TM    IRBSTB3,IRBSCPS    IS THIS PRIMARY STATUS       @DTIMING 02943000
         BZ    INTSMFXT           NO, DONT UPDATE              @DTIMING 02944000
         ICM   R0,15,X'DA'        GET RESOLUTION BYTES         @DA44841 02945000
         SRL   R0,3               ADJUST TO PROPER RESOLUTION  @DA44841 02946000
         ICM   R3,15,CHQCCBB3+1   SAVE SSCH TIME               @DTIMING 02947000
         ST    R0,CHQCCBB3+1      SET DEVICE BUSY START TIME   @DA44841 02948000
         BZ    INTSMFXT           NO START TIME WAS PRESENT    @DTIMING 02949000
         SLR   R0,R3              DEVICE CONNECTED TIME        @DA44841 02950000
         AL    R0,4(,R1)          ACCUMULATED DEVICE CONNECT   @DA44841 02951000
         ST    R0,4(,R1)          SAVE ACCUMULATED CONNECT     @DA44841 02952000
         TM    PBXFLAG2,PBXSMFAC  WE NEED TO DO H/W ALSO       @DA44841 02953000
         BZ    INTSMFXT           NO,                          @DTIMING 02954000
         ST    R0,4(,RF)          SAVE ACCUMULATED CONNECT     @DA44841 02955000
         B     INTSMFXT           WE ARE DONE                  @DTIMING 02956000
INTSMF10 TM    IRBSTB3,IRBSCSS    IS THIS SECONDARY STATUS     @DTIMING 02957000
         BZ    INTSMFXT           NO, DONT UPDATE              @DTIMING 02958000
         ICM   R0,15,X'DA'        GET RESOLUTION BYTES         @DA44841 02959000
         SRL   R0,3               ADJUST TO PROPER RESOLUTION  @DA44841 02960000
INTSMF20 ICM   R3,15,CHQCCBB3+1   SAVE PRIMARY STATUS TIME     @DTIMING 02961000
         ST    R0,CHQCCBB3+1      SET IDLE START TIME          @DA44841 02962000
         BZ    INTSMFXT           NO PRIMARY TIME PRESENT      @DTIMING 02963000
         SLR   R0,R3              DEVICE BUSY ONLY             @DA44841 02964000
         LR    R3,R0              COPY BUSY ONLY TIME          @DAOM123 02965000
         AL    R0,20(,R1)         ADD THE OLD BUSY TIMINGS     @DAOM043 02966000
         ST    R0,20(,R1)         SAVE ACCUMULATED DEVICE BUSY @DA44841 02967000
         TM    PBXFLAG2,PBXSMFAC  WE NEED TO DO MEASURING      @DA44841 02968000
         BZ    INTSMFXT           NO,                          @DTIMING 02969000
         AL    R3,20(,RF)         ADD THE OLD BUSY TIMINGS     @DAOM123 02970000
         ST    R3,20(,RF)         SAVE ACCUMULATED DEVICE BUSY @DA44841 02971000
         DROP  R2                 RELEASE  PUBX     BASE       @DTIMING 02972000
INTSMFXT AMODESW SET,AMODE=24,WR=(RF)                          @DTIMING 02973000
         LM    RE,R3,INTSMFSV     RESTORE REGISTERS            @DTIMING 02974000
         BR    RE                 RETURN=====================> @DTIMING 02975000
         SPACE 1                                               @DTIMING 02976000
INTSMFSV DC    6F'0'              REGISTER SAVE AREA           @DTIMING 02977000
         DROP  R3                 RELEASE  PUB      BASE       @DTIMING 02978000
         DROP  R4                 RELEASE  CHANQ    BASE       @DTIMING 02979000
         DROP  RD                 RELEASE  INTSMFUP BASE       @DTIMING 02980000
         DROP  RE                 RELEASE  IRB      BASE       @DTIMING 02981000
         SPACE 3                                               @DTIMING 02982000
         USING MIHCHECK,RD        MIHCHECK BASE POINTER        @D21IDAP 02983000
         USING PUBADR,R3          PUB BASE POINTER             @D21IDAP 02984000
         USING PBXADR,R2          PUBX BASE POINTER            @D62TAAP 02985000
MIHCHECK L     R1,AMISINTA        LOAD SGMIH BASE ADDRESS      @D52VDAP 02986000
         USING MISINTAT,R1        SGMIH BASE POINTER           @D52VDAP 02987000
         C     R3,EMWPUBPT        INTERUPT MISSING FOR THIS DEV@D51ODGL 02988000
         BNE   MIHCHEXT           NO,                          @D14CDAP 02989000
         XC    EMWPUBPT,EMWPUBPT  INDICATE INTERRUPT RECEIVED  @D61CDAP 02990000
         XC    EMWCHANQ,EMWCHANQ  RESET SAVED CHANQ INDEX      @D51ODGL 02991000
         OI    EMWFLAG1,EMWF1CLR  WE BETTER ISSUE A DOM        @DA43896 02992000
         OI    SNSFLAG,SNSMSGOP   MIH SERVICE NEEDED           @D61CDAP 02993000
         L     R8,ASNSTIB         ADDRESS OF SNS-TIB           @D14CDAP 02994000
         CLI   TIBRQID-TIBADR(R8),SYSBND IS SNS-TASK ACTIVE    @D14CDAP 02995000
         BNE   MIHCHEXT           YES,                         @D14CDAP 02996000
         BAL   RF,POST            POST SNS-TASK                @D14CDAP 02997000
*SGLINK  POST,INPUT=(R6,R8,RF),WORK=RE                         @D14CDAP 02998000
MIHCHEXT BR    R7                 CONT ======================> @D14CDAP 02999000
         DROP  R1                 RELEASE SGMIH BASE           @D14CDAP 03000000
         DROP  R2                 RELEASE PUBX BASE POINTER    @D62TAAP 03001000
         DROP  R3                 RELEASE PUB BASE POINTER     @D37BDAP 03002000
         DROP  RD                 RELEASE MIHCHECK BASE        @D21IDAP 03003000
         TITLE 'VSE SUPERVISOR    IOINTER    CONDITION CODE PROCESSING' 03004000
         USING PROCDLY,RD         PROCDLY BASE POINTER         @D21IDAP 03005000
         USING CHQADR,R4          CHANQ BASE POINTER           @D21IDAP 03006000
         USING PUBADR,R3          PUB BASE POINTER             @D21IDAP 03007000
         USING PBXADR,R2          PUBX BASE POINTER            @D62TAAP 03008000
         USING CCBADR,R1          CCB   BASE POINTER           @D21IDAP 03009000
PROCDLY  LTR   R4,R4              IS CHANQ ADDRESS PRESENT     @D14BDAP 03010000
         BZ    PRDLY050           NO,  VERY STRANGE            @D52VDAP 03011000
*        NI    SYSFLAG2,XFF-AFTIOINT RESET AFTER INTERRUPT     @D28DR06 03012000
         NI    PUBCSFLG,XFF-DEVBSY RESET DEVICE BUSY           @D14BDAP 03013000
         MVI   CHQCCSIO,ZERO      RESET ALL                    @DA44277 03014000
         SLR   R0,R0              CLEAR REGISTER               @D14BDAP 03015000
         ICM   R0,8,CSW           GET DELAYED CC IN R0         @D14BDAP 03016000
         SLL   R0,4               GET CC IN CORRECT POSITION   @D14BDAP 03017000
         NI    CSW,X'FC'          RESET DEFERRED CC            @D14BDAP 03018000
         ICM   R8,15,CSW          IS THIS PRIMARY STATUS       @D62TAAP 03019000
         BNZ   PRDLY030           YES                          @D14BDAP 03020000
         L     R8,CHQCCBAD        GET CCB ADDRESS              @D62TAAP 03021000
         ICM   R8,8,H0            CLEAR HIGH ORDER BYTE        @D68REEF 03022000
         L     R8,8(R8)           GET ASSOCIATED CCW ADDRESS   @D62TAAP 03023000
         ICM   R8,8,CHQCAWKY      GET ASSOCIATED PROTECTION KEY@D62TAAP 03024000
         B     PRDLY040                                        @D14BDAP 03025000
PRDLY030 SH    R8,H8              CALCULATE 1ST CCW ADDRESS    @D62TAAP 03026000
PRDLY040 ST    R8,CAW             AND SAVE IT IN CAW           @D62TAAP 03027000
         DROP  R2                 RELEASE PUBX BASE            @D62TAAP 03028000
         LH    R2,CHNADR          SGSCHED NEEDS CUU IN R2      @D14BDAP 03029000
         L     RD,INITBASE        GET SGSCHED BASE ADDRESS     @D14BDAP 03030000
         USING SGSCHED,RD         SGSCHED BASE POINTER         @D14BDAP 03031000
         SPM   R0                 SET CC INTO PSW              @D14BDAP 03032000
         BC    4,CSWSTORD         CSWST======================> @D51GDAP 03033000
         B     INTENOP                 ======================> @D51GDAP 03034000
PRDLY050 NI    CSW,X'FC'          RESET DEFERRED CC            @D52VDAP 03035000
         B     TSTCSW                  ======================> @D52VDAP 03036000
         DROP  RD                 RELEASE SGSCHED BASE         @D14BDAP 03037000
         DROP  R1                 RELEASE CCB BASE POINTER     @D21IDAP 03038000
         DROP  R3                 RELEASE PUB BASE POINTER     @D21IDAP 03039000
         DROP  R4                 RELEASE CHANQ BASE POINTER   @D21IDAP 03040000
         TITLE 'VSE SUPERVISOR     IOINTER     IGNORE ERROR PROCESSING' 03041000
***************************************************************@D37BDAP 03042000
*                                                              @D37BDAP 03043000
*        UPDATE DIB ENTRY FOR SYSTEM FILES                     @D37BDAP 03044000
*                                                              @D37BDAP 03045000
***************************************************************@D37BDAP 03046000
         SPACE 3                                                        03047000
         USING PROCSYSF,RC        PROCSYSF BASE POINTER        @D31YDAP 03048000
         USING CHQADR,R4          CHANQ BASE POINTER           @D31YDAP 03049000
         USING PUBADR,R3          PUB BASE POINTER             @D21IDAP 03050000
         USING PBXADR,R2          PUBX BASE POINTER            @D62TAAP 03051000
         USING CCBADR,R1          CCB   BASE POINTER           @D21IDAP 03052000
PROCSYSF EQU   *                                               @D35IDAP 03053000
         TM    CHQDEV,CHQFBA      IS THIS FBA DEVICE           @D35IDAP 03054000
         BO    CHNDPST            YES,                         @D35IDAP 03055000
         TM    CHQPROC,CHQFILE    IS THIS CKD SYSFIL REQUEST   @D35IDAP 03056000
         BZ    PROCRDRF           NO, TEST WHETHER IT IS SYSIN @D35IDAP 03057000
         NI    CHQPROC,XFF-CHQFILE PREVENT ANOTHER DIB UPDATE  @D14BDAP 03058000
DIBUPDTE L     RD,INITBASE        SCHEDULER BASE ADDRESS       @D35IDAP 03059000
         USING SGSCHED,RD                                      @D35IDAP 03060000
         LR    RF,R5              LOAD COM REG ADDRESS         @D35IDAP 03061000
         BAL   R8,DIBENTRY        GET CORRECT DIB ENTRY ADDRESS         03062000
*SGLINK  DIBENTRY,INPUT=(R1,R8,RD,RF),OUTPUT=R5                @D369DAP 03063000
         USING DIB,R5             DECLARE BASE                          03064000
         DROP  RD                                              @D35IDAP 03065000
         CLI   PUBDEVTY,T3540     TEST FOR 3540 DEVICE         @DL29R01 03066000
         BE    DIBUP010           YES, CHECK OCHR FORMAT       @DL29R01 03067000
         LA    R8,CURAD+D6        ADDR. OF RECORD NO. IN DIB            03068000
         CLC   CURAD+D6(L1),MAXREC MAX. NO. OF RECORDS/TRACK            03069000
         BNE   DIBUP030           NO, UPDATE RECORD NO.                 03070000
         MVI   CURAD+D6,ONE       FIRST RECORD OF NEW TRACK             03071000
         BCTR  R8,R0              ADDR. OF TRACK NO. IN DIB             03072000
         CLC   CURAD+D5(L1),UL    DID WE REACH UPPER LIMIT              03073000
         BNE   DIBUP030           NO, UPDATE TRACK NUMBER               03074000
         MVC   CURAD+D5(L1),LL    POINT TO 1ST TRACK                    03075000
         LH    R8,CURAD+2         GET CURRENT CYLINDER NUMBER           03076000
         LA    R8,D1(R8)          INCREASE IT BY ONE                    03077000
         STH   R8,CURAD+2         STORE BACK NEW CYLINDER NO.           03078000
         B     DIBUPEND           UPDATE OF DIB COMPLETE       @D35IDAP 03079000
.*                                                                      03080000
.*       UPDATE CYL OR REC NUMBER FOR 3540 DIB                 @DL29R01 03081000
.*                                                                      03082000
         SPACE 1                                               @D35IDAP 03083000
DIBUP010 L     R8,8(R1)           GET CCW ADDRESS FROM CCB     @DL29R01 03084000
         LA    R8,0(R8)           CLEAR HIGH ORDER BYTE        @DL29R01 03085000
         TM    4(R8),CC           IS THIS CCW CHAINED          @DL29R01 03086000
         BNO   DIBUPEND           NO, MUST HAVE BEEN CONT CCW  @D35IDAP 03087000
         CLI   0(R8),SEEK         WAS FIRST CCW A SEEK         @DL29R01 03088000
         BE    DIBUP020           YES, CONTINUE                @DL29R01 03089000
         CLI   8(R8),SEEK         IS THE SECOND CCW A SEEK     @DL29R01 03090000
         BNE   DIBUPEND           NO BRANCH                    @D35IDAP 03091000
DIBUP020 LA    R8,CURAD+6         POINT TO REC IN DIB          @DL29R01 03092000
         CLC   CURAD+D6(1),MAXREC 26 REC PER TRACK             @DL29R01 03093000
         BNE   DIBUP030           INCREASE BY ONE              @DL29R01 03094000
         MVI   CURAD+D6,ONE       RESET REC NUMBER TO ONE      @DL29R01 03095000
         LA    R8,CURAD+4         POINT TO CYL NUMBER          @DL29R01 03096000
DIBUP030 IC    R5,0(R8)           HEAD OR RECORD NO. IN R5              03097000
         LA    R5,1(R5)           ADD 1 TO IT                           03098000
         STC   R5,0(R8)           AND STORE IT IN DIBENTRY              03099000
DIBUPEND LR    R5,RF              RESTORE COMREG POINTER       @D35IDAP 03100000
         SPACE 3                                                        03101000
***************************************************************@D37BDAP 03102000
*                                                              @D37BDAP 03103000
*        LOCATE SPECIAL SYSIN RECORDS AND SET INDICATORS       @D37BDAP 03104000
*                                                              @D37BDAP 03105000
***************************************************************@D37BDAP 03106000
         SPACE 3                                                        03107000
         USING COMREG,R5                                       @D35IDAP 03108000
PROCRDRF SR    R0,R0              FIRST BYTE IN DATA AREA      @D35IDAP 03109000
         CLI   CHQSLUB,SYSIPT     IS UNIT SYSRDR OR SYSIPT     @D36JDAP 03110000
         BE    PROCIPTF           YES, IT IS SYSIPT            @D62TAAP 03111000
         BH    CHNDPST            NO,                          @D35IDAP 03112000
PROCRDR  TM    INSIZE,RDR81T      81 BYTE SYSRDR               @D35IDAP 03113000
         BNO   PROCR020           NO                           @D35IDAP 03114000
         B     PROCR010           YES,                                  03115000
PROCIPTF TM    INSIZE,IPT81T      SYSIPT, IS IT 81 CLMN READER @D35IDAP 03116000
         BNO   PROCR020           NO                           @D35IDAP 03117000
PROCR010 AH    R0,H1              INCREASE OFFSET POINTER      @D35IDAP 03118000
PROCR020 L     R8,CSW             GET LAST CCW ADDRESS+8       @D35IDAP 03119000
         LA    R8,ZERO(R8)        CLEAR HIGH ORDER BYTE        @D35IDAP 03120000
         SH    R8,H8              REAL LAST CCW ADDRESS        @D35IDAP 03121000
         BM    CHNDPST            NO PRIMARY STATUS            @D37DDAP 03122000
         STNSM PSWMASK,XFF-SMDAT  SET OFF DAT-BIT              @D35IDAP 03123000
         TM    PBXFLAG,PBXDASD    IS THIS A DASD DEVICE        @D62TAAP 03124000
         BNO   PROCR050           NO,                          @D62TAAP 03125000
         CLI   PUBDEVTY,TFBA      IS THIS AN FBA DEVICE        @D62TAAP 03126000
         BE    PROCR070           YES,                         @D62TAAP 03127000
.* IT IS ASSUMED THAT THE COMPLETE TRANSLATED RPS CHANNEL      @DM09658 03128000
.* PROGRAM WILL FIT WITHIN ONE COPY-BLOCK (NOT OVER 7 CCW'S).  @DM09658 03129000
         CLI   0(R8),RDSECT       IS COMMAND A READ-SECTOR     @DM09658 03130000
         BNE   PROCR030           NO, IT IS A NORMAL READ      @D369DAP 03131000
         SH    R8,H8              BACKUP ONE CCW               @DM09658 03132000
PROCR030 CLI   0(R8),READSCNT     IS COMMAND A READ-COUNT      @D37DDAP 03133000
         BE    PROCR040           YES, BACK UP 1 MORE CCW      @D369DAP 03134000
         CLI   0(R8),READMCNT     IS THIS A M/T READ COUNT     @D37DDAP 03135000
         BNE   PROCR050           NO, THEN ASSUME A READ       @D369DAP 03136000
PROCR040 SH    R8,H8              ELSE BACKUP ONE MORE CCW     @DA13844 03137000
PROCR050 TM    PBXFLAG,PBXTAPE    IS THIS A TAPE DEVICE        @D62TAAP 03138000
         BNO   PROCR070           NO,                          @D62TAAP 03139000
         CLI   0(R8),READB        IS THIS A READ BACKWARDS     @DA30654 03140000
         BE    PROCR060           YES, ASSUME TAPE ERP REQUEST @DM16788 03141000
         TM    0(R8),CONTROL      IS THIS A CONTROL COMMAND    @DM16788 03142000
         BNO   PROCR070           NO, ASSUME REGULAR COMMAND   @DM16788 03143000
PROCR060 STOSM PSWMASK,SMDAT      SET DAT BIT ON               @D35IDAP 03144000
         B     CHNDPST            SKIP TEST, ASSUMING TAPE ERP @D35IDAP 03145000
PROCR070 STOSM PSWMASK,SMDAT      SET ON DAT BIT AGAIN         @D35IDAP 03146000
         BAL   R7,GETDADR         GET VIRTUAL ADDRESS                   03147000
*SGLINK  GETDADR,INPUT=(R0,R6,R7,R8),OUTPUT=RF                 @D369DAP 03148000
         CLI   0(RF),SLASH        IS THIS CHARACTER A /        @D35EDAP 03149000
         BNE   CHNDPST            NO,                          @D35IDAP 03150000
         LA    RF,2(,RF)          POINT TO THIRD BYTE          @D35EDAP 03151000
         CLI   0(RF),C' '         IS THIRD CHAR. BLANK         @D35EDAP 03152000
         BNE   CHNDPST            NO,                          @D35IDAP 03153000
         BCTR  RF,R0              POINT TO SECOND BYTE         @D35EDAP 03154000
         CLI   0(RF),ASTR         TEST FOR /*                  @D35EDAP 03155000
         BE    PROCR090           BRANCH ON END OF FILE        @D149DAP 03156000
         CLI   0(RF),AMPERS       TEST FOR /&                  @D149DAP 03157000
         BNE   CHNDPST            NO,                          @D35IDAP 03158000
         TM    INSIZE,CATALSA     /& ALLOWED ON SYSIPT         @D14BDAP 03159000
         BNO   PROCR080           NO, INDICATE EOJ             @D149DAP 03160000
         CLI   CHQSLUB,SYSIPT     WAS IT SYSIPT                @D36JDAP 03161000
         BE    CHNDPST            YES, SKIP POSTING            @D35IDAP 03162000
PROCR080 OI    PIBFLG2,JOBDUN     SET /& READ IN PIB           @D51IDAP 03163000
PROCR090 OI    CHQCCBB1,EOF       POST EOF IN CCB              @D149DAP 03164000
         OI    CHQCSW+D4,UNITEXCP POST UNIT EXCEPTION IN CCB   @D35IDAP 03165000
         CLI   PUBDEVTY,T2501     IS THIS A 2501               @DA11765 03166000
         BNE   CHNDPST            NO,                          @DA11765 03167000
         OI    PUBJCFLG,EOF2501   PREVENT FAST2501 REQUEST     @DA11765 03168000
         B     CHNDPST                                         @D35IDAP 03169000
         DROP  R5                 RELEASE COMREG POINTER       @D36IDAP 03170000
         DROP  RC                 RELEASE PROCSYSF BASE POINTER@D31YDAP 03171000
         DROP  R1                 RELEASE CCB BASE POINTER     @D31YDAP 03172000
         DROP  R2                 RELEASE PUBX BASE POINTER    @D62TAAP 03173000
         DROP  R3                 RELEASE PUB BASE POINTER     @D31YDAP 03174000
         DROP  R4                 RELEASE CHANQ BASE POINTER   @D31YDAP 03175000
         TITLE 'VSE SUPERVISOR     IOINTER     IGNORE ERROR PROCESSING' 03176000
         AIF   (NOT &BGDEBUG).NTXT110                          @D35IDAP 03177000
***************************************************************@D35IDAP 03178000
*                                                              @D35IDAP 03179000
*  FUNCTION:                                                   @D35IDAP 03180000
*            SET UP PUB ASSOCIATED I/O REGISTERS AND           @D35IDAP 03181000
*            EXECUTE APPENDAGE ROUTINE                         @D35IDAP 03182000
*  ENTRY POINT:                                   AT LABEL     @D35IDAP 03183000
*            1. ENTERED  FROM ATTENTION PROCESSOR    IGNORE    @D35IDAP 03184000
*            2. ENTERED  FROM ERPEXIT IN (SGERP)     IGNORE    @D35IDAP 03185000
*  EXITS:                                         TO LABEL     @D35IDAP 03186000
*            STATUS ANALYZER                         TSTPCI    @D35IDAP 03187000
*            DEQUEUE ROUTINE                         DEQUNCON  @D14BDAP 03188000
*  INPUTS:                                                     @D35IDAP 03189000
*            R2= ADDRESS OF PUBX                               @D35IDAP 03190000
*            R3= ADDRESS OF PUB                                @D35IDAP 03191000
*            R6= ADDRESS OF DISPATCHER                         @D35IDAP 03192000
*            R9= IOINTER BASE REGISTER                         @D35IDAP 03193000
*  OUTPUTS:                                                    @D35IDAP 03194000
*  REGISTERS:                                                  @D35IDAP 03195000
*            R1= ADDRESS OF CCB                                @D35IDAP 03196000
*            R2= ADDRESS OF PUBX                               @D35IDAP 03197000
*            R3= ADDRESS OF PUB                                @D35IDAP 03198000
*            R4= ADDRESS OF CHANQ ENTRY                        @D35IDAP 03199000
*            R5= ADDRESS OF COMREG                             @D35IDAP 03200000
*            R7= UNPREDICTABLE                                 @D35IDAP 03201000
*            R8= UNPREDICTABLE                                 @D35IDAP 03202000
*            RA= ADDRESS OF PIB                                @D35IDAP 03203000
*            RD= ADDRESS OF IGNORE ROUTINE                     @D35IDAP 03204000
*  SUBROUTINES:                                                @D35IDAP 03205000
*            CQDSP        -SET UP I/O REGISTERS                @D35IDAP 03206000
*            ........     -EXECUTE USERS APPENDAGE             @D35IDAP 03207000
*                                                              @D35IDAP 03208000
***************************************************************@D35IDAP 03209000
         SPACE 3                                               @D35IDAP 03210000
.NTXT110 ANOP                                                  @D35IDAP 03211000
         USING PUBADR,R3          PUB BASE POINTER             @D21IDAP 03212000
IGNORE   EQU   *                                               @D35IDAP 03213000
         BALR  RD,0               IGNORE ROUTINE BASE ADDRESS  @D31YDAP 03214000
         USING IGNORBEG,RD        IGNORE ROUTINE BASE POINTER  @D31YDAP 03215000
IGNORBEG BAL   R7,CQDSP           SET UP REGISTERS             @D34SDAP 03216000
*SGLINK  CQDSP,INPUT=(R3,R6,R7),OUTPUT=(R1,R4,RA),WORK=R0      @D369DAP 03217000
         BAL   RE,GETPUBX         GET PUB EXTENTION ADDRESS    @D61NDAP 03218000
*SGLINK  GETPUBX,INPUT=(R3,R9,RE),OUTPUT=R2                    @D61NDAP 03219000
         USING PBXADR,R2          PUBX BASE POINTER            @D61NDAP 03220000
         LTR   R4,R4              IS ENTRY STILL AVAILABLE     @D35IDAP 03221000
         BZ    TSTPCI             NO   ======================> @D35IDAP 03222000
         USING CHQADR,R4          CHANQ BASE POINTER           @D21IDAP 03223000
         USING CCBADR,R1          CCB   BASE POINTER           @D21IDAP 03224000
         LA    R4,0(,R4)          CLEAR CHAINING BYTE          @DAOM043 03225000
         TM    CHQPROC,CHQDQUNC   IS REQUEST TO BE DEQUEUED    @D14BDAP 03226000
         BO    DEQUNCON           YES, JUST DEQUEUE IT         @D14BDAP 03227000
         L     R5,CRADDR          RESTORE COMREG POINTER       @D35IDAP 03228000
         USING COMREG,R5          COMREG BASE POINTER          @D36IDAP 03229000
         TM    CHQCCBB3,APPEN     IS THERE AN APPENDAGE        @D35IDAP 03230000
         BZ    TSTPCI             NO,  ======================> @D35IDAP 03231000
         TM    CHQFLG1,CHQHQA     IS THIS A HEAD QUEUE REQUEST @D35IDAP 03232000
         BO    TSTPCI             YES, ======================> @D35IDAP 03233000
         TM    CHQGRP,CHQGRVTM    IS THIS A VTAM REQUEST       @D51GDAP 03234000
         BO    GOAPPVTM           YES                          @D51GDAP 03235000
         L     R8,APUBISVT        GET ADDRESS OF ROUTINE       @D31YDAP 03236000
         BALR  R7,R8              IS PUB OWNED BY VTAM         @D31YDAP 03237000
*SGLINK  PUBISVTM INPUT=(R3,R7,R8,R9),OUTPUT=RF                @D31YDAP 03238000
         BO    GOAPPVTM           YES                          @DA37770 03239000
         TM    POWFLG1,POWPART    IS IT POWER/VS PARTITION     @DA07881 03240000
         BNO   TSTPCI             NO   ======================> @D35IDAP 03241000
         CLI   CHNSTA,X'FE'       DEVICE NOT OPERATIONAL       @D21CDOW 03242000
         BNE   TSTPCI             YES, SKIP APPENDAGE          @DA15307 03243000
GOAPPIGN LA    R0,4               ENTERED FROM IGNORE EXIT     @D14CDFG 03244000
         DROP  RD                 RELEASE IGNORE BASE POINTER  @D62OSAP 03245000
GOAPPI10 L     RD,AGOAPPND        APPENDAGE ROUTINE ADDRESS    @D62OSAP 03246000
         BALR  RF,RD              EXECUTE USERS APPENDAGE      @D62OSAP 03247000
*SGLINK  GOAPPND,INPUT=(R0,R1,R3,R4,R5,R9,RD,RF),WORK=(R5,R7,R8) 62OSAP 03248000
         USING GOAPPI20,RF        GOAPPI20 BASE POINTER        @D62OSAP 03249000
GOAPPI20 MVC   CSW(4),APPCSW      RESTORE INITIAL CSW-WORD1    @DY46396 03250480
         CLC   APPCSW+4(4),CSW+4  DID APPENDAGE CHANGE CSW     @DY46396 03250670
         BNE   GOAPPI30           YES                          @DA31144 03251000
         MVC   CHQCCSIO,APPCCSIO  RESTORE FLAGS                @DA31144 03252000
         B     TSTPCI             DONE ======================> @DA31144 03253000
GOAPPI30 TM    DEVSTA,DEVEND+UNITCK USER FORCED ENDING STATUS  @D37BDAP 03254000
         BZ    TSTPCI             NO,  ======================> @D37BDAP 03255000
         OI    CHQCCSIO,CHQCCACT  INDICATE ENTRY IS ACTIVE     @D37BDAP 03256000
         MVC   CHQCSW+4(2),DEVSTA OVERRULE CSW INFO IN CHANQ   @D37BDAP 03257000
         B     TSTPCI                  ======================> @D35IDAP 03258000
         DROP  RF                 RELEASE GOAPPI20 BASE        @D62OSAP 03259000
         SPACE 2                                               @DA37770 03260000
         USING IGNORBEG,RD        IGNORE ROUTINE BASE POINTER  @D31YDAP 03261000
GOAPPVTM L     RF,PBXERBLK        ADDRESS OF ERROR ENTRY       @DA37770 03262000
         USING ERBLKADR,RF        ERROR ENTRY BASE POINTER     @DA37770 03263000
         TM    ERBLKFLG,ERACTIVE+ERQUEUED OTHER ERROR PRESENT  @DA37770 03264000
         BZ    GOAPPIGN           NO, FREE TO BE USED          @DA37770 03265000
         LA    R0,8               INDICATE MORE ERRORS QUEUED  @DA37770 03266000
         B     GOAPPI10           GOTO VTAM APPENDAGE          @DA37770 03267000
         DROP  RF                 RELEASE ERROR ENTRY BASE     @DA37770 03268000
         DROP  R4                 RELEASE CHANQ BASE POINTER   @D31YDAP 03269000
         DROP  R1                 RELEASE CCB BASE POINTER     @D31YDAP 03270000
         DROP  R2                 RELEASE PUBX BASE POINTER    @D62TAAP 03271000
         DROP  R3                 RELEASE PUB BASE POINTER     @D31YDAP 03272000
         DROP  R5                 RELEASE COMREG POINTER       @D36IDAP 03273000
         DROP  RD                 RELEASE IGNORE POINTER       @D36IDAP 03274000
         TITLE 'VSE SUPERVISOR     IOINTER     TAPE SPECIAL PROCESSING' 03275000
         AIF   (NOT &BGDEBUG).NTXT165                          @D21CDAP 03276000
***************************************************************@D21CDAP 03277000
*                                                              @D21CDAP 03278000
*   FUNCTION      THIS ROUTINE FORCES 'POST AT DEVICE END'     @D21CDAP 03279000
*                 FOR TAPE WRITE COMMANDS BEING THE LAST CCW   @D21CDAP 03280000
*                                                              @D21CDAP 03281000
*   CALLING SEQUENCE                                           @D21CDAP 03282000
*                                                              @D21CDAP 03283000
*                 BALR  RE,RD                                  @D219DAP 03284000
*                                                              @D21CDAP 03285000
*   REGISTER USAGE                                             @D21CDAP 03286000
*                                                              @D21CDAP 03287000
*                 REGISTER  1     CCB POINTER                  @D21CDAP 03288000
*                 REGISTER  3     PUB POINTER                  @D21CDAP 03289000
*                 REGISTER  4     CHANQ POINTER                @D21CDAP 03290000
*                 REGISTER  9     IOINTER BASE                 @D21CDAP 03291000
*                 REGISTER 13     SUBROUTINE BASE              @D21CDAP 03292000
*                 REGISTER 14     LINK REGISTER                @D21CDAP 03293000
*                                                              @D21CDAP 03294000
*   ENTERED FROM                                               @D21CDAP 03295000
*                                                              @D21CDAP 03296000
*                 PSTTAPE         TAPE PROCESSING ROUTINE      @D21CDAP 03297000
*                                                              @D21CDAP 03298000
***************************************************************@D21CDAP 03299000
         SPACE 3                                               @D21CDAP 03300000
         AGO   .YTXT165                                        @D21CDAP 03301000
.NTXT165 ANOP                                                  @D21CDAP 03302000
***************************************************************@D21CDAP 03303000
*                                                              @D21CDAP 03304000
*        FORCE POSTING AT DEVICE END FOR TAPE WRITE COMMANDS   @D21CDAP 03305000
*                                                              @D21CDAP 03306000
***************************************************************@D21CDAP 03307000
.YTXT165 ANOP                                                  @D21CDAP 03308000
         USING CHQADR,R4          CHANQ BASE POINTER           @D21CDAP 03309000
         USING CCBADR,R1          CCB   BASE POINTER           @D21CDAP 03310000
         USING PBXADR,R2          PUBX  BASE POINTER           @D62TAAP 03311000
         USING TTAPERTN,RD        SUBROUTINE BASE POINTER      @D21IDAP 03312000
TTAPERTN DS    0H'0'                                           @D21IDAP 03313000
         TM    DEVSTA,CHANEND     PRIMARY STATUS               @D21CDAP 03314000
         BZR   RE                 NO,  =====================>  @D21CDAP 03315000
         TM    DEVSTA,DEVEND      IS IT ENDING STATUS          @D21CDAP 03316000
         BOR   RE                 YES, =====================>  @D21CDAP 03317000
         LTR   R1,R1              IS CCB ADDRESS AVAILABLE     @D21CDAP 03318000
         BZR   RE                 NO,  =====================>  @D21CDAP 03319000
         SLR   RB,RB              CLEAR DC COMMAND POINTER     @D21CDAP 03320000
         ICM   R8,7,CCBCCW+1      BEGIN OF CCW CHAIN           @DA36623 03321000
         STNSM PSWMASK,XFF-SMDAT  SET OFF DAT-BIT              @D21CDAP 03322000
         BZ    TTAPE070           ADDRESS ZERO IS STRANGE      @D21CDAP 03323000
         LA    RF,256             SET LIMIT COUNTER            @D21CDAP 03324000
TTAPE010 BCT   RF,TTAPE020        PREVENT LOOP                 @D21CDAP 03325000
         CLI   *,0                FORCE CONDITION CODE UNEQUAL @D21CDAP 03326000
         B     TTAPE080           RESET DAT-BIT, FORCE POSTDE  @D21CDAP 03327000
TTAPE020 LA    R8,0(,R8)          CLEAR HIGH ORDER BYTE        @D21CDAP 03328000
         TM    0(R8),TIC          IS THIS A TIC                @D21CDAP 03329000
         BNO   TTAPE030           NO, CERTAINLY NOT            @D21CDAP 03330000
         TM    0(R8),X'0F'-TIC    SURE ITS A TIC               @D21CDAP 03331000
         BNZ   TTAPE030           NO, ITS NOT A TIC            @D21CDAP 03332000
         ICM   R8,7,1(R8)         GET TIC ADDRESS              @D21CDAP 03333000
         B     TTAPE010                                        @D21CDAP 03334000
TTAPE030 TM    4(R8),DC           IS THIS DATA CHAINING        @D21CDAP 03335000
         BO    TTAPE050           YES                          @D21CDAP 03336000
         TM    4(R8),CC           IS THIS COMMAND CHAINING     @D21CDAP 03337000
         BZ    TTAPE060           NO                           @D21CDAP 03338000
         SLR   RB,RB              RESET DC COMMAND POINTER     @D21CDAP 03339000
TTAPE040 LA    R8,8(,R8)          POINT TO NEXT CCW            @D21CDAP 03340000
         B     TTAPE010                                        @D21CDAP 03341000
TTAPE050 LTR   RB,RB              IN COMMAND CHAIN PROCESS     @D21CDAP 03342000
         BNZ   TTAPE040           YES                          @D21CDAP 03343000
         LR    RB,R8              SAVE COMMAND POINTER         @D21CDAP 03344000
         B     TTAPE040                                        @D21CDAP 03345000
TTAPE060 LTR   RB,RB              WAS LAST CCW DC CHAIN        @D21CDAP 03346000
         BZ    TTAPE070           NO,                          @D21CDAP 03347000
         LR    R8,RB              SET COMMAND CODE POINTER     @D21CDAP 03348000
TTAPE070 DS    0H'0'                                           @D21CDAP 03349000
         CLI   0(R8),WRITE        WAS LAST COMMAND A WRITE     @D21CDAP 03350000
TTAPE080 STOSM PSWMASK,SMDAT      SET DAT BIT ON               @D21CDAP 03351000
         BNER  RE                 NO,  =====================>  @D21CDAP 03352000
         OI    CHQCCBB1,POSTDE    FORCE POST AT DEVICE END     @D21CDAP 03353000
         B     INITRG             START======================> @D21CDAP 03354000
         DROP  R1                 RELEASE CCB BASE POINTER     @D21CDAP 03355000
         DROP  R2                 RELEASE PUBX BASE POINTER    @D62TAAP 03356000
         DROP  R4                 RELEASE CHANQ BASE POINTER   @D21CDAP 03357000
         DROP  RD                 RELEASE SUBROUTINE BASE      @D21CDAP 03358000
         TITLE 'VSE SUPERVISOR     IOINTER     ALLOCATE ERROR ENTRY   ' 03359000
         AIF   (NOT &BGDEBUG).NTXT140                          @D35IDAP 03360000
***************************************************************@D34SDAP 03361000
*                                                              @D34SDAP 03362000
*                                                              @D34SDAP 03363000
*   FUNCTION      THIS ROUTINE IS MAINLY USED TO SET UP AN     @D379DFG 03364000
*                 ERROR ENTRY, WHICH WILL BE FURTHER PROCESSED @D379DFG 03365000
*                 BY THE ERROR RECOVERY PROCEDURES.            @D34SDAP 03366000
*                 IF THE ENTRY IS ALREADY IN SOME ERROR CHAIN  @D379DFG 03367000
*                 DUE TO A PREVIOUS SOLICITED INTERRUPT WITH   @D379DFG 03368000
*                 UNIT CHECK, THE NEW ERROR IS COMBINED WITH   @D379DFG 03369000
*                 THE OLD ONE, IF ALSO SOLICITED, OR IGNORED   @D379DFG 03370000
*                 EXCEPT FOR CLEARING THE UC, IF UNSOLICITED.  @D379DFG 03371000
*                 IF A CCB IS AVAILABLE (NOT DEQUEUED) IT WILL @D34SDAP 03372000
*                 BE MODIFIED DUE TO USERS SPECIFICATIONS. IN  @D34SDAP 03373000
*                 ADDITION, PROVISIONS WILL BE MADE TO ALLOW   @D34SDAP 03374000
*                 THE FAILING I/O OPERATION TO BE RETRIED AT A @D34SDAP 03375000
*                 LATER POINT IN TIME.                         @D34SDAP 03376000
*                 THE FLAG BYTE IN THE PUB ENTRY OF THE FAILING@D34SDAP 03377000
*                 DEVICE WILL BE MODIFIED TO PREVENT ANY I/O   @D34SDAP 03378000
*                 OPERATION FOR THE TIME THE ERP IS RUNNING    @D34SDAP 03379000
*                                                              @D34SDAP 03380000
*   CALLING SEQUENCE                                           @D34SDAP 03381000
*                                                              @D34SDAP 03382000
*                 BAL   R7,INTEQSET                            @D34SDAP 03383000
*                                                              @D34SDAP 03384000
*   REGISTER USAGE                                             @D34SDAP 03385000
*                                                              @D34SDAP 03386000
*                 REGISTER  1     POINTER TO CCB               @D34SDAP 03387000
*                 REGISTER  2     ADDRESS OF ERROR ENTRY       @D34SDAP 03388000
*                 REGISTER  3     POINTER TO PUB               @D34SDAP 03389000
*                 REGISTER  4     CHANNEL QUEUE POINTER        @D379DFG 03390000
*                 REGISTER  5     ERBLOC POINTER               @D379DFG 03391000
*                 REGISTER  6     DISPATCHER BASE ADDRESS      @D149DAP 03392000
*                 REGISTER  7     USERS LINK REGISTER          @D34SDAP 03393000
*                 REGISTER  8     USED INTERNALLY              @D34SDAP 03394000
*                 REGISTER  9     BASE REGISTER                @D34SDAP 03395000
*                 REGISTER 14     USED INTERNALLY              @D34SDAP 03396000
*                                                              @D34SDAP 03397000
*   ENTERED FROM                                               @D34SDAP 03398000
*                                                              @D34SDAP 03399000
*                 INTERRUC        UNIT CHECK PROCESSOR         @D34SDAP 03400000
*                 INTERRCC        ABNORMAL CHANNEL STATUS PROC.@D34SDAP 03401000
*                                                              @D34SDAP 03402000
***************************************************************@D34SDAP 03403000
         SPACE 3                                               @D35IDAP 03404000
         AGO   .YTXT140                                        @D35IDAP 03405000
.NTXT140 ANOP                                                  @D35IDAP 03406000
***************************************************************@D35IDAP 03407000
*                                                              @D35IDAP 03408000
*        FIND AND FILL UP ERROR ENTRY                          @D379DFG 03409000
*                                                              @D35IDAP 03410000
***************************************************************@D35IDAP 03411000
         SPACE 2                                               @D34SDAP 03412000
.YTXT140 ANOP                                                  @D35IDAP 03413000
         USING CCBADR,R1          CCB BASE POINTER             @D37BDFG 03414000
         USING PUBADR,R3          PUB BASE POINTER             @D21IDAP 03415000
         USING CHQADR,R4          CHANQ BASE POINTER           @D37BDFG 03416000
         USING INTEQSET,RD        I/O ERROR ROUTINE BASE       @D3CADAP 03417000
*SGLINK  INTEQSET,ENTRY,INPUT=(R1,R3,R4,R6,R7,R9,RD),                  *03418000
               WORK=(R8,RE),OUTPUT=(R2,R5)                              03419000
INTEQSET DS    0H'0'              SET UP ERROR ENTRY           @D51GDAP 03420000
         L     R5,AERBLOC         ERBLOC BASE ADDRESS          @D51GDAP 03421000
         USING ERBLOC,R5          ERBLOC BASE POINTER          @D37BDFG 03422000
         BAL   RE,GETPUBX         GET PUB EXTENTION ADDRESS    @D51GDAP 03423000
*SGLINK  GETPUBX,INPUT=(R3,R9,RE),OUTPUT=R2                    @D51GDAP 03424000
         USING PBXADR,R2          PUBX BASE POINTER            @D51GDAP 03425000
         BAL   RE,RMSGETP2        GET ADDRESS OF PUB 2         @DA44277 03426000
*SGLINK  RMSGETP2,INPUT=(R3,R6,RE),OUTPUT=R8                   @DA44277 03427000
         USING PUB2ADR,R8         SET BASE                     @DA44277 03428000
         TM    P2TFLG1,P2TUNSOL+P2TECPT EXCEPTIONAL CONDITION  @DA44277 03429000
         BO    INTEQS10           YES,                         @DA44277 03430000
         DROP  R8                 RELEASE PUB2 BASE            @DA44277 03431000
         OC    CSW(8),CSW         IS THIS A DUMMY ERROR ENTRY  @D52VDAP 03432000
         BZ    INTEQS10           YES,                         @D52VDAP 03433000
         STM   R7,R8,INTEQS78     SAVE REGISTER                @D52VDAP 03434000
         L     RE,IRB$            ADDRESS OF IRB               @D51GDAP 03435000
         USING IRB,RE             IRB BASE POINTER             @D51GDAP 03436000
         SLR   R8,R8              CLEAR REGISTER               @D52VDAP 03437000
         ICM   R8,1,IRBLPUM       GET LAST PATH USED MASK      @SCSI    03438000
         BZ    INTEQS00           WE DON'T HAVE THAT INFO      @SCSI    03439000
         IC    R8,PREXTTAB(R8)    GET (OFFSET+1)*4             @D52VDAP 03440000
         SRL   R8,2               DIVIDE BY FOUR               @D52VDAP 03441000
         BCTR  R8,0               SUBTRACT ONE                 @D52VDAP 03442000
INTEQS00 IC    R8,PBXCHPID(R8)    PATH-ID OF LAST PATH USED    @SCSI    03443000
         STC   R8,PBXERRCP        SAVE CHPID IN PUBX           @D52BDAP 03444000
         TM    IRBSTB0,IRBL       EXTENDED STATUS PRESENT      @D51GDAP 03445000
         BZ    INTEQXXA           NO                           @D52VDAP 03446000
         TM    IRBSTB3,IRBSCSP    WAS STATUS PENDING           @D51GDAP 03447000
         BZ    INTEQXXA           NO                           @D52VDAP 03448000
         TM    IRBERW,IRBERW0P    PATH VERIFICATION REQUIRED   @D51GDAP 03449000
         BZ    INTEQXXA           NO,                          @D52VDAP 03450000
         OC    PBXPVPEN(1),IRBLPUM SAVE PATH ID                @D52VDAP 03451000
         OI    PBXFLAG3,PBXF3PVT  INDICATE PATH VERIFICATION   @D52VDAP 03452000
         OI    SNSFLAG,SNSPVWRK   DO PATH VERIFICATION         @D52VDAP 03453000
INTEQXXA LM    R7,R8,INTEQS78     RELOAD REGISTERS             @D52VVAP 03454000
         B     INTEQS10                                        @D52VDAP 03455000
         DROP  RE                 RELEASE IRB BASE             @D52VDAP 03456000
INTEQS78 DC    2F'0'              SAVE AREA                    @D52VDAP 03457000
         SPACE 3                                                        03458000
INTEQS10 TM    PBXFLAG,PBXTAPE    ERROR ON TAPE DEVICE         @D63ADAP 03459000
         BNO   INTEQS20           NO                           @D63ADAP 03460000
         ICM   RE,15,PBXCCW       ADDRESS OF PREFIX CCW'S      @D63ADAP 03461000
         BZ    INTEQS20           NOT YET ALLOCATED            @D63ADAP 03462000
         USING SMCCWADR,RE        SET-MODE-CCW BASE POINTER    @D63ADAP 03463000
         MVI   SMCCWSET,ZERO      FORCE A MODE SET ON NEXT SIO @D63ADAP 03464000
         DROP  RE                 RELEASE CCW STRING POINTER   @D63ADAP 03465000
INTEQS20 LTR   R1,R1              HAS TASK BEEN POSTED         @D63ADAP 03466000
         BZ    INTEQS30           YES,                         @D63ADAP 03467000
         CLC   CHQTID,IORQ4AR     SYSTEM TASK REQUEST          @D66ADAP 03468000
         BNL   INTEQS30           NO,                          @D63ADAP 03469000
         TM    CHQCCSIO,CHQCCACT+CHQCCNOP SOLICITED INTERRUPT  @DA44616 03470000
         BZ    INTEQS30           NO, USE NORMAL ERROR ENTRY   @D63ADAP 03471000
         DROP  R2                 RELEASE PUBX BASE            @D51GDAP 03472000
         LR    RE,R2              SAVE PUBX ADDRESS            @DA44052 03473000
         L     R2,TCBPTR          SYSTEM TASK TCB              @D37BDFG 03474000
         L     R2,TCBERBLK-TCBADR(R2) SYSTEM TASK ERROR BLOCK  @D37BDFG 03475000
         USING ERBLKADR,R2        ERROR ENTRY BASE POINTER     @DA44052 03476000
         TM    ERBLKFLG,ERACTIVE  ERROR ENTRY ALREADY ACTIVE   @DA44052 03477000
         BZ    INTEQS40           NO, WE WILL USE IT           @DA44052 03478000
         CLC   CHQTID,IORQ4PMR    IS IT A PMR REQUEST          @D66ADAP 03479000
         BE    INTEQS90           YES,                         @DA44841 03480000
         LR    R2,RE              RESTORE PUBX ADDRESS         @DA44052 03481000
         USING PBXADR,R2          PUBX BASE POINTER            @D51GDAP 03482000
INTEQS30 L     R2,PBXERBLK        ADDRESS OF ERROR ENTRY       @DA37187 03483000
         USING ERBLKADR,R2        ERROR ENTRY BASE POINTER     @D37BDFG 03484000
INTEQS40 TM    ERBLKFLG,ERACTIVE  ERROR ENTRY ALREADY ACTIVE   @D37BDFG 03485000
         BZ    INTEQUSE           NO, FREE TO BE USED          @D37BDFG 03486000
         TM    ERBLKFLG,ERQUEUED  ERROR ENTRY IN ANY QUEUE     @D37BDFG 03487000
         BZ    INTEQS70           NO, SKIP DEQUEUEING          @DA41206 03488000
         SLR   RE,RE              CLEAR REGISTER               @D14BDAP 03489000
         NI    ERBLKFL1,NEEDTASK  RESET UNUSED BITS            @D14BDAP 03490000
         IC    RE,ERBLKFL1        FLAG BYTE                    @D14BDAP 03491000
         IC    RE,ERCHNOFT(RE)    OFFSET TO ERROR CHAIN HEADER @D37BDFG 03492000
         LA    RE,ERCHNOFT(RE)    POINT TO ERROR CHAIN HEADER  @D37BDFG 03493000
         USING ERCHNADR,RE        DEVICE ERROR ENTRY BASE      @D37BDFG 03494000
INTEQS50 C     R2,ERCHNPTR        CURRENT ENTRY FOUND          @D37BDFG 03495000
         BE    INTEQS60           YES,                         @D37BDFG 03496000
         L     RE,ERCHNPTR        NEXT ENTRY IN CHAIN          @D37BDFG 03497000
         B     INTEQS50           FIND MATCHING ENTRY          @D37BDFG 03498000
INTEQS60 MVC   ERCHNPTR,ERBLKPTR  DEQUEUE ENTRY FROM CHAIN     @D37BDFG 03499000
         NI    ERBLKFLG,XFF-ERQUEUED RESET QUEUED IN ERROR     @D37BDFG 03500000
         DROP  RE                 RELEASE ERROR ENTRY BASE     @D37BDFG 03501000
         TM    PUBCSFLG,QEDERR    IS DEVICE QUEUED IN ERROR    @DA42411 03502000
         BZ    INTEQUSE           NO, REUSE THE ENTRY          @DA42411 03503000
INTEQS70 CLI   ERRQMSG,RMSAE      IS IT AN ALTERNATE ENTRY     @D37BDFG 03504000
         BE    INTEQUSE           YES, REUSE ENTRY             @D37BDFG 03505000
         CLI   ERRQCSW+5,X'FE'    IS DEVICE NOT OPERATIONAL    @DA33314 03506000
         BE    INTEQUSE           YES, REUSE ENTRY             @DA33314 03507000
         TM    ERRQFLG,RECONLY    RECORDING REQUEST            @DA33314 03508000
         BO    INTEQUSE           YES, REUSE ENTRY             @DA33314 03509000
         OC    ERRQCHQA(4),ERRQCHQA UNSOLICITED INTERRUPT      @D51GDAP 03510000
         BZ    INTEQUSE           YES, NEW COULD BE SOLICITED  @D51GDAP 03511000
         TM    ERBLKFL1,NEEDRAS   USED BY RAS                  @DA41206 03512000
         BO    INTEQUSE           YES, REUSE ENTRY             @DA41206 03513000
         TM    ERBLKFL1,NEEDSNS   IS SENSE OUTSTANDING         @DA33314 03514000
         BZ    INTEQS80           NO,  DECREMENT ERROR COUNT   @D21WDAP 03515000
         OC    ERRQCSW,CSW        COMBINE STATUS               @D37BDFG 03516000
         NI    ERBLKFL1,XFF-NEEDSNS RESET SENSE OUTSTANDING    @D14BDAP 03517000
INTEQS80 OI    ERRQPRFL,ERRQPSOV  MORE THAN ONE UNIT CHECK     @DA37004 03518000
         SR    R8,R8              INDICATE ENTRY WAS IN USE    @DA37004 03519000
         BR    R7                      ======================> @D21WDAP 03520000
.*                                                                      03521000
.*       ALLOCATE ERROR ENTRY FROM WITHIN THE DEVCB CONTROL             03522000
.*                                                                      03523000
INTEQS90 DS    0H'0'                                           @DA44841 03524000
         STM   R7,R8,INTEQS78     SAVE REGISTER CONTENTS       @DA44841 03525000
         L     R2,IJBPMCOM        PMCOM BASE ADDRESS           @DA44841 03526000
         USING PMCOM,R2           PMCOM BASE POINTER           @DA44841 03527000
         LH    R8,DEVCBNUM        GET LOOP COUNT               @DA44841 03528000
         L     R2,ADEVCB          DEVCB BASE ADDRESS           @DA44841 03529000
         USING DEVCB,R2           DEVCB BASE POINTER           @DA44841 03530000
INTEQS95 L     R7,DEVDPD          DPDENTR BASE ADDRESS         @DA44841 03531000
         USING DPDENTR,R7         DPDENTR BASE POINTER         @DA44841 03532000
         CLC   PUBCHANN(2),DPDUNT IS THIS THE RIGHT DEVCB      @DA44841 03533000
         BE    INTEQSA0           YES, THEN USE IT             @DA44841 03534000
         L     R2,DEVCBNXT        ADDRESS OF NEXT DEVICE BLOCK @DA44841 03535000
         BCT   R8,INTEQS95        BRANCH IF NOT YET DONE       @DA44841 03536000
         TM    IJBFLG08,IJBDEBUG  IS DEBUG ACTIVE              @DA44841 03537000
         BZ    INTEQSB0           NO,                          @DA44841 03538000
         BAL   R5,SYSERROR        NO MATCHING DEVCB FOUND      @DA44841 03539000
INTEQSA0 TM    DEVSTAT,DEVSTRT    IS A PAGE REQUEST ONGOING    @DA44841 03540000
         BO    INTEQSA5           YES,                         @DA44841 03541000
         TM    IJBFLG08,IJBDEBUG  IS DEBUG ACTIVE              @DA44841 03542000
         BZ    INTEQSA5           NO, USE IT ANYWAY            @DA44841 03543000
         BAL   R5,SYSERROR        NONE DEVICE WAS ACTIVE       @DA44841 03544000
INTEQSA5 LA    R2,DEVERBLK        ADDRESS OF ERROR ENTRY       @DA44841 03545000
         DROP  R2                 RELEASE DEVCB BASE           @DA44841 03546000
         LM    R7,R8,INTEQS78     RELOAD REGISTER CONTENTS     @DA44841 03547000
         DROP  R7                 RELEASE DPDENTRY BASE        @DA44841 03548000
         B     INTEQS40           JOIN MAIN LINE               @DA44841 03549000
INTEQSB0 LM    R7,R8,INTEQS78     RELOAD REGISTER CONTENTS     @DA44841 03550000
         B     INTEQS30           JOIN MAIN LINE NORMAL        @DA44841 03551000
         SPACE 3                                                        03552000
         TITLE 'VSE SUPERVISOR     IOINTER     SET UP ERROR ENTRY     ' 03553000
         USING ERBLKADR,R2        ERBLKADR BASE POINTER        @DA44841 03554000
INTEQUSE TM    ERBLKFLG,ERACTIVE  ERROR ENTRY ACTIVE           @DA43415 03555000
         BZ    INTEQU00           NO                           @DA43415 03556000
         ICM   RE,15,ERRQCHQA     IS CHANQ ENTRY PRESENT       @DA43415 03557000
         BZ    INTEQU00           NO                           @DA43415 03558000
         NI    CHQFLG1-CHQADR(RE),XFF-CHQCSBSY-CHQCSQED RESET  @DA43415 03559000
INTEQU00 NI    ERBLKFLG,XFF-ERSNSDAV SENSE DATA NOT YET AVAIL. @D61NDAP 03560000
         NI    ERBLKFL1,XFF-NEEDSNS-NEEDDSK-NEEDERP-NEEDRAS    @D14BDAP 03561000
         SLR   RE,RE              CLEAR REGISTER               @D37BDFG 03562000
         IC    RE,ERBLKSNL        LENGTH OF SENSE DATA         @D37BDFG 03563000
         LA    RE,ERRQSNS-ERRQCHQA(RE) ADD VARIABLE LENGTH     @D51GDAP 03564000
         BCTR  RE,0               ADJUST FOR EXECUTE           @D37BDFG 03565000
         EX    RE,INTEQCLR        CLEAR ERROR ENTRY            @D37BDFG 03566000
         LR    R8,R2              SAVE ERROR ENTRY ADDRESS     @D52VDAP 03567000
         BAL   RE,GETPUBX         GET PUB EXTENTION ADDRESS    @D62TAAP 03568000
*SGLINK  GETPUBX,INPUT=(R3,R9,RE),OUTPUT=R2                    @D62TAAP 03569000
         LR    RE,R2              PUBX BASE ADDRESS            @D52VDAP 03570000
         LR    R2,R8              RESTORE ERROR ENTRY ADDRESS  @D52VDAP 03571000
         USING PBXADR,RE          PUBX BASE POINTER            @D62TAAP 03572000
         TM    DEVSTA,UNITCK      IS IT UNIT CHECK             @D37BDFG 03573000
         BZ    INTEQU10           NO, PASS TO ERP              @D37BDFG 03574000
         TM    PBXFLAG,PBXDASD    ERROR ON DASD DEVICE         @D62TAAP 03575000
         BO    INTEQU20           YES, PASS TO DISK ERP        @D62TAAP 03576000
INTEQU10 OI    ERBLKFL1,NEEDERP   PASS TO ERP                  @D14BDAP 03577000
         B     INTEQU30           SKIP                         @D37BDFG 03578000
INTEQU20 OI    ERBLKFL1,NEEDDSK   PASS TO DISK ERP             @D14BDAP 03579000
INTEQU30 OI    ERBLKFLG,ERACTIVE  MARK ENTRY AS ACTIVE         @D14BDAP 03580000
         MVC   ERRQCSW,CSW        SAVE FAILING CSW             @D37BDFG 03581000
         STH   R3,ERRQPUB         SAVE PUB POINTER             @D34SDAP 03582000
         MVC   ERRQPATH(1),PBXERRCP SAVE FAILING PATH          @D62TAAP 03583000
INTEQU40 MVI   ERRQCQPT,NOTQUED   DEFAULT TO UNSOLICITED       @D37BDFG 03584000
         AIF   (&AG1 LE 254).NMDV150                           @D51ODGL 03585000
         L     R8,ERQCHQA1        GET ERBLOC CHANQ FOR LATER   @D51ODGL 03586000
         TM    IJBFLG08,IJBDEBUG  IS DEBUG ACTIVE              @D61NDAP 03587000
         BZ    INTEQU50           NO,                          @D61NDAP 03588000
         LTR   R8,R8              IS HIGH ORDER BYTE ZERO      @D51ODGL 03589000
         BNM   INTEQU50           NO, THEN ITS O.K.            @D51ODGL 03590000
         BAL   R5,SYSERROR                                     @D51ODGL 03591000
.NMDV150 ANOP                                                           03592000
INTEQU50 LA    R4,0(,R4)          CLEAR HIGH ORDER BYTE        @D51ODGL 03593000
         ST    R4,ERRQCHQA        SAVE ADDRESS OF CHANQ ENTRY  @D51GDAP 03594000
         LTR   R4,R4              CHANQ ENTRY AVAILABLE        @D37BDFG 03595000
         BZ    INTEQU90           NO, UNSOLICITED INTERRUPT    @D37BDFG 03596000
         TM    CHQCCBB3,APPEN     CHANNEL END APPENDAGE PRESENT@D14CDOW 03597000
         BO    INTEQU60           YES, TREAT AS SOLICITED      @D14CDOW 03598000
         TM    CHQCCSIO,CHQCCACT+CHQCCNOP SOLICITED INTERRUPT  @D21CDOW 03599000
         BZ    INTEQU70           NO, IT IS UNSOLICITED        @DA37004 03600000
         TM    ERQFLG1,OCCUP      ERBLOC CURRENTLY IN USE      @D14CDOW 03601000
         BZ    INTEQU60           NO, INTERRUPT IS SOLICETED   @D14CDOW 03602000
         CLI   PUBDEVTY,T3480     IS THIS A 3480/3490 DEVICE   @D61NDAP 03603000
         BE    INTEQU60           YES, INTERRUPT IS SOLICITED  @D61NDAP 03604000
         CLI   PUBDEVTY,TTPA      IS THIS A TPA DEVICE         @D62TAAP 03605000
         BE    INTEQU60           YES, INTERRUPT IS SOLICITED  @D62TAAP 03606000
         AIF   (&AG1 LE 254).NMDV160                           @D51ODGL 03607000
         CR    R8,R4              ERBLOC CHANQ ADDRESS IS SAME @D51ODGL 03608000
.NMDV160 AIF   (&AG1 GT 254).YMDV160                           @D51ODGL 03609000
         CLC   PUBCHQPT,ERQCCB1   CHANQ ENTRY PROCESSED BY ERP @D14CDOW 03610000
.YMDV160 ANOP                                                  @D51ODGL 03611000
         BNE   INTEQU60           NO, HANDLE AS SOLICITED      @DA33314 03612000
         TM    ERQFLG1,RECONLY    ERBLOC ENTRY IS RECORDING    @DA33314 03613000
         BZ    INTEQU70           NO, NEW ERROR IS UNSOLICITED @DA37004 03614000
.*                                                             @D51ODGL 03615000
.*       ERROR TO BE HANDLED AS SOLICITED ERROR                @D51GDAP 03616000
.*                                                             @D51ODGL 03617000
INTEQU60 DS    0H                                              @D51ODGL 03618000
         TM    CHQGRP,CHQGRVTM    IS THIS A VTAM ENTRY         @DY46396 03618170
         BZ    INTEQU65           NO, PROCEED MAIN LINE        @DY46396 03618240
         TM    CCBCLS,CCBCOPY     IS THIS A COPIED CCB         @DY46396 03618310
         BZ    INTEQU65           NO,                          @DY46396 03618380
         LM    R0,R1,VTM31SAV     GET CSWCCW AND VTAM-CCB ADDR.@DY46396 03618540
         ST    R0,CSW             SAVE CCW ADDRESS IN CSW      @DY46396 03618580
         ST    R0,ERRQCSW         SAVE CCW IN ERRQ FOR VTAM TR.@DY46396 03618620
INTEQU65 DS    0H'0'                                           @DY46396 03618790
         AIF   (&AG1 GT 254).YMDV170                           @D51ODGL 03619000
         ICM   R1,8,PUBCHQPT      GET CHANNEL QUEUE INDEX      @D37BDFG 03620000
         ST    R1,ERRQCCB+(ERRQCQPT-ERRQCCB) SAVE INDEX+CCB ADR@D149DAP 03621980
         ICM   R1,8,H0            CLEAR HIGH ORDER BYTE        @DY46396 03622470
.YMDV170 AIF   (&AG1 LE 254).NMDV170                           @D51ODGL 03623000
         ICM   R1,8,H0            CLEAR HIGH ORDER BYTE        @DY46396 03624490
         ST    R1,ERRQCCB         SAVE CCB POINTER             @D149DAP 03625000
.NMDV170 ANOP                                                  @D51ODGL 03626000
         B     INTEQU90                                        @D61NDAP 03627000
.*                                                             @D51ODGL 03628000
.*       ERROR TO BE HANDLED AS UNSOLICITED ERROR              @D51GDAP 03629000
.*                                                             @D51ODGL 03630000
INTEQU70 XC    ERRQCHQA(4),ERRQCHQA DONT PASS CHANQ ENTRY      @D61NDAP 03631000
INTEQU80 CLC   CHQTID,IORQ4DSK    RECOVERY IN PROCESS          @D66ADAP 03632000
         BNH   INTEQUA0           YES, DONT GATE THE PUB       @D21BMAP 03633000
         CLC   CHQTID,IORQ4ERP    RECOVERY IN PROCESS          @D66ADAP 03634000
         BE    INTEQUA0           YES, DONT GATE THE PUB       @DA39133 03635000
INTEQU90 OI    PUBCSFLG,QEDERR    PREVENT FURTHER I/O          @D34SDAP 03636000
INTEQUA0 MVI   ERRQFLG,IGNERR+OCCUP INDICATE NO RETRY POSSIBLE @D21BMAP 03637000
         OC    ERRQCHQA(4),ERRQCHQA IS IT SOLICITED I/O ERROR  @DA44241 03638000
         BZ    INTEQUXT           NO, IT IS UNSOLICITED ERROR  @DA44241 03639000
         LTR   R1,R1              CCB STILL AVAILABLE          @DA33589 03640000
         BZ    INTEQUXT           NO, CANT RETRY               @DA37004 03641000
         SLR   R8,R8              CLEAR CCW POINTER            @D36IDAP 03642000
         ICM   R8,7,CSW+1         GET FAILING CCW ADDRESS+8    @D34SDAP 03643490
         AIF   (NOT &VTAM31).NV31060                           @DY46396 03644190
         BNZ   INTEQUB0           ADDRESS IS PROVIDED          @DY46396 03644380
         TM    CHQGRP,CHQGRVTM    IS THIS A VTAM ENTRY         @DY46396 03644570
         BZ    INTEQUE0           NO, ERR. AFTER CHAN.END      @D21CDOW 03644760
         TM    CCBBY3,CCWABOVE    IS THIS A COPIED CCB         @DY46396 03644950
.NV31060 ANOP                                                  @DY46396 03645140
         BZ    INTEQUE0           NO, ERR. AFTER CHAN.END      @D21CDOW 03645330
INTEQUB0 MVI   ERRQFLG,RTYERR+OCCUP INDICATE RETRY IS POSSIBLE @D35IDAP 03645520
         TM    CHQCCBB2,CHNBIT    FAILING CCW RETRY WANTED     @D35IDAP 03646000
         BZ    INTEQUE0           NO, DONT UPDATE USERS CCB    @D21CDOW 03647000
         TM    CHQDEV,CHQDASD+CHQFBA IS THIS A DISK REQUEST    @DA14881 03648000
         BNZ   INTEQUE0           YES, DONT UPDATE CCW ADDRESS @D21CDOW 03649000
         CLI   PUBDEVTY,TTPA      IS THIS A TPA DEVICE         @D62TAAP 03650000
         BE    INTEQUE0           YES, DONT UPDATE CCW ADDRESS @D62TAAP 03651000
         CLI   PUBDEVTY,T3480     IS THIS A 3480 TYPE TAPE     @D21CDOW 03652000
         BE    INTEQUE0           YES, DONT UPDATE CCW ADDRESS @D21CDOW 03653000
         CLI   PUBDEVTY,T4248     IS THIS A 4248 PRINTER       @D21CDOW 03654000
         BE    INTEQUE0           YES, DONT UPDATE CCW ADDRESS @D21CDOW 03655000
.*                                                             @D35IDAP 03656000
.*       COMMAND RETRY IS NOT COVERED PROPERLY FOR SYSTEM TASK @D35IDAP 03657000
.*       REQUEST, SINCE SYSTEM TASK DONT USE COMMAND RETRY     @D35IDAP 03658000
.*                                                             @D35IDAP 03659000
         SH    R8,H8              POINT BACK TO FAILING CCW    @D34SDAP 03660000
         C     R8,DASDCPT         IS IT A SUPVR-CHAINED CCW    @D14BDAP 03661000
         BL    INTEQUD5           NO, THEN MODIFY CCW IN CCB   @D14BDAP 03662000
         C     R8,DASDCPTA        IS IT A SUPVR-CHAINED CCW    @D14BDAP 03663000
         BNH   INTEQUE0           YES, THEN DONT CHANGE CCBCCW @D21IDAP 03664000
INTEQUD5 STCM  R8,M7,CCBCCW+1     UPDATE CCW ADDRESS IN CCB    @D34SDAP 03665000
.*                                                             @D35IDAP 03666000
.*  INVALIDATE REPLICA IN CASE ERP CHANGES CCW ADDRESS IN CCB  @D219DAP 03667000
.*                                                             @D35IDAP 03668000
INTEQUE0 DS    0H'0'              ENTRY POINT                  @D34SDAP 03669000
         TM    CHQCCBB3,USENSE    DOES USER WANT SENSE         @D35IDAP 03670000
         BZ    INTEQUF0           NO,                          @D35IDAP 03671000
         TM    CCBCLS,CCBIORB     IS THIS AN IORB              @D35IDAP 03672000
         BO    INTEQUF0           YES, CANT BE USER SENSE      @DA44172 03673000
         CLC   CHQTID,IORQ4SVT    IS THIS THE SERVICE TASK     @D66ADAP 03674000
         BNE   INTEQUF5           NO, THEN MUST POST ERROR     @DA44172 03675000
INTEQUF0 TM    CHQCCBB1,OWNUCRT   DOES USER HANDLE ERROR       @D35IDAP 03676000
         BZ    INTEQUXT           NO,                          @DA37004 03677000
INTEQUF5 OI    ERRQFLG,RECONLY    REQUEST RECORDING ONLY       @D62TAAP 03678000
         OI    CHQCCBB1,DISERR    POST DISASTER ERROR IN CCB   @D35IDAP 03679000
INTEQUXT LR    R8,R2              COPY ERROR ENTRY ADDRESS     @DA37004 03680000
         BR    R7                      ======================> @D35IDAP 03681000
INTEQCLR XC    ERRQCHQA(0),ERRQCHQA CLEAR VARIABLE FIELDS      @D51GDAP 03682000
         SPACE 1                                                        03682500
         DROP  R1                 RELEASE CCB BASE             @D37BDAP 03683000
         DROP  R3                 RELEASE PUB BASE             @D21IDAP 03684000
         DROP  R4                 RELEASE CHANQ BASE           @D37BDFG 03685000
         DROP  R5                 RELEASE ERBLOC BASE          @D37BDFG 03686000
         DROP  RE                 RELEASE PUBX   BASE          @D62TAAP 03687000
         SPACE 2                                               @D37BDFG 03688000
         TITLE 'VSE SUPERVISOR     IOINTER     POST ERROR ENTRY       ' 03689000
         AIF   (NOT &BGDEBUG).NTXT150                          @D37BDFG 03690000
***************************************************************@D379DFG 03691000
*                                                              @D379DFG 03692000
*   FUNCTION      THIS ROUTINE INSERTS AN ERROR ENTRY          @D379DFG 03693000
*                 INTO THE SNS, DSK, ERP OR RAS ERROR CHAIN.   @D379DFG 03694000
*                 THE CORRESPONDING SYSTEM TASKIS ACTIVATED.   @D379DFG 03695000
*                 SYSTEM TASK ERROR BLOCKS ARE ENQUEUED IN     @D379DFG 03696000
*                 LIFO NORMAL DEVICE ENTRIES IN FIFO ORDER.    @D379DFG 03697000
*                                                              @D379DFG 03698000
*   CALLING SEQUENCE                                           @D379DFG 03699000
*                                                              @D379DFG 03700000
*                 BAL   R7,INTEQPST                            @D379DFG 03701000
*                                                              @D379DFG 03702000
*   REGISTER USAGE                                             @D379DFG 03703000
*                                                              @D379DFG 03704000
*                 REGISTER  2     ADDRESS OF ERROR BLOCK       @D379DFG 03705000
*                 REGISTER  5     ERBLOC POINTER               @D379DFG 03706000
*                 REGISTER  7     USERS LINK REGISTER          @D379DFG 03707000
*                 REGISTER  8     TIB POINTER                  @D379DFG 03708000
*                 REGISTER  9     BASE REGISTER                @D379DFG 03709000
*                 REGISTER 14     USED INTERNALLY              @D379DFG 03710000
*                                                              @D379DFG 03711000
*   ENTERED FROM                                               @D379DFG 03712000
*                                                              @D379DFG 03713000
*                 INTERRUC        UNIT CHECK PROCESSOR         @D379DFG 03714000
*                 INTEEXIT        EXIT FROM I/O ERROR PROC.    @D379DFG 03715000
*                 ERPEXIT         EXIT FROM DISK ERP PROC.     @D379DFG 03716000
*                 PROCAPP         APPENDAGE PROCESSOR          @D379DFG 03717000
*                                                              @D379DFG 03718000
***************************************************************@D379DFG 03719000
         SPACE 3                                               @D35IDAP 03720000
         AGO   .YTXT150                                        @D37BDFG 03721000
.NTXT150 ANOP                                                  @D37BDFG 03722000
***************************************************************@D379DFG 03723000
*                                                              @D379DFG 03724000
*        ENQUEUE ERROR BLOCK AND POST SYSTEM TASK              @D379DFG 03725000
*                                                              @D379DFG 03726000
***************************************************************@D379DFG 03727000
         SPACE 2                                               @D37BDFG 03728000
.YTXT150 ANOP                                                  @D37BDFG 03729000
*SGLINK  INTEQPST,ENTRY,INPUT=(R2,R7,RD),WORK=(R5,R8,RE)       @D149DAP 03730000
INTEQPST TM    ERBLKFLG,ERQUEUED  ERROR ENTRY ALREADY QUEUED   @D37BDFG 03731000
         BOR   R7                 YES, ======================> @D37BDFG 03732000
         XC    ERBLKPTR,ERBLKPTR  CLEAR ERROR CHAIN POINTER    @D37BDFG 03733000
         NI    ERBLKFL1,NEEDTASK  RESET UNUSED BITS            @D14BDAP 03734000
         BZ    INTEQP60           DEACTIVATE ENTRY AND RETURN  @D14BDAP 03735000
         SLR   RE,RE              CLEAR REGISTER               @D14BDAP 03736000
         IC    RE,ERBLKFL1        GET TASK FLAGS               @D14BDAP 03737000
         L     R5,AERBLOC         POINT TO ERBLOC              @D37BDFG 03738000
         USING ERBLOC,R5                                       @D37BDFG 03739000
         IC    RE,ERCHNOFT(RE)    GET OFFSET OF CHAIN HEADER   @D37BDFG 03740000
         LA    RE,ERCHNOFT(RE)    POINT TO CHAIN HEADER        @D37BDFG 03741000
         USING ERCHNADR,RE                                     @D37BDFG 03742000
         L     R8,ERCHNTIB        TIB OF OWNING SYSTEM TASK    @D37BDFG 03743000
         TM    ERBLKFLG,HQERBLK   HEAD QUEUE ERROR BLOCK       @D37BDFG 03744000
         BZ    INTEQP10           NO,                          @D37BDFG 03745000
         MVC   ERBLKPTR,ERCHNPTR  ENQUEUE ON TOP OF CHAIN      @D37BDFG 03746000
         B     INTEQP20                                        @D37BDFG 03747000
INTEQP10 CLC   ERCHNPTR,F0        END OF CHAIN                 @D37BDFG 03748000
         BE    INTEQP20           YES,                         @D37BDFG 03749000
         L     RE,ERCHNPTR        POINT TO NEXT IN CHAIN       @D37BDFG 03750000
         B     INTEQP10           CHECK AGAIN                  @D37BDFG 03751000
INTEQP20 ST    R2,ERCHNPTR        INSERT NEW ERROR IN CHAIN    @D37BDFG 03752000
         OI    ERBLKFLG,ERQUEUED+ERACTIVE  MARK AS ENQUEUED    @D37BDFG 03753000
         TM    ERBLKFL1,NEEDERP   IS ERP-TASK TO BE POSTED     @D61NDAP 03754000
         BZ    INTEQP30           NO, ITS THE DSK-TASK         @D61NDAP 03755000
         CLI   TIBRQID-TIBADR(R8),WAITBND IS ERP-TASK WAITBOUND@D61NDAP 03756000
         BE    INTEQP40           YES,                         @D61NDAP 03757000
INTEQP30 CLI   TIBRQID-TIBADR(R8),SYSBND       ALREADY ACTIVE  @D14BDFR 03758000
         BNER  R7                 YES, ======================> @D37BDFG 03759000
         LR    R5,RF              SAVE CONTENTS OF RF          @D61NDAP 03760000
         B     INTEQP50                                        @D61NDAP 03761000
         DROP  R5                 RELEASE ERBLOC BASE          @D61CDAP 03762000
         DROP  RE                 RELEASE ERROR CHAIN BASE     @D37BDFG 03763000
INTEQP40 LR    R5,RF              SAVE CONTENTS OF RF          @D61NDAP 03764000
         L     RF,ERPBASE         GET ERP BASE ADDRESS         @D61NDAP 03765000
         USING SGERP,RF           SGERP BASE POINTER           @D61NDAP 03766000
         OI    SGERPECB+2,TRABIT  POST ERP MASTER ECB          @D61NDAP 03767000
         DROP  RF                 RELEASE SGERP BASE POINTER   @D61NDAP 03768000
INTEQP50 BAL   RF,POST            SET TASK READY               @D61NDAP 03769000
*SGLINK  POST,INPUT=(R6,R8,RF),WORK=RE                         @D14CDAP 03770000
         LR    RF,R5              RESTORE RF                   @D61NDAP 03771000
         L     R5,AERBLOC         POINT TO ERBLOC              @D37BDFG 03772000
         USING ERBLOC,R5                                       @D37BDFG 03773000
         BR    R7                      ======================> @D37BDFG 03774000
INTEQP60 NI    ERBLKFLG,XFF-ERACTIVE  FREE ERROR ENTRY         @D14BDAP 03775000
         BR    R7                      ======================> @D14BDAP 03776000
         SPACE 2                                               @D37BDFG 03777000
.*   PART MOVED DUE TO ADDRESSABILITY PROBLEMS                 @D21LDAP 03778000
         USING PUBADR,R3          SET BASE FOR PUB             @D21LDAP 03779000
         USING CHQADR,R4          SET BASE FOR CHANNEL QUEUE   @D21LDAP 03780000
INTSDAID TM    ERBLKFLG,ERSNSDAV  SENSE DATA ALREADY AVAILABLE @D37BDFG 03781000
         BO    INTSDA10           IF YES, DO NOT OVERWRITE     @D37BDFG 03782000
         IC    R7,ERBLKSNL        GET LENGTH OF SENSE DATA     @D37BDFG 03783000
         STC   R7,ERRQLSNS        USE ALLOCATED SENSE BYTES    @D21IDAP 03784000
         BCTR  R7,0               MINUS ONE                    @D37BDFG 03785000
         EX    R7,INTSDMOV        MOVE PROVIDED SENSE TO ENRTY @D37BDFG 03786000
         MVI   SNSSDAID,XFF       SET SDAID SENSE INVALID      @D35IDAP 03787000
         MVC   ERRQSEK(L4),SNSSDSEK SAVE HOME ADDRESS          @D35ADAP 03788000
         B     INTGSNS            CONTINUE UNIT CHECK PROCESS. @D35ADAP 03789000
INTSDA10 MVI   SNSSDAID,XFF       SET SDAID SENSE INVALID      @D37BDFG 03790000
         B     INTGSNS            CONTINUE UNIT CHECK PROCESS  @D61NDAP 03791000
INTSDMOV MVC   ERRQSNS(0),SNSSDAID  LENGTH FROM EX INSTRUCTION @D37BDFG 03792000
         DROP  R5                 RELEASE ERROR BLOCK POINTER  @D35IDAP 03793000
         SPACE 3                                               @D369DAP 03794000
INTTPERR TM    PUBCSFLG,QEDERR    IS AN ERROR ALREADY QUEUED   @D34SDAP 03795000
         BO    INTEUC10           YES, JUST CLEAR UNIT CHECK   @DA15858 03796000
         BAL   R7,INTEQSET        FILL THE ALLOCATED ENTRY     @D34SDAP 03797000
*SGLINK  INTEQSET,INPUT=(R1,R3,R4,R6,R7,RD),WORK=(R8,RE),OUTPUT=(R2,R5) 03798000
         IC    R7,ERBLKSNL        GET LENGTH OF SENSE DATA     @D21IDAP 03799000
         STC   R7,ERRQLSNS        USE ALLOCATED SENSE BYTES    @D21IDAP 03800000
         B     INTTOERP                                        @D34SDAP 03801000
         SPACE 2                                               @D34SDAP 03802000
INTPSNS  L     R5,AERBLOC         ADDRESS OF ERROR BLOCK       @D35ADAP 03803000
         USING ERBLOC,R5          MAKE SNSSDAID ADDRESSABLE    @D35ADAP 03804000
         CLI   SNSSDAID,XFF       DID SDAID PROVIDE SENSE      @D35ADAP 03805000
         BE    INTPSNS5           NO                           @D35ADAP 03806000
         L     RF,ASNSINIT        SET UP SENSE TASK BASE       @D21LDAP 03807000
         USING SNSINIT,RF         MAKE SENSE TASK ADDRESSABLE  @D21LDAP 03808000
         MVC   SNSAREA,SNSSDAID   MOVE SDAID PROVIDED SENSE    @D35ADAP 03809000
         DROP  RF                 RELEASE SNS-TASK BASE        @D21LDAP 03810000
INTPSNS5 OI    CHQCCBB1,TRABIT+DISERR INDICATE I/O COMPLETE    @D35IDAP 03811000
         BAL   RF,INTERTRN        RESTORE I/O POINTERS         @DA20807 03812000
         B     CHNDPST                 ======================> @D35IDAP 03813000
.*   END OF PART MOVED DUE TO ADDRESSABILITY PROBLEMS          @D21LDAP 03814000
         DROP  R2                 ERROR ENTRY BASE             @D37BDFG 03815000
         DROP  R3                 RELEASE PUB POINTER          @D21LDAP 03816000
         DROP  R4                 RELEASE CHANQ POINTER        @D21LDRP 03817000
         DROP  R5                 RELEASE ERBLOC BASE          @D37BDFG 03818000
         DROP  RD                 RELEASE SUBROUTINE BASE      @D3CADAP 03819000
         TITLE 'VSE SUPERVISOR     IOINTER      SNS SYSTEM TASK       ' 03820000
         AIF   (NOT &BGDEBUG).NTXT170                          @D35IDAP 03821000
***************************************************************@D34SDAP 03822000
*                                                              @D34SDAP 03823000
*                                                              @D34SDAP 03824000
*   FUNCTION      THIS ROUTINE IS EXCLUSIVLY USED TO ISSUE A   @D34SDAP 03825000
*                 SENSE COMMAND TO ANY DEVICE KNOWN TO THE     @D34SDAP 03826000
*                 VSE/VS SUPERVISOR.                           @D34SDAP 03827000
*                 THE ROUTINE ITSELF HAS BEEN SET UP AS A      @D34SDAP 03828000
*                 SYSTEM TASK AND IT IS ASSUMED THAT THIS      @D34SDAP 03829000
*                 'SNSTASK' HAS THE HIGHEST PRIORITY OF ALL    @D34SDAP 03830000
*                 EXISTING SYSTEM TASKS. THIS SYSTEM TASK      @D34SDAP 03831000
*                 WILL BE ACTIVATED WHENEVER AN UNIT CHECK     @D34SDAP 03832000
*                 IS ENCOUNTERED FOR A DEVICE FOR WHICH A      @D34SDAP 03833000
*                 PUB-ENTRY DOES EXIST. IN CASE THE SENSE      @D34SDAP 03834000
*                 COMMAND IS TERMINATED ABNORMALLY, IT WILL    @D34SDAP 03835000
*                 BE RETRIED THREE TIMES. THE ERROR IF THAN    @D34SDAP 03836000
*                 STILL EXISTING WILL CAUSE THE PUB TO BE      @D34SDAP 03837000
*                 FLAGGED IN ERROR AND THE PROGRAM TO BE       @D34SDAP 03838000
*                 CANCELED.                                    @D34SDAP 03839000
*                                                              @D34SDAP 03840000
*   ENTRY POINTS                                               @D34SDAP 03841000
*                                                              @D34SDAP 03842000
*                 SNSTASK         ENTRY POINT FROM  INTERRUC   @D34SDAP 03843000
*                 SNSMORE         ENTRY POINT FROM  DISPATCHER @D34SDAP 03844000
*                                                              @D34SDAP 03845000
*   REGISTER INPUT                                             @D34SDAP 03846000
*                                                              @D34SDAP 03847000
*                 REGISTER  2     ADDRESS OF ERROR ENTRY       @D34SDAP 03848000
*                 REGISTER  7     USERS LINK REGISTER          @D34SDAP 03849000
*                 REGISTER 11     SUPERVISOR BASE REGISTER     @D34SDAP 03850000
*                 REGISTER 13     BASE REGISTER                @D34SDAP 03851000
*                                                              @D34SDAP 03852000
*   REGISTER OUTPUT                                            @D34SDAP 03853000
*                                                              @D34SDAP 03854000
*                 REGISTER  0     UNPREDICTABLE                @D34SDAP 03855000
*                 REGISTER  8     SNSTASK SAVE AREA ADDRESS    @D34SDAP 03856000
*                                                              @D34SDAP 03857000
***************************************************************@D34SDAP 03858000
         SPACE 3                                               @D35IDAP 03859000
         AGO   .YTXT170                                        @D35IDAP 03860000
.NTXT170 ANOP                                                  @D35IDAP 03861000
***************************************************************@D35IDAP 03862000
*                                                              @D35IDAP 03863000
*        SENSE SYSTEM TASK        (RETRIEVE SENSE INFORMATION) @D35IDAP 03864000
*                                                              @D35IDAP 03865000
***************************************************************@D35IDAP 03866000
         SPACE 2                                               @D35IDAP 03867000
.YTXT170 ANOP                                                  @D35IDAP 03868000
         DC    C'SNS-TASK'                                     @D14BDAP 03869000
         USING SNSTASK,RD                                      @D37BDFG 03870000
SNSTASK  DS    0H                                              @D37BDFG 03871000
SNSINIT  L     R9,AINTER          IOINTER BASE ADDRESS         @DA42922 03872000
         L     R5,AERBLOC         GET ERBLOC BASE ADDRESS      @D61CDAP 03873000
         USING ERBLOC,R5          ERBLOC BASE POINTER          @D35IDAP 03874000
         ICM   R2,15,SNSERCHN     GET FIRST ENTRY IN SNS CHAIN @D62NDAP 03875000
         BZ    SNSPVCHK           NO, LOOK FOR OTHER WORK      @D62NDAP 03876000
         USING ERBLKADR,R2        ERROR ENTRY BASE             @D37BDFG 03877000
SNSIN000 MVC   SNSERCHN,ERBLKPTR  DEQUEUE ENTRY FROM CHAIN     @D37BDFG 03878000
         NI    ERBLKFLG,XFF-ERQUEUED RESET ERROR QUEUED        @D14BDAP 03879000
         NI    ERBLKFL1,XFF-NEEDSNS RESET SNS NEEDED           @D14BDAP 03880000
         SLR   R4,R4              CLEAR REGISTER               @D37BDFG 03881000
         ST    R4,ERBLKPTR        RESET CHAIN POINTER          @D37BDFG 03882000
         LA    R8,SNSTID          IN CASE NO SERVICE OWNER     @D37BDFG 03883000
         ICM   R4,15,ERRQCHQA     CHANNEL QUEUE ADDRES PRESENT @D51GDAP 03884000
         BZ    SNSOWNER           NO, SET SNS AS OWNER         @D51GDAP 03885000
         ICM   R8,3,CHQTID-CHQADR(R4) GET REQUEST OWNER        @D37BDFG 03886000
SNSOWNER SGSETUP SWOWNER,STASK=SNS,WR1=R8,WR2=R7,OPTION=SETOWNER        03890000
         LH    R3,ERRQPUB         ADDRESS OF FAILING PUB       @D37BDFG 03891000
         USING PUBADR,R3          PUB BASE POINTER             @D21IDAP 03892000
SNSPUBNO DS    0H'0'              GET PUBX ADDRESS             @D51GDAP 03893000
         LR    R8,R2              SAVE ERROR ENTRY BASE        @D51GDAP 03894000
         DROP  R2                 RELEASE ERROR ENTRY BASE     @D62TAAP 03895000
         BAL   RE,GETPUBX         GET PUBX ADDRESS             @D51GDAP 03896000
*SGLINK  GETPUBX,INPUT=(R3,R9,RE),OUTPUT=R2                    @D51GDAP 03897000
         LR    RE,R2              PUBX BASE ADDRESS            @D62TAAP 03898000
         USING PBXADR,RE          PUBX BASE POINTER            @D62TAAP 03899000
         LR    R2,R8              RESTORE ERROR ENTRY BASE     @D62TAAP 03900000
         USING ERBLKADR,R2        ERROR ENTRY BASE POINTER     @D37BDFG 03901000
         LA    R0,X'80'           INITIALIZE PATH MASK         @DA44311 03902000
         LA    R8,PBXCHPID        POINT TO CHPID FIELD         @DA44311 03903000
SNSPUB10 CLC   0(1,R8),ERRQPATH   IS THIS THE FAILING PATH     @DA44311 03904000
         BE    SNSPUB20           YES,                         @DA44311 03905000
         LA    R8,1(,R8)          ADVANCE CHPID POINTER        @DA44311 03906000
         SRL   R0,1               UPDATE MASK BIT              @DA44311 03907000
         BNZ   SNSPUB10           NOT YET DONE                 @DA44311 03908000
SNSPUB20 STC   R0,SNSCCB+CCBCCW-CCBADR USE FAILING PATH ONLY   @D62TAAP 03909000
         TM    PBXFLAG3,PBXF3PVT  PATH VERIFICATION REQUIRED   @D51GDAP 03910000
         BZ    SNSPUB30           NO,                          @DA44311 03911000
         AIF   (NOT &VSE270).NREF020                                    03912490
         OC    PBXACONF(4),PBXACONF HAS NED BEEN READ          @D68REEF 03913000
         BZ    SNSPUB30           NO, NOT YET                  @D68REEF 03914000
.NREF020 ANOP                                                           03915000
         OI    ERRQPRFL,ERRQPVT   PATH VERIFICATION            @D62TAAP 03916000
         OI    SNSCCW+4,CC        CHAIN THE SENSE ID COMMAND   @DY46037 03917000
SNSPUB30 LR    R0,R3              COPY PUB ADDRESS             @D37BDAP 03918000
         SH    R0,YPUBTAB         OFFSET OF PUB WITHIN TABLE   @D34SDAP 03919000
         SRL   R0,THREE           PUB ENTRY NUMBER             @D34SDAP 03920000
         LA    R1,SNSCCB          SET UP CCB POINTER           @D34SDAP 03921000
         USING CCBADR,R1          SNS-CCB BASE POINTER         @D369DAP 03922000
         STH   R0,CCBCLS          STORE PUB NO. IN CCB         @D34SDAP 03923000
         OI    CCBCLS,CCBPHYAD+XCPR PHYSICAL ADDRESSING EXCPR  @DY46010 03924000
         LA    R0,THREE           SET UP THE RETRY COUNT       @D34SDAP 03925000
         XC    SNSAREA,SNSAREA    CLEAR SENSE AREA             @D34SDAP 03926000
         XC    SNSDEVID,SNSDEVID  CLEAR EXTENDED SENSE AREA    @D34SDAP 03927000
         TM    ERRQCSW+4,CHANEND  IS THIS ENDING STATUS        @DA29955 03928000
         BZ    SNSPUB40           YES                          @DA29955 03929000
         TM    ERRQCSW+4,DEVEND   IS THIS FINAL STATUS         @DA29955 03930000
         BO    SNSPUB40           YES                          @DA29955 03931000
         OI    PUBCSFLG,DEVBSY    RE-SET DEVICE BUSY IN PUB    @DA29955 03932000
SNSPUB40 DS    0H'0'                                           @DY46037 03933000
         CLI   PUBDEVTY,TQDIO     IS THIS A QDIO DEVICE        @DY46037 03935000
         BNE   SNSSENSE           NO,                          @DY46037 03936000
         LR    R8,RE              SAVE PUBX POINTER            @DY46037 03937000
         BAL   RE,FMT0FMT1        CONVERT TO FORMAT1 CCW       @DY46037 03938000
         LR    RE,R8              RESTORE PUBX BASE POINTER    @DY46037 03939000
SNSSENSE L     R8,AMISINTA        SGMIH BASE ADDRESS           @D52VDAP 03941000
         USING MISINTAT,R8        SGMIH BASE POINTER           @D52VDAP 03942000
         MVI   RID+1,USERTID      INDICATE SNS IS RUNNING      @D52BDAP 03943000
         OI    EMWFLAG1,EMWF1SNS  INDICATE SENSE IS ACTIVE     @D14CDAP 03944000
         SVC   15                 ISSUE SENSE COMMAND          @D14CDAP 03945000
         TM    CCBCOM1,TRABIT     IS REQUEST COMPLETE          @D34SDAP 03946000
         BO    SNSGOTIT           PROVIDE FOR TIMER INTERRUPT  @D34SDAP 03947000
         MVI   RID+1,DISPID       PREVENT REGISTER SAVING      @D34SDAP 03948000
         B     ALBRTRN                 ====================>>> @D34SDAP 03949000
SNSGOTIT NI    EMWFLAG1,XFF-EMWF1SNS  RESET SENSE IS ACTIVE    @D37BDAP 03950000
         MVC   MIHCACTO(8),MIHACTSV RESTORE ACTIVE OLD VALUE   @DA37824 03951000
         DROP  R8                 RELEASE MISINTAT BASE        @DY46037 03952000
         CLI   PUBDEVTY,TQDIO     WAS THIS A QDIO DEVICE       @DY46037 03954000
         BNE   SNSGOT00           NO,                          @DY46037 03955000
         LR    R8,RE              SAVE PUBX POINTER            @DY46037 03956000
         BAL   RE,FMT1FMT0        RESTORE FORMAT-0 CHAIN       @DY46037 03957000
         LR    RE,R8              RESTORE PUBX BASE POINTER    @DY46037 03958000
SNSGOT00 TM    SNSAREA,X'40'      IS IT INTERV REQUIRED        @DY46037 03960000
         BZ    SNSGOT10           NO,                          @DA41772 03961000
         OI    PUBCSFLG,DEVBSY    PROVIDE FOR READY RECOGNITION@DA41772 03962000
SNSGOT10 TM    SNSCCW+4,CC        DID WE DO PATH VERIFICATION  @DY46037 03963000
         BZ    SNSGOT40           NO                           @D51GDAP 03964000
         MVC   ERRQE4ID(L'ERRQE4ID),SNSDEVID SAVE SENSE-ID     @D62TAAP 03965000
         L     R8,IRB$            ADDRESS OF IRB               @D62TAAP 03966000
         USING IRB,R8             IRB BASE POINTER             @D62TAAP 03967000
         OC    PBXLPM(1),IRBLPUM  ENSURE THIS PATH             @D52VDAP 03968000
         XC    PBXLPM(1),IRBLPUM  TO BE USED NO LONGER         @D52VDAP 03969000
         CLC   PBXSNSID(7),SNSDEVID IS IT THE SAME DEVICE      @D51GDAP 03970000
         BE    SNSGOT30           YES,                         @D51GDAP 03971000
         OC    PBXNOPVF(1),IRBLPUM FENCE THIS PATH             @D52VDAP 03972000
         B     SNSGOT30                                        @D51GDAP 03973000
SNSGOT30 LA    R1,SNSCCB          RELOAD R1                    @D51GDAP 03974000
         MVI   CCBCCW,ZERO        RESET PATHING INFORMATION    @D52VDAP 03975000
SNSGOT40 L     R8,TCBPTR          POINT TO SENSE TASK TCB      @D36IDAP 03976000
         USING TCBADR,R8          TCB BASE POINTER             @D36IDAP 03977000
         L     R8,TCBSAVE         ADDRESS OF SNS SAVE AREA     @D36IDAP 03978000
         USING SVEARA,R8          MAKE IT ADDRESSABLE          @D34SDAP 03979000
         STCM  RD,M7,SVEPSW2+1    STORE ENTRY POINT IN PSW     @D37BDFG 03980000
         DEBUG USERDATA,XXDBGMC,SNSAREA,SNSAREA+16 SAVE DATA   @D61COAP 03981000
         TM    CCBSTA2,CHANER+CHNCHK ANY CHANNEL ERROR         @D34SDAP 03982000
         BNZ   SNSERR             YES, ERROR ON RECOVERY       @D34SDAP 03983000
         TM    CCBCOM1,DISERR     ANY I/O ERROR                @DA38705 03984000
         BNZ   SNSERR             YES, TRY AGAIN               @DA38705 03985000
         TM    CCBSTA1,UNITCK     UNIT CHECK                   @D34SDAP 03986000
         BZ    SNSLOAD            NO, RESTORE AND RETURN       @D34SDAP 03987000
SNSERR   OC    SNSAREA,SNSAREA    DID WE GET BACK ANY SENSE    @D368DAP 03988000
         BZ    SNSRETRY           NO, SENSE ITSELF FAILED      @D368DAP 03989000
         NI    SNSCCW+4,XFF-CC    UNCHAIN READ SENSE ID COMMAND@DY46037 03990000
         OI    SNSCCW+4,SKIP      DONT OVERWRITE SENSE         @DY46037 03991000
SNSRETRY BCT   R0,SNSPUB40        RETRY SENSE OPERATION        @DY46037 03992000
         MVI   ERRQMSG,RCVMSG     SET MESSAGE CODE             @D34SDAP 03993000
         SLR   R4,R4              CLEAR SENSE BYTE COUNTER     @D21IDAP 03994000
         B     SNSLOAD1           INDICATE NO SENSE DATA       @D21IDAP 03995000
         DROP  R8                 RELEASE SAVE AREA BASE       @D36IDAP 03996000
SNSLOAD  LA    R4,L'SNSAREA       NO. OF SENSE BYTES REQUESTED @D21IDAP 03997000
         SH    R4,CCBCNT          SUBTRACT RESIDUAL COUNT      @D21IDAP 03998000
SNSLOAD1 STC   R4,ERRQLSNS        ACTUAL BYTES RECEIVED        @D21IDAP 03999000
         NI    SNSCCW+4,XFF-CC-SKIP RESTORE TO INITIAL STATE   @DY46037 04000000
SNSRSCSW MVC   CSW(8),ERRQCSW     RESTORE ORIGINAL CSW         @D34SDAP 04001000
         TM    DEVSTA,CHANEND     WAS THIS FINAL STATUS        @D35IDAP 04002000
         BZ    SNSETCUU           YES, DONT DESTROY IT         @D35IDAP 04003000
         OI    DEVSTA,DEVEND      FORCE FINAL STATUS PROCESS   @D35IDAP 04004000
SNSETCUU MVC   CHNADR,PUBCHANN    RESTORE CUU IN LOW CORE      @D35IDAP 04005000
         MVI   RID+1,SYSTEMID     RESTORE RID TO SYSTEM        @D36IDAP 04006000
         B     INTRSNS                 =====================>> @D37BDFG 04007000
FMT0FMT1 DS    0H'0'              CONVERT TO FORMAT1 CCW       @DY46037 04009000
         STM   RE,R1,FMTSAVE      SAVE REGISTERS               @DY46037 04010000
         SLR   RF,RF              CLEAR REGISTER               @DY46037 04011000
         ICM   RF,7,9(R1)         GET CCW ADDRESS              @DY46037 04012000
         OI    12(R1),CCBCCWF1    INDICATE FORMAT-1 CCW CHAIN  @DY46037 04013000
         TM    12(R1),USENSE      USER WANTS SENSE             @DY46037 04014000
         BZ    FMT0010            NO,                          @DY46037 04015000
         LR    RE,R1              SAVE CCB ADDRESS             @DY46074 04016000
         LM    R0,R1,16(R1)       GET CCW INFO                 @DY46037 04017000
         ICM   R0,B'1000',H0      LEAVE ADDRESS ONLY           @DY46037 04018000
         STCM  R1,B'1011',17(RE)  SET FLAG AND CCW COUNT       @DY46074 04019000
         STCM  R0,B'1111',20(RE)  SET I/O AREA ADDRESS         @DY46074 04020000
FMT0010  LM    R0,R1,0(RF)        GET CCW INTO R0-R1           @DY46037 04021000
         ICM   R0,B'1000',H0      LEAVE ADDRESS ONLY           @DY46037 04022000
         STCM  R1,B'1011',1(RF)   SET FLAG AND CCW COUNT       @DY46037 04023000
         STCM  R0,B'1111',4(RF)   SET I/O AREA ADDRESS         @DY46037 04024000
         TM    1(RF),CC+DC        ANY CHAINING                 @DY46037 04025000
         LA    RF,8(,RF)          ADVANCE CCW POINTER          @DY46037 04026000
         BNZ   FMT0010            MORE CCW'S TO PROCESS        @DY46037 04027000
         LM    RE,R1,FMTSAVE      RESTORE REGISTERS            @DY46037 04028000
         BR    RE                      ======================> @DY46037 04029000
         SPACE 1                                                        04030000
FMT1FMT0 DS    0H'0'              CONVERT TO FORMAT0 CCW       @DY46037 04031000
         STM   RE,R1,FMTSAVE      SAVE REGISTERS               @DY46037 04032000
         SLR   RF,RF              CLEAR REGISTER               @DY46037 04033000
         ICM   RF,7,9(R1)         GET CCW ADDRESS              @DY46037 04034000
         NI    12(R1),XFF-CCBCCWF1 RESET FORMAT1 INDICATOR     @DY46037 04035000
         TM    12(R1),USENSE      USER WANTED SENSE            @DY46037 04036000
         BZ    FMT1010            NO,                          @DY46037 04037000
         LR    RE,R1              SAVE CCB ADDRESS             @DY46074 04038000
         LM    R0,R1,16(R1)       GET F1-CCW CONTENTS          @DY46037 04039000
         STCM  R1,B'0111',17(RE)  RESTORE SNS AREA ADDRESS     @DY46074 04040000
         STCM  R0,B'0100',20(RE)  RESTORE FLAG BYTE            @DY46074 04041000
         MVI   21(RE),ZERO        SET UNUSED BYTE ZO ZERO      @DY46074 04042000
         STCM  R0,B'0011',22(RE)  RESTORE CCW COUNT            @DY46074 04043000
FMT1010  LM    R0,R1,0(RF)        GET FORMAT-1 CCW CONTENTS    @DY46037 04044000
         STCM  R1,B'0111',1(RF)   RESTORE I/O AREA ADDRESS     @DY46037 04045000
         STCM  R0,B'0100',4(RF)   RESTORE FLAG BYTE            @DY46037 04046000
         MVI   5(RF),ZERO         SET UNUSED BYTE ZO ZERO      @DY46037 04047000
         STCM  R0,B'0011',6(RF)   RESTORE CCW COUNT            @DY46037 04048000
         TM    4(RF),CC+DC        ANY CHAINING                 @DY46037 04049000
         LA    RF,8(,RF)          ADVANCE CCW POINTER          @DY46037 04050000
         BNZ   FMT1010            MORE CCW'S TO PROCESS        @DY46037 04051000
         LM    RE,R1,FMTSAVE      RESTORE REGISTERS            @DY46037 04052000
         BR    RE                      ======================> @DY46037 04053000
         SPACE 1                                               @DY46037 04054000
FMTSAVE  DC    4F'0'              SAVE AREA                    @DY46037 04055000
         DROP  R1                 RELEASE SNS-CCB POINTER      @D369DAP 04057000
         DROP  R2                 RELEASE ERBLKADR BASE        @D62NDAP 04058000
         DROP  R3                 RELEASE PUB BASE             @D62NDAP 04059000
         DROP  RE                 RELEASE PUBX BASE POINTER    @D62TAAP 04060000
SNSMORE  L     R9,AINTER          RESTORE IOINTER BASE         @D52VDAP 04061000
         ICM   R2,15,SNSERCHN     ANY ERROR TO PROCESS         @D62NDAP 04062000
         BNZ   SNSIN000           YES,                         @D62NDAP 04063000
SNSPVCHK TM    SNSFLAG,SNSPVWRK   PATH VERIFICATION            @D52VDAP 04064000
         BO    SNSPVRTN           YES, GET IT DONE             @D52VDAP 04065000
         TM    SNSFLAG,SNSMSGOP   MIH MESSAGE NEEDED           @D61CDAP 04066000
         BZ    SNSDEACT           NO,                          @D52VDAP 04067000
         L     RD,AMISINTA        SGMIH BASE ADDRESS           @D52VDAP 04068000
         USING MISINTAT,RD        SGMIH BASE POINTER           @D52VDAP 04069000
         B     EMWPRMSG                =====================>> @D52VDAP 04070000
         EJECT ,                                               @D52VDAP 04071000
.**************************************************************@D52VDAP 04072000
.*                                                             @D52VDAP 04073000
.* FUNCTION- ACTIVATE SENSE TASK                               @D52VDAP 04074000
.* ENTRY POINT- SNSACT  USED TO ACTIVATE SENSE TASK            @D52VDAP 04075000
.*              L    RD,ASNSINIT  LOAD BASE REGISTER           @D52VDAP 04076000
.*              BAL  R7,SNSACT-SNSINIT(RD)                     @D52VDAP 04077000
.* INPUTS-   RD= SNS-TASK BASE ADDRESS                         @D52VDAP 04078000
.*           R7= RETURN REGISTER                               @D52VDAP 04079000
.* OUTPUTS-  SENSE TASK HAS BEEN ACTIVATED                     @D52VDAP 04080000
.*                                                             @D52VDAP 04081000
.**************************************************************@D52VDAP 04082000
SNSACTPV DS    0H'0'                                           @D68REEF 04083000
         L     R6,DISPAD          LOAD DISPATCHER BASE         @D68REEF 04084000
         USING DISP,R6            DISP BASE POINTER            @D68REEF 04085000
         L     RD,ASNSINIT        BASE ADDRESS OF SNSINIT      @D68REEF 04086000
         USING SNSTASK,RD                                      @D68ADAP 04087000
*SGLINK  SNSACT,INPUT=(R6,R7,RD),WORK=(R8)                     @D52VDAP 04088000
SNSACT   L     R8,ASNSTIB         ADDRESS OF SNS TIB           @D52VDAR 04089000
         USING TIBADR,R8          SVT TIB                      @D52VDAR 04090000
         OI    SNSFLAG,SNSPVWRK   INDICATE PATH VERIFICATION   @D52VDAP 04091000
         CLI   TIBRQID,SYSBND     IS SNS-TASK ACTIVE           @D52VDAR 04092000
         BNER  R7                 YES, ====================>>> @D52VDAP 04093000
         STM   RE,RF,SNSACTRE     SAVE CONTENTS OF RE, RF      @D52VDAP 04094000
         BAL   RF,POST            MAKE SENSE TASK READY        @D52VDAP 04095000
*SGLINK  POST,INPUT=(R6,R8,RF),WORK=RE                         @D52VDAP 04096000
         LM    RE,RF,SNSACTRE     RESTORE CONTENTS OF RE, RF   @D52VDAP 04097000
         BR    R7                      ====================>>> @D52VDAP 04098000
         DROP  R8                 RELEASE TIB BASE             @D52VDAP 04099000
         SPACE 1                                               @D52VDAP 04100000
SNSACTRE DC    A(0,0)             SAVE AREA FOR RE AND RF      @D52VDAP 04101000
         SPACE 3                                               @D52VDAP 04102000
SNSDEACT L     RB,TCBPTR          POINT TO SENSE TASK TCB      @D52VDAP 04103000
         USING TCBADR,RB          TCB BASE POINTER             @D52VDAP 04104000
         L     RB,TCBSAVE         ADDRESS OF SNS SAVE AREA     @D52VDAP 04105000
         USING SVEARA,RB          MAKE IT ADDRESSABLE          @D52VDAP 04106000
         ST    RD,SVEPSW2         STORE ENTRY POINT IN PSW     @D68ADAP 04107000
         ST    RD,SVER0D          STORE BASE POINTER           @D68REEF 04108000
         STM   R5,R6,SVER05       RESTORE ERBLOK/DISP ADDRESS  @D68REEF 04109000
         SGSETUP UNPOST,STASK=SNS,WR1=RB                       @D36IDAP 04110000
         B     INITRG             EXIT ======================> @DSCSI   04111000
         DROP  RB                 RELEASE SAVE AREA BASE       @D52VDAP 04112000
         SPACE 3                                                        04113000
SNSCCB   CCB   SYSRES,SNSCCW,X'1400'                           @DY46037 04114000
         ORG   SNSCCB+12                                       @D35IDAP 04115000
         DC    AL1(ERRTIN)        INDICATE ERP REQUEST         @DY46037 04116000
         ORG   ,                                               @D35IDAP 04117000
SNSCCW   CCW   X'04',SNSAREA,X'20',L'SNSAREA                   @DY46037 04118000
         CCW   X'E4',SNSDEVID,X'60',L'SNSDEVID                 @DY46037 04119000
         CCW   X'04',SNSAREA,X'30',L'SNSAREA                   @DY46037 04120000
         SPACE 1                                                        04121000
*                                                                       04122000
*  N O T E     WHEN MAKING CHANGES, MAKE SURE THE FOLLOWING             04123000
*              DSECT IS ADOPTED ACCORDINGLY FOR IJBPVPR                 04124000
*                                                                       04125000
SNSIOBLK DS    0D                                                       04126000
SNSCCWE4 CCW   SNSID,SNSDEVID,SLI,L'SNSDEVID SENSE ID INFO     @DY46037 04127000
SNSCCWFA CCW   RCD,SNSRCDIO,SLI,2*L'SNSRCDIO READ CONFIG. DATA @DY46299 04128490
SNSCCW34 CCW   SNID,SNSIDSET,SLI,L'SNSIDSET SENSE PATH GROUP   @DY46037 04129000
SNSCCWAF CCW   SPID,SNSIDSET,SLI,L'SNSIDSET SET PATH GROUP ID  @DY46037 04130000
SNSAREA  DC    XL32'00'                                        @D35GDAP 04131000
SNSDEVID DC    XL32'00'           SENSE ID INFORMATION         @D51GDAP 04132000
SNSCIWID EQU   SNSDEVID+8         EXTENDED SID INFO            @D52VDAP 04133000
SNSCIWEN DC    A(SNSDEVID+L'SNSDEVID) END OF SID AREA          @D52VDAP 04134000
         DC    XL2'00'            SENSE ID INFORMATION         @D51GDAP 04135000
SNSRDCIO DC    XL64'0'            DEVICE CHARACTERISTICS BUFFER@D52VDAP 04136000
SNSRCDIO DC    XL256'0'               CONFIGURATION DATA PART1 @DY46299 04137290
         DC    XL256'0',XL4'40FF0000' CONFIGURATION DATA PART2 @DY46299 04137580
SNSIDSET DC    CL12' '                 VM'S SET PATH GROUP ID  @D52BDAP 04138000
*                                                                       04139000
*  N O T E     WHEN MAKING CHANGES, MAKE SURE THE PREVIOUS              04140000
*              CSECT IS ADOPTED ACCORDINGLY FOR IJBPVPR                 04141000
*                                                                       04142000
         AGO   .NPVP010                                                 04143000
.YPVP010 ANOP                                                  @D68REEF 04144000
PVTIOMAP DSECT                    CCW AND I/O AREA MAPPING     @D68REEF 04145000
PVTCCWE4 CCW   0,0,0,0            SENSE ID INFO                @D68REEF 04146000
PVTCCWFA CCW   0,0,0,0            READ CONFIGURATION DATA      @D68REEF 04147000
PVTCCW34 CCW   0,0,0,0            SENSE PATH GROUP             @D68REEF 04148000
PVTCCWAF CCW   0,0,0,0            SET PATH GROUP               @D68REEF 04149000
PVTAREA  DC    XL32'00'                                        @D68REEF 04150000
PVTDEVID DC    XL32'00'           SENSE ID INFORMATION         @D68REEF 04151000
PVTCIWID EQU   PVTDEVID+8         EXTENDED SID INFO            @D68REEF 04152000
PVTCIWEN DC    A(PVTDEVID+L'PVTDEVID) END OF SID AREA          @D68REEF 04153000
         DC    XL2'00'            SENSE ID INFORMATION         @D68REEF 04154000
PVTRDCIO DC    XL64'0'            DEVICE CHARACTERISTICS BUFFER@D68REEF 04155000
PVTRCDIO DC    XL256'0'               CONFIGURATION DATA PART1 @DY46299 04156290
         DC    XL256'0',XL4'40FF0000' CONFIGURATION DATA PART2 @DY46299 04156580
PVTIDSET DC    CL12' '            VM'S SET PATH GROUP ID       @D68REEF 04157000
         MEXIT                                                 @D68REEF 04158000
.NPVP010 ANOP                                                  @D68REEF 04159000
         DROP  R5                 RELEASE ERBLOC BASE          @D369DAP 04160000
         DROP  R6                 RELEASE DISP BASE            @D369DAP 04161000
         DROP  R9                 RELEASE IOINTER BASE         @D369DAP 04162000
         DROP  RA                 RELEASE PIB BASE             @D369DAP 04163000
         DROP  RD                 RELEASE SNSTASK BASE         @D369DAP 04164000
         SPACE 1                                                        04165000
         EJECT ,                                                        04166000
*                                                                       04167000
*        PATH VERIFICATION PROCESSING                                   04168000
*                                                                       04169000
         SPACE 2                                                        04170000
         USING SNSTASK,RD                                      @D37BDFG 04171000
         USING DISP,R6                 DISPATCHER BASE         @D52VDAP 04172000
SNSPVRTN DS    0H'0'                   PATH VERIFICATION PROC. @D68REEF 04173000
         NI    SNSFLAG,XFF-SNSPVWRK    RESET PATH VERIFICATION @D68REEF 04174000
         AIF   (NOT &VSE270).NREF040                           @D68REEF 04175490
         L     RF,AARCMTAB             INTERFACE TABLE ADDRESS @D68REEF 04176000
         USING ARSUPTAB,RF             INTERFACE TABLE BASE    @D68REEF 04177000
         LR    RC,RD                   SAVE SNSTASK BASE       @D68REEF 04178000
         DROP  RD                      RELEASE PERM BASE       @D68REEF 04179000
         USING SNSTASK,RC              NEW TEMP BASE           @D68REEF 04180000
         ICM   RD,15,ARPVRTN           IS ROUTINE PRESENT      @D68REEF 04181000
         BZ    SNSPVIPL                NO PROCEED THE OLD WAY  @D68REEF 04182000
         LA    RB,SNSIOBLK             SNSIOBLK BASE           @D68REEF 04183000
         BASSM RE,RD                   IJBPVPR WILL HANDLE IT  @D68REEF 04184000
*SGLINK  IJBPVPR INPUT=(R6,RB,RD,RE,RF)                        @D68REEF 04185000
SNSPVXIT LR    RD,RC                   RESTORE SNSTASK BASE    @D68REEF 04186000
         DROP  RC                      RELEASE TEMP BASE       @D68REEF 04187000
         USING SNSTASK,RD                                      @D68REEF 04188000
         B     SNSMORE                      =================> @D68REEF 04189000
AARCMTAB DC    A(ARCOMTAB)                                     @D68REEF 04190000
         DROP  RD                      RELEASE SNSTASK BASE    @D68REEF 04191000
SNSPVIPL LR    RD,RC                   IPL PROCESSING PART     @D68REEF 04192000
         USING SNSTASK,RD                                      @D68REEF 04193000
         L     RF,APBXAREA             PUBX AREA ADDRESS       @D68REEF 04194000
         LH    R3,YPUBTAB              PUB BASE ADDRESS        @D68REEF 04195000
         USING PUBADR,R3               PUB BASE POINTER        @D68REEF 04196000
         B     SNNPV020                                        @D68REEF 04197000
SNNPV010 L     RF,SNSPXPTR             RESTORE PUBX ADDRESS PNT@D68REEF 04198000
SNNPV015 LA    R3,NEXTPUB              POINT TO NEXT PUB       @D68REEF 04199000
SNNPV020 ICM   R9,15,0(RF)             GET PUBX BASE ADDRESS   @D68REEF 04200000
         LA    RF,4(,RF)               NEXT PUBX ADDRESS PNTR. @D68REEF 04201000
         BM    SNSMORE                 DONE =================> @D68REEF 04202000
         USING PBXADR,R9               PUBX BASE POINTER       @D68REEF 04203000
         TM    PBXFLAG3,PBXF3PVT+PBXF3INS SPECIAL WORK TO DO   @D68REEF 04204000
         BZ    SNNPV015                NO                      @D68REEF 04205000
         OI    PBXFLAG2,PBXNOSIO       WE WANT NEW CHARACTSTICS@D68REEF 04206000
         MVI   PBXPVPEN,ZERO           RESET PATH INFO         @D68REEF 04207000
         CLI   PBXLPM,ZERO             IS LPM PRESENT          @D68REEF 04208000
         BNE   SNNPV030                YES,                    @D68REEF 04209000
         MVI   PBXLPM,X'80'            USE DEFAULT             @D68REEF 04210000
SNNPV030 DS    0H'0'                                           @D68REEF 04210300
         AIF   (NOT &VSE280).N280240                           @D68REEF 04210600
*HERE FCP INSERTED                                             @D68REEF 04211000
         CLI   PUBDEVTY,TQDIO          IS THIS A QDIO DEVICE   @D68REEF 04212490
         BNE   SNNPV050                NO,                     @D68REEF 04213000
         CLI   PUBOPTN,PUBOPFCP        IS THIS THE FCP ADAPTER @D68REEF 04214000
         BE    SNNPVFCP                YES,                    @D68REEF 04215000
         B     SNNPV050                NO,                     @D68REEF 04216000
SNNPV040 L     RF,SNSPXPTR             RESTORE PUBX ADDR. PNTR @D68REEF 04217000
*HERE FCP INSERT END                                           @D68REEF 04218000
.N280240 ANOP                                                           04218500
SNNPV050 CLI   PUBCHQPT,NOTQUED        IS I/O REQUEST ENQUEUED @D68REEF 04219000
         BE    SNNPV015                NO, PROCESS NEXT DEVICE @D68REEF 04220000
         ST    RF,SNSPXPTR             SAVE PUBX AREA POINTER  @D68REEF 04221000
         LH    R8,PUBCHANN             GET CUU ADDRESS         @D68REEF 04222000
         L     RE,TCBPTR               SNS-TCB ADDRESS         @D68REEF 04223000
         USING TCBADR,RE               TCB BASE POINTER        @D68REEF 04224000
         L     RE,TCBSAVE              SNS-SAVE AREA ADDRESS   @D68REEF 04225000
         USING SVEARA,RE               SAVE AREA BASE POINTER  @D68REEF 04226000
         LA    RF,SNNPV010             CONTINUATION ADDRESS    @D68REEF 04227000
         SVC   33                      SAVE OUR CURRENT STATUS @D68REEF 04228000
         STCM  RF,M7,SVEPSW2+1         ENSURE PROPER REDISPATCH@D68REEF 04229000
         ST    R8,IOINF                PROVIDE CUU IN LOW CORE @D68REEF 04230000
         ST    R3,INTPARM              PROVIDE INTERR. PARM.   @D68REEF 04231000
         L     R9,AINTER               IOINTER BASE ADDRESS    @D68REEF 04232000
         USING INTRTN,R9               IOINTER BASE POINTER    @D68REEF 04233000
         B     SNSPVINIT               GET THE I/O STARTED     @D68REEF 04234000
         DROP  R9                      RELEASE INTRTN BASE     @D68REEF 04234300
         AIF   (NOT &VSE280).N280260                                    04234600
*HERE FCP INSERTED                                                      04235000
         USING PBXADR,R9               PUBX BASE POINTER       @D68REEF 04236000
SNNPVFCP DS    0H'0'                   FCP INSTALL PROCESSING  @D68REEF 04237000
         ST    RF,SNSPXPTR             SAVE PUBX AREA POINTER  @D68REEF 04238000
         L     RE,ASNSSCHI             GET ADDRESS OF SCHIB    @D68REEF 04239000
         USING SCHIB,RE                SCHIB BASE POINTER      @D68REEF 04240000
         LH    R1,PBXSCH               GET SUBCHANNEL NUMBER   @D68REEF 04241000
         ICM   R1,12,H1                SUBCHANNEL ID WORD      @D68REEF 04242000
         STSCH SCHIB                   STORE SUBCHANNEL        @D68REEF 04243000
         BC    1,SNNPV040                   =================> @D68REEF 04244000
         TM    SCHFLGS,SCHV            IS DEVICE NUMBER VALID  @D68REEF 04245000
         BNO   SNNPV040                NO,  =================> @D68REEF 04246000
         MVC   PBXIRB(12),SCHSTB0      SAVE SUBCHANNEL STATUS  @D68REEF 04247000
         MVC   PBXIRB+IRBLPUM-IRB(1),SCHLPUM SAVE LAST PATH    @D68REEF 04248000
         MVC   PBXPIM(1),SCHPIM        GET PIM MASK            @D68REEF 04249000
         MVC   PBXPAM(1),SCHPAM        GET PAM MASK            @D68REEF 04250000
         TM    PBXFLAG3,PBXF3INS       INSTALL PROCESSING      @D68REEF 04251000
         BZ    SNNPV040                NO,  =================> @D68REEF 04252000
         NI    PBXFLAG3,XFF-PBXF3INS-PBXF3PVT RESET FLAGS      @D68REEF 04253000
.* INSTALL PROCESSING (APPLIES TO FCP DEVICES ONLY)            @D68REEF 04254000
         MVI   PBXNOERP,0              RESET ALL ERROR PATHES  @D68REEF 04255000
         MVI   PBXCHPID,XFF            SET ALL CHPID'S INVALID @D68REEF 04256000
         MVC   PBXCHPID+1(L'PBXCHPID-1),PBXCHPID PROPAGATE FF  @D68REEF 04257000
         XC    PBXRCDCA(2),PBXRCDCA    CLEAR OLD SETTING       @D68REEF 04258000
         SLR   R1,R1                   CLEAR REGISTER          @D68REEF 04259000
         ICM   R1,1,SCHLPUM            GET LAST PATH USED      @D68REEF 04260000
         BNZ   SNNPVF10                IT DID EXIST            @D68REEF 04261000
         IC    R1,PBXLPM               START WITH LAST USED    @D68REEF 04262000
SNNPVF10 STC   R1,PBXLPM               START WITH LAST USED    @D68REEF 04263000
         SRL   R1,4                    LIMIT TO FIRST 4 BITS   @D68REEF 04264000
         X     R1,F15                  PATH NOT INSTALLED MASK @D68REEF 04265000
         ICM   R8,15,SCHCPID0          GET CHPIDS 0-3          @D68REEF 04266000
         EX    R1,SNSPEXS1             INVALIDATE NOT INSTALLED@D68REEF 04267000
         ST    R8,PBXCHPID             SAVE CHANNEL ID         @D68REEF 04268000
         IC    R1,SCHPIM               GET PATH INSTALLED MASK @D68REEF 04269000
         N     R1,F15                  LIMIT TO LAST  4 BITS   @D68REEF 04270000
         X     R1,F15                  PATH NOT INSTALLED MASK @D68REEF 04271000
         ICM   R8,15,SCHCPID4          GET CHPIDS 4-7          @D68REEF 04272000
         EX    R1,SNSPEXS2             SET CHPID  4-7          @D68REEF 04273000
         ST    R8,PBXCHPID+4           SAVE CHANNEL ID         @D68REEF 04274000
         IC    R1,PBXLPM               GET LAST PATH USED MASK @D68REEF 04275000
.* INITIALIZE PROCESSING                                       @D68REEF 04276000
         STC   R1,SCHLPM               SAVE CURRENT PATH MASK  @D68REEF 04277000
         XC    SNSDEVID,SNSDEVID       CLEAR SENSE-ID FIELD    @D68REEF 04278000
         LA    RF,SNSCCWE4             POINT TO SNSID CCW      @D68REEF 04279000
         ICM   RF,8,SCHLPM             GET PATH MASK IN BYTE 0 @D68REEF 04280000
         BAL   R8,SNSIORTN             READ SNSID DATA         @D68REEF 04281000
         B     SNNPVF30             0  I/O COMPLETED SUCCESSF. @D68REEF 04282000
         B     SNNPV040             4       =================> @D68REEF 04283000
         CLM   RF,8,H0              8  PERM. ERR. DUE TO NOT-OP@D68REEF 04284000
         BNE   SNNPV040                NO,  =================> @D68REEF 04285000
         OC    PBXNOERP(1),SCHLPM      SET PATH INOPERATIVE    @D68REEF 04286000
         OC    PBXLPM(1),SCHLPM        ENSURE BIT TO BE        @D68REEF 04287000
         XC    PBXLPM(1),SCHLPM        TURNED OFF IN LPM       @D68REEF 04288000
         B     SNNPV040                NEXT =================> @D68REEF 04289000
SNNPVF30 OC    SNSDEVID+8(L'SNSDEVID-8),SNSDEVID+8 XTEND SNSID @D68REEF 04290000
         BZ    SNNPV040                NO,  =================> @D68REEF 04291000
         BAL   R8,SNSPV300             PROVIDE RCD INFORMATION @D68REEF 04292000
*SGLINK  SNSPV300,INPUT=(R8,R9),WORK=(R5,RA,RF)                @D68REEF 04293000
         B     SNNPV040             0  N/A  =================> @D68REEF 04294000
         B     SNNPV040             4  NO RCD================> @D68REEF 04295000
         NOP   0                    8  WE FOUND AN RCD CIW     @D68REEF 04296000
         OC    PBXRCDPI(1),SCHLPM      SAVE PATH MASK          @D68REEF 04297000
         SLR   R8,R8                   INDICATE RCD CIW FOUND  @D68REEF 04298000
SNNPVF50 LTR   R8,R8                   DID WE FIND A RCD CIW   @D68REEF 04299000
         BNZ   SNNPV040                NO, ==================> @D68REEF 04300000
         MVC   SNSCCWFA(1),PBXRCDCC    SET COMMAND CODE        @D68REEF 04301000
         XC    SNSRCDIO(L'SNSRCDIO),SNSRCDIO CLEAR BUFFER      @D68REEF 04302000
         XC    SNSRCDIO+256(L'SNSRCDIO),SNSRCDIO+256 CONF DATA @DY46299 04302500
         LA    RF,SNSCCWFA             GET RCD CCW ADDRESS     @D68REEF 04303000
         ICM   RF,8,SCHLPM             GET PATH MASK IN BYTE 0 @D68REEF 04304000
         BAL   R8,SNSIORTN             READ CONFIGURATION DATA @D68REEF 04305000
         B     SNNNEDPR             0  I/O COMPLETED SUCCESSF. @D68REEF 04306000
         B     SNNPV040             4  REJ  =================> @D68REEF 04307000
         B     SNNPV040             8  PERM =================> @D68REEF 04308000
         SPACE 2                                               @D68REEF 04309000
SNNNEDPR DS    0H'0'                                           @D68REEF 04310000
         LA    RF,SNSRCDIO             POINT TO DATA BUFFER    @D68REEF 04311000
SNNNED10 TM    0(RF),X'C0'             IS THIS A NED           @D68REEF 04312000
         BO    SNNNED30                YES, DO NED PROCESSING  @D68REEF 04313000
         TM    0(RF),X'80'             IS THIS THE GENERAL NEQ @D68REEF 04314000
         BO    SNNPV040                YES, =================> @D68REEF 04315000
SNNNED20 LA    RF,32(,RF)              ADVANCE TO NEXT ENTRY   @D68REEF 04316000
         B     SNNNED10                PRECESS NEXT ENTRY      @D68REEF 04317000
SNNNED30 CLI   1(RF),1                 IS IT THE I/O DEVICE NED@D68REEF 04318000
         BNE   SNNNED20                NO,                     @D68REEF 04319000
         MVC   PBXRCDCA(2),30(RF)      GET CHPID AND DEV ADDR. @D68REEF 04320000
         MVC   PBXQCULA(1),63(RF)      GET CNTRL UNIT IMAGE ID @D68REEF 04321000
         B     SNNPV040                PATH =================> @D68REEF 04322000
         DROP  R3                      RELEASE PUB BASE        @D68REEF 04323000
         DROP  R9                      RELEASE PUBX BASE       @D68REEF 04324000
         DROP  RE                      RELEASE SCHIB BASE      @D68REEF 04325000
*HERE FCP INSERT END                                           @D68REEF 04325300
.N280260 ANOP                                                           04325600
         USING INTRTN,R9               IOINTER BASE POINTER    @D68REEF 04326000
         USING PUBADR,R3               PUB BASE POINTER        @D68REEF 04327000
         AGO   .YREF020                                                 04329000
.NREF040 ANOP                                                           04330000
SNSPVOLD OC    SNSSPGID,SNSSPGID       FIRST TIME THROUGH      @D68REEF 04331000
         BNZ   SNSPV000                NO,                     @DY45586 04332000
         STIDP SNSCPUID                STORE CPUID             @D52BDAP 04333000
         MVC   SNSSPGID,SNSCPUID+1     MOVE IN CPU SERIAL NO.  @D66ADAP 04334000
         TM    SNSCPUID+6,X'80'        IS THIS THE NEW FORMAT  @D66ADAP 04335000
         BO    SNSPV000                YES,                    @D27ADAP 04336000
         MVI   SNSSPGFM,ZERO           NO, RESET FORMAT BIT    @D27ADAP 04337000
SNSPV000 L     RF,APBXAREA             PUBX AREA ADDRESS       @DY45586 04338000
         LH    R3,YPUBTAB              PUB BASE ADDRESS        @D52VDAP 04339000
         USING PUBADR,R3               PUB BASE POINTER        @D52BDAP 04340000
         ST    RF,SNSPXPTR             SAVE PUBX AREA POINTER  @D52VDAP 04341000
         B     SNSPV020                                        @D52VDAP 04342000
SNSPV010 L     RF,SNSPXPTR             RESTORE PUBX ADDRESS PNT@D52VDAP 04343000
SNSPV015 LA    R3,NEXTPUB              POINT TO NEXT PUB       @D52VDAP 04344000
SNSPV020 ICM   R9,15,0(RF)             GET PUBX BASE ADDRESS   @D52VDAP 04345000
         LA    RF,4(,RF)               NEXT PUBX ADDRESS PNTR. @D52VDAP 04346000
         BM    SNSMORE                 DONE =================> @D52VDAP 04347000
         USING PBXADR,R9               PUBX BASE POINTER       @D52VDAP 04348000
         TM    PBXFLAG3,PBXF3PVT+PBXF3INS SPECIAL WORK TO DO   @D52VDAP 04349000
         BZ    SNSPV015                NO                      @D52VDAP 04350000
         OC    PBXSIMAD,PBXSIMAD       IS A SIMULATOR ACTIVE   @DAOM093 04351000
         BZ    SNSPV025                NO,                     @DAOM093 04352000
         NI    PBXFLAG3,XFF-PBXF3PVT-PBXF3INS RESET BITS       @DAOM093 04353000
         MVI   PBXPVPEN,ZERO           RESET PATH INFO         @DAOM093 04354000
         B     SNSPV015                CHECK NEXT DEVICE       @DAOM093 04355000
SNSPV025 OI    PBXFLAG2,PBXNOSIO       WE WANT NEW CHARACTSTICS@TEST    04356000
         TM    SNSFLAG,SNSMSGRO        IS REPLY OUTSTANDING    @DY45768 04357000
         BZ    SNSPV030                NO,                     @DY45768 04358000
         L     R1,AMISINTA             SGMIH BASE ADDRESS      @DY45768 04359000
         USING MISINTAT,R1             SGMIH BASE POINTER      @DY45768 04360000
         C     R3,EMWPUBPT             MIH FOR THIS DEVICE     @DY45768 04361000
         BNE   SNSPV030                NO,                     @DY45768 04362000
         OI    SNSFLAG,SNSPVDLY        FORCE DELAYED PROCESS   @DY45768 04363000
         B     SNSPV015                PROCESS NEXT DEVICE     @DY45768 04364000
         DROP  R1                      RELEASE SGMIH BASE      @DY45768 04365000
SNSPV030 ST    RF,SNSPXPTR             SAVE PUBX AREA POINTER  @DY45768 04366000
         CLC   PBXSCH(2),XFFFF         VALID SUBCHANNEL        @DA44471 04367000
         BE    SNSPV250                NO,                     @DA44471 04368000
SNSPV035 L     RE,ASNSSCHI             GET ADDRESS OF SCHIB    @D52BDAP 04369000
         USING SCHIB,RE                SCHIB BASE POINTER      @D52BDAP 04370000
         LH    R1,PBXSCH               GET SUBCHANNEL NUMBER   @D52BDAP 04371000
         ICM   R1,12,H1                SUBCHANNEL ID WORD      @D52BDAP 04372000
SNSPV040 STSCH SCHIB                   STORE SUBCHANNEL        @D52BDAP 04373000
         BC    1,SNSPV250              SUBCHANNEL NOT EXISTING @D52BDAP 04374000
         TM    SCHFLGS,SCHV            IS DEVICE NUMBER VALID  @D52BDAP 04375000
         BNO   SNSPV250                NO,                     @D52BDAP 04376000
         MVC   PBXIRB(12),SCHSTB0      SAVE SUBCHANNEL STATUS  @D61NDAP 04377000
         MVC   PBXIRB+IRBLPUM-IRB(1),SCHLPUM SAVE LAST PATH    @D61NDAP 04378000
         LA    R2,8                    LIMIT TO EIGHT PATHES   @D52VDAP 04379000
         MVC   PBXPIM(1),SCHPIM        GET PIM MASK            @D52BDAP 04380000
         MVC   PBXPAM(1),SCHPAM        GET PAM MASK            @D52BDAP 04381000
         OC    PBXNOERP(1),PBXPVPEN    TURN BITS ON            @D61ADAP 04382000
         XC    PBXNOERP(1),PBXPVPEN    RESET IN ERROR PATHES   @D61ADAP 04383000
         TM    PBXFLAG3,PBXF3INS       INSTALL PROCESSING      @D52VDAP 04384000
         BZ    SNSPV080                NO, DO PATH PROCESSING  @D52VDAP 04385000
.* INSTALL PROCESSING                                                   04386000
         MVI   PBXNOERP,0              RESET ALL ERROR PATHES  @D61ADAP 04387000
         MVC   PBXPVPEN,PBXPIM         PATHES TO BE VERIFIED   @D52BDAP 04388000
         MVC   PBXLPM,SCHLPUM          START WITH LAST USED    @D52BDAP 04389000
         MVI   PBXCHPID,XFF            SET ALL CHPID'S INVALID @D52BDAP 04390000
         MVC   PBXCHPID+1(L'PBXCHPID-1),PBXCHPID PROPAGATE FF  @D52BDAP 04391000
         XC    PBXDVNED,PBXDVNED       RESET DEVICE NED        @D61ADAP 04392000
         XC    PBXRCDCA(2),PBXRCDCA    CLEAR OLD SETTING       @D61ADAP 04393000
         SLR   R1,R1                   CLEAR REGISTER          @D52VDAP 04394000
         IC    R1,SCHPIM               GET PATH INSTALLED MASK @D52VDAP 04395000
         SRL   R1,4                    LIMIT TO FIRST 4 BITS   @DA42922 04396000
         X     R1,F15                  PATH NOT INSTALLED MASK @DA42922 04397000
         ICM   R8,15,SCHCPID0          GET CHPIDS 0-3          @D52VDAP 04398000
         EX    R1,SNSPEXS1             INVALIDATE NOT INSTALLED@DA42922 04399000
         ST    R8,PBXCHPID             SAVE CHANNEL ID         @DA42922 04400000
         IC    R1,SCHPIM               GET PATH INSTALLED MASK @D52VDAP 04401000
         N     R1,F15                  LIMIT TO LAST  4 BITS   @D52VDAP 04402000
         X     R1,F15                  PATH NOT INSTALLED MASK @DA42922 04403000
         ICM   R8,15,SCHCPID4          GET CHPIDS 4-7          @D52VDAP 04404000
         EX    R1,SNSPEXS2             SET CHPID  4-7          @D52VDAP 04405000
         ST    R8,PBXCHPID+4           SAVE CHANNEL ID         @DA42922 04406000
SNSPV050 SLR   R1,R1                   CLEAR REGISTER          @D52VDAP 04407000
         ICM   R1,1,SCHLPUM            GET LAST PATH USED MASK @D61NDAP 04408000
         BNZ   SNSPV090                IT DID EXIST            @D52VDAP 04409000
.* INITIALIZE PROCESSING                                                04410000
SNSPV080 LA    R1,X'80'                DEFAULT TO 1ST PATH MASK@D52VDAP 04411000
SNSPV090 OC    PBXPVPEN(1),PBXNOOPR    TURN BITS ON            @DA44616 04412000
         XC    PBXPVPEN(1),PBXNOOPR    EXCLUDE OFFLINED PATHES @DA44616 04413000
         EX    R1,SNSPEXTM             THIS PATH TO BE VERIFIED@D52VDAP 04414000
         BZ    SNSPV220                NO,                     @D52VDAP 04415000
         STC   R1,SCHLPM               SAVE CURRENT PATH MASK  @D52BDAP 04416000
         XC    SNSDEVID,SNSDEVID       CLEAR SENSE-ID FIELD    @D52VDAP 04417000
         LA    RF,SNSCCWE4             POINT TO SNSID CCW      @D52BDAP 04418000
         ICM   RF,8,SCHLPM             GET PATH MASK IN BYTE 0 @D52BDAP 04419000
         BAL   R8,SNSIORTN             SENSE ID COMMAND        @D52BDAP 04420000
         B     SNSPV0B0             0  I/O COMPLETED SUCCESSF. @D61NDAP 04421000
         B     SNSPV0B0             4  COMMAND REJECT          @D61NDAP 04422000
         CLM   RF,8,H0              8  PERM. ERR. DUE TO NOT-OP@D52VDAP 04423000
         BNE   SNSPV200                NO,                     @D52BDAP 04424000
         OC    PBXNOERP(1),SCHLPM      SET PATH INOPERATIVE    @D52VDAP 04425000
         OC    PBXLPM(1),SCHLPM        ENSURE BIT TO BE        @D52VDAP 04426000
         XC    PBXLPM(1),SCHLPM        TURNED OFF IN LPM       @D52VDAP 04427000
         B     SNSPV210                PROCESS NEXT PATH       @D52BDAP 04428000
SNSPV0B0 OC    SNSDEVID+8(L'SNSDEVID-8),SNSDEVID+8 XTEND SNSID @DA43487 04429000
         BNZ   SNSPV0C0                YES,                    @DA43487 04430000
         LCR   R8,R1                   GET COMPLEMENT          @DA42602 04431000
         BCTR  R8,0                    ONES COMPLEMENT         @DA42602 04432000
         EX    R8,SNSPEXTP             SINGLE PATH DEVICE      @DA42602 04433000
         BZ    SNSPV200                YES, SKIP VERIFICATION  @DA42602 04434000
SNSPV0C0 CLI   PUBDEVTY,TCTCA          IS THIS A CTC CONNECTION@DA43487 04435000
         BE    SNSPV200                YES, SKIP COMMAND       @D52VDAP 04436000
         CLC   PUBCHANN(2),SYSOCDEV    IS THIS THE CONSOLE     @DA42940 04437000
         BE    SNSPV200                YES, PREVENT 401 ERROR  @DA42940 04438000
         BAL   R8,SNSPV300             PROVIDE RCD INFORMATION @D52BDAP 04439000
*SGLINK  SNSPV300,INPUT=(R8,R9),WORK=(R5,RA,RF)                @D68ADAP 04440000
         B     SNSPV0E0             0  SNSID WAS NOT AVAILABLE @D52BDAP 04441000
         B     SNSPV0E0             4  NO RCD CIW FOUND        @D52VDAP 04442000
         NOP   0                    8  WE FOUND AN RCD CIW     @D52VDAP 04443000
         OC    PBXRCDPI(1),SCHLPM      SAVE PATH MASK          @D52VDAP 04444000
         OC    PBXLPM(1),SCHLPM        ENSURE BIT TO BE        @D52VDAP 04445000
         XC    PBXLPM(1),SCHLPM        TURNED OFF IN LPM       @D52VDAP 04446000
         SLR   R8,R8                   INDICATE RCD CIW FOUND  @D52VDAP 04447000
SNSPV0E0 LTR   R8,R8                   DID WE FIND A RCD CIW   @DA44311 04448000
         BNZ   SNSPV120                NO,                     @D52VDAP 04449000
         MVC   SNSCCWFA(1),PBXRCDCC    SET COMMAND CODE        @D52BDAP 04450000
         XC    SNSRCDIO(L'SNSRCDIO),SNSRCDIO CLEAR BUFFER      @D52BDAP 04451000
         XC    SNSRCDIO+256(L'SNSRCDIO),SNSRCDIO+256 CONF DATA @DY46299 04451500
         LA    RF,SNSCCWFA             GET RCD CCW ADDRESS     @D52BDAP 04452000
         ICM   RF,8,SCHLPM             GET PATH MASK IN BYTE 0 @D52BDAP 04453000
         BAL   R8,SNSIORTN             READ CONFIGURATION DATA @D52BDAP 04454000
         B     SNSPV100             0  I/O COMPLETED SUCCESSF. @D52BDAP 04455000
         B     SNSPV120             4  COMMAND REJECT          @D52BDAP 04456000
         B     SNSPV120             8  PERMANENT I/O ERROR     @D52BDAP 04457000
SNSPV100 BAL   R8,SNSNEDPR             SET DEVICE NED IN PUBX  @D52BDAP 04458000
*SGLINK  SNSNEDPR,INPUT=(R3,R8,R9,RE),OUTPUT=RF                @D52BDAP 04459000
         B     SNSPV210             0  PATH IN ERROR RETURN    @D52BDAP 04460000
SNSPV120 XC    SNSIDSET,SNSIDSET    4  RESET I/O AREA          @D52BDAP 04461000
         LA    RF,SNSCCW34             POINT TO SNSID CCW      @D52BDAP 04462000
         ICM   RF,8,SCHLPM             GET PATH MASK IN BYTE 0 @D52BDAP 04463000
         BAL   R8,SNSIORTN             SENSE PATH GROUP ID     @D52BDAP 04464000
         B     SNSPV140             0  I/O COMPLETED SUCCESSF. @D52BDAP 04465000
         B     SNSPV200             4  COMMAND REJECT          @D52BDAP 04466000
         B     SNSPV200             8  PERMANENT ERROR         @D52BDAP 04467000
SNSPV140 TM    SNSIDSET,X'C0'          IS THE DEVICE GROUPED   @D52BDAP 04468000
         BO    SNSPV1B0                YES,                    @D52BDAP 04469000
         TM    SNSIDSET,X'80'          IS DEVICE UNGROUPED     @D52BDAP 04470000
         BO    SNSPV160                YES,                    @D52BDAP 04471000
.*                                                             @D52BDAP 04472000
.*       RESET STATE                                           @D52BDAP 04473000
.*       PATH GROUP DOES NOT EXIST FOR CHANNEL, NOR FOR DEVICE @D52BDAP 04474000
.*                                                             @D52BDAP 04475000
SNSPV150 MVC   SNSIDSET(12),SNSPGID1   INITIALIZE PATH GROUP   @D52BDAP 04476000
.*                                                             @D52BDAP 04477000
.*       UNGROUPED STATE                                       @D52BDAP 04478000
.*       PATH GROUP EXISTS FOR CHANNEL, BUT NOT FOR OUR DEVICE @D52BDAP 04479000
.*                                                             @D52BDAP 04480000
SNSPV160 MVI   SNSIDSET,ZERO           FORCE SINGLE PATH MODE  @D52BDAP 04481000
         TM    PBXFLAG,PBXDASD+PBXTAPE IS THIS DASD OR TAPE    @D62TAAP 04482000
         BZ    SNSPV170                NO,                     @D62TAAP 04483000
         MVI   SNSIDSET,X'80'          TRY MULTI-PATH MODE 1ST @D52BDAP 04484000
SNSPV170 LA    RF,SNSCCWAF             SET PATH GROUP CCW      @D52BDAP 04485000
         ICM   RF,8,SCHLPM             GET PATH MASK IN BYTE 0 @D52BDAP 04486000
         BAL   R8,SNSIORTN             SET PATH GROUP          @D52BDAP 04487000
         B     SNSPV180             0  I/O COMPLETED SUCCESSF. @D52BDAP 04488000
         B     SNSPV190             4  COMMAND REJECT          @D52BDAP 04489000
         B     SNSPV200             8  PERMANENT I/O ERROR     @D52BDAP 04490000
SNSPV180 TM    SNSIDSET,X'80'          MULTI PATH MODE         @D52BDAP 04491000
         BZ    SNSPV200                NO,                     @D52BDAP 04492000
         TM    SCHFLGS,SCHD            DYNAMIC RECONNECT ACTIVE@D52VDAP 04493000
         BO    SNSPV200                YES,                    @D52VDAP 04494000
         B     SNSPV1C0                                        @D52BDAP 04495000
SNSPV190 TM    SNSIDSET,X'80'          DID WE ATTEMPT MULT.PATH@D52BDAP 04496000
         BZ    SNSPV200                NO,                     @D52BDAP 04497000
         TM    SCHFLGS,SCHD            DID WE DECIDE MULTI PATH@D52BDAP 04498000
         BZ    SNSPV1A0                NO, NOT YET             @D52BDAP 04499000
         OC    PBXNOERP(1),SCHLPM      DONT USE THIS PATH      @D52BDAP 04500000
         OC    PBXLPM(1),SCHLPM        ENSURE BIT TO BE        @D52VDAP 04501000
         XC    PBXLPM(1),SCHLPM        TURNED OFF IN LPM       @D52VDAP 04502000
         B     SNSPV210                                        @D52BDAP 04503000
SNSPV1A0 NI    SNSIDSET,X'FF'-X'80'    TURN OFF MULTI-PATH BIT @D52BDAP 04504000
         ICM   RF,8,SCHLPM             GET PATH MASK IN BYTE 0 @D52BDAP 04505000
         BAL   R8,SNSIORTN             SET PATH GROUP SINGLE   @D52BDAP 04506000
         B     SNSPV200             0  I/O COMPLETED SUCCESSF. @D52BDAP 04507000
         B     SNSPV200             4  COMMAND REJECT          @D52BDAP 04508000
         B     SNSPV200             8  PERMANENT I/O ERROR     @D52BDAP 04509000
.*                                                             @D52BDAP 04510000
.*       GROUPED STATE                                         @D52BDAP 04511000
.*       PATH GROUP EXISTS FOR CHANNEL AND FOR DEVICE          @D52BDAP 04512000
.*                                                             @D52BDAP 04513000
SNSPV1B0 DS    0H'0'                                           @D52BDAP 04514000
         TM    SNSIDSET,X'08'          OPERATING IN MULTI-PATH @D52BDAP 04515000
         BZ    SNSPV200                NO,                     @D52BDAP 04516000
SNSPV1C0 OI    SCHFLGS,SCHD            ENABLE  DYNAMIC RECONN. @D52BDAP 04517000
         LH    R1,PBXSCH               GET SUBCHANNEL NUMBER   @D52VDAP 04518000
         ICM   R1,12,H1                COMPLETE SUBCHANNEL WORD@D52VDAP 04519000
SNSPV1D0 MSCH  SCHIB                   MODIFY SUBCHANNEL       @D52BDAP 04520000
         BC    9,SNSPV200              SUBCHANNEL WAS AVAILABLE@D52BDAP 04521000
         SVC   33                      GET INTERRUPT PROCESSED @D52VDAP 04522000
         B     SNSPV1D0                TRY IT AGAIN            @D52BDAP 04523000
SNSPV200 SR    R1,R1                   CLEAR REG               @D52BDAP 04524000
         IC    R1,SCHLPM               GET LAST PATH USED      @D52BDAP 04525000
         EX    R1,SNSPEXOI             SET PBXLPM ACCORDINGLY  @D52BDAP 04526000
SNSPV210 EX    R1,SNSPEXXI             PATH VERIFY COMPLETED   @D52BDAP 04527000
         CLI   PBXPVPEN,ZERO           WAS THIS THE LAST/ONLY  @D52BDAP 04528000
         BE    SNSPV255                YES, ALL WORK IS DONE   @D52BDAP 04529000
SNSPV220 BCT   R2,SNSPV230             PROVIDE FOR NEXT PATH   @D52BDAP 04530000
         B     SNSPV240                                        @D52VDAP 04531000
SNSPV230 SRL   R1,1                    PROVIDE FOR NEXT PATH   @D52BDAP 04532000
         LTR   R1,R1                   WAS THIS THE LAST PATH  @D52VDAP 04533000
         BNZ   SNSPV090                NO,                     @D52BDAP 04534000
         LA    R1,X'80'                BEGIN WITH FIRST PATH   @D52VDAP 04535000
         B     SNSPV090                AGAIN.                  @D52VDAP 04536000
SNSPV240 CLI   PBXPVPEN,ZERO           ARE WE DONE             @D52BDAP 04537000
         BNE   SNSPV035                NO,  DO IT ALL OVER     @D52BDAP 04538000
         B     SNSPV255                RETURN TO MAIN LINE     @D52BDAP 04539000
         DROP  RE                      RELEASE SCHIB BASE      @DA44471 04540000
*                                                              @D52VDAP 04541000
*         SUBCHANNEL DOES NOT EXIST OR DEVICE-ID INVALID       @D52VDAP 04542000
*                                                              @D52VDAP 04543000
SNSPV250 SLR   R1,R1              CLEAR REGISTER               @D52BDAP 04544000
         BCTR  R1,0               SET TO ALL FFFFFFFF          @D52BDAP 04545000
         STH   R1,PBXSCH          INVALIDATE SUBCHANNEL        @D52BDAP 04546000
         OI    PBXFLAG3,PBXF3INS  FORCE INSTALL PROCESSING     @D52BDAP 04547000
         MVI   PBXCHPID,XFF       SET ALL CHPID'S INVALID      @D52BDAP 04548000
         MVC   PBXCHPID+1(L'PBXCHPID-1),PBXCHPID PROPAGATE FF  @D52BDAP 04549000
         MVI   PBXPVPEN,ZERO      RESET PATH INFORMATION       @D52BDAP 04550000
         MVI   PBXPIM,ZERO        RESET PATH INSTALLED         @D52BDAP 04551000
         MVI   PBXPAM,ZERO        RESET PATH AVAILABLE         @D52BDAP 04552000
         MVI   PBXLPM,ZERO        RESET LOGICAL PATH MASK      @D52BDAP 04553000
         B     SNSPV260           CHECK IF REQUEST IS PENDING  @D52VDAP 04554000
SNSPV255 NI    PBXFLAG3,XFF-PBXF3INS-PBXF3PVT  PROCESSING DONE @D52VDAP 04555000
SNSPV260 CLI   PUBCHQPT,NOTQUED   IS I/O REQUEST ENQUEUED      @DA44311 04556000
         BNE   SNSPV270           YES, GET IT STARTED          @DA44311 04557000
         AIF   (NOT &VSE280).N280280                           @DSCSI   04558490
         CLC   PBXSCH(2),XFFFF    DID SUBSCHANNEL BECOME READY @DSCSI   04559000
         BE    SNSPV010           NO, PROCESS NEXT DEVICE      @DSCSI   04560000
         CLI   PUBDEVTY,TQDIO     IS THIS A QDIO DEVICE        @DSCSI   04561000
         BNE   SNSPV265           NO,                          @DSCSI   04562000
         CLI   PUBOPTN,PUBOPFCP   IS THIS THE FCP ADAPTER      @DSCSI   04563000
         BE    SNSPV270           YES,                         @DSCSI   04564000
.N280280 ANOP                                                  @DSCSI   04565490
SNSPV265 CLI   PUBDEVTY,TOSA      IS THIS THE OSA CARD         @DOSA    04566000
         BNE   SNSPV010           NO,                          @DOSA    04567000
         CLI   PUBOPTN,PUBOPOFE   IS IT THE FE CONTROLLER PORT @DOSA    04568000
         BNE   SNSPV010           NO,                          @DOSA    04569000
         ICM   R8,15,PBXATTIB     LOAD CONTROLLING TIB POINTER @DOSA    04570000
         BZ    SNSPV010           NONE EXISTING                @DOSA    04571000
         USING TIBADR,R8          TIB BASE POINTER             @DOSA    04572000
         ICM   RF,15,PBXATECB     GET ECB ADDRESS              @DOSA    04573000
         BZ    SNSPV010           NONE EXISTING                @DOSA    04574000
         LH    RA,TIBRTID         GET TASK ID                  @DOSA    04575000
         SGSETUP SWOWNER,STASK=SNS,WR1=RA,WR2=R7,OPTION=SETOWNER OSA    04576000
         OI    2(RF),TRABIT       POST THE PROPER ECB          @DOSA    04577000
         LA    RA,SNSTID          SNS TASK ID                  @DOSA    04578000
         SGSETUP SWOWNER,STASK=SNS,WR1=RA,WR2=R7,OPTION=SETOWNER OSA    04579000
         BAL   RF,IOPOST1         POST I/O COMPLETE IF I/O BND @DOSA    04580000
*SGLINK  IOPOST1,INPUT=(R6,R8,RF),WORK=RE                      @DOSA    04581000
         B     SNSPV010           PROCESS NEXT DEVICE          @DOSA    04582000
         DROP  R8                 RELEASE TIB BASE             @DA44277 04583000
SNSPV270 LH    R8,PUBCHANN        GET CUU ADDRESS              @D52VDAP 04584000
         L     RE,TCBPTR          POINT TO SENSE TASK TCB      @D52VDAP 04585000
         USING TCBADR,RE          TCB BASE POINTER             @D52VDAP 04586000
         L     RE,TCBSAVE         ADDRESS OF SNS SAVE AREA     @D52VDAP 04587000
         USING SVEARA,RE          MAKE IT ADDRESSABLE          @D52VDAP 04588000
         LA    RF,SNSPV010        CONT. ADDR. AFTER DEV RESTART@D68REEF 04589000
         SVC   33                 SAVE OUR CURRENT STATUS      @D52VDAP 04590000
         STCM  RF,M7,SVEPSW2+1    PROVIDE FOR PROPER REDISPATCH@D52VDAP 04591000
         ST    R8,IOINF           SET CUU ADDRESS IN LOW CORE  @DY46074 04592000
         ST    R3,INTPARM         SET INTERRUPT PARAMETER      @DY46074 04593000
         L     R9,AINTER          GET IOINTER BASE ADDRESS     @D52VDAP 04594000
         USING INTRTN,R9          IOINTER BASE POINTER         @D52VDAP 04595000
         AIF   (NOT &VSE280).N280300                           @DSCSI   04596490
         CLC   PUBDEVTY(2),TFCPID IS THIS THE FCP ADAPTER      @DSCSI   04597000
         BNE   SNSPVINIT          NO,                          @DSCSI   04598000
         XC    CSW(8),CSW         CLEAR WHOLE CSW              @DSCSI   04599000
         MVI   DEVSTA,DEVEND      SIMULATE DEVICE END          @DSCSI   04600000
         BR    R9                      ======================> @DSCSI   04601000
.N280300 ANOP                                                  @DSCSI   04602490
         DROP  RE                 RELEASE SAVE AREA BASE       @DSCSI   04603000
.*                                                                      04604000
.*       THE PENDING I/O OPERATION WILL FIRST BE STARTED BEFORE         04605000
.*       THE PVT PROCESS WILL BE RESUMED.                               04606000
.*       SNS-TASK SAVE AREA HAS BEEN SET UP PROPERLY                    04607000
.*                                                                      04608000
.YREF020 ANOP                                                           04609000
SNSPVINIT  DS  0H'0'              GET PENDING I/O STARTED      @D68REEF 04610000
         SLR   R4,R4              CLEAR REGISTER               @DA44364 04611000
         IC    R4,PUBCHQPT        GET CHANQ POINTER            @DA44364 04612000
         AIF   (&AG1 LE 254).NMDV180                           @DA44364 04613000
         ICM   R4,2,PUBCHQPH      INCLUDING HIGH BYTE          @DA44364 04614000
.NMDV180 ANOP                                                  @DA44364 04615000
         SLL   R4,CHQSLEN         MULTIPLY WITH ENTRY LENGTH   @DA44364 04616000
         A     R4,ACHANQ          ABSOLUTE ENTRY ADDRESS       @DA44364 04617000
         ICM   R4,8,H0            CLEAR CHAINING BYTE          @D68REEF 04618000
         USING CHQADR,R4          CHANQ BASE POINTER           @DA44364 04619000
         BAL   RF,INTERTRN        PROVIDE FOR RESTART          @D52VDAP 04620000
         USING PBXADR,R2          PUBX BASE POINTER            @DA44364 04621000
         CLC   PBXSCH,XFFFF       IS SUBCHANNEL VALID          @DA44364 04622000
         BNE   INITRG             YES, ======================> @DA44364 04623000
         TM    PBXFLAG,PBXFBAVD   IS THIS A VDISK DEVICE       @DA44634 04624000
         BZ    SNSPVI40           NO,                          @DA44634 04625000
         OC    PBXSIMAD(4),PBXSIMAD IS THIS VIRTUAL FBA DEVICE @DA44634 04626000
         BNZ   INITRG             YES, ======================> @DA44634 04627000
SNSPVI40 TM    PUBCSFLG,DEVBSY    IS DEVICE BUSY               @DA44364 04628000
         BZ    INITRG             NO,  ======================> @DA44364 04629000
         TM    CHQCCSIO,CHQCCACT  HAS DEVICE BEEN STARTED      @DA44364 04630000
         BZ    INITRG             NO,  ======================> @DA44364 04631000
         XC    CSW(8),CSW         CLEAR WHOLE CSW              @DA44364 04632000
         MVI   DEVSTA,DEVEND      PREPARE FOR ENDING STATUS    @DA44462 04633000
         OC    CHQCCBAD+1(3),CHQCCBAD+1 USER ALREADY POSTED    @DA44462 04634000
         BZR   R9                 YES, =====================>> @DA44462 04635000
         MVI   CSW,DELAYINT       SIMULATE DEFERRED CC=3       @DA44364 04636000
         BR    R9                      =====================>> @DA44364 04637000
         DROP  R2                 RELEASE PUBX BASE            @DA44364 04638000
         DROP  R4                 RELEASE CHANQ BASE           @DA44364 04639000
         DROP  R9                 RELEASE IOINTER BASE         @D52VDAP 04640000
         USING PBXADR,R9          PUBX BASE POINTER            @D52VDAP 04641000
         AIF   (&VSE270).YREF040                                        04642490
SNSPV290 DS    0H'0'              ENTRY POINT AFTER RESTART    @DELETE  04643000
         B     SNSPV010           GO CHECK FOR ANOTHER REQUEST @DELETE  04644000
.YREF040 ANOP                                                           04645000
         SPACE ,                                               @D52BDAP 04646000
*SGLINK  SNSPV300,INPUT=(R8,R9),WORK=(R5,RA,RF)                @D68ADAP 04647000
SNSPV300 CLI   SNSDEVID,XFF       SENSE ID VALID               @D52VDAP 04648000
         BNE   0(,R8)             NO,  =====================>> @D52VDAP 04649000
         MVC   PBXSNSID(7),SNSDEVID UPDATE SENSE-ID INFORMATION@D52VDAP 04650000
         LA    RF,SNSCIWID        POINT TO COMMAND INFO WORD   @D52VDAP 04651000
SNSPV310 TM    0(RF),X'40'        IS THIS A CIW                @D52VDAP 04652000
         BZ    SNSPV340           NO,                          @D66ADAP 04653000
         NI    0(RF),X'BF'        RESET CIW INDICATOR          @D66ADAP 04654000
         CLI   0(RF),X'00'        IS THIS A RCD CIW            @D66ADAP 04655000
         BNE   SNSPV330           NO                           @D66ADAP 04656000
         CLI   PBXRCDCC,XFF       INITIAL VALUE                @D52VDAP 04657000
         BE    SNSPV320           YES,                         @D52VDAP 04658000
         CLC   PBXRCDCC(1),1(RF)  SAME COMMAND CODE            @D52VDAP 04659000
         BNE   8(R8)              NO,  ======================> @D52VDAP 04660000
SNSPV320 MVC   PBXRCDCC(3),1(RF)  SAVE RCD COMMAND AND LENGTH  @D52VDAP 04661000
         B     SNSPV340           PROCEED MAIN LINE            @D28DR06 04662000
SNSPV330 DS    0H'0'                                           @D66ADAP 04663000
         CLI   PBXPUBCD,TQDIO     IS THIS A QDIO DEVICE        @D66ADAP 04665000
         BNE   SNSPV340           NO,                          @D66ADAP 04666000
SNSPVQ00 CLI   0(RF),X'03'        IS THIS THE ESTABLISCH CMD   @D66ADAP 04667000
         BE    SNSPVQ10           YES, SAVE THE DATA           @D66ADAP 04668000
         CLI   0(RF),X'04'        IS THIS THE ACTIVATE   CMD   @D66ADAP 04669000
         BNE   SNSPBQRT           NO, JOIN MAIN LINE           @D66ADAP 04670000
         MVC   PBXQACT(3),1(RF)   SAVE ACTIVATE COMMAND INFO   @D66ADAP 04671000
         B     SNSPBQRT           JOIN MAIN LINE               @D66ADAP 04672000
SNSPVQ10 MVC   PBXQEST(3),1(RF)   SAVE ACTIVATE COMMAND INFO   @D66ADAP 04673000
         B     SNSPBQRT           JOIN MAIN LINE               @D66ADAP 04674000
         EJECT                                                 @D52BDAP 04675000
SNSPBQRT DS    0H'0'              QDIO SPECIAL PROCESSING      @D66EDAP 04676000
         AIF   (NOT &VSE280).N280320                           @D68ADAP 04677490
         STM   RE,R5,SNSPBQSV     SAVE REGISTERS               @D66ADAP 04678000
         L     RE,ASNSPBQ00       GET ROUTINE ADDRESS          @D66EDAP 04679000
         BASSM R0,RE              GET INTO 31-BIT MODE         @D66EDAP 04680000
ASNSPBQ00 DC   A(SNSPBQ00+X'80000000')                         @D55EDAP 04681000
SNSPBQ00 DS    0H'0'              CHSC SPECIAL PROCESSING      @D68ADAP 04682000
         LA    RE,SNSPV340        EXIT ADDRESS                 @D68ADAP 04683000
         L     R5,AINTER          IOINTER BASE ADDRESS         @D68ADAP 04684000
         USING INTRTN,R5          IOINTER BASE POINTER         @D68ADAP 04685000
         L     R2,AINTTAB1        ADDR. OF CHARACTERISTICS     @D68ADAP 04686000
         USING INTTAB1,R2         INTTAB1 BASE POINTER         @D28DR06 04687000
         CLI   CHSC#FLG,XFF       IS CHSC SUPPORTED            @D68ADAP 04688000
         BNE   SNSPBQXT           NO, SKIP THE WHOLE PROCESS   @D68ADAP 04689000
         ICM   R1,15,ACHSC#BA     GET BUFFER ADDRESS           @D28DR06 04690000
         BZ    SNSPBQXT           NO, SKIP THE WHOLE PROCESS   @D28DR06 04691000
         XC    0(16,R1),0(R1)     CLEAR REQUEST BLOCK          @D28DR06 04692000
         MVI   1(R1),X'10'        LENGTH IS 4 FULLWORDS        @D28DR06 04693000
         MVI   3(R1),X'10'        STORE CH.SUBSYS CHARACSTIC.  @D28DR06 04694000
         LA    R4,16(,R1)         RESPONSE BUFFER ADDRESS      @D28DR06 04695000
         DC    XL4'B25F0010'      GET SUBSYS.CHARACTERIST.     @D28DR06 04696000
         CLC   2(2,R4),H1         IS RESPONSE CODE VALID       @D28DR06 04697000
         BE    SNSPBQ10           YES, PROCEED                 @D28DR06 04698000
         MVI   CHSC#FLG,ZERO      INDICATE DATA NOT VALID      @D28DR06 04699000
         B     SNSPBQXT           AND EXIT                     @D28DR06 04700000
SNSPBQ10 LA    R5,CHSC#GEN        POINT TO GENERAL CHARACSTICS @D68ADAP 04701000
         USING CHSC#GEN,R5        CHARACTERISTIC TABLE BASE    @D68ADAP 04702000
         MVC   CHSC#GEN(32),8(R4) SAVE GENERAL-CHARACTERISTICS @D28DR06 04703000
         LA    RA,CHSC#CHS        ADDR. OF CHSC-CHARACTERSTICS @D68ADAP 04704000
         USING CHSC#CHS,RA        CHN.SUBSYS CHARACTERISTICS   @D68ADAP 04705000
         MVC   CHSC#CHS(32),512*4(R4) CHSC CHARACTERISTICS     @D28DR06 04706000
         TM    CHSC#CHS+8/8,X'80' IS STORE-QDIO-DATA SUPPORTED @D68ADAP 04707000
         BZ    SNSPBQXT           NO, SKIP IT ALL              @D68ADAP 04708000
         XC    0(16,R1),0(R1)     CLEAR REQUEST BLOCK          @D28DR06 04709000
         MVI   1(R1),X'10'        LENGTH IS 16 BYTES           @D66EDAP 04710000
         MVI   3(R1),X'24'        SUBCHANNEL QDIO REQUEST      @D66EDAP 04711000
         MVC   6(2,R1),PBXSCH     FIRST SUBCHANNEL NUMBER      @D66EDAP 04712000
         MVC   10(2,R1),PBXSCH    LAST  SUBCHANNEL NUMBER      @D66EDAP 04713000
         DC    XL4'B25F0010'      GET QDIO DATA                @D66EDAP 04714000
         LA    RF,16(,R1)         POINT TO RESPONSE BUFFER     @D66EDAP 04715000
         CLC   2(2,RF),H1         IS RESPONSE CODE VALID       @D66EDAP 04716000
         BNE   SNSPBQXT           NO, LEAVE IT ALL UNCHANGED   @D66EDAP 04717000
         LA    RF,8(,RF)          SKIP HEADER BYTES            @D66EDAP 04718000
         TM    0(RF),X'C0'        IS THIS A VALID ENTRY        @D66EDAP 04719000
         BNO   SNSPBQXT           NO, LEAVE IT ALL UNCHANGED   @D66EDAP 04720000
         MVI   PUBOPTN,ZERO       RESET OLD SETTINGS           @D68ADAP 04721000
         NI    PBXQFLG1,XFF-PBXQAINT-PBXQSCSC-PBXQSCSF RESET   @D68ADAP 04722000
         CLI   4(RF),2            IQDIO DEVICE (HYPER-SOCKET)  @D66EDAP 04723000
         BH    SNSPBQ40           NO, UNSUPPORTED DEVICE       @D66EDAP 04724000
         BE    SNSPBQ30           YES, IT IS A IQDIO DEVIVE    @D66EDAP 04725000
         CLI   4(RF),0            OSA EXPRESS                  @D66EDAP 04726000
         BE    SNSPBQ40           YES, THIS IS OSAX DEVICE     @D66EDAP 04727000
         MVI   PUBOPTN,PUBOPFCP   INDICATE QDIO-FCP DEVICE     @D68ADAP 04728000
         B     SNSPBQ40           JOIN MAIN LINE               @D66EDAP 04729000
SNSPBQ30 MVI   PUBOPTN,PUBOPIQD   INDICATE HYPER-SOCKET        @D68ADAP 04730000
SNSPBQ40 MVC   PBXQQFMT(1),4(RF)  SAVE QUEUE FORMAT            @D66EDAP 04731000
         MVC   PBXQFEAT(1),6(RF)  ADAPTER CHARACTERISTICS      @D66EDAP 04732000
         MVC   PBXQIQNO(1),9(RF)  NO. OF INPUT  QUEUES         @D66EDAP 04733000
         MVC   PBXQOQNO(1),11(RF) NO. OF OUTPUT QUEUES         @D66EDAP 04734000
         CLI   PUBOPTN,PUBOPIQD   HYPER-SOCKET DEVICE          @D68ADAP 04735000
         BNE   SNSPBQ50           NO,                          @D68ADAP 04736000
         TM    CHSC#GEN+41/8,X'40' ADAP.-INTERR.-FACITY SUPP.  @D68ADAP 04737000
         BO    SNSPBQ60           YES, INDICATE IS PRESENT     @D68ADAP 04738000
         B     SNSPBQ70           NO, JOIN MAIN LINE           @D68ADAP 04739000
SNSPBQ50 TM    CHSC#GEN+67/8,X'10' QDIO ADAPT.INTERR.FACLTY    @D68ADAP 04740000
         BZ    SNSPBQ70           NOT PROVIDED                 @D68ADAP 04741000
SNSPBQ60 OI    PBXQFLG1,PBXQAINT  DEV. SUPP. THIN INTERRUPTS   @D68ADAP 04742000
SNSPBQ70 TM    CHSC#CHS+107/8,X'10' SET-CHARAC. CMD SUPPORTED  @D68ADAP 04743000
         BZ    SNSPBQ80           NO                           @D68ADAP 04744000
         OI    PBXQFLG1,PBXQSCSC  SET-CHARSTIC SUPPORTED       @D68BDAP 04745000
SNSPBQ80 TM    CHSC#CHS+108/8,X'08' SET-CHARSTIC-FAST SUPP.    @D68ADAP 04746000
         BZ    SNSPBQXT           NO,                          @D68ADAP 04747000
         OI    PBXQFLG1,PBXQSCSF  SET CHSC FAST AVAIL.         @D68ADAP 04748000
         DROP  RA                 RELEASE CHSC#CHS BASE        @D68ADAP 04749000
SNSPBQXT DC    0H'0'              EXIT PROCESSING              @D68ADAP 04750000
         LR    RA,RE              COPY EXIT ADDRESS            @D68ADAP 04751000
         LM    RE,R5,SNSPBQSV     RESTORE REGISTERS (RE-R5)    @D66ADAP 04752000
         BSM   0,RA               TAKE SPECIFIED EXIT          @D68ADAP 04753000
         DROP  R5                 RELEASE CHSC#GEN BASE        @D68ADAP 04754000
         DROP  R2                 RELEASE INTTAB1 BASE POINTER @D68ADAP 04755000
         AGO   .Y280100                                        @D68ADAP 04756000
.N280320 ANOP                                                  @D68ADAP 04757290
         AIF   (&VSE270).YREF060                               @D68PGA  04757580
         TM    SYSFLAG2,IPLBIT    IS IPL IN PROCESS            @D28DR06 04758000
         BO    SNSPV340           YES, SKIP PROCESS            @D28DR06 04759000
         L     RA,ASNSPBQ00       GET ROUTINE ADDRESS          @D66EDAP 04760000
         BASSM R0,RA              GET INTO 31-BIT MODE         @D66EDAP 04761000
ASNSPBQ00 DC   A(SNSPBQ00+X'80000000')                         @D55EDAP 04762000
SNSPBQ00 LR    RA,RF              SAVE REGISTER                @D66EDAP 04763000
         L     R0,F4096           GET LENGTH                   @D66EDAP 04764000
*        GETVIS PFIX=YES,ADDRESS=(1),LENGTH=(0),PAGE=YES,SVA=YES,      X04765000
               LOC=ANY,SPID=QDIONAME                           @D66EDAP 04766000
         GETVIS PFIX=YES,ADDRESS=(1),LENGTH=(0),PAGE=YES,SVA=YES,      X04767000
               LOC=ANY,SPID=QDIONAME                           @D66EDAP 04768000
         LTR   RF,RF              DID WE GET THE STORAGE       @D66EDAP 04769000
         BZ    SNSPBQ20           YES, ====================>>> @D66EDAP 04770000
         LR    RF,RA              RESTORE REGISTER             @D66BDAP 04771000
         LA    RA,SNSPV250        24-BIT ADDRESS               @D66BDAP 04772000
         BSM   0,RA               CONTINUE IN 24-BIT MODE      @D66BDAP 04773000
SNSPBQ20 MVI   1(R1),X'10'        LENGTH IS 16 BYTES           @D66EDAP 04774000
         MVI   3(R1),X'24'        SUBCHANNEL QDIO REQUEST      @D66EDAP 04775000
         MVC   6(2,R1),PBXSCH     FIRST SUBCHANNEL NUMBER      @D66EDAP 04776000
         MVC   10(2,R1),PBXSCH    LAST  SUBCHANNEL NUMBER      @D66EDAP 04777000
         DC    XL4'B25F0010'      GET QDIO DATA                @D66EDAP 04778000
         LA    RF,16(,R1)         POINT TO RESPONSE BUFFER     @D66EDAP 04779000
         CLC   2(2,RF),H1         IS RESPONSE CODE VALID       @D66EDAP 04780000
         BNE   SNSPBQXT           NO,                          @D66EDAP 04781000
         LA    RF,8(,RF)          POINT TO QDIO ENTRY          @D66EDAP 04782000
         TM    0(RF),X'C0'        IS THIS A VALID ENTRY        @D66EDAP 04783000
         BNO   SNSPBQXT           NO,                          @D66EDAP 04784000
         NI    PUBOPTN,XFF-PUBOPIQD RESET HYPER SOCKET BIT     @D66EDAP 04785000
         CLI   4(RF),2            HYPER SOCKET DEVICE          @D66EDAP 04786000
         BH    SNSPBQXT           NO, UNSUPPORTED DEVICE       @D66EDAP 04787000
         BE    SNSPBQ30           YES, IT IS HYPER SOCKET      @D66EDAP 04788000
         CLI   4(RF),0            OSA EXPRESS                  @D66EDAP 04789000
         BE    SNSPBQ40           YES, IS SUPPORTED            @D66EDAP 04790000
         B     SNSPBQXT           NO, UNSUPPORTED DEVICE       @D66EDAP 04791000
SNSPBQ30 OI    PUBOPTN,PUBOPIQD   INDICATE HYPER SOCKET        @D66EDAP 04792000
SNSPBQ40 MVC   PBXQQFMT(1),4(RF)  SAVE QUEUE FORMAT            @D66EDAP 04793000
         MVC   PBXQFEAT(1),6(RF)  ADAPTER CHARACTERISTICS      @D66EDAP 04794000
         MVC   PBXQIQNO(1),9(RF)  NO. OF INPUT  QUEUES         @D66EDAP 04795000
         MVC   PBXQOQNO(1),11(RF) NO. OF OUTPUT QUEUES         @D66EDAP 04796000
.*                                                                      04797000
.*       GET  CHANNEL SUBSYSTEM CHARACTERISTICS                         04798000
.*                                                                      04799000
         XC    0(256,R1),0(R1)    CLEAR FIRST PART  ONLY       @D66EDAP 04800000
         MVI   1(R1),X'10'        LENGTH OF REQ. BLOCK         @D66BDAP 04801000
         MVI   3(R1),X'10'        CHANNEL SUBSYS. CHAR.CMD     @D66BDAP 04802000
         DC    XL4'B25F0010'      GET DATA                     @D66BDAP 04803000
         LA    RF,16(,R1)         ADDR OF RESPONSE BUFFER      @D66BDAP 04804000
         CLC   2(2,RF),H1         IS RESPONSE CODE VALID       @D66BDAP 04805000
         BNE   SNSPBQXT           NO,                          @D66BDAP 04806000
         CLI   PUBOPTN,PUBOPIQD   IS THIS HYPER SOCKET         @DY46075 04807000
         BNE   SNSPBQ50           NO, MUST BE OSA EXPRESS      @DY46075 04808000
         TM    13(RF),X'40'       ADAP.-INT.-FAC.(BIT 41)      @D66BDAP 04809000
         BO    SNSPBQ60           IS BEING PROVIDED            @DY46075 04810000
         B     SNSPBQ70           NOT PROVIDED                 @DY46075 04811000
SNSPBQ50 TM    16(RF),X'10'       QDIO ADAP.-INT-FAC. (BIT 67) @DY46075 04812000
         BZ    SNSPBQ70           NOT PROVIDED                 @DY46075 04813000
SNSPBQ60 OI    PBXQFLG1,PBXQAINT  ADAP.-INT.-FAC. AVAIL.       @DY46075 04814000
SNSPBQ70 TM    512*4+107/8(RF),X'10' SET-CHSC SUP (B107)       @D66BDAP 04815000
         BZ    SNSPBQ80           NO                           @D66BDAP 04816000
         OI    PBXQFLG1,PBXQSCSC  SET-CHSC COMMAND SUPPORT     @D66BDAP 04817000
SNSPBQ80 TM    512*4+108/8(RF),X'08' SET-CHSC SUP (B108)       @D66BDAP 04818000
         BZ    SNSPBQXT           NO                           @D66BDAP 04819000
         OI    PBXQFLG1,PBXQSCSF  SET CHSC FAST AVAIL.         @D66BDAP 04820000
SNSPBQXT DS    0H'0'              BACKEND PROCESSING           @D66BDAP 04821000
*        FREEVIS ADDRESS=(1),LENGTH=(0),SVA=YES                @D66BDAP 04822000
         FREEVIS ADDRESS=(1),LENGTH=(0),SVA=YES                @D66BDAP 04823000
         LTR   RF,RF              DID FREEVIS WORK             @D66BDAP 04824000
         BZ    SNSPBQOK           YES,                                  04825000
         BAL   R5,SYSERROR        BETTER STOP RIGHT AWAY       @D66BDAP 04826000
SNSPBQOK LR    RF,RA              RESTORE REGISTER             @D66BDAP 04827000
         LA    RA,SNSPV340        24-BIT ADDRESS               @D66BDAP 04828000
         BSM   0,RA               CONTINUE IN 24-BIT MODE      @D66BDAP 04829000
         SPACE 1                                                        04830000
QDIONAME DC    XL8'00'            SUBPOOL STRING                        04831000
         ORG   QDIONAME           OVERLAY                               04832000
         DC    CL6'$QNAME'        SUBPOOL NAME                          04833000
         ORG   ,                                                        04834000
.Y280100 ANOP                                                           04835000
SNSPBQSV DC    8F'0'              SAVE AREA                             04836000
.YREF060 ANOP                                                  @D68PGA  04838990
SNSPV340 LA    RF,4(,RF)          POINT TO NEXT CIW            @D52VDAP 04841000
         C     RF,SNSCIWEN        WAS THIS THE LAST CIW        @D52VDAP 04842000
         BL    SNSPV310           NO, CHECK NEXT ENTRY         @D52VDAP 04843000
         CLI   PBXRCDCC,XFF       DID WE FIND VALID INFO       @D66ADAP 04844000
         BNE   8(R8)              YES, ======================> @D66ADAP 04845000
         B     4(R8)              NO,  ======================> @D52VDAP 04846000
         SPACE ,                                               @D52VDAP 04847000
SNSPEXTM TM    PBXPVPEN,*-*            TEST IF BIT IS ON       @D52BDAP 04848000
SNSPEXTP TM    PBXPIM,*-*              TEST IF BIT IS ON       @DA42602 04849000
SNSPEXXI XI    PBXPVPEN,*-*            FORCE THIS BIT OFF      @D52BDAP 04850000
SNSPEXOI OI    PBXLPM,*-*              SET BIT IN LPM          @D52BDAP 04851000
SNSPEXS1 ICM   R8,0,PBXCHPID           INVALIDATE UNUSED 0-3   @DA42922 04852000
SNSPEXS2 ICM   R8,0,PBXCHPID+4         INVALIDATE UNUSED 4-7   @DA42922 04853000
         DROP  R3                      RELEASE PUB BASE        @D52BDAP 04854000
         DROP  R9                      RELEASE PUBX BASE       @D52BDAP 04855000
         AIF   (&VSE270).YREF080                                        04856490
         EJECT ,                                               @D52BDAP 04857000
SNSCPUID DC    D'0'                    CPUID                   @D52BDAP 04858000
SNSINSRG DC    5F'0'                   REGISTER RE - R2 SAVE   @D52BDAP 04859000
SNSPGID1 DC    XL12'00'                VSE PATH GROUP ID       @D52BDAP 04860000
         ORG   *-12                    OVERLAY                 @D52BDAP 04861000
         DC    X'00'                   MULTI-PATH MODE         @D52BDAP 04862000
SNSSPGFM DC    X'80'                   FORMAT IDENTITIER       @D66ADAP 04863000
SNSSPGID DC    XL6'00'                 CPU IDENTIFICATION NO.  @D52BDAP 04864000
         DC    C'*VSE'                 UNIQUE VSE-ID           @D66ADAP 04865000
         ORG   ,                                               @D52BDAP 04866000
         SPACE ,                                               @D52BDAP 04867000
         EJECT                                                 @D52BDAP 04868000
         USING PUBADR,R3               PUB BASE POINTER        @D52BDAP 04869000
         USING PBXADR,R9               PUBX BASE POINTER       @D52BDAP 04870000
         USING SCHIB,RE                SCHIB BASE POINTER      @D61ADAP 04871000
*SGLINK  SNSNEDPR,INPUT=(R3,R8,R9,RE),OUTPUT=RF                @D52BDAP 04872000
SNSNEDPR DS    0H'0'                                           @D52BDAP 04873000
         LA    RF,SNSRCDIO             POINT TO DATA BUFFER    @D52BDAP 04874000
SNSNED10 TM    0(RF),X'C0'             IS THIS A NED           @D52BDAP 04875000
         BO    SNSNED30                YES, DO NED PROCESSING  @D61ADAP 04876000
         TM    0(RF),X'80'             IS THIS THE GENERAL NEQ @D61ADAP 04877000
         BO    SNSNEQPR                YES, DO NEQ PROCESSING  @D61ADAP 04878000
SNSNED20 LA    RF,32(,RF)              ADVANCE TO NEXT ENTRY   @D61ADAP 04879000
         B     SNSNED10                PRECESS NEXT ENTRY      @D61ADAP 04880000
.*                                                             @D61ADAP 04881000
.*       DO NED PROCESSING                                     @D61ADAP 04882000
.*                                                             @D61ADAP 04883000
SNSNED30 CLI   1(RF),1                 IS IT AN I/O DEVICE TYPE@D52BDAP 04884000
         BNE   SNSNED20                NO,                     @D52BDAP 04885000
         CLI   2(RF),1                 IS THIS A DASD          @D52BDAP 04886000
         BNE   SNSNED40                NO                      @D52BDAP 04887000
         TM    PBXFLAG,PBXDASD         DEVICE ADDED AS DASD    @D52BDAP 04888000
         B     SNSNED50                DO FINAL CHECK          @D52BDAP 04889000
SNSNED40 CLI   2(RF),2                 IS THIS A TAPE          @D52BDAP 04890000
         BNE   SNSNED60                NO, ACCEPT AS IS        @D61NDAP 04891000
         TM    PBXFLAG,PBXTAPE         DEVICE ADDED AS TAPE    @D52BDAP 04892000
SNSNED50 BNO   SNSNED80                NO, FORCE PATH DOWN     @D52BDAP 04893000
         MVC   PBXSNSID,6(RF)          SAVE DEVICE NED IN PUBX @TEST    04894000
SNSNED60 DS    0H'0'                                           @D61ADAP 04895000
         OC    PBXDVNED,PBXDVNED       IS DEVICE NED PRESENT   @D61ADAP 04896000
         BNZ   SNSNED70                YES, VERIFY THIS PATH   @D61ADAP 04897000
         MVC   PBXDVNED,13(RF)         INITIALIZE NED FIELD    @D61ADAP 04898000
         CLI   PBXPUBCD,TQDIO          IT IT QDIO DEVICE       @D66ADAP 04900000
         BNE   SNSNED70                NO,                     @D66ADAP 04901000
         MVC   PBXRCDCA(2),30(RF)      GET CHPID AND DEV ADDR. @D66ADAP 04902000
         MVC   PBXQCULA(1),63(RF)      GET CNTRL UNIT IMAGE ID @D67FDMZ 04903000
SNSNED70 CLC   PBXDVNED,13(RF)         SAME DEVICE             @D61ADAP 04905000
         BNE   SNSNED80                NO,                     @D61ADAP 04906000
         LA    R8,4(,R8)               FORCE OK RETURN         @D61ADAP 04907000
         B     SNSNEQPR                DO NEQ PROCESSING       @D61ADAP 04908000
SNSNED80 OC    PBXNOPVF(1),SCHLPM      PATH VERIFY FAILURE     @D61ADAP 04909000
.*                                                             @D61ADAP 04910000
.*       DO NEQ PROCESSING                                     @D61ADAP 04911000
.*                                                             @D61ADAP 04912000
SNSNEQPR TM    0(RF),X'C0'             IS THIS A NED           @D61ADAP 04913000
         BO    SNSNEQ10                YES, SKIP IT            @D61ADAP 04914000
         TM    0(RF),X'80'             IS THIS THE GENERAL NEQ @D61ADAP 04915000
         BO    SNSNEQ20                YES,                    @D61ADAP 04916000
         TM    0(RF),X'40'             IS THIS END OF DATA     @D61ADAP 04917000
         BO    0(,R8)                  YES,  ================> @D61ADAP 04918000
SNSNEQ10 LA    RF,32(,RF)              ADVANCE TO NEXT ENTRY   @D61ADAP 04919000
         B     SNSNEQPR                                        @D61ADAP 04920000
SNSNEQ20 DS    0H'0'                   NEQ PROCESSING          @D66ADAP 04921000
         TM    PBXFLAG,PBXDASD         DEVICE ADDED AS DASD    @D66ADAP 04922000
         BZ    0(,R8)                  NO,  =================> @D66ADAP 04923000
         TM    17(RF),X'40'            ESCON ATTACHED          @D66ADAP 04924000
         BZ    0(,R8)                  NO,  =================> @D66ADAP 04925000
         OC    PBXRCDCA(2),PBXRCDCA    INFO ALREADY PRESENT    @D61ADAP 04926000
         BNZ   0(,R8)                  YES, =================> @D61ADAP 04927000
         MVC   PBXRCDCA(1),11(RF)      SAVE CCA                @D61ADAP 04928000
         MVC   PBXRCDDC(1),13(RF)      SAVE DDC                @D61ADAP 04929000
         B     0(,R8)                  DONE =================> @D61ADAP 04930000
         DROP  R3                      RELEASE PUB BASE        @D52BDAP 04931000
         DROP  R9                      RELEASE PUBX BASE       @D52BDAP 04932000
         DROP  RE                      RELEASE SCHIB BASE      @D61ADAP 04933000
         EJECT ,                                               @D68REEF 04934000
.YREF080   ANOP                                                         04935490
           USING INTRTN,R9           IOINTER BASE              @D68REEF 04936000
           USING TIBADR,RA           TIB     BASE              @D68REEF 04937000
SUPSOWN    DS    0H'0'               SET SERVICE OWNER         @D68REEF 04938000
           L     R6,DISPAD           DISPATCHER BASE           @D68REEF 04939000
           USING DISP,R6             DISP    BASE              @D68REEF 04940000
*SGLINK    SUPSOWN,INPUT=(R6,R9,RA,RE)                         @D68REEF 04941000
           SGSETUP SWOWNER,STASK=SNS,WR1=RA,WR2=R7,OPTION=SETOWNER REEF 04942000
*          SGSETUP SWOWNER,STASK=SNS,WR1=RA,WR2=R7,OPTION=SETOWNER REEF 04943000
           BSM   0,RE               RETRN=====================>@D68REEF 04944000
           DROP  R6                 RELEASE DISP    BASE       @D68REEF 04945000
           DROP  R9                 RELEASE IOINTER BASE       @D68REEF 04946000
           DROP  RA                 RELEASE TIB     BASE       @D68REEF 04947000
           SPACE 1                                             @D68REEF 04948000
           USING TIBADR,R8          TIB     BASE               @D68REEF 04949000
           USING INTRTN,R9          IOINTER BASE               @D68REEF 04950000
*SGLINK  SUPPOST1,INPUT=(R6,R8,R9,RE,RF)                       @D68REEF 04951000
SUPPOST1   DS    0H'0'              POST USER TASK             @D68REEF 04952000
           L     R6,DISPAD          DISPATCHER BASE            @D68REEF 04953000
           USING DISP,R6            DISP    BASE               @D68REEF 04954000
           BAL   RF,IOPOST1         POST I/O COMPLETE          @D68REEF 04955000
*SGLINK    IOPOST1,INPUT=(R6,R8,RF),WORK=RE                    @D68REEF 04956000
           BSM   0,RE               RETRN====================> @D68REEF 04957000
           DROP  R6                 RELEASE DISP    BASE       @D68REEF 04958000
           DROP  R8                 RELEASE TIB     BASE       @D68REEF 04959000
           DROP  R9                 RELEASE IOINTER BASE       @D68REEF 04960000
           SPACE 2                                             @D68REEF 04961000
           EJECT                                               @D68REEF 04962000
*SGLINK  SNS4ARIO,INPUT=(R3,R8,RD,RF)                          @D68REEF 04963000
SNS4ARIO   DS    0H'0'                 SPECIAL I/O ROUTINE     @D68REEF 04964000
           L     R6,DISPAD             DISPATCHER BASE ADDRESS @D68REEF 04965000
           USING DISP,R6               DISP    BASE            @D68REEF 04966000
           USING PUBADR,R3             PUB BASE POINTER        @D52BDAP 04967000
           USING CCWADR,RF             CCW BASE POINTER        @D52BDAP 04968000
*SGLINK    SNSIORTN,INPUT=(R3,R8,RD,RF),OUTPUT=R8              @D52VDAP 04969490
SNSIORTN DS    0H'0'                   GENERAL I/O ROUTINE     @D52BDAP 04970000
         STM   RE,R2,SNSIORRG          SAVE CALLERS REGISTER   @D52BDAP 04971000
         LA    R0,3                    SET RETRY COUNTER       @D52BDAP 04972000
SNSIOR00 XC    SNSAREA,SNSAREA         CLEAR SENSE AREA        @D52BDAP 04973490
SNSIOR10 LA    R1,SNSGCCB              GENERAL PURPOSE CCB     @D52BDAP 04974000
         USING CCBADR,R1               CCB BASE POINTER        @D52BDAP 04975000
         ST    RF,CCBCCW               SET PATH AND CCW ADDR.  @D52BDAP 04976000
         LR    R2,R3                   COPY PUB POINTER        @D52BDAP 04977000
         SH    R2,YPUBTAB              GET PUB OFFSET          @D52BDAP 04978000
         SRL   R2,PUBSLEN              DIVIDE BY PUB LENGTH    @D52BDAP 04979000
         STCM  R2,3,CCBCLS             SET PHYSICAL ADDRESS    @D52BDAP 04980000
         OI    CCBCLS,CCBPHYAD+XCPR    PHYSICAL+REAL ACCESS    @D52BDAP 04981000
         CLI   PUBDEVTY,TQDIO          IS THIS A QDIO DEVICE   @DY46037 04983000
         BNE   SNSIOR20                NO,                     @DY46037 04984000
         BAL   RE,FMT0FMT1             CONVERT TO FORMAT1 CCW  @DY46037 04985000
SNSIOR20 L     RE,AMISINTA             GET SGMIH BASE          @D52VDAP 04987000
         USING MISINTAT,RE             SGMIH BASE ADDRESS      @D52VDAP 04988000
         MVI   RID+1,USERTID           INDICATE SNS IS RUNNING @D52BDAP 04989490
         OI    EMWFLAG1,EMWF1SNS+EMWF1PVT  SENSE IS ACTIVE     @D62NDAP 04990000
         SVC   15                      EXECUTE THE COMMAND(S)  @D52BDAP 04991000
         TM    CCBCOM1,TRABIT          IS REQUEST COMPLETE     @D52BDAP 04992000
         BO    SNSIOR40                YES,                    @D52BDAP 04993000
         MVI   RID+1,DISPID            PREVENT REGS SAVING     @D52BDAP 04994000
         DROP  RE                      RELEASE MISINTAT BASE   @DY46337 04995190
         LA    RE,ALBRTRN              ALLBOUND ROUTINE ADDRESS@DY46337 04995380
         BSM   0,RE                         ===============>>> @DY46337 04995570
         USING MISINTAT,RE             SGMIH BASE ADDRESS      @DY46337 04995760
SNSIOR40 NI    EMWFLAG1,XFF-EMWF1SNS-EMWF1PVT INDICATE INACTIVE@D52BDAP 04996000
         DROP  RE                      RELEASE MISINTAT BASE   @DY46037 04997000
         CLI   PUBDEVTY,TQDIO          IS THIS A QDIO DEVICE   @DY46037 04999000
         BNE   SNSIOR50                NO,                     @DY46037 05000000
         BAL   RE,FMT1FMT0             RESTORE TO FORMAT0      @DY46037 05001000
SNSIOR50 OC    SNSAREA,SNSAREA         DID WE HAVE ERROR BEFORE@D52BDAP 05003000
         BNZ   SNSIOR70                YES,                    @D52BDAP 05004000
         TM    CCBCOM1,DISERR          ANY ERROR NOW           @D52BDAP 05005000
         BZ    SNSIORR0                NO,                     @D52BDAP 05006000
         CLI   CCBSTA2,X'FE'           DEVICE NOT OPERATIONAL  @D52VDAP 05007000
         BNE   SNSIOR60                NO,                     @D52VDAP 05008000
         MVI   SNSIORRG+4,ZERO         INDICATE PATH NOT AVAIL.@D52VDAP 05009000
         B     SNSIORR8                                        @D52VDAP 05010000
SNSIOR60 TM    CCBSTA1,UNITCK          UNIT CHECK              @D52VDAP 05011000
         BO    SNSIOR80                YES, DO THE SENSE       @D52VDAP 05012000
         TM    CCBSTA2,X'3F'           ANY CHANNEL ERROR       @DA42602 05013000
         BZ    SNSIORR8                NO, PERMANENT ERROR     @DA42602 05014000
SNSIOR70 TM    SNSAREA,COMREJ          WAS ERROR COMMAND REJECT@D52BDAP 05015000
         BO    SNSIORR4                YES,                    @DA42602 05016000
         L     RF,SNSIORRG+4           RESTORE RF              @D52VDAP 05017000
         BCT   R0,SNSIOR00             RETRY                   @D52BDAP 05018990
         B     SNSIORR8                PERMANENT ERROR         @DA42602 05020000
SNSIOR80 MVI   SNSAREA,X'10'           DEFAULT TO EQUIPM. CHECK@DA42602 05021000
         LA    RF,SNSCCW               ADDRESS OF SENSE CCW    @D52VDAP 05022000
         ICM   RF,8,CCBCCW             GET PATH BIT            @D52VDAP 05023000
         B     SNSIOR10                DO THE SENSE            @D52VDAP 05024000
         DROP  RF                      RELEASE CCWADR BASE     @DA44277 05025000
SNSIORR8 LA    R8,4(,R8)               PERMANENT ERROR RETURN  @D52BDAP 05026000
SNSIORR4 LA    R8,4(,R8)               COMMAND REJECT  RETURN  @D52BDAP 05027000
SNSIORR0 L     RE,AERBLOC              ERBLOC BASE ADDRESS     @DA43697 05028000
         USING ERBLOC,RE               ERBLOC BASE POINTER     @DA43697 05029000
         L     RF,SNSERCHN             1ST ENTRY IN SNS CHAIN  @DA43697 05030000
         LTR   RF,RF                   ANY ERROR WAITING       @DA43697 05031000
         BZ    SNSIOEXT                NO                      @DA43697 05032000
         OI    SNSFLAG,SNSPVWRK        YES, FORCE REINVOCATION @DA43697 05033000
.*       WE NEED TO GO BACK INTO 24-BIT MODE                   @DY46332 05034580
         BSM   0,RD                         =================> @DY46332 05034670
         DROP  RE                      RELEASE ERBLOC BASE     @DA43697 05035000
SNSIOEXT LM    RE,R2,SNSIORRG          RESTORE REGISTERS       @DA43697 05036000
         BR    R8                           =================> @D52BDAP 05037000
SNSIORRG DC    5F'0'                   SAVE AREA               @D52BDAP 05038000
         DROP  R1                      RELEASE CCB BASE        @D52BDAP 05039000
         DROP  R3                      RELEASE PUB BASE        @DA44277 05040000
         DROP  R6                      RELEASE DISPATCHER BASE @D52VDAP 05041000
         DROP  RD                      RELEASE SNSINIT BASE    @DA44277 05042000
SNSGCCB  CCB   SYS000,0,X'1500',SNSAREA                        @DY46037 05043000
         SPACE 3                                                        05044000
SNSPXPTR DC    A(0)                    SAVE PUBX AREA POINTER  @D52VDAP 05045000
ASNSSCHI DC    A(SNSSCHIB)             ADDRESS OF SCHIB        @D52VDAP 05046000
         DS    0F'0'                                           @D52VDAP 05047000
         DC    CL8'SNSSCHIB'                                   @D52VDAP 05048000
SNSSCHIB DC    XL(SCHLEN)'00'                                  @D52VDAP 05049000
         TITLE 'VSE SUPERVISOR     SGMIH     MIH SUPPORT              ' 05050000
         SGMIH                                                 @D37BDAP 05051000
         EJECT                                                 @D14BDAP 05052000
         TITLE 'VSE SUPERVISOR     SGATT     ATTENTION INTERFACE      ' 05053000
         SGATT                                                 @D14BDAP 05054000
         AIF   (NOT &VSE270).N270080                           @DAOM    05055490
         SGAOM                                                 @DAOM    05056000
.N270080 ANOP                                                  @DAOM    05057990
         SGVTAP                                                @D66ADAP 05059000
         AIF   (NOT &IOINTER).NPRT060                          @D21LDAP 05061000
         PRINT NOGEN              SUPPRESS PRINTING            @D14BDAP 05062000
.NPRT060 ANOP                                                  @D21LDAP 05063000
         MEND                                                           05064000
