*%GOTO  PCE; /*PLS COMMENT TO AVOID UNMATCHED QUOTE PROBLEMS            00001000
         MACRO                                                          00002000
         MAPPCE &DSECT=YES,&LG=                                         00003000
         GBLB  &IJBSGEN           SUPERVISOR GENERATION                 00004000
.*       IBM DISK OPERATING SYSTEM                            *         00005000
         AIF   (&IJBSGEN).NOWARN1                                       00006000
***************************************************************         00007000
.NOWARN1 ANOP                                                           00008000
*        SUPERVISOR - MAPPCE   - 5686-032-06                  *@D52VDIS 00009000
         AIF   (&IJBSGEN).NOWARN                                        00010000
*        LICENSED MATERIALS - PROPERTY OF IBM                 *         00011000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"          *         00012000
*        5686-032 (C) COPYRIGHT IBM CORP. 1990, 1995          *         00013000
*        SEE COPYRIGHT INSTRUCTIONS                           *         00014000
***************************************************************         00015000
         SPACE 4                                                        00016000
.**** START OF SPECIFICATIONS ***************************************** 00017000
.*                                                                      00018000
.*01* MODULE-TYPE = MACRO                                               00019000
.*                                                                      00020000
.*01* DESCRIPTIVE NAME = MAPPING OF PARTITION RELATED                   00021000
.*                       CONTROL BLOCKS (DSECT=YES)                     00022000
.*                                                                      00023000
.*  STATUS           =  NEW MACRO - FIRST RELEASE AF 5.1.0              00024000
.*                                                                      00025000
.*  FUNCTION         =  SHOW LAYOUT OF PCE CONTROL BLOCK                00026000
.*                      AND CODE                                        00027000
.*                      DSECT = YES    GIVES THE MAPPING OF THE PCE     00028000
.*                      DSECT = INLINE GIVES THE PCE MAPPING FOR        00029000
.*                                     PCB EXTENSION                    00030000
.*                      DSECT = NO     PRODUCES INLINE CODE FOR         00031000
.*                                     PCB EXTENSION                    00032000
.*                      AND CODE                                        00033000
.*                      IF INCLUDE BY A PLS MAPPING MACRO:              00034000
.*                      INPUT PCELVL = ',' CAUSES LEVEL ONE TO BE       00035000
.*                      SKIPPED                                         00036000
.*                      INPUT PCENDC = ',' CAUSES INCLUDED VARIABLES    00037000
.*                      TO END WITH A SEMICOLON (INLINE VARIABLES)      00038000
.*  NOTES            =  NONE                                            00039000
.*  DEPENDENCIES     =  NONE                                            00040000
.*  RESTRICTIONS     =  NONE                                            00041000
.*  REGISTER CONVENT.=  NONE                                            00042000
.*  REGISTER CONVENT.=  NONE                                            00043000
.*  PATCH LABEL      =  NONE                                            00044000
.*  MACRO TYPE       =  ASSEMBLER AND PLS                               00045000
.*  PROCESSOR        =  ASM                                             00046000
.*  MACRO SIZE       =  SEE OUTPUT                                      00047000
.*  ATTRIBUTES       =  MAPPING DSECT AND/OR INLINE CODE                00048000
.*  ENTRY POINT      =  PCEADR                                          00049000
.*  PURPOSE          =  SEE FUNCTION                                    00050000
.*  LINKAGE          =  NONE                                            00051000
.*  CALLER           =  SUPERVISOR OR OTHER VSE/SP COMPONENTS           00052000
.*  INPUT            =  SEE MACRO PARAMETER                             00053000
.*  OUTPUT           =  DSECT OR INLINE CODE                            00054000
.*                                                                      00055000
.*  EXIT NORMAL      =  MACRO EXPANSION                                 00056000
.*                                                                      00057000
.*  EXIT ERROR       =  NONE                                            00058000
.*                                                                      00059000
.*  EXTERNAL REFEREN.=  NONE                                            00060000
.*  ROUTINES         =  NONE                                            00061000
.*  DATA AREAS       =  NONE                                            00062000
.*  CONTROL BLOCKS   =  PCE                                             00063000
.*  TABLES           =  NONE                                            00064000
.*  MACROS           =  NONE                                            00065000
.*  CHANGE ACTIVITIES=  NEW MACRO - FIRST RELEASE AF 5.1.0     @D....IS 00066000
.*    01 INSERT NEW FLAG FOR SECURITY INITIALIZATION           @D52VDIS 00067000
.*    02 AF 6.1 SUPPORT                                        @D61NDMZ 00068000
.*  MODIFIED REGISTER=  SEE DESCRIPTION OF ENTRY POINT                  00069000
.*                                                                      00070000
.**** END OF SPECIFICATIONS ******************************************* 00071000
.*                                                                      00072000
*********************************************************************** 00073000
* WARNING: THE FOLLOWING MAPPING REFERS TO AN INTERNAL SUPERVISOR     * 00074000
*          CONTROL BLOCK. IT MAY CHANGE ANY TIME AND IS NOT COMMITTED * 00075000
*          AS AN INTERFACE OF ANY LEVEL.                              * 00076000
*********************************************************************** 00077000
         AGO   .DSECT                                                   00078000
.NOWARN  ANOP                                                           00079000
.* ******************************************************************** 00080000
.* PROCESS SUPERVISOR REQUEST                                           00081000
.* ******************************************************************** 00082000
.DSECT   ANOP                                                           00083000
         AIF   (T'&DSECT EQ 'O').PRDS                                   00084000
         AIF   ('&DSECT' EQ 'YES').PRDS                                 00085000
         AIF   ('&DSECT' NE 'INLINE').NODS                              00086000
PCEADR   DS    0F           PARTITION CONTROL BLOCK EXTENSION           00087000
         AGO   .INL1                                                    00088000
.NODS    ANOP                                                           00089000
         AIF   ('&DSECT' NE 'NO').ERR2                                  00090000
         AIF   (&IJBSGEN).INLINE                                        00091000
         AGO   .ERR2                                                    00092000
.* ******************************************************************** 00093000
.PRDS    ANOP                                                           00094000
**********************************************************************  00095000
* PCEADR   - DSECT OF PARTITION RELATED CONTROL BLOCKS                  00096000
*********************************************************************** 00097000
         SPACE 1                                                        00098000
PCEADR   DSECT                                                          00099000
.INL1    ANOP                                                           00100000
PCEPWECB DS  0XL4         ABNORMAL TERMINATION ECB TO POST POWER        00101000
*                         FOR DEALLOCATION                              00102000
PCELEN   DS  H            LENGTH OF PCE                                 00103000
PCEFLAG  DS  X            FLAG BYTE                                     00104000
PCEPWPST EQU   X'80'        0  POSTED, WHEN ABNORMAL TERMINATION        00105000
*                              OF PARTITION OR CLEAN UP PROCESSING      00106000
*                              COMPLETED.                               00107000
PCEDYNP  EQU   X'40'        1  DYNAMIC PARTITION IDENTIFICATION         00108000
PCEPREP  EQU   X'20'        2  PREPARATION ACTIVE                       00109000
PCECLEAN EQU   X'10'        3  CLEAN UP ACTIVE                          00110000
PCEINIT  EQU   X'08'        4  PARTITION INITIALIZATION ACTIVE          00111000
PCEPRTYC EQU   X'04'        5  PRIORITY CHANGED (SET IN POWER           00112000
*                              PCE ONLY !)                              00113000
PCEICNCL EQU   X'02'        6  INITIALIZATION CANCELLED                 00114000
PCEIOPCN EQU   X'01'        7  INIT OPERATOR CANCELLED         @DY46408 00115490
PCEFLAG1 DS  X            FLAG BYTE                                     00116000
PCEDOPR  EQU   X'80'        0  PROCESS PREPARATION                      00117000
PCERESEC EQU   X'40'        1  RESET SECURITY INITIALIZATION   @D52VDMZ 00118000
PCEDOCL  EQU   X'20'        2  PROCESS CLEAN UP                         00119000
PCESGERR EQU   X'10'        3  SYSTEM GETVIS ERROR DURING               00120000
*                              IJBSINP INITIALIZATION                   00121000
PCEPHOLD EQU   X'08'        4  HOLD PARTITION DURING CLEAN UP           00122000
*                              (DYNCLASS ID=HOLD)                       00123000
PCEPWAIT EQU   X'04'        5  PARTITION IS WAITING BECAUSE OF HOLD     00124000
PCEPROK  EQU   X'02'        6  PREPARATION DONE FOR PARTITION           00125000
PCEDOSEC EQU   X'01'        7  DO SECURITY INITIALIZATION      @D52VDIS 00126000
PCEKEY   DS  X            PARTITION'S STORAGE KEY                       00127000
PCEHCNT  DS  X            DYNCLASS ID=HOLD COUNT                        00128000
         DS  2X           NOT USED                                      00129000
PCEPIK   DS  H            PARTITION'S IDENTIFICATION KEY                00130000
PCELID   DS  CL2          PARTITION'S SYSLOG ID                         00131000
PCECOMRA DS  A            PARTITION'S COMREG ADDRESS                    00132000
PCEALUB  DS  A            PARTITION'S LUBTAB ADDRESS                    00133000
PCEADIB  DS  A            PARTITION'S DIB ADDRESS                       00134000
PCEPIB   DS  A            PARTITION'S PIB ADDRESS                       00135000
PCEPIB2  DS  A            PARTITION'S PIB2 ADDRESS                      00136000
PCECLASS DS  A            PARTITION'S CLASS INFORMATION                 00137000
PCEPOWJB DS  A            VSE/POWER JOB INFORMATION ADDRESS             00138000
PCELENQ  EQU   *-PCEADR     EQUATE FOR PCE LENGTH                       00139000
         MEXIT              LEAVE MACRO                                 00140000
.* ******************************************************************** 00141000
.* INLINE CODE FOR STATIC PARTITION PCE AND DEFAULT DYNAMIC PART. PCE   00142000
.* ******************************************************************** 00143000
.INLINE  ANOP                                                           00144000
         AIF   (T'&LG EQ 'O').ERR1                                      00145000
&LG.PCE  DS    0F               PARTITION CONTROL BLOCK EXTENSION       00146000
         DC    AL2(PCELENQ)     LENGTH OF PCE                           00147000
         AIF   ('&LG' NE 'DP').NODP0                                    00148000
.* PCEDYNP MUST NOT BE SET FOR DPPCE SINCE DPPCB IS USED AS             00149000
.* ALLBOUND PCB, TOO.                                                   00150000
         DC    B'00101000'      FLAG BYTE                               00151000
         AGO   .DP1                                                     00152000
.NODP0   ANOP                                                           00153000
         DC    X'00'            FLAG BYTE                               00154000
.DP1     ANOP                                                           00155000
         DC    X'00'            FLAG BYTE                               00156000
         AIF   ('&LG' NE 'AR').NOTAR                                    00157000
         DC    AL1(SYSPIK)      PARTITION'S STORAGE KEY                 00158000
         DC    3X'00'           NOT USED                                00159000
         DC    AL2(SYSPIK)      SYSTEM'S    IDENTIFICATION KEY          00160000
         DC    CL2'&LG.'        SYSTEM'S    SYSLOG ID                   00161000
         DC    A(ARCOMREG)      SYSTEM'S    COMREG ADDRESS              00162000
         DC    A(LUBTAB)        SYSTEM'S    ADDRESS                     00163000
         DC    A(0)             SYSTEM'S    DIB ADDRESS                 00164000
         AGO   .WASAR                                                   00165000
.NOTAR   ANOP                                                           00166000
         DC    AL1(&LG.PIK)     PARTITION'S STORAGE KEY                 00167000
         DC    3X'00'           NOT USED                                00168000
         DC    AL2(&LG.PIK)     PARTITION'S IDENTIFICATION KEY          00169000
         DC    CL2'&LG.'        PARTITION'S SYSLOG ID                   00170000
         DC    A(&LG.COMREG)    PARTITION'S COMREG ADDRESS              00171000
         AIF   ('&LG' NE 'BG').NOTBG                                    00172000
         DC    A(LUBTAB)        BG'S LUBTAB ADDRESS                     00173000
         AGO   .WASBG                                                   00174000
.NOTBG   ANOP                                                           00175000
         DC    A(0)             PARTITION'S LUBTAB ADDRESS              00176000
.WASBG   ANOP                                                           00177000
         AIF   ('&LG' EQ 'DP').DPDIB                                    00178000
         DC    A(DSKPOS&LG.)    PARTITION'S DIB ADDRESS                 00179000
         AGO   .WASAR                                                   00180000
.DPDIB   ANOP                                                           00181000
         DC    A(DSKPOSBG)      USE BG DIB FOR INIT. OF DYN. PART.      00182000
.WASAR   ANOP                                                           00183000
         DC    A(&LG.PIB1)      PARTITION'S PIB ADDRESS                 00184000
         DC    A(&LG.PIB2)      PARTITION'S PIB2 ADDRESS                00185000
         DC    A(0)             PARTITION'S CLASS INFORMATION           00186000
         AIF   ('&LG' NE 'AR' AND '&LG' NE 'DP').NOTAR1                 00187000
         DC    A(0)             NO JOB INFO AVAILABLE                   00188000
         MEXIT              LEAVE MACRO                                 00189000
.NOTAR1  ANOP                                                           00190000
         DC    A(&LG.PJB)       VSE/POWER JOB INFORMATION               00191000
         MEXIT              LEAVE MACRO                                 00192000
.* ******************************************************************** 00193000
.* ERROR MESSAGES                                                       00194000
.* ******************************************************************** 00195000
.ERR1    MNOTE 8,'LG IS REQUIRED FOR DSECT=&DSECT.'                     00196000
         MEXIT                                                          00197000
.ERR2    MNOTE 8,'DSECT=&DSECT IS INVALID.'                             00198000
         MEND                                                           00199000
* END OF PLS COMMENT */ %PCE : ;                                        00200000
* /*-----------------------------------------------------------------*  00201000
*  *  PCEADR   - PARTITION CONTROL BLOCK EXTENSION                   *  00202000
*  *-----------------------------------------------------------------*/ 00203000
* %IF PCENDC ¬= ','                                                     00204000
* %THEN                                                                 00205000
* %PCENDC = ';';                                                        00206000
* %IF PCELVL ¬= ''                                                      00207000
* %THEN %GOTO PCESKL1;                                                  00208000
* DECLARE                                                   /*D51IDIS*/ 00209000
* 1    PCEADR   BASED,    /* MAP OF PCE                              */ 00210000
* %GOTO PCESKL2;                                                        00211000
* %PCESKL1:;                                                            00212000
*  2   PCEADR,            /* MAP OF PCE                              */ 00213000
* %PCESKL2:;                                                            00214000
*   3  PCEPWECB BIN(32),  /* ABNORMAL TERMINATION ECB TO POST POWER  */ 00215000
*    4 PCELEN   PTR(16),  /* LENGTH OF PCE                           */ 00216000
*    4 PCEFLAG  BIT(8),   /* FLAG BYTE                               */ 00217000
*    5 PCEPWPST BIT(1),   /*   0  POSTED, WHEN ABNORMAL TERMINATION     00218000
*                                 OF PARTITION OR CLEAN UP PROCESSING   00219000
*                                 COMPLETED.                         */ 00220000
*    5 PCEDYNP  BIT(1),   /*   1  DYNAMIC PARTITION IDENTIFICATION   */ 00221000
*    5 PCEPREP  BIT(1),   /*   2  PREPARATION ACTIVE                 */ 00222000
*    5 PCECLEAN BIT(1),   /*   3  CLEAN UP ACTIVE                    */ 00223000
*    5 PCEINIT  BIT(1),   /*   4  PARTITION INITIALIZATION ACTIVE    */ 00224000
*    5 PCEPRTYC BIT(1),   /*   5  PRIORITY CHANGED (SET IN POWER PCE)*/ 00225000
*    5 PCEICNCL BIT(1),   /*   6  INITIALIZATION PROCESS CANCELLED   */ 00226000
*    5 PCEIOPCN BIT(1),   /*   7  INIT OPERATOR CANCELLED            */ 00227490
*    4 PCEFLAG1 BIT(8),   /* FLAG BYTE                               */ 00228000
*    5 PCEDOPR  BIT(1),   /*   0  PROCESS PREPARATION                */ 00229000
*    5 PCERESEC BIT(1),   /*   1  RESET SECURITY INIT.       @D52VDMZ*/ 00230000
*    5 PCEDOCL  BIT(1),   /*   2  PROCESS CLEAN UP                   */ 00231000
*    5 PCESGERR BIT(1),   /*   3  SYSTEM GETVIS ERROR DURING            00232000
*                                 IJBSINP INITIALIZATION             */ 00233000
*    5 PCEPHOLD BIT(1),   /*   4  HOLD PARTITION DURING CLEAN UP        00234000
*                                 (DYNCLASS ID=HOLD)                 */ 00235000
*    5 PCEPWAIT BIT(1),   /*   5  PARTITION IS WAITING               */ 00236000
*    5 PCEPROK  BIT(1),   /*   6  PREPARATION DONE FOR PARTITION     */ 00237000
*    5 PCEDOSEC BIT(1),   /*   7  DO SECURITY INITIALIZATION @D52VDIS*/ 00238000
*   3 PCEKEY   BIT(8),    /* PARTITION'S STORAGE KEY                 */ 00239000
*   3 PCEHCNT  FIXED(8),  /* DYNCLASS ID=HOLD COUNT                  */ 00240000
*   3 *        PTR(16),   /* NOT USED                                */ 00241000
*   3 PCEPIK   PTR(16),   /* PARTITION'S IDENTIFICATION KEY          */ 00242000
*   3 PCELID   CHAR(2),   /* PARTITION'S SYSLOG ID                   */ 00243000
*   3 PCECOMRA PTR(32),   /* PARTITION'S COMREG ADDRESS              */ 00244000
*   3 PCEALUB  PTR(32),   /* PARTITION'S LUBTAB ADDRESS              */ 00245000
*   3 PCEADIB  PTR(32),   /* PARTITION'S DIB ADDRESS                 */ 00246000
*   3 PCEPIB   PTR(32),   /* PARTITION'S PIB ADDRESS                 */ 00247000
*   3 PCEPIB2  PTR(32),   /* PARTITION'S PIB2 ADDRESS                */ 00248000
*   3 PCECLASS PTR(32),   /* PARTITION'S CLASS INFORMATION           */ 00249000
*   3 PCEPOWJB PTR(32)    /* VSE/POWER JOB INFORMATION ADDRESS       */ 00250000
*                PCENDC   /* TERMINATING SIGN                        */ 00251000
