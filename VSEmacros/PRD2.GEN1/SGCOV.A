         MACRO                                                          00050000
&LABNAM  SGCOV &ACTIVE=,&BEGIN=,&END=,&REPORT=,&MC=,&SKIP=,            X00083301
               &OFFESA=,&OFFVM=,&OFF370=,&OFFVMES=             #COVERGL 00116601
.**************************************************************         00150000
.*       IBM VSE / ADVANCED FUNCTIONS                         *         00200000
.*       SUPERVISOR - SGCOV - 5686-032-06-C56                 *         00250000
.*       COPYRIGHT SEE BELOW VARIABLE DEFINITIONS             *         00300000
.**************************************************************         00350000
.*  THE NAMES TABLE:                                           #COVERGL 00400000
.*    ALL NAMES OF ACTIVATED TOPICS ARE GIVEN HERE IN A        #COVERGL 00450000
.*    CONSECUTIVE ORDER.                                       #COVERGL 00500000
.*    &$COVNAX NORMALLY IS THE LAST USED IN &$COVNAM           #COVERGL 00550000
.*    ONLY DURING ACTIVATION CALL PROCESSING IT POINTS         #COVERGL 00600000
.*    TO THE NEXT FREE ENTRY IN &$COVNAM.                      #COVERGL 00650000
.*-------------------------------------------------------------#COVERGL 00700000
         GBLC  &$COVNAM(200)   NAMES OF ACTIVE TOPICS                   00750000
         GBLA  &$COVNAX        NEXT FREE ENTRY IN COVNAM                00800000
.*-------------------------------------------------------------#COVERGL 00850000
.*  THE REPORT STRING:                                         #COVERGL 00900000
.*    ALL CONSECUTIVE GENERATIONS OF MONITOR CALLS FOR ONE AND #COVERGL 00950000
.*    THE SAME TOPIC FORM ONE ENTRY IN THE REPORT TABLE:       #COVERGL 01000000
.*  &$COVPR1 CONTAINS THE INDEX OF THE TOPIC IN &$COVNAM       #COVERGL 01050000
.*           IF THE INDEX IS NEGATIVE, THE VALUES BELONG TO    #COVERGL 01066601
.*           SKIPPED MONITOR CALL NUMBERS.                     #COVERGL 01083201
.*  &$COVPR2 CONTAINS THE FIRST GENERATED MONITOR CALL NUMBER  #COVERGL 01100000
.*  &$COVPR3 CONTAINS THE LAST  GENERATED MONITOR CALL NUMBER  #COVERGL 01150000
.*  &$COVPRX IS INDEX OF NEXT FREE COUPLE OF ENTRIES           #COVERGL 01200000
.*  &$COVTOV IS SET TO 1, IF THE REPORT TABLE HAD AN OVERFLOW  #COVERGL 01250000
.*-------------------------------------------------------------#COVERGL 01300000
         GBLA  &$COVPR1(200)   &$COVNAM INDEX OF REP.ENTRY OWNER        01350000
         GBLA  &$COVPR2(200)   BEGIN OF RANGE OF THIS ENTRY             01400000
         GBLA  &$COVPR3(200)   END OF RANGE OF THIS ENTRY               01450000
         GBLA  &$COVPRX        NEXT FREE ENTRY IN REPORT TABLE          01500000
         GBLB  &$COVTOV        FLAG REPORT TABLE OVERFLOW      #COVERGL 01550000
.*-------------------------------------------------------------#COVERGL 01600000
.*  THE RANGE TABLE:                                           #COVERGL 01650000
.*    IT CONTAINS THE ANSWER TO THE QUESTION WHICH MC NUMBER   #COVERGL 01700000
.*    TO GIVE NEXT TO A MONITOR CALL.                          #COVERGL 01750000
.*    IF &A IS THE INDEX OF A TOPIC IN &$COVNAM, THEN          #COVERGL 01800000
.*    &$COVRA1(&$COVRAX(&A)) IS THE BEGIN VALUE OF MC NUMBERS  #COVERGL 01850000
.*                           FOR THIS TOPIC                    #COVERGL 01900000
.*    &$COVRA2(&$COVRAX(&A)) IS THE NEXT FREE NUMBER TO USE    #COVERGL 01950000
.*                           FOR THIS TOPIC                    #COVERGL 02000000
.*    &$COVRA3(&$COVRAX(&A)) IS THE LAST NUMBER THAT MAY BE    #COVERGL 02050000
.*                           USED FOR MC'S OF THIS TOPIC       #COVERGL 02100000
.*    IN BUILDING THE &$COVRA1,2,3 TABLE &$COVACT IS THE       #COVERGL 02150000
.*    NEXT FREE ENTRY TO USE.                                  #COVERGL 02200000
.*                                                             #COVERGL 02250000
.*-------------------------------------------------------------#COVERGL 02300000
         GBLA  &$COVRA1(200)   BEGIN OF RANGE FOR MC'S OF TOPIC         02350000
         GBLA  &$COVRA2(200)   NEXT FREE VALUE FOR MONITOR CALLS        02400000
         GBLA  &$COVRA3(200)   END   OF RANGE FOR MC'S OF TOPIC         02450000
         GBLA  &$COVRAX(200)   INDEX OF &$COVRA1,2,3 FOR TOPIC          02500000
         GBLA  &$COVRAS(200)   SELF OR AUTO NUMBERING SWITCHES          02550000
         GBLA  &$COVACT        NUMBER OF ACTIVATION CALLS.              02600000
.*-------------------------------------------------------------#COVERGL 02650000
.*  SOME FLAGS:                                                #COVERGL 02700000
.*-------------------------------------------------------------#COVERGL 02750000
         GBLB  &$COVINS        PASSED FIRST INSTRUMENTATION CALL        02800000
         GBLB  &$COVALL        ACTIVE=ALL WAS SPECIFIED        #COVERGL 02850000
         GBLB  &$COVAIL        FATAL ERROR IN SOME ACTIVATION. #COVERGL 02900000
.*-------------------------------------------------------------#COVERGL 02950000
.*  SOME VARIABLES TO IDENTIFY THE EXPANSION ENVIRONMENT       #COVERGL 02954101
.*-------------------------------------------------------------#COVERGL 02958201
         GBLB  &IJBSGEN        INDICATES SUPERVISOR ASSEMBLY   #COVERGL 02962301
         GBLB  &BGVM           INDICATES VM SUPERVISOR         #COVERGL 02966401
         GBLB  &BGXA           INDICATES ESA FEATURE SUPERVISOR#COVERGL 02970501
         GBLB  &BG370          INDICATES 370 SUPERVISOR        #COVERGL 02974601
         GBLA  &$COVOFF(4)     OFFSET VALUES FOR SUPERVISORS   #COVERGL 02978701
         GBLA  &$COVOFV        CURRENT VALID OFFSET VALUE      #COVERGL 02982801
         GBLA  &$COVNSU        NR OF MCS GENERATED AND IJBSGEN #COVERGL 02986901
.*                             WAS OFF                         #COVERGL 02991001
.*-------------------------------------------------------------#COVERGL 02995101
.*  SOME LOCAL VARIABLES:                                      #COVERGL 03000000
.*-------------------------------------------------------------#COVERGL 03050000
         LCLA  &PRSX           ACTUAL INDEX OF TOPIC IN &$COVPRS BSTP   03100000
         LCLA  &PRS2X          AUXILIARY INDEX IN PRINTING BSTP         03150000
         LCLB  &PRSSKIP        FLAG FOR SKIPPED SGCOV INVOCATIONS       03175001
         LCLA  &L,&N,&P                                                 03200000
         LCLA  &ACTMCNR        ACTUAL MONITOR CALL NUMBER               03250000
         LCLA  &TOPNAMX        ACTUAL INDEX OF TOPIC IN &$COVNAM        03300000
         LCLA  &ACTRAX         ACTUAL INDEX OF TOPIC IN &$COVRA1,2,3    03350000
         LCLC  &STARTC,&ENDC   START AND END OF RANGE AS CHARACTER      03400000
         LCLA  &STARTR,&ENDR   START AND END OF RANGE AS NUMBERS        03450000
         LCLC  &TOPNAM         TOPIC NAME DEALT WITH RIGHT NOW          03500000
         LCLC  &GENLAB         GENERATED LAST 7 CHARS OF LABEL          03550000
         LCLC  &GENARGS        GENERATED ARGUMENTS OF MC INSTR.         03600000
         ACTR  1000       LOOP CONTROL                                  03650000
.**************************************************************         03700000
         AGO   .CPRIGHT                                                 03750001
*        LICENSED MATERIALS - PROGRAM PROPERTY OF IBM         *         03800000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"          *         03850000
*        5686-032 (C) COPYRIGHT IBM CORP. 1990                *         03900000
*        SEE COPYRIGHT INSTRUCTIONS                           *         03950000
.CPRIGHT ANOP                                                 *         04000000
.**************************************************************         04050000
.*                                                            *         04100000
.*  FUNCTION:  GENERATE MONITOR CALLS FOR COVERAGE MEASURE-   *         04150000
.*             MENT BY TESTMON.                               *         04200000
.*                                                            *         04212501
.*SERVICELEVEL:  28                                           *         04225001
.*                                                            *         04237501
.*                                                            *         04250000
.*  INVOCATIONS:                                              *         04300000
.*                                                            *         04350000
.*   1.) 'ACTIVATION INVOCATION'                              *         04400000
.*                                                            *         04450000
.*        SGCOV  ACTIVE=(TOPIC1,TOPIC2,...TOPICN),            *         04500000
.*               BEGIN=NUMBER1,END=NUMBER2                    *         04550000
.*                                                            *         04600000
.*        INTENTION OF THIS IS TO EXPAND ALL THOSE            *         04650000
.*        INSTRUMENTATION INVOCATIONS (SEE BELOW) INTO REAL   *         04700000
.*        MONITOR CALL CODE THAT MENTION EITHER TOPIC OF      *         04750000
.*        TOPIC1 THRU TOPICN                                  *         04800000
.*                                                            *         04850000
.*        SGCOV  ACTIVE=ALL,                                  *         04900000
.*               BEGIN=NUMBER1,END=NUMBER2                    *         04950000
.*                                                            *         05000000
.*        INTENTION OF THIS IS TO EXPAND ALL INSTRUMENTATION  *         05050000
.*        INVOCATIONS, WHATEVER TOPIC THEY MENTION AS OPERAND.*         05100000
.*                                                            *         05150000
.*   2.) 'INSTRUMENTATION INVOCATION'                         *         05200000
.*                                                            *         05250000
.*        SGCOV  TOPIC1,TOPIC2,...,TOPICN                     *         05300000
.*                                                            *         05350000
.*        THIS SHALL IDENTIFY A SPOT IN THE PROGRAM,          *         05400000
.*        THAT SHOULD BE COVERED BY TESTING, IF ANY OF        *         05450000
.*        THE TOPICS1 THRU N IS SUBJECT TO TESTING, I.E.      *         05500000
.*        ACTIVATED IN AN ACTIVATION INVOCATION.              *         05550000
.*                                                            *         05600000
.*   2.) 'REPORT INVOCATION'                                  *         05650000
.*                                                            *         05700000
.*        SGCOV  REPORT=YES                                   *         05750000
.*                                                            *         05800000
.*        GENERATES A REPORT, WHICH MONITOR CALL WAS USED     *         05850000
.*        FOR WHICH TOPIC.                                    *         05900000
.*                                                            *         05950000
.**************************************************************         06000000
.*                                                                      06050000
.*01  MODULE-TYPE = MACRO                                               06100000
.*01  CHANGE ACTIVITY                                                   06150000
.* A000000-999999                                              #COVERGL 06200000
.*                                                                      06250000
.**************************************************************#COVERGL 06300000
.*  WRITE SOME LABEL FOR POSSIBLE BRANCHES:                   *#COVERGL 06307101
.**************************************************************#COVERGL 06314201
         AIF (T'&LABNAM EQ 'O').NONAM                          #COVERGL 06321301
&LABNAM  DS    0H                                              #COVERGL 06328401
.NONAM   ANOP                                                  #COVERGL 06335501
.**************************************************************#COVERGL 06336501
.*                                                            *#COVERGL 06337501
.*  IF SGCOV SHALL BE MADE USABLE AGAIN, SWITCH OFF THIS MEXIT*#COVERGL 06338501
.*                                                            *#COVERGL 06339501
.**************************************************************#COVERGL 06340501
.*       MEXIT                                                 @COVERGL 06341501
.**************************************************************#COVERGL 06342601
.*                                                            *#COVERGL 06350000
.*  IS EXPANSION    FOR ACTIVATION,REPORT OR INSTRUMENTATION? *#COVERGL 06400000
.*                                                            *#COVERGL 06450000
.**************************************************************#COVERGL 06500000
         AIF   (T'&OFFESA  NE 'O').STRT100                     #COVERGL 06510001
         AIF   (T'&OFF370  NE 'O').STRT100                     #COVERGL 06520001
         AIF   (T'&OFFVMES NE 'O').STRT100                     #COVERGL 06530001
         AIF   (T'&OFFVM   NE 'O').STRT100                     #COVERGL 06540001
         AIF   (T'&ACTIVE  NE 'O').STRT000                     #COVERGL 06550000
         AIF   (T'&REPORT  NE 'O').STRT010                     #COVERGL 06600000
         AIF   (T'&BEGIN   NE 'O').STRT001                     #COVERGL 06650000
         AIF   (T'&END     NE 'O').STRT004                     #COVERGL 06700000
         AIF   (&$COVACT GT 0).GENMON                                   06750000
         MEXIT                                                 #COVERGL 06800000
.STRT000 AIF   (T'&REPORT  NE 'O').STRT002                     #COVERGL 06850000
         AIF   (T'&SKIP    NE 'O').STRT008                     #COVERGL 06866601
         AIF   (T'&MC      NE 'O').STRT009                     #COVERGL 06883201
         AIF   (T'&BEGIN   EQ 'O').STRT003                     #COVERGL 06900000
         AIF   (T'&END     EQ 'O').STRT005                     #COVERGL 06950000
         AGO   .SETPERM                                        #COVERGL 07000000
.STRT010 AIF   (T'&BEGIN   NE 'O').STRT006                     #COVERGL 07050000
         AIF   (T'&SKIP    NE 'O').STRT008                     #COVERGL 07066601
         AIF   (T'&MC      NE 'O').STRT009                     #COVERGL 07083201
         AIF   (T'&END     NE 'O').STRT007                     #COVERGL 07100000
         AGO   .GENREP                                         #COVERGL 07150000
.STRT100 AIF   (T'&REPORT  NE 'O').STRT101                     #COVERGL 07156201
         AIF   (T'&ACTIVE  NE 'O').STRT101                     #COVERGL 07162401
         AIF   (T'&BEGIN   NE 'O').STRT101                     #COVERGL 07168601
         AIF   (T'&END     NE 'O').STRT101                     #COVERGL 07174801
         AIF   (T'&SKIP    NE 'O').STRT101                     #COVERGL 07181001
         AIF   (T'&MC      NE 'O').STRT101                     #COVERGL 07187201
         AGO   .SETOFF                                         #COVERGL 07193401
.STRT001 MNOTE 8,'ONLY GIVE BEGIN= IN ACTIVATION CALL!'        #COVERGL 07200000
         MEXIT                                                 #COVERGL 07250000
.STRT004 MNOTE 8,'ONLY GIVE END=   IN ACTIVATION CALL!'        #COVERGL 07300000
         MEXIT                                                 #COVERGL 07350000
.STRT002 MNOTE 8,'REPORT AND ACTIVATE MAY NOT BOTH BE GIVEN!'  #COVERGL 07400000
         MEXIT                                                 #COVERGL 07450000
.STRT003 MNOTE 8,'MISSING BEGIN= OF RANGE OF MC NUMBERS!'      #COVERGL 07500000
         MEXIT                                                 #COVERGL 07550000
.STRT005 MNOTE 8,'MISSING END=   OF RANGE OF MC NUMBERS!'      #COVERGL 07600000
         MEXIT                                                 #COVERGL 07650000
.STRT006 MNOTE 8,'ONLY SET BEGIN= IN ACTIVATION CALL!'         #COVERGL 07700000
         MEXIT                                                 #COVERGL 07750000
.STRT007 MNOTE 8,'ONLY SET END=   IN ACTIVATION CALL!'         #COVERGL 07800000
         MEXIT                                                 #COVERGL 07810001
.STRT008 MNOTE 8,'ONLY SET SKIP= IN NORMAL GENERATION CALL!'   #COVERGL 07820001
         MEXIT                                                 #COVERGL 07830001
.STRT009 MNOTE 8,'ONLY SET MC= IN NORMAL GENERATION CALL!'     #COVERGL 07840001
         MEXIT                                                 #COVERGL 07843301
.STRT101 MNOTE 8,'WITH OFFSET DEFINITION NO OTHERS ALLOWED!'   #COVERGL 07846601
.STRTEX  MEXIT                                                 #COVERGL 07850000
.SETPERM ANOP                                                  #COVERGL 07900000
.**************************************************************#COVERGL 07950000
.*                                                            *#COVERGL 08000000
.*  INVOCATION FOR ACTIVATION OF TOPIC:                       *#COVERGL 08050000
.*                                                            *#COVERGL 08100000
.**************************************************************#COVERGL 08150000
.*                                                             #COVERGL 08200000
.*------------------------------------------------------------*#COVERGL 08250000
.*  TEST SOME ERRORS:                                         *#COVERGL 08300000
.*------------------------------------------------------------*#COVERGL 08350000
         AIF   (&$COVAIL).ACTEXIT                                       08400000
         AIF   (&$COVALL).ACTER08                              #COVERGL 08450000
         AIF   (T'&BEGIN NE  'N').ACTER04                      #COVERGL 08500000
         AIF   (T'&END   NE  'N').ACTER05                      #COVERGL 08550000
         AIF   (&BEGIN   GT  &END).ACTER03                     #COVERGL 08600000
         AIF   (&BEGIN   LT  0).ACTER11                        #COVERGL 08650001
         AIF   (&END     GT  64000).ACTER12                    #COVERGL 08700001
.*------------------------------------------------------------*#COVERGL 08750000
.*  HANDLE INVOCATION NUMBER:                                 *#COVERGL 08800000
.*------------------------------------------------------------*#COVERGL 08850000
&$COVACT SETA  &$COVACT+1         SET NR OF THIS INVOCATION    #COVERGL 08900000
         AIF   (&$COVACT GT 200).ACTER00                       #COVERGL 08950000
.*------------------------------------------------------------*#COVERGL 09000000
.*  AND LAST USED ENTRY IN &$COVNAM:                          *#COVERGL 09050000
.*------------------------------------------------------------*#COVERGL 09100000
&$COVNAX SETA  &$COVNAX+1         SET TO NEXT FREE ENTRY:      #COVERGL 09150000
         AIF   (&$COVNAX GT 200).ACTER00                       #COVERGL 09200000
.*------------------------------------------------------------*#COVERGL 09250000
.*  SEE IF SOME OR ALL MONITOR CALLS SHALL BE ACTIVE:         *#COVERGL 09300000
.*------------------------------------------------------------*#COVERGL 09350000
         AIF   ('&ACTIVE' EQ 'ALL').PALL000                    #COVERGL 09400000
.*------------------------------------------------------------*#COVERGL 09450000
.*  FILL IN BEGIN AND END OF RANGE OF MONITOR CALLS:          *#COVERGL 09500000
.*------------------------------------------------------------*#COVERGL 09550000
&$COVRA1(&$COVACT) SETA &BEGIN                                 #COVERGL 09600000
&$COVRA2(&$COVACT) SETA &BEGIN                                 #COVERGL 09650000
&$COVRA3(&$COVACT) SETA &END                                   #COVERGL 09700000
.*------------------------------------------------------------*#COVERGL 09750000
.*  INITIALIZE LOOP END VALUE FOR PERMISSION LOOP:            *#COVERGL 09800000
.*------------------------------------------------------------*#COVERGL 09850000
&L       SETA  N'&ACTIVE                                                09900000
         AIF   (&L LT 1).ACTER10                               #COVERGL 09950000
.*------------------------------------------------------------*#COVERGL 10000000
.*  LOOP FOR ALL TOPICS IN ACTIVE= KEYWORD:                   *#COVERGL 10050000
.*------------------------------------------------------------*#COVERGL 10100000
.PERM000 ANOP                                                           10150000
&N       SETA  &N+1                                                     10200000
         AIF   (&N GT &L).PERM400                                       10250000
         AIF   (T'&ACTIVE(&N) EQ 'O').PERM000                           10300000
.*------------------------------------------------------------*#COVERGL 10350000
.*  EXPAND TOPIC TO 8 BYTES IN LENGTH                         *#COVERGL 10400000
.*------------------------------------------------------------*#COVERGL 10450000
&TOPNAM  SETC  '&ACTIVE(&N)           '(1,8)                   #COVERGL 10500000
.*------------------------------------------------------------*#COVERGL 10550000
.*  SEE, IF IT WAS GIVEN BEFORE                               *#COVERGL 10600000
.*------------------------------------------------------------*#COVERGL 10650000
&TOPNAMX SETA  0                                                        10700000
.PERM200 ANOP                                                  #COVERGL 10750000
&TOPNAMX SETA  &TOPNAMX+1                                               10800000
         AIF   (&TOPNAMX EQ &$COVNAX).PERM220                           10850000
         AIF   ('&TOPNAM' NE '&$COVNAM(&TOPNAMX)').PERM200              10900000
         AGO   .ACTER07                                        #COVERGL 10950000
.PERM220 ANOP                                                  #COVERGL 11000000
.*------------------------------------------------------------*#COVERGL 11050000
.*  INSERT INTO FREE ENTRY, IF THERE IS ONE                   *#COVERGL 11100000
.*------------------------------------------------------------*#COVERGL 11150000
         AIF   (&TOPNAMX GT 200).ACTER06                       #COVERGL 11200000
&$COVNAM(&TOPNAMX) SETC '&TOPNAM'                              #COVERGL 11250000
&$COVNAX SETA  &$COVNAX+1                                      #COVERGL 11300000
.*------------------------------------------------------------*#COVERGL 11350000
.*  SET INDEX OF RANGE OF POSSIBLE MC NUMBERS:                *#COVERGL 11400000
.*------------------------------------------------------------*#COVERGL 11450000
&$COVRAX(&TOPNAMX) SETA &$COVACT                               #COVERGL 11500000
.*------------------------------------------------------------*#COVERGL 11550000
.*  TEST IF THIS RANGE OF NUMBERS HAS BEEN USED BEFORE:       *#COVERGL 11600000
.*------------------------------------------------------------*#COVERGL 11650000
         AIF  (&$COVACT EQ 1).NRANTST                          #COVERGL 11700000
         AIF  (&$COVRA1(&$COVACT) LE &$COVRA3(&$COVACT-1)).ACTER02      11750000
.NRANTST ANOP                                                  #COVERGL 11800000
.*------------------------------------------------------------*#COVERGL 11850000
.*  END OF LOOP FOR ALL ENTRIES IN ACTIVATION LIST.           *#COVERGL 11900000
.*------------------------------------------------------------*#COVERGL 11950000
         AGO   .PERM000                                        #COVERGL 12000000
.PERM400 ANOP                                                  #COVERGL 12050000
.*------------------------------------------------------------*#COVERGL 12100000
.*  RESET NEXT FREE ENTRY IN &$COVNAM TO LAST USED AGAIN:     *#COVERGL 12150000
.*------------------------------------------------------------*#COVERGL 12200000
&$COVNAX SETA  &$COVNAX-1                                      #COVERGL 12250000
.*------------------------------------------------------------*#COVERGL 12300000
.*  END OF ACTIVATION CALL EXPANSION:                         *#COVERGL 12350000
.*------------------------------------------------------------*#COVERGL 12400000
.ACTEXIT MEXIT          TURN TO ANOP FOR TESTING               #COVERGL 12450000
.*------------------------------------------------------------*#COVERGL 12500000
.*  FOR TEST OF MACRO: PRINT ALL PERMISSIONS:                 *#COVERGL 12550000
.*------------------------------------------------------------*#COVERGL 12600000
&TOPNAMX SETA  0                                                        12650000
&N       SETA  0                                               #COVERGL 12700000
         MNOTE *,'MONITOR CALLS MAY EXIST FOR TOPIC:'          #COVERGL 12750000
.PRTPER0 ANOP                                                           12800000
&TOPNAMX SETA  &TOPNAMX+1                                               12850000
         AIF   (&TOPNAMX GT 200).PRTPER1                                12900000
         AIF   ('&$COVNAM(&TOPNAMX)' EQ '').PRTPER0                     12950000
&N       SETA  1                                               #COVERGL 13000000
         MNOTE *,' COVNAM    &$COVNAM(&TOPNAMX)'                        13050000
         MNOTE *,' COVRAX    &$COVRAX(&TOPNAMX)'                        13100000
         MNOTE *,' BEGIN     &$COVRA1(&$COVRAX(&TOPNAMX))'              13150000
         MNOTE *,' NEXT FREE &$COVRA2(&$COVRAX(&TOPNAMX))'              13200000
         MNOTE *,' END       &$COVRA3(&$COVRAX(&TOPNAMX))'              13250000
         AGO   .PRTPER0                                        #COVERGL 13300000
.PRTPER1 ANOP                                                           13350000
         AIF   (&N GT 0).PRTPER2                                        13400000
         MNOTE *,'    NO TOPIC'                                #COVERGL 13450000
.PRTPER2 ANOP                                                           13500000
         MEXIT                                                          13550000
.*                                                             #COVERGL 13600000
.*------------------------------------------------------------*#COVERGL 13650000
.*  LIST OF POSSIBLE ERRORS IN ACTIVATION PART:               *#COVERGL 13700000
.*------------------------------------------------------------*#COVERGL 13750000
.PALL000 ANOP                                                  #COVERGL 13800000
         AIF   (&$COVACT NE 1).ACTER09                         #COVERGL 13850000
&$COVALL SETB  1                                               #COVERGL 13900000
&$COVRA1(1) SETA &BEGIN                                        #COVERGL 13950000
&$COVRA2(1) SETA &BEGIN                                        #COVERGL 14000000
&$COVRA3(1) SETA &END                                          #COVERGL 14050000
         MEXIT                                                 #COVERGL 14100000
.*------------------------------------------------------------*#COVERGL 14150000
.*  LIST OF POSSIBLE ERRORS IN ACTIVATION PART:               *#COVERGL 14200000
.*------------------------------------------------------------*#COVERGL 14250000
.ACTER00 ANOP                                                  #COVERGL 14300000
&$COVACT SETA  200                                             #COVERGL 14350000
         MNOTE 8,'MORE THAN 200 TOPICS NOT POSSIBLE!'          #COVERGL 14400000
         AGO   .ACTERR                                         #COVERGL 14450000
.ACTER02 MNOTE 8,'BEGIN= VALUE BELOW LAST END VALUE!'          #COVERGL 14500000
         AGO   .ACTERR                                         #COVERGL 14550000
.ACTER03 MNOTE 8,'END= VALUE SMALLER THAN BEGIN!'              #COVERGL 14600000
         AGO   .ACTERR                                         #COVERGL 14650000
.ACTER04 MNOTE 8,'BEGIN= HAS NO NUMBERIC VALUE!'               #COVERGL 14700000
         AGO   .ACTERR                                         #COVERGL 14750000
.ACTER05 MNOTE 8,'END=  HAS NO NUMERIC VALUE!'                 #COVERGL 14800000
         AGO   .ACTERR                                         #COVERGL 14850000
.ACTER06 MNOTE 8,'TOO MANY TOPICS (>200),OVERFLOW OF TABLE!'   #COVERGL 14900000
         AGO   .ACTERR                                         #COVERGL 14950000
.ACTER07 MNOTE 8,'DUPLICATE DEFINITION OF TOPIC'               #COVERGL 15000000
         AGO   .ACTERR                                         #COVERGL 15050000
.ACTER08 MNOTE 8,'ACTIVE=ALL HAS BEEN GIVEN ALREADY !'         #COVERGL 15100000
         AGO   .ACTERR                                         #COVERGL 15150000
.ACTER09 MNOTE 8,'ACTIVE=ALL ONLY LEGAL AS 1ST ACTIVATION !'   #COVERGL 15200000
         AGO   .ACTERR                                         #COVERGL 15250000
.ACTER10 MNOTE 8,'GIVE LIST OF TOPICS FOR ACTIVATION OR ALL!'           15300000
         AGO   .ACTERR                                         #COVERGL 15350000
.ACTER11 MNOTE 8,'BEGIN VALUE IS NEGATIVE!'                             15400000
         AGO   .ACTERR                                         #COVERGL 15450000
.ACTER12 MNOTE 8,'END VALUE EXCEEDS 64000!'                             15500001
.ACTERR  ANOP                                                  #COVERGL 15550000
&$COVAIL SETB  1                                               #COVERGL 15600000
         MEXIT                                                 #COVERGL 15650000
.**************************************************************#COVERGL 15700000
.*                                                            *#COVERGL 15750000
.*  INVOCATION FOR GENERATION OF REPORT:                      *#COVERGL 15800000
.*                                                            *#COVERGL 15850000
.**************************************************************#COVERGL 15900000
.GENREP  ANOP                                                           15950000
         AIF   (&$COVAIL).REPEXIT                              #COVERGL 16000000
         MNOTE *,' '                                                    16050000
         MNOTE *,'  >>>>>> INSTRUMENTATION REPORT: <<<<<<<<'   #COVERGL 16100000
         MNOTE *,' '                                                    16150000
.*------------------------------------------------------------*#COVERGL 16200000
.*  GENERATE REPORT FOR ACTIVE=ALL:                           *#COVERGL 16250000
.*------------------------------------------------------------*#COVERGL 16300000
         AIF   (NOT &$COVALL).PRTGEN3                          #COVERGL 16350000
         MNOTE *,'  ALL TOPICS WERE ACTIVATED'                 #COVERGL 16400000
         MNOTE *,'  GENERATED MONITOR CALL NUMBERS ARE:'       #COVERGL 16450000
         MNOTE *,' '                                                    16500000
&STARTR  SETA  &$COVRA1(1)                                     #COVERGL 16550000
&ENDR    SETA  &$COVRA2(1)-1                                   #COVERGL 16600000
         MNOTE *,'  FIRST IS &STARTR,  LAST IS &ENDR'          #COVERGL 16650000
         MNOTE *,' '                                                    16700000
         MEXIT                                                 #COVERGL 16750000
.PRTGEN3 ANOP                                                  #COVERGL 16800000
.*------------------------------------------------------------*#COVERGL 16850000
.*  IF TABLE OVERFLOW OCCURED WRITE WARNING MESSAGE:          *#COVERGL 16900000
.*------------------------------------------------------------*#COVERGL 16950000
         AIF   (NOT &$COVTOV).PRTGEN4                          #COVERGL 17000000
         MNOTE *,' THERE WAS AN OVERFLOW IN MY TABLES (SORRY)' #COVERGL 17050000
         MNOTE *,' THE REPORT MAY THEREFORE BE INCOMPLETE.'    #COVERGL 17100000
.PRTGEN4 ANOP                                                  #COVERGL 17150000
.*------------------------------------------------------------*#COVERGL 17200000
.*  SEE IF REPORT SHALL BE DRAWN FOR ALL TOPICS OR SELECTION: *#COVERGL 17250000
.*------------------------------------------------------------*#COVERGL 17300000
         AIF   ('&REPORT' EQ 'YES').REPYES                     #COVERGL 17350000
         AIF   ('&REPORT'(1,1) NE '(').REPER00                          17400000
.*------------------------------------------------------------*#COVERGL 17450000
.*  GENERATE REPORT FOR SELECTION LIST OF MONITOR CALLS:      *#COVERGL 17500000
.*------------------------------------------------------------*#COVERGL 17550000
&L       SETA  N'&REPORT                                                17600000
         AIF   (&L LT 1).REPER01                               #COVERGL 17650000
.*------------------------------------------------------------*#COVERGL 17700000
.*  LOOP FOR ALL TOPICS IN REPORT=(LIST..):                   *#COVERGL 17750000
.*------------------------------------------------------------*#COVERGL 17800000
&N       SETA  0                                                        17850000
.REPS000 ANOP                                                           17900000
&N       SETA  &N+1                                                     17950000
         AIF   (&N GT &L).REPS400                                       18000000
         AIF   (T'&REPORT(&N) EQ 'O').REPS000                           18050000
.*------------------------------------------------------------*#COVERGL 18100000
.*  EXPAND TOPIC TO 8 BYTES IN LENGTH                         *#COVERGL 18150000
.*------------------------------------------------------------*#COVERGL 18200000
&TOPNAM  SETC  '&REPORT(&N)         '(1,8)                              18250000
.*------------------------------------------------------------*#COVERGL 18300000
.*  SEE, IF TOPIC WAS ACTIVATED:                              *#COVERGL 18350000
.*------------------------------------------------------------*#COVERGL 18400000
&TOPNAMX SETA  0                                                        18450000
.REPS200 ANOP                                                  #COVERGL 18500000
&TOPNAMX SETA  &TOPNAMX+1                                               18550000
         AIF   (&TOPNAMX GT &$COVNAX).REPS000                           18600000
         AIF   ('&TOPNAM' NE '&$COVNAM(&TOPNAMX)').REPS200              18650000
.*------------------------------------------------------------*#COVERGL 18700000
.*  WRITE ALL INSTANCES OF USAGE OF THIS TOPIC:               *#COVERGL 18750000
.*------------------------------------------------------------*#COVERGL 18800000
         MNOTE *,'MONITOR CALLS GENERATED FOR &TOPNAM'         #COVERGL 18850000
&P       SETA  0                                               #COVERGL 18900000
&PRSX    SETA  0                                                        18950000
.REPS310 ANOP                                                           19000000
&PRSX    SETA  &PRSX+1                                                  19050000
         AIF   (&PRSX GT &$COVPRX).REPS320                              19100000
&PRS2X   SETA  &$COVPR1(&PRSX)                                 #COVERGL 19150000
&PRSSKIP SETB  0                                               #COVERGL 19166601
         AIF   (&PRS2X GE 0).REPS312                           #COVERGL 19183201
&PRSSKIP SETB  1                                               #COVERGL 19199801
&PRS2X   SETA  -&PRS2X                                         #COVERGL 19216401
.REPS312 AIF   (&PRS2X NE &TOPNAMX).REPS310                    #COVERGL 19233001
&TOPNAM  SETC  '&$COVNAM(&TOPNAMX)'                                     19250000
&P       SETA  1                                               #COVERGL 19300000
&STARTC  SETC  '&$COVPR2(&PRSX)      '(1,5)                    #COVERGL 19350001
&ENDC    SETC  '&$COVPR3(&PRSX)      '(1,5)                    #COVERGL 19400001
         AIF   (&PRSSKIP).REPS314                              #COVERGL 19425001
         MNOTE *,' &STARTC - &ENDC'                            #COVERGL 19450000
         AGO   .REPS310                                        #COVERGL 19466601
.REPS314 MNOTE *,' &STARTC - &ENDC  (SKIPPED)'                 #COVERGL 19483201
         AGO   .REPS310                                        #COVERGL 19500000
.REPS320 ANOP                                                           19550000
         AIF   (&P GT 0).REPS330                                        19600000
         MNOTE *,'  NO MONITOR CALL'                           #COVERGL 19650000
.REPS330 ANOP                                                           19700000
         MNOTE *,' '                                           #COVERGL 19750000
.*------------------------------------------------------------*#COVERGL 19800000
.*  END OF LOOP FOR ALL ENTRIES IN ACTIVATION LIST.           *#COVERGL 19850000
.*------------------------------------------------------------*#COVERGL 19900000
         AGO   .REPS000                                        #COVERGL 19950000
.REPS400 ANOP                                                  #COVERGL 20000000
         MEXIT                                                 #COVERGL 20050000
.*------------------------------------------------------------*#COVERGL 20100000
.*  GENERATE REPORT FOR ALL MONITOR CALLS:                    *#COVERGL 20150000
.*------------------------------------------------------------*#COVERGL 20200000
.REPYES  ANOP                                                  #COVERGL 20250000
         MNOTE *,'#$ '                                                  20300001
         MNOTE *,'#$ THE FOLLOWING MONITOR CALLS WERE GENERATED:'       20350001
         MNOTE *,'#$ '                                                  20400001
&N       SETA  0                                               #COVERGL 20450000
.PRTGEN0 ANOP                                                           20500000
&PRSX    SETA  &PRSX+1                                                  20550000
         AIF   (&PRSX GT &$COVPRX).PRTGEN1                              20600000
&TOPNAMX SETA  &$COVPR1(&PRSX)                                 #COVERGL 20650000
&PRSSKIP SETB  0                                               #COVERGL 20658301
         AIF   (&TOPNAMX GE 0).PRTGEN5                         #COVERGL 20666601
&PRSSKIP SETB  1                                               #COVERGL 20674901
&TOPNAMX SETA  -&TOPNAMX                                       #COVERGL 20683201
.PRTGEN5 ANOP                                                  #COVERGL 20691501
&N       SETA  1                        INDICATE ONE WAS FOUND #COVERGL 20700000
&TOPNAM  SETC  '&$COVNAM(&TOPNAMX)'                                     20750000
&STARTC  SETC  '&$COVPR2(&PRSX)      '(1,5)                    #COVERGL 20800001
&ENDC    SETC  '&$COVPR3(&PRSX)      '(1,5)                    #COVERGL 20850001
         AIF   (&PRSSKIP).PRTGEN6                              #COVERGL 20875001
         MNOTE *,'#$ &TOPNAM  &STARTC - &ENDC  /* ... */'               20900001
         AGO   .PRTGEN0                                                 20916601
.PRTGEN6 MNOTE *,'#$ /*  &STARTC - &ENDC  SKIPPED FOR &TOPNAM */'       20933201
         AGO   .PRTGEN0                                        #COVERGL 20950000
.PRTGEN1 ANOP                                                           21000000
         AIF   (&N GT 0).PRTGEN2                                        21050000
         MNOTE *,'#$  NO MACRO'                                #COVERGL 21100001
.PRTGEN2 ANOP                                                           21150000
         MNOTE *,'#$ '                                         #COVERGL 21200001
         MEXIT                                                 #COVERGL 21250000
.*------------------------------------------------------------*#COVERGL 21300000
.*  SOME ERROR MESSAGES OF REPORT GENERATION:                 *#COVERGL 21350000
.*------------------------------------------------------------*#COVERGL 21400000
.REPER00 MNOTE 8,'GIVE LIST OF TOPICS OR KEYWORD >YES<!'       #COVERGL 21450000
         MEXIT                                                 #COVERGL 21500000
.REPER01 MNOTE 8,'AT LEAST ONE ELEMENT MUST BE IN LIST!'       #COVERGL 21550000
.REPEXIT MEXIT                                                 #COVERGL 21600000
.**************************************************************#COVERGL 21650000
.*                                                            *#COVERGL 21700000
.*  INVOCATION FOR INSTRUMENTATION OF MACRO:                  *#COVERGL 21750000
.*                                                            *#COVERGL 21800000
.**************************************************************#COVERGL 21850000
.GENMON  ANOP                                                  #COVERGL 21900000
.*------------------------------------------------------------*#COVERGL 21950000
.*  SEE IF AN ACTIVATION ERROR OCCURED:                       *#COVERGL 22300000
.*------------------------------------------------------------*#COVERGL 22350000
         AIF   (&$COVAIL).INSEXIT                              #COVERGL 22400000
         AIF   (T'&MC NE 'O').INSSLF                           #COVERGL 22450000
.*------------------------------------------------------------*#COVERGL 22500000
.*  FIND OUT IF INVOCATION HAD NO POSITIONAL PARAMETER:       *#COVERGL 22550000
.*------------------------------------------------------------*#COVERGL 22600000
         AIF   (&$COVALL).GENALL                               #COVERGL 22650000
&L       SETA  N'&SYSLIST                                               22700000
         AIF   (&L LE 0).INSEXIT                               #COVERGL 22750000
.*------------------------------------------------------------*#COVERGL 22800000
.*  EXPAND TOPIC TO 8 BYTES IN LENGTH                         *#COVERGL 23450000
.*------------------------------------------------------------*#COVERGL 23500001
&TOPNAM  SETC  '&SYSLIST(1)          '(1,8)                             23550001
.*                                                                      23600000
.*------------------------------------------------------------*#COVERGL 23650000
.*  SEE, IF IT WAS ACTIVATED:                                 *#COVERGL 23700000
.*------------------------------------------------------------*#COVERGL 23750000
&TOPNAMX SETA  0                                                        23800000
.GENM200 ANOP                                                  #COVERGL 23850000
&TOPNAMX SETA  &TOPNAMX+1                                               23900000
         AIF   (&TOPNAMX GT &$COVNAX).INSEXIT                           23950001
.GENM210 AIF   ('&TOPNAM' NE '&$COVNAM(&TOPNAMX)').GENM200              24050000
.*------------------------------------------------------------*#COVERGL 24100000
.*  FIND OUT IF THERE IS A MIXTURE OF SELF AND AUTO NUMBERING *#COVERGL 24150000
.*------------------------------------------------------------*#COVERGL 24200000
         AIF   (&$COVRAS(&TOPNAMX) EQ 2).INSER04                        24250000
&$COVRAS(&TOPNAMX) SETA 1                                      #COVERGL 24300000
.*-------------------------------------------------------------#COVERGL 24350000
.*  INCREMENT VALUE FOR MONITOR CALL NUMBER:                   #COVERGL 24400000
.*-------------------------------------------------------------#COVERGL 24450000
&ACTRAX  SETA  &$COVRAX(&TOPNAMX)                              #COVERGL 24500000
&ACTMCNR SETA  &$COVRA2(&ACTRAX)                               #COVERGL 24550000
         AIF   (&ACTMCNR EQ -1).INSEXIT                        #COVERGL 24600000
         AIF   (&ACTMCNR GT &$COVRA3(&ACTRAX)).INSER00         #COVERGL 24650000
&$COVRA2(&ACTRAX) SETA &ACTMCNR+1                              #COVERGL 24700000
.*-------------------------------------------------------------#COVERGL 24750000
.*  ADD OFFSET VALUE, IF IN SUPERVISOR ASSEMBLY                #COVERGL 24752001
.*-------------------------------------------------------------#COVERGL 24754001
.INSOFF  ANOP  ENTRY FOR MC= GENERATION OF MONITOR CALLS       #COVERGL 24756001
         AIF   (NOT &IJBSGEN).SUPV999                          #COVERGL 24758001
         AIF   (&$COVNSU NE 0).SUPER00                         #COVERGL 24760001
         AIF   (NOT &BG370).SUPV100                            #COVERGL 24762001
&$COVOFV SETA  &$COVOFF(2)                                     #COVERGL 24764001
         AIF   (NOT &BGXA).SUPV900                             #COVERGL 24766001
&$COVOFV SETA  &$COVOFF(1)                                     #COVERGL 24768001
         AGO   .SUPV900                                        #COVERGL 24770001
.SUPV100 AIF   (NOT &BGVM).SUPER01                             #COVERGL 24772001
&$COVOFV SETA  &$COVOFF(4)                                     #COVERGL 24774001
         AIF   (NOT &BGXA).SUPV900                             #COVERGL 24776001
&$COVOFV SETA  &$COVOFF(3)                                     #COVERGL 24778001
         AGO   .SUPV900                                        #COVERGL 24780001
.SUPER00 MNOTE 8,'SOME SGCOV OCCURED BEFORE IJBSGEN SETTING'   #COVERGL 24782001
         MEXIT                                                 #COVERGL 24784001
.SUPER01 MNOTE 8,'NEITHER BG370 NOR BGVM SET IN SUPERVISOR'    #COVERGL 24786001
         MEXIT                                                 #COVERGL 24788001
.SUPV900 ANOP                                                  #COVERGL 24790001
&ACTMCNR SETA &ACTMCNR+&$COVOFV                                #COVERGL 24792001
.SUPV999 ANOP                                                  #COVERGL 24794001
.*-------------------------------------------------------------#COVERGL 24796001
.*  GENERATE LAST SEVEN CHARACTERS OF LABEL OF MONITOR CALL:   #COVERGL 24800000
.*-------------------------------------------------------------#COVERGL 24850000
.INSALL  ANOP                           ENTRY FOR ALL OPTION   #COVERGL 24900000
&GENLAB  SETC '&ACTMCNR'                                       #COVERGL 24950000
         AIF (&ACTMCNR GT 9999).APPEND0                        #COVERGL 24978501
         AIF (&ACTMCNR GT 999).APPEND1                         #COVERGL 25007001
         AIF (&ACTMCNR GT 99).APPEND2                          #COVERGL 25035501
         AIF (&ACTMCNR GT 9).APPEND3                           #COVERGL 25064001
&GENLAB  SETC '0&GENLAB'                                       #COVERGL 25092501
.APPEND3 ANOP                                                  #COVERGL 25121001
&GENLAB  SETC '0&GENLAB'                                       #COVERGL 25150000
.APPEND2 ANOP                                                  #COVERGL 25200000
&GENLAB  SETC '0&GENLAB'                                       #COVERGL 25250000
.APPEND1 ANOP                                                  #COVERGL 25300000
&GENLAB  SETC '0&GENLAB'                                       #COVERGL 25350000
.APPEND0 ANOP                                                  #COVERGL 25400000
&GENLAB  SETC '$$&GENLAB'                                      #COVERGL 25450001
.*-------------------------------------------------------------#COVERGL 25500000
.*  GENERATE ARGUMENT OF MONITOR CALL INSTRUCTION:             #COVERGL 25550000
.*-------------------------------------------------------------#COVERGL 25600000
&L       SETA &ACTMCNR                                         #COVERGL 25650000
&N       SETA -1                                               #COVERGL 25700000
.CREG000 ANOP                                                  #COVERGL 25750000
&L       SETA &L-4096                                          #COVERGL 25800000
&N       SETA &N+1                                             #COVERGL 25850000
         AIF (&L GE 0).CREG000                                 #COVERGL 25900000
&L       SETA &L+4096                                          #COVERGL 25950000
&GENARGS SETC '&L'.'(&N),15'                                   #COVERGL 26000000
.*-------------------------------------------------------------#COVERGL 26050000
.*  SET LABEL, INSTRUCTION AND ARGUMENTS WITH CROSS REFERENCE  #COVERGL 26083301
.*  IF THE COVERAGE MACRO WAS NOT FLAGGED TO BE SKIPPED:       #COVERGL 26116601
.*-------------------------------------------------------------#COVERGL 26150000
         AIF   ('&SKIP' EQ 'YES').SKIPCOV                      #COVERGL 26175001
$&GENLAB MC    $&GENLAB-*+&GENARGS                                      26200000
.SKIPCOV ANOP                                                  #COVERGL 26225001
.*-------------------------------------------------------------#COVERGL 26228501
.*  KEEP IN MIND YOU GENERATED SOMEONE.                        #COVERGL 26232001
.*-------------------------------------------------------------#COVERGL 26235501
         AIF   (&IJBSGEN).REMEMB0                              #COVERGL 26239001
&$COVNSU SETA  &$COVNSU+1                                      #COVERGL 26242501
.REMEMB0 ANOP                                                  #COVERGL 26246001
.*-------------------------------------------------------------#COVERGL 26250000
.*  SKIP REPORT PREPARATION IF OVERFLOW OR ALL OPTION:         #COVERGL 26300000
.*-------------------------------------------------------------#COVERGL 26350000
         AIF  (&$COVALL).INSEXIT                               #COVERGL 26400000
         AIF  (&$COVTOV).INSEXIT                               #COVERGL 26450000
.*-------------------------------------------------------------#COVERGL 26500000
.*  PREPARE REPORT GENERATION:                                 #COVERGL 26550000
.*-------------------------------------------------------------#COVERGL 26600000
.*                                                             #COVERGL 26612501
.*  DID I EVER DO THIS PREPARATION ALREADY?                    #COVERGL 26625001
.*                                                             #COVERGL 26637501
         AIF  (&$COVPRX GT 0).PRPP000                          #COVERGL 26650000
&$COVPRX SETA 1                                                #COVERGL 26700000
&$COVPR1(&$COVPRX)   SETA &TOPNAMX                                      26750000
&$COVPR2(&$COVPRX)   SETA &ACTMCNR                                      26800000
&$COVPR3(&$COVPRX)   SETA &ACTMCNR                                      26833301
         MEXIT                                                 #COVERGL 26866601
.PRPP000 ANOP                                                           26900001
.*                                                             #COVERGL 26904501
.*  DO I HAVE TO LOOK FOR GENERATED OR SKIPPED ENTRIES?        #COVERGL 26909001
.*                                                             #COVERGL 26913501
&N       SETA &TOPNAMX                                         #COVERGL 26918001
         AIF  ('&SKIP' NE 'YES').PRPP00A                       #COVERGL 26922501
&N       SETA -&TOPNAMX                                        #COVERGL 26927001
.PRPP00A ANOP                                                  #COVERGL 26931501
.*                                                             #COVERGL 26936001
.*  START A LOOP TO LOOK FOR THE LAST ENTRY OF THIS TOPIC      #COVERGL 26940501
.*  TO CONTINUE ENTRIES, THAT ARE JUST NUMBERED ON.            #COVERGL 26945001
.*                                                             #COVERGL 26949501
&L       SETA 1                                                #COVERGL 26954001
.PRPPF00 ANOP                                                  #COVERGL 26958501
         AIF  (&$COVPR1(&L) NE &N).PRPPF01                     #COVERGL 26963001
         AIF  (&$COVPR3(&L) EQ &ACTMCNR-1).PRPP020             #COVERGL 26967501
.PRPPF01 ANOP                                                  #COVERGL 26972001
&L       SETA &L+1                                             #COVERGL 26976501
         AIF  (&L LE &$COVPRX).PRPPF00                                  26981001
.*                                                             #COVERGL 26985501
.*  DO NOT OPEN A NEW ENTRY, IF NO ONE AVAILABLE.              #COVERGL 26990001
.*                                                             #COVERGL 26994501
.PRPP001 AIF  (&$COVPRX LE 200).PRPP010                        #COVERGL 27000000
&$COVTOV SETB 1                                                #COVERGL 27050000
         MEXIT                                                 #COVERGL 27100000
.*                                                             #COVERGL 27112501
.*  OPEN A NEW ENTRY OR                                        #COVERGL 27125001
.*                                                             #COVERGL 27137501
.PRPP010 ANOP                                                  #COVERGL 27150000
&$COVPRX SETA &$COVPRX+1                                                27200000
&L       SETA &$COVPRX                                         #COVERGL 27221401
&$COVPR1(&L) SETA &N         (CAN BE > OR < 0 HERE..)                   27242801
&$COVPR2(&L) SETA &ACTMCNR                                              27264201
.*                                                             #COVERGL 27285601
.*  APPEND TO AN EXISTING ONE                                  #COVERGL 27307001
.*                                                             #COVERGL 27328401
.PRPP020 ANOP                                                  #COVERGL 27350000
&$COVPR3(&L) SETA &ACTMCNR                                              27400001
.*-------------------------------------------------------------#COVERGL 27450000
.*  END OF NORMAL INSTRUMENTATION OF SGCOV                     #COVERGL 27500000
.*-------------------------------------------------------------#COVERGL 27550000
         MEXIT                                                 #COVERGL 27600000
.*-------------------------------------------------------------#COVERGL 27650000
.*                                                             #COVERGL 27700000
.*  START OF INSTRUMENTATION FOR ACTIVE=ALL OPTION             #COVERGL 27750000
.*-------------------------------------------------------------#COVERGL 27800000
.GENALL  ANOP                                                  #COVERGL 27850000
&ACTMCNR SETA  &$COVRA2(1)                                     #COVERGL 27900000
&$COVRA2(1) SETA &ACTMCNR+1                                    #COVERGL 27950000
         AIF   (&ACTMCNR GT &$COVRA3(1)).INSER01               #COVERGL 28000000
         AGO   .INSALL                                         #COVERGL 28050000
.*-------------------------------------------------------------#COVERGL 28100000
.*                                                             #COVERGL 28150000
.*  START OF INSTRUMENTATION FOR GIVEN MONITOR CALL NUMBER     #COVERGL 28200000
.*-------------------------------------------------------------#COVERGL 28250000
.INSSLF  ANOP                                                  #COVERGL 28300000
         AIF   (N'&SYSLIST NE 1).INSER02                       #COVERGL 28350000
.*------------------------------------------------------------*#COVERGL 28400000
.*  SEE, IF IT WAS ACTIVATED:                                 *#COVERGL 28450000
.*------------------------------------------------------------*#COVERGL 28500000
&TOPNAM  SETC  '&SYSLIST(1)           '(1,8)                   #COVERGL 28550000
&TOPNAMX SETA  0                                                        28600000
.INSS010 ANOP                                                  #COVERGL 28650000
&TOPNAMX SETA  &TOPNAMX+1                                               28700000
         AIF   (&TOPNAMX GT &$COVNAX).INSEXIT                           28750000
         AIF   ('&TOPNAM' NE '&$COVNAM(&TOPNAMX)').INSS010              28800000
.*------------------------------------------------------------*#COVERGL 28850000
.*  FIND OUT IF USER ALWAYS USED MC= OR NEVER. NO MIXING!     *#COVERGL 28900000
.*------------------------------------------------------------*#COVERGL 28950000
         AIF   (&$COVRAS(&TOPNAMX) EQ 1).INSER04                        29000000
&$COVRAS(&TOPNAMX) SETA 2                                      #COVERGL 29050000
.*------------------------------------------------------------*#COVERGL 29100000
.*  TAKE USER PROVIDED MONITOR CALL NUMBER AND GO:            *#COVERGL 29150000
.*------------------------------------------------------------*#COVERGL 29200000
         AIF   (T'&MC NE 'N').INSER03                          #COVERGL 29250000
.*------------------------------------------------------------*#COVERGL 29350000
.*  SEE IF IT IS IN PERMITTED RANGE:                          *#COVERGL 29400000
.*------------------------------------------------------------*#COVERGL 29450000
&ACTRAX  SETA  &$COVRAX(&TOPNAMX)                              #COVERGL 29500000
&ACTMCNR SETA  &MC+&$COVRA1(&ACTRAX)                           #D61NDGL 29525001
.*       MNOTE *,'ACTMCNR &ACTMCNR'                            #COVERGL 29550000
.*       MNOTE *,'ACTRAX  &ACTRAX '                            #COVERGL 29600000
.*       MNOTE *,'COVRA1  &$COVRA1(&ACTRAX)'                   #COVERGL 29650000
.*       MNOTE *,'COVRA2  &$COVRA2(&ACTRAX)'                   #COVERGL 29700000
.*       MNOTE *,'COVRA3  &$COVRA3(&ACTRAX)'                   #COVERGL 29750000
         AIF   (&$COVRA2(&ACTRAX) EQ -1).INSEXIT               #COVERGL 29800000
         AIF   (&ACTMCNR LT &$COVRA1(&ACTRAX)).INSER05         #COVERGL 29850000
         AIF   (&ACTMCNR GT &$COVRA3(&ACTRAX)).INSER06         #COVERGL 29900000
.*------------------------------------------------------------*#COVERGL 29950000
.*  GO FOR LABEL AND MONITOR CALL GENERATION                  *#COVERGL 30000000
.*------------------------------------------------------------*#COVERGL 30050000
.*       MNOTE *,'ACTMCNR &ACTMCNR'                            #COVERGL 30100000
.*       MNOTE *,'ACTRAX  &ACTRAX '                            #COVERGL 30150000
.*       MNOTE *,'COVRA1  &$COVRA1(&ACTRAX)'                   #COVERGL 30200000
.*       MNOTE *,'COVRA2  &$COVRA2(&ACTRAX)'                   #COVERGL 30250000
.*       MNOTE *,'COVRA3  &$COVRA3(&ACTRAX)'                   #COVERGL 30300000
         AGO   .INSOFF                                         #COVERGL 30350001
.*-------------------------------------------------------------#COVERGL 30400000
.*  SOME ERROR MESSAGES OF INSTRUMENTATION CALL OF SGCOV:      #COVERGL 30450000
.*-------------------------------------------------------------#COVERGL 30500000
.INSER00 ANOP                                                  #COVERGL 30550000
&$COVRA2(&ACTRAX) SETA -1                                      #COVERGL 30600000
         MNOTE 8,'RANGE FOR MON. CALLS FOR &TOPNAM TOO SMALL'  #COVERGL 30650000
         MEXIT                                                 #COVERGL 30700000
.INSER01 ANOP                                                  #COVERGL 30750000
&$COVAIL SETB  1                                               #COVERGL 30800000
         MNOTE 8,'RANGE FOR MC INSTR. TOO SMALL FOR ACTIVE=ALL'         30850000
         MEXIT                                                 #COVERGL 30900000
.INSER02 ANOP                                                  #COVERGL 30950000
         MNOTE 8,'FOR SELF NUMBERING MENTION EXACT. 1 TOPIC!'  #COVERGL 31000000
         MEXIT                                                 #COVERGL 31050000
.INSER03 ANOP                                                  #COVERGL 31100000
         MNOTE 8,'MC= DOES NOT HAVE A NUMERIC VALUE!'          #COVERGL 31150000
         MEXIT                                                 #COVERGL 31200000
.INSER04 ANOP                                                  #COVERGL 31250000
         MNOTE 8,'DO NOT MIX SELF AND AUTO NUMBERING !'        #COVERGL 31300000
         MEXIT                                                 #COVERGL 31350000
.INSER05 ANOP                                                  #COVERGL 31400000
         MNOTE 8,'MC= LESS THEN PERMITTED RANGE!'              #COVERGL 31450000
         MEXIT                                                 #COVERGL 31500000
.INSER06 ANOP                                                  #COVERGL 31550000
         MNOTE 8,'MC= BIGGER THAN PERMITTED RANGE!'            #COVERGL 31600000
.INSEXIT MEXIT                                                 #COVERGL 31650000
.**************************************************************#COVERGL 31652301
.*  TAKE THE VALUES FOR OFFSETS OF ESA,370,VMESA,VM            #COVERGL 31654601
.**************************************************************#COVERGL 31656901
.SETOFF  ANOP                                                  #COVERGL 31659201
 MNOTE *,'HALLO '                                              #COVERGL 31661501
         AIF (T'&OFFESA  NE 'N').SETOER0                       #COVERGL 31663801
         AIF (T'&OFF370  NE 'N').SETOER0                       #COVERGL 31666101
         AIF (T'&OFFVMES NE 'N').SETOER0                       #COVERGL 31668401
         AIF (T'&OFFVM   NE 'N').SETOER0                       #COVERGL 31670701
 MNOTE *,'HALLO1 '                                             #COVERGL 31673001
&$COVOFF(1) SETA &OFFESA                                       #COVERGL 31675301
&$COVOFF(2) SETA &OFF370                                       #COVERGL 31677601
&$COVOFF(3) SETA &OFFVMES                                      #COVERGL 31679901
&$COVOFF(4) SETA &OFFVM                                        #COVERGL 31682201
 MNOTE *,'OFFSET FOR ESA   CODE IS &$COVOFF(1)'                #COVERGL 31684501
 MNOTE *,'OFFSET FOR 370   CODE IS &$COVOFF(2)'                #COVERGL 31686801
 MNOTE *,'OFFSET FOR VMESA CODE IS &$COVOFF(3)'                #COVERGL 31689101
 MNOTE *,'OFFSET FOR VM    CODE IS &$COVOFF(4)'                #COVERGL 31691401
         MEXIT                                                 #COVERGL 31693701
.SETOER0 MNOTE 8,'OFFSET VALUES MUST BE NUMBERS!'              #COVERGL 31696001
         MEND                                                           31700000
