         MACRO                                                          00100001
         MAPDISPI                                                       00150001
         GBLA  &TSNUM             NUMBER OF STATIC PARTITIONS + CLASSES 00200001
         GBLA  &NPART             NUMBER OF STATIC PARTITIONS           00250001
         GBLA  &NCLASS            NUMBER OF CLASSES                     00300001
         GBLB  &IJBSGEN           SUPERVISOR GENERATION                 00350001
         LCLC  &CS                TEMPORARY VALUE                       00400001
         LCLC  &BL                TEMPORARY VALUE                       00450001
         LCLA  &Y                 TEMPORARY VALUE                       00500001
.**************************************************************         00600009
.*       IBM DISK OPERATING SYSTEM                            *         00610009
*        SUPERVISOR - MAPDISPI - 5686-066-06                  *         00650009
         AGO   .ZZZ                                                     00700001
*        LICENSED MATERIALS - PROPERTY OF IBM                 *         00750001
*        "RESTRICTED MATERIALS OF IBM"                        *         00800009
*        5686-066                                             *         00850009
*        (C) COPYRIGHT IBM CORP. 1994, 2001                   *         00860009
***************************************************************         00950001
.ZZZ     ANOP                                                           01000001
.**** START OF SPECIFICATIONS ***************************************** 01050001
.*                                                                      01100001
.*01* MODULE-TYPE = MACRO                                               01150001
.*                                                                      01200001
.*01* DESCRIPTIVE NAME = MAPPING OR INLINE CODE OF                      01250001
.*                       DISPATCHER ($IJBDSP.) INTERFACES               01300001
.*                                                                      01350001
.*  STATUS           =  NEW MACRO - FIRST RELEASE AF 6.1                01400001
.*                                                                      01450001
.*  FUNCTION         =  SHOW LAYOUT OF DISPATCHER INTERFACES            01500001
.*                      AND CODE                                        01550001
.*  NOTES            =  NONE                                            01600001
.*  DEPENDENCIES     =  NONE                                            01650001
.*  RESTRICTIONS     =  NONE                                            01700001
.*  REGISTER CONVENT.=  NONE                                            01750001
.*  REGISTER CONVENT.=  NONE                                            01800001
.*  PATCH LABEL      =  NONE                                            01850001
.*  MACRO TYPE       =  ASSEMBLER                                       01900001
.*  PROCESSOR        =  ASM                                             01950001
.*  MACRO SIZE       =  SEE OUTPUT                                      02000001
.*  ATTRIBUTES       =  MAPPING DSECT AND/OR INLINE CODE                02050001
.*  ENTRY POINT      =  DISPINF                                         02100001
.*  PURPOSE          =  SEE FUNCTION                                    02150001
.*  LINKAGE          =  NONE                                            02200001
.*  CALLER           =  SUPERVISOR OR DISPATCHER SVA ROUTINE            02250001
.*  INPUT            =  SEE MACRO PARAMETER                             02300001
.*  OUTPUT           =  DSECT OR INLINE CODE OF INTERFACES              02350001
.*                                                                      02400001
.*  EXIT NORMAL      =  MACRO EXPANSION                                 02450001
.*                                                                      02500001
.*  EXIT ERROR       =  NONE                                            02550001
.*                                                                      02600001
.*  EXTERNAL REFEREN.=  NONE                                            02650001
.*  ROUTINES         =  NONE                                            02700001
.*  DATA AREAS       =  NONE                                            02750001
.*  CONTROL BLOCKS   =  NONE                                            02800001
.*  TABLES           =  NONE                                            02850001
.*  MACROS           =  NONE                                            02900001
.*  CHANGE ACTIVITIES=  NEW MACRO - FIRST RELEASE AF 6.1       @D61RDIS 02950001
.*      MORE THAN 255 TASKS                                    @D66ODOW 02960009
.*  MODIFIED REGISTER=  SEE DESCRIPTION                                 03000001
.*                                                                      03050001
.**** END OF SPECIFICATIONS ******************************************* 03100001
.*                                                                      03150001
         AIF   (&IJBSGEN).NOWARN                                        03200001
*********************************************************************** 03250001
* WARNING: THE FOLLOWING MAPPING REFERS TO AN INTERNAL SUPERVISOR     * 03300001
*          CONTROL BLOCK. IT MAY CHANGE ANY TIME AND IS NOT COMMITTED * 03350001
*          AS AN INTERFACE OF ANY LEVEL.                              * 03400001
*********************************************************************** 03450001
         AGO   .DES1                                                    03500001
.NOWARN  ANOP                                                           03550001
*------------------------------------------------------------------     03600001
* DISPINF  - START OF PARTITION BALANCING INTERFACES                    03650001
*------------------------------------------------------------------     03700001
DISPINF  DS    0F           START OF INTERFACE AREA                     03750001
&CS      SETC  'C'                                                      03800001
&BL      SETC  ''                                                       03850001
         AGO   .INL1                                                    03900001
.DES1    ANOP                                                           03950001
*******************************************************************     04000001
* DISPINF  - START OF PARTITION BALANCING INTERFACES                    04050001
*******************************************************************     04100001
DISPINF  DSECT                                                          04150001
&CS      SETC  'S'                                                      04200001
&BL      SETC  ' '                                                      04250001
.INL1    ANOP                                                           04300001
*----------- ADDRESSES OF EXTERNAL ROUTINES -----------------------     04350001
ADISPB   D&CS  A&BL.(DISP)        DISPATCHER BASE ADDRESS               04400001
AVALID   D&CS  A&BL.(VALID)       VALIDATION ROUTINE                    04450001
AVALIDSV D&CS  A&BL.(VALIDSVA)    SVA VALIDATION                        04500001
AUPDJA   D&CS  A&BL.(UPDJA)       UPDATE JA POINTERS                    04550001
ACLTABI  D&CS  A&BL.(CLTABINT)    ROUTINE ADDRESS TO LOOK FOR ENTRY     04600001
ADSPSAVE D&CS  A&BL.(DSPSAVAR)    ADDRESS OF SYSTEM TASK SAVE AREA      04650001
ASYSERRO D&CS  A&BL.(SYSERROR)    ADDR. OF HARDWAIT ROUTINE             04700001
AGENENT  D&CS  A&BL.(X'80000000'+GENENT31) ADDR. OF STATUS SAVING ROUT. 04750001
ASVC10DS D&CS  A&BL.(SVC10DSP)    SVC 10 ROUTINE (CALLED BY DSP TASK)   04800001
AACCTPCN D&CS  A&BL.(ACCTPCNT)    POINTS TO ACTIVE PARTITION COUNT      04850001
ASVC57X  D&CS  A&BL.(SVC57)       SVC 57 ROUTINE                        04900001
ADISPEXA D&CS  A&BL.(DISPEXRT)    GENERAL DISPATCHER SCHED. ADDRESS     04950001
APOST    D&CS  A&BL.(POST)        POST ROUTINE                          05000001
AUNPOST  D&CS  A&BL.(UNPOST)      UNPOST ROUTINE                        05050001
ASYSDEAC D&CS  A&BL.(SYSDEACT)    SYSTEM TASK DEACTIVATION              05100001
AFLIHB   D&CS  A&BL.(FLIH)        ADDRESS OF FLIH                       05150001
ADISPFPO D&CS  A&BL.(0)           FORMER ADDR.- OLD POST/FAST  @D62ADIS 05166602
ADISPFPN D&CS  A&BL.(0)           FORMER ADDR.-OLD FAST DISP.  @D62XDIS 05183202
ARQTAB   D&CS  A&BL.(RQTAB-4)     START ADDRESS OF PSS QUEUES           05250001
ARQREADY D&CS  A&BL.(RQREADY)     PSS QUEUE ADDRESS                     05300001
ARQSAVE  D&CS  A&BL.(RQREADY+RQPSS-RQADR) ADDRESS OF PSS SAVE AREA      05350001
AARQTABE D&CS  A&BL.(ARQTABE)     ADDRESS OF END OF PSS QUEUES          05400001
ADSPTIBX D&CS  A&BL.(DSPTIB)      ADDRESS OF DSP SYSTEM TASK TIB        05550001
ADISPATX D&CS  A&BL.(DISPATAB)    ADDRESS OF BYTE OFFSET TABLE          05600001
APCBCTAB D&CS  A&BL.(PCBCTAB)     ADDRESS OF CPCB TABLE                 05700001
APREXTAB D&CS  A&BL.(PREXTTAB)    ADDRESS OF TRANSLATION TABLE          05800001
ASTATPOX D&CS  A&BL.(ASTATPOW)    ADDR. OF STATIC PRTY TABLE ADDR.      05850001
APPRTYOX D&CS  A&BL.(PPRTYOWN-4)  ADDR. OF DYNAMIC PRTY TABLE ADDR.     05950001
AARUNTIM D&CS  A&BL.(ARUNTIME)    ADDR. OF ACCOUNTING POINTER           06000001
AINITTIM D&CS  A&BL.(INITTIME)    ADDR. OF CPU TIMER VALUE              06050001
AALBPCBX D&CS  A&BL.(DPPCB)       ADDR. OF ALLBOUND PCB                 06150001
ATDSAVE  D&CS  A&BL.(TDSAVE)      ADDR. OF TD SAVE AREA                 06175001
APICTR   D&CS  A&BL.(PICTR)       ADDR. OF PAGE-IN COUNT       @DY43697 06212500
ASPINIT  D&CS  A&BL.(SPINIT)      SUBPOOL ADDRESS FOR TSCPS/TSCTS       06250001
ADISPULO D&CS  A&BL.(DISPULOW)    ADDR. OF LOW CORE SYNCHRON.ROUTINE    06275001
         AIF   (&IJBSGEN).SUPVR                                @D66ODOW 06277006
ARTIDH   D&CS  AL2&BL.(ARTID)     AR TASK ID                   @D66ODOW 06300006
DIRTIDH  D&CS  AL2&BL.(DIRTID)    DIR TASK ID                  @D66ODOW 06310006
SUPTIDH  D&CS  AL2&BL.(SUPTID)    SUP TASK ID                  @D66ODOW 06320006
LCKTIDH  D&CS  AL2&BL.(LCKTID)    LCK TASK ID                  @D66ODOW 06330006
         AGO   .SUPVRE                                         @D66ODOW 06341006
.SUPVR   ANOP                                                  @D66ODOW 06341106
         D&CS  AL2&BL.(ARTID)     AR TASK ID                   @D66ODOW 06341206
         D&CS  AL2&BL.(DIRTID)    DIR TASK ID                  @D66ODOW 06341306
         D&CS  AL2&BL.(SUPTID)    SUP TASK ID                  @D66ODOW 06341406
         D&CS  AL2&BL.(LCKTID)    LCK TASK ID                  @D66ODOW 06341506
.SUPVRE  ANOP                                                  @D66ODOW 06343006
*----------- PARTITION BALANCING FIELDS ---------------------------     06350001
TIMEUNIT DS    0CL8               ACTUAL TIME SLICE FOR PBAL            06400001
         D&CS  H'0'                                                     06450001
TSLICE   D&CS  AL4(62500)             DEFAULT 1 SECOND                  06500001
         D&CS  H'0'                                                     06550001
CPUTIME  DS    0CL8               HIGH TIME VALUE FOR JA                06600001
         D&CS  H'0'                                                     06650001
HTIME    D&CS  X'70800000'            DEFAULT 8 HOURS                   06700001
         D&CS  H'0'                                                     06750001
         D&CS  H'0'               NOT USED                              06800001
MSECS    D&CS  A&BL.(62500)       BASIC TIME SLICE VALUE                06900001
MAXMSECS D&CS  A&BL.(625000)      10 SECONDS                            06950001
TOTTIME  D&CS  F'0'                                                     07000001
PCBAWTAB D&CS  (22*4)F'0'                                               07050001
*----------- DELAYED INTERRUPT ADDRESSES --------------------------     07100001
AAPBXARE D&CS  A&BL.(APBXAREA)    ADDRESS OF PBXAREA ADDRESS            07150001
AEXTRTNB D&CS  A&BL.(EXTRTN)      EXTERNAL FLIH BASE ADDRESS            07200001
AAFREMSC D&CS  A&BL.(AFREMSCB)    ADDRESS OF ESTAEX CHECKER ADD@D64ADIS 07250004
ASVC150O D&CS  A&BL.(SVC150OS)    ADDRESS OF SVC 150 OLD PSW   @D64ADIS 07251004
         D&CS  A&BL.(0)           NOT USED                     @D61TDIS 07260003
         MEXIT              LEAVE MACRO                                 07750001
         MEND               LEAVE MACRO                                 07800001
