*/* A000000-999999                                           @D35ADRR*/ 00001023
*/********************************************************************/ 00002023
*/*                                                          @D65EDJS*/ 00003023
*/*        LICENSED MATERIALS - PROPERTY OF IBM              @D65EDJS*/ 00004023
*/*        "RESTRICTED MATERIALS OF IBM"                     @D65EDJS*/ 00005023
*/*        5686-066                                          @D65EDJS*/ 00006023
*/*        (C) COPYRIGHT IBM CORP. 2000                      @D65EDJS*/ 00007023
*/*                                                          @D65EDJS*/ 00008023
*/********************************************************************/ 00009023
*%GOTO GDTLBL1 ;                                                        00010023
         MACRO                                                          00011023
&NAME    SDAGDT &DSECT=                                                 00012023
         LCLC   &C,&S                                        @D65EDJS   00013023
         AGO  .ZZZ                                           @D31EDJO   00014023
*********************************************************************** 00015023
*                                                                       00016023
*    SUPERVISOR - SDAGDT - 5686 - 066 - 06 - 65C               @D66ODow 00017023
*                                                                       00018023
* /* START OF SPECIFICATION ***                                         00019023
*                                                                       00020023
*01* MODULE-NAME = SDAGDT                                               00021023
*                                                                       00022023
*01* DESCRIPTIVE-NAME = GLOBAL DATA TABLE FOR SDAID SERVICES            00023023
*                                                                       00024023
*01* MODULE-TYPE = SUPERVISOR GENERATION MACRO                          00025023
*                                                                       00026023
*02*   PARAMETER-RECEIVED = DSECT                                       00027023
*03*     HOW-PASSED = OPTIONAL KEYWORD                                  00028023
*03*     TYPE = CHARACTER STRING                                        00029023
*04*       VALUE = 'YES' INDICATES THAT THE TABLE IS USED AS DSECT      00030023
*                  'NO'  INDICATES THAT THE TABLE IS USED AS CSECT      00031023
*                  ---DEFAULT IS 'NO'                                   00032023
*                                                                       00033023
*01* CHANGE-ACTIVITY                                                    00034023
*    NEW MACRO - FIRST RELEASE 35 DOS/VS                                00035023
*                                                                       00036023
*    SETUP VIA SYSIN                                           @D14PDJO 00037023
*    DESCRIPTION OF TABLE CONTAINS WRONG DISPLACEMENTS         @DA24554 00038023
*    SPE DY37265                                               @D21LDJO 00039023
*    DATA SPACES                                               @D52VDJO 00040023
*    DY46039: AVOID LOCKING OF ATTENTION ROUTINE AFTER STOPSD  @DY46039 00040323
*             OR ENDSD. ($$BATTN3)                             @DY46039 00040623
*                                                                       00041023
**** END OF SPECIFICATIONS *******************************************/ 00042023
         SPACE                                                          00043023
.ZZZ     ANOP                                                  @D31EDJO 00044023
&C       SETC   'C'                SET 'DC'                    @D65EDJS 00045023
&S       SETC   ''                 NO SEPERATOR                @D65EDJS 00046023
         AIF   ('&DSECT' NE 'YES').DSCT1                                00047023
&C       SETC   'S'                SET 'DS'                    @D65EDJS 00048023
&S       SETC   ' '                SEPERATOR                   @D65EDJS 00049023
&NAME    DSECT                                                          00050023
.DSCT1   ANOP                                                           00051023
         SPACE                                                          00052023
SAVSDAR  DS    0D                  SDAID GLOBAL DATA TABLE              00053023
GDTSDAR  DS    0D                  SDAID GLOBAL DATA TABLE              00054023
GDTNAME  DC    CL8'SDAGDT  '       CONTROL BLOCK NAME                   00055023
GDTFL1   DC    X'00'               F*1 FLAG BYTE 1                      00056023
GDTFL2   DC    X'00'               F*2 FLAG BYTE 2                      00057023
GDTFL3   DC    X'00'               F*3 FLAG BYTE 3                      00058023
GDTFL4   DC    X'00'               F*4 FLAG BYTE 4                      00059023
GDTPERUS DC    H'0'                # OF INTERACTIVE TRACES     @D61HDJO 00060023
         DC    H'0'                RESERVED                    @D61HDJO 00061023
GDTROTLP DC    F'0'                LOAD  POINT OF ROOT PHASE            00062023
GDTROTLG DC    F'0'                LENGTH OF ROOT PHASE                 00063023
GDTMSGLP DC    F'0'                LOAD  POINT OF MSG LIBRARY           00064023
GDTMSGLG DC    F'0'                LENGTH OF MSG LIBRARY                00065023
GDTIOFL  DC    X'00'               F*5 I/O INTERFACE FLAG               00066023
GDTIOFLW EQU   X'80'                   WRITE REQUEST                    00067023
GDTIOFLR EQU   X'40'                   READ  REQUEST                    00068023
GDTIOFLP EQU   X'20'                   WRITE ALSO ON SYSLST             00069023
GDTIOIF  DC    AL3(0)              PTR TO CONSOLE I/O ROUTINE           00070023
GDTIOBUF DC    A(0)                PTR TO CONSOLE BUFFER                00071023
GDTIDTLP DC    A(0)                PTR TO    INITIALISATION DATA TABLE  00072023
GDTIDTLG DC    F'0'                LENGTH OF INITIALISATION DATA TABLE  00073023
GDTIDTEP DC    A(0)                ENTRY  OF INITIALISATION DATA TABLE  00074023
         DC    A(0)                RESERVED                             00075023
GDTEDTLP DC    A(0)                PTR TO    EXECUTION DATA TABLE       00076023
GDTEDTLG DC    F'0'                LENGTH OF EXECUTION DATA TABLE       00077023
GDTEDTEP DC    A(0)                ENTRY OF  EXECUTION DATA TABLE       00078023
         DC    A(0)                RESERVED                             00079023
GDTSTOLP DC    A(0)                ADDRESS OF SDAID AREA                00080023
GDTSTOLG DC    F'0'                LENGTH OF SDAID AREA                 00081023
GDTPGMN  DC    D'0'                SAVED PGM-NEW PSW                    00082023
GDTXR08  DC    F'0'                SAVED CONTROL REG08                  00083023
GDTXR09  DC    F'0'                SAVED CONTROL REG09                  00084023
GDTXR10  DC    F'0'                SAVED CONTROL REG10                  00085023
GDTXR11  DC    F'0'                SAVED CONTROL REG11                  00086023
GDTPWBLP DC    F'0'                                                     00087023
GDTPWBLG DC    F'0'                                                     00088023
         AIF   ('&DSECT' EQ 'YES').DSCT3                                00089023
GDTCCWT1 DC    A(DEVTRTAB)         ADDRESS OF DEV-TYPE TRANSL. TABLE    00090023
GDTCCWT2 DC    A(DEVLIST)          ADDRESS OF DEVICE SPEC. TABLES       00091023
GDTSENSE DC    A(SNSSDAID)         ADDRESS OF SUPVR SNS & HOMEADDR AREA 00092023
GDTOUT   DC    A(0)                SDAID OUTDEVICE CHARACTERISTICS      00093023
GDTTIBA  DC    A(TIBATAB+4)        ADDR OF TIB ADDRESS TABLE  @D36IDRR  00094023
GDTINTID DC    H'0'                TID OF SETUP PARTITION     @D14PDJO  00095023
GDTOUTX  DC    H'0'                OUTDEV TAPE PUB INDEX      @D52VDJO  00096023
         DC    F'0'                FORMER GDTPIB1             @D51IDJO  00097023
         DC    F'0'                FORMER GDTPIB2             @D51IDJO  00098023
GDTATID  DC    AL2(ARTID)          TID OF HIGHEST SYST TASK   @D36IDRR  00099023
         DC    H'0'                FORMER GDTMTID             @D51IDJO  00100023
GDTSTID  DC    AL2(0)              TID OF HIGHEST SUB TASK    @D66ODOW  00101023
GDTTIDER DC    AL2(ERPTID)         TID OF ERP TASK            @D51IDJO  00102023
         AGO   .DSCT4                                                   00103023
.DSCT3   ANOP                                                           00104023
GDTCCWT1 DC    F'0'                                                     00105023
GDTCCWT2 DC    F'0'                                                     00106023
GDTSENSE DC    F'0'                                                     00107023
GDTOUT   DC    F'0'                                                     00108023
GDTTIBA  DC    F'0'                ADDR OF TIB ADDRESS TABLE  @D36IDRR  00109023
GDTINTID DC    H'0'                TID OF SETUP PARTITION     @D14PDJO  00110023
GDTOUTX  DC    H'0'                OUTDEV TAPE PUB INDEX      @D52VDJO  00111023
         DC    F'0'                RESERVED (GDTPIB1)         @D51GDJO  00112023
         DC    F'0'                RESERVED (GDTPIB2)         @D51GDJO  00113023
GDTATID  DC    H'0'                TID OF HIGHEST SYST TASK   @D36IDRR  00114023
         DC    H'0'                RESERVED (GDTMTID)         @D51GDJO  00115023
GDTSTID  DC    H'0'                TID OF HIGHEST SUB TASK    @D36IDRR  00116023
GDTTIDER DC    H'0'                TID OF ERP TASK            @D51IDJO  00117023
.DSCT4   ANOP                                                           00118023
GDTWRBEP DC    A(0)                ENTRY POINT OF WRAP AROUND BUFFER    00119023
GDTWRBLG DC    F'0'                LENGTH OF WRAP AROUND BUFFER         00120023
*---------------------------------------------------------------------* 00121023
*        FIELDS USED BY DUMP COMPONENT                                * 00122023
*---------------------------------------------------------------------* 00123023
         DC    F'0'     RESERVED (GDTDCBUF)                   @D61HDJO  00124023
         DC    F'0'     RESERVED (GDTCOM2)                    @D61HDJO  00125023
         AIF   ('&DSECT' NE 'YES').DSCT7                                00126023
GDTCHNCT DC    F'0'     CHANNEL CONTROL TABLE                 @D52VDJO  00127023
         AGO   .DSCT8                                                   00128023
.DSCT7   ANOP                                                           00129023
GDTCHNCT DC    A(CHNTAB)  CHANNEL CONTROL TABLE               @D52VDJO  00130023
.DSCT8   ANOP                                                           00131023
GDTRTCOM DC    F'0'     INTERFACE AREA TO $$BATTN3            @D14PDJO  00132023
         AIF   ('&DSECT' NE 'YES').DSCTA                      @D52VDJO  00133023
GDTAPB2A DC    F'0'                                           @D52VDJO  00134023
         AGO   .DSCTB                                         @D52VDJO  00135023
.DSCTA   ANOP                                                 @D52VDJO  00136023
GDTAPB2A DC    A(APB2AREA-SYSS00) PTR TO PUB2 AREA            @D52VDJO  00137023
.DSCTB   ANOP                                                 @D52VDJO  00138023
         DC    F'0'     RESERVED                              @D52VDJO  00139023
*---------------------------------------------------------------------* 00140023
*        CP COMMAND WORK AREA FOR IJSDROT          X'D0'      @D52VDJO* 00141023
*---------------------------------------------------------------------* 00142023
GDTCPCMD DC    4F'0'          CP COMMAND WORK AREA (V=REAL)   @DA33314  00143023
GDTCPCLS DC    CL3'   '       CLOSE COMMAND                   @D52VDJO  00144023
GDTCPDEV DC    CL3'   '       OUTDEV PRINTER CUU              @D52VDJO  00145023
         DC    H'0'           RESERVED                        @D52VDJO  00146023
         DC    2F'0'          RESERVED                        @D61HDJO  00147023
*********************************************************************** 00148023
*                                                                     * 00149023
*        IJSDINT-TURBODP COMMUNICATION AREA    X'F0'          @D65EDJS* 00150023
*                                                                     * 00151023
*********************************************************************** 00152023
         DS    0F                                             @D65EDJS  00153023
         D&C   CL4&S.'TDCB'                                   @D65EDJS  00154023
GDTSDFLB DS    0F             SDAID FLAG BYTES                @D65EDJS  00155023
GDTSDFL1 DC    X'00'             FLAG BYTE 1                  @D65EDJS  00156023
SCMSDELE EQU   X'40'               STOPSD/ENDSD GIVEN         @D65EDJS  00157023
GDTSDFL2 DC    X'00'             FLAG BYTE 2                  @D65EDJS  00158023
         DC    XL2'0000'         RESERVED                     @D65EDJS  00159023
                                                                        00160023
GDTINTEP D&C   F&S.'0'        ENTRY POINT FOR EXT/PGMC EVENTS @D65EDJS  00161023
GDTINTSV D&C   F&S.'0'        ENTRY POINT FOR SVC EVENTS      @D65EDJS  00162023
*---------------------------------------------------------------------* 00163023
*        CPU RELATED BIT PATTERNS TO SHOW STATUS OF SDAID     @D65EDJS* 00164023
*---------------------------------------------------------------------* 00165023
GDTSDACT D&C   F&S.'0'        ACTIVE CPUS AT STARTSD TIME     @D65EDJS  00166023
GDTSTOPG D&C   F&S.'0'        CPUS WAITING FOR STOP PROCESSING@D65EDJS  00167023
GDTTERMG D&C   F&S.'0'        CPUS WAITING DUE TO OPT=TERMINAT@D65EDJS  00168023
*---------------------------------------------------------------------* 00169023
*        RETURN ADDRESSES TO CALLING INTERRUPT HANDLER ROUTINE@D65EDJS* 00170023
*---------------------------------------------------------------------* 00171023
GDTEXRET D&C   A&S.(SDEXTRET) EXTERNAL INTERRUPT HANDLER      @D65EDJS  00172023
GDTSVRET D&C   A&S.(SDSVCRET) SVC      INTERRUPT HANDLER      @D65EDJS  00173023
GDTPGRET D&C   A&S.(SDPGMRET) PGMC     INTERRUPT HANDLER      @D65EDJS  00174023
*                                                                       00175023
GDTCCADR D&C   A&S.(GDTCCODE) GET ENTRY POINT OF CODE PART    @D65EDJS  00176023
*---------------------------------------------------------------------* 00177023
*  FIELDS USED FOR STOPSD / ENDSD  ECB(ATT-ROUT) POSTING      @D65EDJS* 00178023
*---------------------------------------------------------------------* 00179023
GDTSDECB DC    F'0'           ECB FOR ATTENTION ROUTINE       @D65EDJS  00180023
GDTXFFFF DC    XL4'FFFFFFFF'  MERGE STOPSD/TERMINATE GATE     @D65EDJS  00181023
GDTPOST  DC    XL4'00008000'  POST ECB FOR ATTENTION ROUTINE  @D65EDJS  00182023
                                                                        00183023
         AIF   ('&DSECT' EQ 'YES').DSMEND                     @D65EDJS  00184023
         DC    CL4'TERM'                                      @D65EDJS  00185023
*********************************************************************** 00186023
* SDAID TERMINATION ROUTINE                                           * 00187023
*                                                                     * 00188023
*    FUNCTION: DELAYED STOPSD / ENDSD PROCESSING                      * 00189023
*    CALLER  : IJSDINT                                                * 00190023
*    EXIT    : INTERRUPT HANDLER ROUTINE OR USER PROGRAM              * 00191023
*                                                                     * 00192023
* EACH CPU IN THE CEC IS REPRESENTED BY A BIT                         * 00193023
*           BIT0 = CPU0                                               * 00194023
*           BITN = CPUN                                               * 00195023
*---------------------------------------------------------------------* 00196023
*   TERMINATION BECAUSE OF STOPSD/ENDSD                               * 00197023
*     ENTRY POINT <GDTSTOP>                                           * 00198023
*       GATE <GDTSTOPG> IS A BIT PATTERN. EACH CPU TURNS ITS BIT OFF. * 00199023
*       THE GATE VALUE IS EVALUATED IN ROUTINE IJSDROT.               * 00200023
*       IF GATE IS ZERO, IJSDROT WILL FINISH ENDSD PROCESSING         * 00201023
*---------------------------------------------------------------------* 00202023
*   TERMINATION CAUSED BY ERROR OR OPTION=TERMINATE                   * 00203023
*     ENTRY POINT <GDTTERM>                                           * 00204023
*       GATE <GDTTERMG> IS A BIT PATTERN. EACH CPU TURNS ITS BIT ON.  * 00205023
*       THE GATE VALUE IS EVALUATED IN IJSDROT IN CASE OF A           * 00206023
*       CONSECUTIVE STOPSD/ENDSD COMMAND.                             * 00207023
*********************************************************************** 00208023
GDTCCODE L     RC,IJBSAVSD       GET BASE OF CONTLBLK GDTCB    @D65EDJS 00209023
         USING SAVSDAR,RC                                      @D65EDJS 00210023
         BASR  RD,0              GET BASE OF SDAGDT CODE PART  @D65EDJS 00211023
         USING *,RD                                            @D65EDJS 00212023
         TM    FSTFLAGS,FSTFLAGT CALLED BY ERROR OR .....      @D65EDJS 00213023
         BNZ   GDTSDTRM          ....OPTION=TERMINATE          @D65EDJS 00214023
*                                                              @D65EDJS 00215023
GDTSDDEL DS    0H                ENTRY FOR DELAYED END PROCESS @D65EDJS 00216023
WAITSTOP L     R1,FSTSDDEL       LOAD CPUS BIT PATTERN         @D65EDJS 00217023
         L     R2,GDTSTOPG       LOAD ACTUAL VALUE OF STOPGATE @D65EDJS 00218023
         NR    R1,R2             TURN CPUS ACTIVE FLAG OFF     @D65EDJS 00219023
         CS    R2,R1,GDTSTOPG    STOPGATE UPDATE SUCCESSFULL ? @D65EDJS 00220023
         BNE   WAITSTOP          NO!! TRY IT AGAIN             @D65EDJS 00221023
         B     SDRETURN          COMMON RETURN PROCESSING      @D65EDJS 00222023
*                                                              @D65EDJS 00223023
GDTSDTRM DS    0H                ENTRY FOR TERMINATE PROCESS   @D65EDJS 00224023
WAITTERM L     R1,FSTSDDEL       LOAD CPUS BIT PATTERN         @D65EDJS 00225023
         L     R2,GDTTERMG       LOAD ACTUAL VALUE OF TERMGATE @D65EDJS 00226023
         OR    R1,R2             TURN FLAG ON                  @D65EDJS 00227023
         CS    R2,R1,GDTTERMG    TERMGATE UPDATE SUCCESSFULL ? @D65EDJS 00228023
         BNE   WAITTERM          NO!! TRY IT AGAIN             @D65EDJS 00229023
*********************************************************************** 00230023
*                   !!! SDAID RETURN !!!                       @D65EDJS 00231023
*   RETURN TO INTERRUPTED PROGRAM OR INTERRUPT HANDLER ROUTINE @D65EDJS 00232023
*   POST ATTENTION ROUTINE (PUT INTO WAIT BY IJSDROT)          @D65EDJS 00233023
*********************************************************************** 00234023
SDRETURN DS    0H                                              @D65EDJS 00235023
         L     R1,GDTSTOPG        LOAD GATE FOR STOPSD/ENDSD   @D65EDJS 00236023
         L     R2,GDTTERMG        LOAD GATE FOR OPT=TERMINATE  @D65EDJS 00237023
         X     R2,GDTXFFFF        MERGE GATE VALUES .......    @D65EDJS 00238023
         NR    R2,R1              ... INTO ................    @D65EDJS 00239023
         CS    R1,R2,GDTSTOPG     ...... GDTSTOPG .........    @D65EDJS 00240023
         BNZ   SDRETURN           TRY IT AGAIN                 @D65EDJS 00241023
         TM    GDTSDFL1,SCMSDELE  STOPSD/ENDSD GIVEN ????      @D65EDJS 00242023
         BZ    SDNOPOST           NO !! NO POSTING REQUIRED    @D65EDJS 00243023
         L     R1,GDTSTOPG        TURN POST BIT ON IN ATTENT-  @D65EDJS 00244023
         L     R2,GDTPOST         ROUTINE-ECB IF THIS IS THE   @D65EDJS 00245023
         CS    R1,R2,GDTSDECB     LAST CPU IN THE CEC.         @D65EDJS 00246023
         BNZ   SDNOPOST           NO POSTING IF NOT LAST CPU   @D65EDJS 00247023
*                                                                       00248023
         CLI   FSTFUNCD,FSTFCEXT  WAIT FOR EXTERNAL INTERRUPT! @D65EDJS 00249023
         BE    SDPOST             YES ! POST ECB               @D65EDJS 00250023
         MVI   FSTFLAGS,X'00'     NO  ! DO NOT TERMINATE SDAID.@D65EDJS 00251023
         SLR   R1,R1                    RESET POST BIT IN ECB  @D65EDJS 00252023
         ST    R1,GDTSDECB              AND CONTINUE NORMAL    @D65EDJS 00253023
         B     SDNOPOST                 UNTIL EXT-INT ARRIVES. @D65EDJS 00254023
*---------------------------------------------------------------------* 00255023
*           POST ATTENTION ROUTINE                             @D65EDJS 00256023
*---------------------------------------------------------------------* 00257023
SDPOST   MVC   SDREGSAV(64),ERA         SAVE ERA OF EXT-INTRPT @D65EDJS 00258023
         LA    R0,ARTID                 LOAD AR TASK ID        @D65EDJS 00259023
*        TREADY TASK=(R0),COND=IO       POST ATTENTION ROUTINE @D65EDJS 00260023
         TREADY TASK=(R0),COND=IO                              @D65EDJS 00261023
         MVC   ERA(64),SDREGSAV         RESTOR ERA FOR EXT-INT @D65EDJS 00262023
*                                                                       00263023
SDNOPOST L     R3,FSTLPSWM       PSW TO RETURN FROM SDAID      @D65EDJS 00264023
         LTR   R3,R3             IF MASK IS NOT ZERO.....      @D65EDJS 00265023
         BNZ   INTROUTN          ....RETURN TO USER PROGRAM    @D65EDJS 00266023
         L     RF,FSTLPSWA       IF MASK IS ZERO, RETURN TO    @D65EDJS 00267023
         BSM   0,RF              CALLING INTERRUPT-HDL ROUTINE @D65EDJS 00268023
*                                                                       00269023
INTROUTN TM    FSTFUNCD,FSTFCPGM CALLED FROM PGMC INTRPT-HDLR? @D65EDJS 00270023
         BZ    LREGSERA          NO!! IT WAS A SVC OR EXT-INT  @D65EDJS 00271023
         LM    R9,R8,SDREGSAV    LOAD REGS FROM SDAID SAVEAREA @D65EDJS 00272023
         LPSW  FSTLPSW           RETURN TO INTERRUPTED USER    @D65EDJS 00273023
*                                                                       00274023
LREGSERA LM    R9,R8,ERA         LOAD REGS FROM ERA            @D65EDJS 00275023
         LPSW  FSTLPSW           RETURN TO INTERRUPTED USER    @D65EDJS 00276023
         DROP  RC,RD                                           @D65EDJS 00277023
         EJECT                                                          00278023
.DSMEND  MEND                                                  @D65EDJS 00279023
* /*-----------------------------------------------------------------*/ 00280023
*%GDTLBL1 : ;                                                           00281023
*/*COPY BOOK=SDAGDT                                                  */ 00282023
*/********************************************************************/ 00283023
*/* BGNSEGMENT(SDAGDT,PROLOGUE)., ************************************/ 00284023
*/*                                                                  */ 00285023
*/*        GDTCB - SDAID GLOBAL DATA TABLE.                          */ 00286023
*/*                                                                  */ 00287023
*/*        THE GDTCB CONTAINS INFORMATION NEEDED WHEN THE SDAID      */ 00288023
*/*        PROGRAM IS ACTIVE.                                        */ 00289023
*/*        THE ADDRESS OF THE GDTCB IS AT DISPLACEMENT X'88'         */ 00290023
*/*        OF THE SYSTEM COMMUNICATION REGION (SYSCOM).              */ 00291023
*/*        THE GDTCB IS PART OF THE DOS/VS SUPERVISOR.               */ 00292023
*/*                                                                  */ 00293023
*/*        CREATED BY     MODIFIED BY    USED BY                     */ 00294023
*/*        -------------------------------------------------         */ 00295023
*/*        IJSDROT        IJSDROT        ALL SDAID ROUTINES          */ 00296023
*/*                       IJSDRDY                                    */ 00297023
*/*                                                                  */ 00298023
*/*        +-----.-----.-----.-----.-----.-----.-----.-----+         */ 00299023
*/*      0 |                   GDTNAME                     |         */ 00300023
*/*        |-----------------------------------------------|         */ 00301023
*/*      8 | F*1 | F*2 | F*3 | F*4 |       RESERVED        | @D21LDJO*/ 00302023
*/*        |-----------------------------------------------|         */ 00303023
*/*     10 |       GDTROTLP        |       GDTROTLG        |         */ 00304023
*/*        |-----------------------------------------------|         */ 00305023
*/*     18 |       GDTMSGLP        |       GDTMSGLG        |         */ 00306023
*/*        |-----------------------------------------------|         */ 00307023
*/*     20 | F*5 |   GDTIORTN      |       GDTIOBUF        |         */ 00308023
*/*        |-----------------------------------------------|         */ 00309023
*/*     28 |       GDTIDTLP        |       GDTIDTLG        |         */ 00310023
*/*        |-----------------------------------------------|         */ 00311023
*/*     30 |       GDTIDTEP        |                       |         */ 00312023
*/*        |-----------------------------------------------|         */ 00313023
*/*     38 |       GDTEDTLP        |       GDTEDTLG        |         */ 00314023
*/*        |-----------------------------------------------|         */ 00315023
*/*     40 |       GDTEDTEP        |                       |         */ 00316023
*/*        |-----------------------------------------------|         */ 00317023
*/*     48 |       GDTSTOLP        |       GDTSTOLG        |         */ 00318023
*/*        |-----------------------------------------------|         */ 00319023
*/*     50 |                   GDTPGMN                     |         */ 00320023
*/*        |-----------------------------------------------|         */ 00321023
*/*     58 |                   GDTSVCN                     |         */ 00322023
*/*        |-----------------------------------------------|         */ 00323023
*/*     60 |                   GDTION                      |         */ 00324023
*/*        |-----------------------------------------------|         */ 00325023
*/*     68 |                   GDTEXTN                     |         */ 00326023
*/*        |-----------------------------------------------|         */ 00327023
*/*     70 |       GDTXR08         |       GDTXR09         |         */ 00328023
*/*        |-----------------------------------------------|         */ 00329023
*/*     78 |       GDTXR10         |       GDTXR11         |         */ 00330023
*/*        |-----------------------------------------------|         */ 00331023
*/*     80 |       GDTPWBLP        |       GDTPWBLG        |         */ 00332023
*/*        |-----------------------------------------------|         */ 00333023
*/*     88 |       GDTCCWT1        |       GDTCCWT2        |         */ 00334023
*/*        |-----------------------------------------------|         */ 00335023
*/*     90 |       GDTSENSE        |       GDTOUT          |         */ 00336023
*/*        |-----------------------------------------------| @D36IDRR*/ 00337023
*/*     98 |       GDTTIBA         | GDTINTID  | GDTOUTX   | @D52VDJO*/ 00338023
*/*        |-----------------------------------------------| @D36IDRR*/ 00339023
*/*     A0 |       RESERVED        |       RESERVED        | @D52VDJO*/ 00340023
*/*        |-----------------------------------------------| @D36IDRR*/ 00341023
*/*     A8 | GDTATID   |           | GDTSTID   | GDTTIDER  | @D52VDJO*/ 00342023
*/*        |-----------------------------------------------|         */ 00343023
*/*     B0 |       GDTWRBEP        |       GDTWRBLG        | @DA24544*/ 00344023
*/*        |===============================================|         */ 00345023
*/*     B8 |       RESERVED        |       RESERVED        | @D61HDJO*/ 00346023
*/*        |-----------------------------------------------|         */ 00347023
*/*     C0 |       GDTCHNCT        |       GDTRTCOM        | @D52GDJO*/ 00348023
*/*        |===============================================|         */ 00349023
*/*     C8 |       GDTAPB2A        |       RESERVED        | @D52VDJO*/ 00350023
*/*        |-----------------------------------------------|         */ 00351023
*/*     D0 |             GDTCPCMD                          | @D52VDJO*/ 00352023
*/*        |-----------------------------------------------|         */ 00353023
*/*     D8 |             GDTCPCMD (CONTINUED)              | @D52VDJO*/ 00354023
*/*        |-----------------------------------------------|         */ 00355023
*/*     E0 |    GDTCPCLS    |    GDTCPDEV     | RESERVED   | @D52VDJO*/ 00356023
*/*        +===============================================+         */ 00357023
*/*                                                                  */ 00358023
*/* ENDSEGMENT(PROLOGUE).,-------------------------------------------*/ 00359023
*                                                                       00360023
* /*-----------------------------------------------------------------*/ 00361023
* /*     MODULES ACTIVE TO BRING UP SDAID                            */ 00362023
* /*-----------------------------------------------------------------*/ 00363023
*DCL  ROTMOD  ENTRY VALRG(*) BASED(GDTROTLP); /* IJSDROT     @D31EDJO*/ 00364023
*DCL  IOSMOD  ENTRY VALRG(*) BASED(GDTIORTN); /* IJSDIOS     @D31EDJO*/ 00365023
*DCL  IDTMOD  ENTRY VALRG(*) BASED(GDTIDTLP); /* IJSDIDT     @D31EDJO*/ 00366023
*DCL  EDTMOD  ENTRY VALRG(*) BASED(GDTEDTLP); /* IJSDEDT     @D31EDJO*/ 00367023
*DCL  MSGMOD  ENTRY VALRG(*) BASED(GDTMSGLP); /* IJSDMSG     @D31EDJO*/ 00368023
*DCL  PWBMOD  ENTRY VALRG(*) BASED(GDTPWBLP); /* IJSDPWB     @D31EDJO*/ 00369023
*@SPACE;                                                                00370023
*DCL  1      GDTCB    BASED        /* GLOBAL "DATA "TABLE !GDTCB     */ 00371023
*                     BDY(DWORD) ,                                      00372023
*      2     GDTNAME  CHAR(8) ,    /* CONTROL BLOCK NAME ('!GDTCB')  */ 00373023
*      2     GDTFL1   FIXED(8),    /* F*1 FLAG BYTE 1                */ 00374023
*       3    *        BIT(1)  ,    /* NOT USED               @D3CZDRR*/ 00375023
*       3    GDTFL12  BIT(1)  ,    /* !OUTDEV  COMMAND SPECIFIED     */ 00376023
*       3    GDTFL13  BIT(1)  ,    /* !TRACE   COMMAND SPECIFIED     */ 00377023
*       3    *        BIT(4)  ,    /* NOT USED                       */ 00378023
*       3    GDTFL18  BIT(1)  ,    /* !READY  COMMAND SPECIFIED      */ 00379023
*      2     GDTFL2   FIXED(8),    /* F*2 FLAG BYTE 2                */ 00380023
*       3    GDTFL21  BIT(1)  ,    /* !SDAID   COMMAND SPECIFIED     */ 00381023
*       3    GDTFL22  BIT(1)  ,    /* !STOPSD  COMMAND SPECIFIED     */ 00382023
*       3    GDTFL23  BIT(1)  ,    /* !STRTSD   COMMAND SPECIFIED    */ 00383023
*       3    GDTFL24  BIT(1)  ,    /* !ENDSD   COMMAND SPECIFIED     */ 00384023
*      2     GDTFL3   FIXED(8),    /* F*3 FLAG BYTE 3                */ 00385023
*       3    GDTFL31  BIT(1)  ,    /*OUTDEV TAPE IS RESERVED @D21LDJO*/ 00386023
*       3    GDTFL32  BIT(1)  ,    /* !OUTDEV  COMMAND IN PROGRESS   */ 00387023
*       3    GDTFL33  BIT(1)  ,    /* !TRACE   COMMAND IN PROGRESS   */ 00388023
*       3    *        BIT(3)  ,    /* NOT USED               @D61HDJO*/ 00389023
*       3    GDTFL37  BIT(1)  ,    /* !READY  COMMAND IN PROGRESS    */ 00390023
*       3    GDTFL38  BIT(1)  ,    /* !READY  COMMAND IN PROGRESS    */ 00391023
*      2     GDTFL4   FIXED(8),    /* F*4 FLAG BYTE 4                */ 00392023
*       3    GDTFL41  BIT(1)  ,    /* !SDAID COMMAND IN PROGRESS     */ 00393023
*       3    GDTFL42  BIT(1)  ,    /* !STOPSD COMMAND IN PROGRESS    */ 00394023
*       3    GDTFL43  BIT(1)  ,    /* !STRTSD  COMMAND IN PROGRESS   */ 00395023
*       3    GDTFL44  BIT(1)  ,    /* !ENDSD  COMMAND IN PROGRESS    */ 00396023
*      2     GDTPERUS FIXED(16),  /* # OF INTERACTIVE TRACES @D61HDJO*/ 00397023
*      2     *        FIXED(16),   /* RESERVED               @D21LDJO*/ 00398023
*/*------------------------------------------------------------------*/ 00399023
*/*      ANCHOR FOR INITIALIZATION MANAGER !IJSDROT            X'10' */ 00400023
*/*------------------------------------------------------------------*/ 00401023
*      2     GDTROT           ,    /* ANCHOR FOR !IJSDROT            */ 00402023
*       3    GDTROTLP PTR(31) ,    /* LOAD ADDRESS OF PHASE OR 0     */ 00403023
*        4   GDTROTF1 BIT(1)  ,    /* RELEASE !IJSDROT               */ 00404023
*       3    GDTROTLG FIXED(32),   /* PHASE SIZE IN HEX OR 0         */ 00405023
*/*------------------------------------------------------------------*/ 00406023
*/*      ANCHOR FOR MESSAGE LIBRARY AND MANAGER !IJSDMSG       X'18' */ 00407023
*/*------------------------------------------------------------------*/ 00408023
*      2     GDTMSG           ,    /* ANCHOR FOR !IJSDMSG            */ 00409023
*       3    GDTMSGLP PTR(31) ,    /* LOAD ADDRESS OF PHASE OR 0     */ 00410023
*       3    GDTMSGLG FIXED(32),   /* PHASE SIZE IN HEX OR 0         */ 00411023
*/*------------------------------------------------------------------*/ 00412023
*/*      ANCHOR FOR !I/O ROUTINE IN !$$BATTN3                  X'20' */ 00413023
*/*------------------------------------------------------------------*/ 00414023
*      2     GDTIOIF          ,    /* ANCHOR FOR !IORTN IN !$$BATTN3 */ 00415023
*       3    GDTIOFL  FIXED(8),    /* F*5 FLAG BYTE                  */ 00416023
*        4   GDTIOFLW BIT(1)  ,    /* INDICATES !WRITE REQUEST       */ 00417023
*        4   GDTIOFLR BIT(1)  ,    /* INDICATES !READ REQUEST        */ 00418023
*        4   GDTIOFLP BIT(1)  ,    /* WRITE ALSO ON SYSLST  @DY33314 */ 00419023
*       3    GDTIORTN PTR(24)      /* ADDRESS OF !I/O ROUTINE        */ 00420023
*                     BDY(WORD,2),                                      00421023
*       3    GDTIOBUF PTR(31) ,    /* ADDRESS OF CONSOLE !I/O BUFFER */ 00422023
*/*------------------------------------------------------------------*/ 00423023
*/*      ANCHOR FOR "INITIALIZATION "DATA "TABLE !IJSDIDT      X'28' */ 00424023
*/*------------------------------------------------------------------*/ 00425023
*      2     GDTIDT   ,            /* ANCHOR FOR !IJSDIDT            */ 00426023
*       3    GDTIDTLP PTR(31) ,    /* LOAD ADDRESS OF PHASE OR 0     */ 00427023
*       3    GDTIDTLG PTR(31) ,    /* PHASE SIZE IN HEX OR 0         */ 00428023
*       3    GDTIDTEP PTR(31) ,    /* ADDRESS OF "INITIALIZATION "DATA  00429023
*                                     "TABLE !IDTCB                  */ 00430023
*      2     *        PTR(31) ,                                         00431023
*/*------------------------------------------------------------------*/ 00432023
*/*      ANCHOR FOR "EXECUTION "DATA "TABLE !IJSDEDT           X'38' */ 00433023
*/*------------------------------------------------------------------*/ 00434023
*      2     GDTEDT   ,            /* ANCHOR FOR !IJSDEDT            */ 00435023
*       3    GDTEDTLP PTR(31) ,    /* LOAD ADDRESS OF PHASE OR 0     */ 00436023
*       3    GDTEDTLG PTR(31) ,    /* PHASE SIZE IN HEX OR 0         */ 00437023
*       3    GDTEDTEP PTR(31) ,    /* ADDRESS OF "EXECUTION "DATA       00438023
*                                     "TABLE !EDTCB.                 */ 00439023
*       3    *        PTR(31),     /* RESERVED                       */ 00440023
*/*------------------------------------------------------------------*/ 00441023
*/*      ANCHOR FOR !SDAID EXECUTION STORAGE                   X'48' */ 00442023
*/*------------------------------------------------------------------*/ 00443023
*      2     GDTSTO   ,                                                 00444023
*       3    GDTSTOLP PTR(31) ,    /* START ADDRESS OF EXECUTION AREA*/ 00445023
*       3    GDTSTOLG FIXED(32),   /* SIZE OF AREA IN HEX OR 0       */ 00446023
*/*------------------------------------------------------------------*/ 00447023
*/*      SAVE AREAS FOR "NEW PSW"S                             X'50' */ 00448023
*/*------------------------------------------------------------------*/ 00449023
*      2     GDTPGMN  CHAR(8)      /* SAVE AREA FOR "PGM NEW PSW"    */ 00450023
*                     BDY(DWORD),                                       00451023
*        3   *        CHAR(4)   ,  /* NOT USED                       */ 00452023
*        3   GDTPGMA  PTR(31)   ,  /* ENTRY POINT FOR !PGM INTERRUPTS*/ 00453023
*/*------------------------------------------------------------------*/ 00454023
*/*      SAVE AREA FOR CONTROL REGISTERS                       X'70' */ 00455023
*/*------------------------------------------------------------------*/ 00456023
*      2     GDTXR    CHAR(16)     /* SAVE AREA FOR CONTROL REGISTERS*/ 00457023
*                     BDY(WORD)  ,                                      00458023
*       3    GDTXR08  PTR(31)    , /* CONTROL REGISTER 8             */ 00459023
*        4   GDTXR08C CHAR(4)    ,                                      00460023
*       3    GDTXR09  PTR(31)    , /* CONTROL REGISTER 9             */ 00461023
*        4   GDTXR09C CHAR(4)    ,                                      00462023
*       3    GDTXR10  PTR(31)    , /* CONTROL REGISTER 10            */ 00463023
*        4   GDTXR10C CHAR(4)    ,                                      00464023
*       3    GDTXR11  PTR(31)    , /* CONTROL REGISTER 11            */ 00465023
*        4   GDTXR11C CHAR(4)    ,                                      00466023
*/*------------------------------------------------------------------*/ 00467023
*/*      ANCHOR FOR PRINT BUFFER PHASE !IJSDPWB                X'80' */ 00468023
*/*------------------------------------------------------------------*/ 00469023
*      2     GDTPWB   ,            /* ANCHOR FOR !IJSDPWB            */ 00470023
*       3    GDTPWBLP PTR(31)    , /* PHASE LOAD ADDRESS OR 0        */ 00471023
*       3    GDTPWBLG PTR(31)    , /* PHASE SIZE IN HEX OR 0         */ 00472023
*/*------------------------------------------------------------------*/ 00473023
*/*      POINTERS TO DATA AREAS IN !DOS/VSE SUPERVISOR         X'88' */ 00474023
*/*------------------------------------------------------------------*/ 00475023
*      2     GDTCCW              ,                                      00476023
*       3    GDTCCWT1 PTR(31)    , /* ADDRESS OF "DEVICE "TRANSLATION   00477023
*                                     "TABLE !(DEVTRTAB)             */ 00478023
*       3    GDTCCWT2 PTR(31)    , /* ADDRESS OF "DEVICE "SPECIFIC      00479023
*                                     "TABLE !(DEVLIST)              */ 00480023
*       3    GDTSENSE PTR(31)    , /* ADDRESS OF SENSE BUFFER        */ 00481023
*       3    GDTOUT   PTR(31)    , /* OUTDEVICE CHARACTERISTICS      */ 00482023
*        4   GDTOUTCH PTR(16)    , /* OUTDEVICE CHANNEL NUMBER       */ 00483023
*        4   GDTOUTDT PTR(8)     , /* OUTDEVICE DEVICE TYPE          */ 00484023
*        4   *        PTR(8)     , /* RESERVED               @D21LDJO*/ 00485023
*       3    *        PTR(31)    , /* RESERVED (GDTTIBA)     @D61HDJO*/ 00486023
*       3    GDTINTID PTR(16)    , /* TID OF SETUP PARTITION @D14PDJO*/ 00487023
*       3    GDTOUTX  PTR(16)    , /* OUTDEV-TAPE PUB INDEX  @D21LDJO*/ 00488023
*       3    *        PTR(31)    , /* FORMER GDTPIB1         @D51IDJO*/ 00489023
*       3    *        PTR(31)    , /* FORMER GDTPIB2         @D51IDJO*/ 00490023
*       3    GDTATID  PTR(16)    , /* !TID OF HIGHEST SYST TASK         00491023
*                                                            @D36IDRR*/ 00492023
*       3    *        PTR(16)    , /* FORMER GDTMTID         @D51IDJO*/ 00493023
*       3    GDTSTID  PTR(16)    , /* !TID OF HIGHEST SUB-TASK          00494023
*                                                            @D36IDRR*/ 00495023
*       3    *        PTR(16),     /*(GDTTIDER) RESERVED     @D61HDJO*/ 00496023
*/*------------------------------------------------------------------*/ 00497023
*/*      ANCHOR FOR WRAP AROUND BUFFER                         X'B0' */ 00498023
*/*------------------------------------------------------------------*/ 00499023
*      2     GDTWRB   ,            /* ANCHOR FOR WRAP/TAPE BUFFER    */ 00500023
*       3    GDTWRBEP PTR(31) ,    /* START ADDRESS OF BUFFER OR 0   */ 00501023
*       3    GDTWRBLG PTR(31) ,    /* SIZE OF BUFFER IN HEX OR 0     */ 00502023
*/*------------------------------------------------------------------*/ 00503023
*/*      FIELDS USED BY !DOS/VSE DUMP COMPONENT                X'B8' */ 00504023
*/*------------------------------------------------------------------*/ 00505023
*      2     *        PTR(31),      /* RESERVED (GDTDCBUF)   @D61HDJO*/ 00506023
*      2     *        PTR(31),      /* RESERVED (GDTCOM2)    @D61HDJO*/ 00507023
*      2     GDTCHNCT PTR(31)    ,  /* CHANNEL CONTROL TABLE @D52VDJO*/ 00508023
*      2     GDTRTCOM PTR(31)    ,  /*INPUT AREA TO $$BATTN3 @D14PDJO*/ 00509023
*      2     GDTAPB2A PTR(31),      /* PUB2AREA FOR IJBXMPPA @D52VDJO*/ 00510023
*      2     *        PTR(31)    ,   /* RESERVED             @D52VDJO*/ 00511023
*/*------------------------------------------------------------------*/ 00512023
*/*      CP COMMAND WORK AREA FOR IJSDROT           X'D0'    @D52VDJO*/ 00513023
*/*------------------------------------------------------------------*/ 00514023
*      2     GDTCPCMD CHAR(16),     /* CP COMMAND WORK AREA  @D52VDJO*/ 00515023
*      2     GDTCPCLS CHAR(3),      /* VM CLOSE COMMAND      @D52VDJO*/ 00516023
*      2     GDTCPDEV CHAR(3),      /* VM PRINTER CUU        @D52VDJO*/ 00517023
*      2     *        CHAR(10),     /* RESERVED              @D61HDJO*/ 00518023
*/*------------------------------------------------------------------*/ 00519023
*/*      IJSDINT-TURBODP COMMUNICATION AREA    X'F0'         @D65EDJS*/ 00520023
*/*------------------------------------------------------------------*/ 00521023
*      2     *        CHAR(4)       /* CB-NAME=TDCB          @D65EDJS*/ 00522023
*                     BDY(WORD),    /*                       @D65EDJS*/ 00523023
*      2    GDTSDFLB  CHAR(4),      /* SDAID FLAG BYTES      @D65EDJS*/ 00524023
*        3  GDTSDFL1 BIT(8)  ,      /* SDAID FLAG BYTE 1     @D65EDJS*/ 00525023
*         4 *        BIT(1)  ,      /*                       @D65EDJS*/ 00526023
*         4 SCMSDELE BIT(1)  ,      /* DELAYED END FLAG      @D65EDJS*/ 00527023
*         4 SCMSDSEL BIT(1)  ,      /* TASK SELECTION DONE   @D65EDJS*/ 00528023
*         4 SCMSDPCB BIT(1)  ,      /* CREG 9-11 in PCB      @D65EDJS*/ 00529023
*         4 *        BIT(4)  ,      /* NOT USED              @D65EDJS*/ 00530023
*        3  GDTSDFL2 BIT(8)  ,      /* SDAID FLAG BYTE 2     @D65EDJS*/ 00531023
*         4  SCMSDFL1 BIT(1),  /*INITIALIZATION IN PROGRESS  @DA33314*/ 00532023
*         4  SCMSDFL2 BIT(1) , /*INITIALIZATION READY        @D367DRR*/ 00533023
*         4  SCMSDFL3 BIT(1) , /* SDAID STOPPED              @D367DRR*/ 00534023
*         4  SCMSDFL4 BIT(1),  /* SDAID TERM. ON USER REQUEST@DA33314*/ 00535023
*         4  SCMSDFL5 BIT(1),  /* SDAID TERM. VIA OPTION TERM@DA33314*/ 00536023
*         4  SCMSDFL6 BIT(1),  /* INTERNAL PROGRAM CHECK     @D65EDJS*/ 00537023
*         4  SCMSDFL7 BIT(1) , /* I/O ERROR ON OUTDEV        @DA33314*/ 00538023
*         4  SCMSDFL8 BIT(1),  /* SDAID SETUP VIA 'EXEC'     @D52VDJO*/ 00539023
*        3  *         CHAR(2) ,     /* NOT USED              @D65EDJS*/ 00540023
*                                                                       00541023
*/*------------------------------------------------------------------*/ 00542023
*/*      ENTRY POINTS FOR INTERRUPTS TO BE TRACED            @D65EDJS*/ 00543023
*/*------------------------------------------------------------------*/ 00544023
*      2    GDTINTEP PTR,           /* EXTERNAL / PGMCK      @D65EDJS*/ 00545023
*        3  *        PTR(8) ,       /*                       @D65EDJS*/ 00546023
*        3  GDTINTEA PTR(24),       /*                       @D65EDJS*/ 00547023
*      2    GDTINTSV PTR    ,       /* SVC                   @D65EDJS*/ 00548023
*        3  *        PTR(8) ,       /*                       @D65EDJS*/ 00549023
*        3  GDTINTSA PTR(24),       /*                       @D65EDJS*/ 00550023
*/*------------------------------------------------------------------*/ 00551023
*/*      CPU RELATED BIT PATTERNS TO SHOW STATUS OF SDAID    @D65EDJS*/ 00552023
*/*------------------------------------------------------------------*/ 00553023
*      2    GDTSDACT FIXED(32) ,    /* ACTIVE CPUS           @D65EDJS*/ 00554023
*        3  GDTACSTR FIXED(16) ,    /* ACTIVE CPUS STARTSD   @dy46039*/ 00554324
*        3  GDTACSTP FIXED(16) ,    /* ACTIVE CPUS STOPSD    @dy46039*/ 00554624
*      2    GDTSTOPG FIXED(32) ,    /* CPUS IN STOP PROCESS  @D65EDJS*/ 00555023
*      2    GDTTERMG FIXED(32) ,    /* WAITING FOR OPT=TERM  @D65EDJS*/ 00556023
*/*------------------------------------------------------------------*/ 00557023
*/*    RETURN ADDRESSES TO CALLED INTERRUPT HANDLER ROUTINES @D65EDJS*/ 00558023
*/*------------------------------------------------------------------*/ 00559023
*      2    GDTEXRET FIXED(32),     /* EXTERNAL              @D65EDJS*/ 00560023
*      2    GDTSVRET FIXED(32),     /* SVC                   @D65EDJS*/ 00561023
*      2    GDTPGRET FIXED(32),     /* PGMC                  @D65EDJS*/ 00562023
*                                                                       00563023
*      2    GDTCCADR FIXED(32),     /* ADDRESS OF CODE-PART  @D65EDJS*/ 00564023
*      2    GDTSDECB FIXED(32),     /* ATT-ROUTINE ECB       @D65EDJS*/ 00565023
*      2    GDTXFFFF FIXED(32),     /* MERGE GATES           @D65EDJS*/ 00566023
*      2    GDTPOST  FIXED(32);     /* POST ATT-ROUT ECB     @D65EDJS*/ 00567023
*                                                                       00568023
*                                                                       00569023
*/*COPY BOOK END                                                     */ 00570023
