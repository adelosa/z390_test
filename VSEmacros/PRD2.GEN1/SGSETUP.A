         MACRO                                                          00050000
&NAME    SGSETUP &FUNCT,                                               *00100000
               &STASK=,                                                *00150000
               &WR1=,                                                  *00200000
               &WR2=,                                                  *00250000
               &WR3=,                                          @D51IDIS*00300000
               &SP=,                                           @D14NDFG*00350000
               &VA=,                                           @D14NDFG*00400000
               &RA=,                                           @D14NDFG*00450000
               &OPTION=,                                               *00500000
               &NEXTPCB=,                                              *00550000
               &NEXTSCB=,                                      @D52GDIS*00575001
               &EXIT=,                                                 *00600000
               &ERREXIT=                                       @D51IDIS 00650000
         LCLC  &INDEX                                          @D14BDFR 00900000
         LCLC  &INDEX1                                         @D14ADVB 00950000
         LCLC  &INDEX2                                         @D51GDRP 01000000
         LCLC  &INDEX3                                         @D61NDIS 01020001
         LCLC  &INDEX4                                         @D61NDIS 01040001
         LCLC  &INDEX5                                         @D64XDIS 01044003
         LCLC  &INDEX6                                         @D64XDIS 01048003
         LCLC  &INDEX7                                         @D64XDIS 01052003
         LCLC  &INDEX8                                         @D64XDIS 01056003
         LCLC  &NOSWTCH                                        @D61NDIS 01060001
         LCLC  &SWTCH                                          @D61NDIS 01080001
         LCLC  &BR                                             @D14BDRP 01100000
         LCLC  &EXIT1                                          @D14BDRP 01150000
         LCLC  &ISKMSK                                         @D14BDRP 01200000
         LCLC  &R2                                             @D14BDRP 01250000
.*       IBM DISK OPERATING SYSTEM                            *@D368DEM 01300000
         SPACE 1                                               @D149DFR 01350000
**************************************************             @D149DIS 01400000
*        SUPERVISOR - SGSETUP - 5686-066                      *@D61NDIS 01450001
         AGO   .ZZZ                                           *@D368DEM 01500000
*        LICENSED MATERIALS - PROPERTY OF IBM                 *@D368DEM 01550000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"          *@D31UDMZ 01600000
*        5686-066 (C) COPYRIGHT IBM CORP. 1979, 1996          *@D61PDIS 01650004
*        SEE COPYRIGHT INSTRUCTIONS                           *@D31UDMZ 01700000
.ZZZ     ANOP                                                 *@D368DEM 01750000
.* /* START OF SPECIFICATIONS ********************************          01800000
.*01  MODULE TYPE      = SUPERVISOR GENERATION MACRO                    01850000
.*01  DESCRIPTIVE NAME = DE/ACTIVATE SYSTEM TASKS                       01900000
.*01  NOTES            = CHANGE ACTIVITY                                01950000
.*    TASKING EXTENSIONS, JA ROUTINES, LRA INSTR. SIMULATION   @D149DFR 02000000
.*    MULTIPLE VIRTUAL MEMORIES                                @D14NDFG 02050000
.*    NEW SYSTEM LAYOUT                                        @D51GDRP 02100000
.*    ESA ACCESS REGISTER SUPPORT                              @D51ADTP 02150000
.*    31 BIT ADDRESSING                                        @D52VDIS 02175001
.*    DATA SPACE SUPPORT                                       @D52GDIS 02187501
.*    LINKAGE STACK SUPPORT                                    @D61PDOW 02190601
.*    DISPATCHER USAGE TO SET UP SYSTEM TASK ENVIRONMENT       @D61PDIS 02193701
.*    PROVIDE SHARED SCB FOR SHARED TASKS                      @D61NDIS 02196801
.*    TRACE: SUBTASKING NOT CONSIDERED                         @KXA0065 02198401
.*    ESA EXPLOITATION                                         @D64XDIS 02199203
.*    FUNC=XLRA REMOVED                                        @D64ADIS 02199303
.*    A000000-999999                                           @D36IDFR 02200000
.*02  PROCESSOR        = ASSEMBLER                                      02250000
.*END OF SPECIFICATIONS       *******************************/          02300000
 MNOTE *,'       &FUNCT '                                               02400000
         AIF   (T'&STASK EQ 'O').NOSTASK                                02450000
 MNOTE *,'              &STASK-TASK'                                    02500000
.NOSTASK AIF   (T'&SP EQ 'O').NOSP                             @D14NDFG 02550000
 MNOTE *,'                          SP=&SP'                    @D14NDFG 02600000
.NOSP    AIF   (T'&VA EQ 'O').NOVA                             @D14NDFG 02650000
 MNOTE *,'                          VA=&VA'                    @D14NDFG 02700000
.NOVA    AIF   (T'&RA EQ 'O').NORA                             @D14NDFG 02750000
 MNOTE *,'                          RA=&RA'                    @D14NDFG 02800000
.NORA    AIF   (T'&WR1 EQ 'O').NOWR1                           @D14NDFG 02850000
 MNOTE *,'                          WR1=&WR1'                           02900000
.NOWR1   AIF   (T'&WR2 EQ 'O').NOWR2                                    02950000
 MNOTE *,'                          WR2=&WR2'                           03000000
.NOWR2   AIF   (T'&WR3 EQ 'O').NOWR3                                    03050000
 MNOTE *,'                          WR3=&WR3'                           03100000
.NOWR3   AIF   (T'&OPTION EQ 'O').NOOPT                                 03150000
 MNOTE *,'                       OPTION=&OPTION'                        03200000
.NOOPT   AIF   (T'&NEXTPCB EQ 'O').NOPCB                                03250000
 MNOTE *,'                      NEXTPCB=&NEXTPCB'                       03300000
.NOPCB   AIF   (T'&NEXTSCB EQ 'O').NOSCB                       @D52VDIS 03325001
 MNOTE *,'                      NEXTSCB=&NEXTSCB'              @D52VDIS 03350001
.NOSCB   AIF   (T'&EXIT EQ 'O').NOEXIT                         @D52VDIS 03375001
 MNOTE *,'                         EXIT=&EXIT'                          03400000
.NOEXIT  ANOP                                                  @D51IDIS 03450000
         AIF   (T'&ERREXIT EQ 'O').NODEBUG                     @D51IDIS 03500000
 MNOTE *,'                         ERREXIT=&ERREXIT'           @D51IDIS 03550000
.NODEBUG ANOP                                                  @D149DIS 03600000
 MNOTE *,'************************************************'    @D149DIS 03650000
         SPACE 1                                               @D149DIS 03700000
         AIF   (T'&NAME EQ 'O').NONAME                                  03750000
&NAME    DS    0H                                                       03800000
.NONAME  ANOP                                                           03850000
         AIF   ('&FUNCT' EQ 'SETSCB').RSSCB                    @D52VDIS 03862501
         AIF   ('&FUNCT' EQ 'RESETSCB').RSSCB                  @D52VDIS 03875001
         AIF   ('&FUNCT' EQ 'SETLCTL').SLCTL                   @D52VDIS 03881201
         AIF   (T'&NEXTSCB NE 'O').ERRSCB                      @D52VDIS 03887501
         AIF   ('&FUNCT' EQ 'UPDTIME').UPDTIME                          03900000
         AIF   ('&FUNCT' EQ 'LRA').LRA                         @D14BDRP 03950000
         AIF   ('&FUNCT' EQ 'UNPOST').UNPOST                            04050000
         AIF   (T'&STASK EQ 'O').ERROR                                  04100000
         AIF   (T'&WR1 EQ 'O').ERROR                                    04150000
         AIF   ('&FUNCT' EQ 'ACTIVATE').ACTIVAT                         04200000
         AIF   ('&FUNCT' EQ 'SWOWNER').SWOWNER                 @D14BDFR 04250000
.ERRSCB  MNOTE 5,'NEXTSCB NOT ALLOWED - MACRO IGNORED'         @D52VDIS 04362501
         MEXIT                                                 @D52VDIS 04375001
.* --------------- SWOWNER  START ----------------------------          04387501
.SWOWNER ANOP                                                  @D61PDIS 04408301
         AIF   ('&OPTION' EQ 'SETOWNER').SETOWNR               @D61PDIS 04429101
         AIF   ('&OPTION' EQ 'SYSOWNER').SYSOWNR               @D14BDRP 04450000
.OPTERR  MNOTE 5,'INVALID OPTION PARAMETER - MACRO IGNORED'             04500000
         MEXIT                                                          04550000
.*                                                                      04600001
.* --------------- SWOWNER START, OPTION=SETOWNER ------------          04650001
.SETOWNR ANOP                                                  @D14BDFR 06650000
*        SET SERVICE OWNER USING TASK ID IN WR1                @D14BDFR 06700000
         AIF   (T'&WR2 EQ 'O').ERROR                           @D14BDFR 06750000
         AIF   ('&WR1' EQ '&WR2').REGERR                       @D14BDFR 06800000
         STM   R0,RF,TDSAVE       SAVE REGISTERS               @D61RDIS 06850001
         LR    R2,&WR1            GET TASK ID                  @D61NDIS 06950001
         L     R8,A&STASK.TIB     POINTER TO SYSTEM TASK TIB   @D61NDIS 07050001
         LA    RF,136             SET FUNCTION CODE            @D61RDIS 07100001
         L     RD,ADISPRT               GET DISP ROUTINE ADDR. @D61RDIS 07150001
         BASSM RE,RD              CALL ROUTINE                 @D14BDFR 07250001
         LM    R0,RF,TDSAVE       RESTORE REGISTERS            @D14BDFR 07300001
         MEXIT                                                          08410101
.* --------------- SWOWNER, OPTION=SETOWNER END --------------          08420201
.*                                                                      08430301
.* --------------- SWOWNER, OPTION=SYSOWNER START ------------          08440401
.* -------------------------------|                                     08450501
.* SYSTEM TASK  |OPTION  |REGISTER|                                     08460601
.* -------------+--------+--------|                                     08470701
.* PMR          |SYSOWNER|WR1     |                                     08480801
.* OTHERS       |SYSOWNER|WR1     |                                     08490901
.* -------------------------------|                                     08501001
.SYSOWNR ANOP                                                  @D14BDFR 08511101
*        SET SYSTEM TASK TO ITS OWN SERVICE OWNER              @D14BDFR 08521201
         STM   R0,RF,TDSAVE       SAVE REGISTERS               @D61RDIS 08556901
         LA    R1,&STASK.TID      GET TASK ID                  @D61NDIS 08592601
         L     R8,A&STASK.TIB     POINTER TO SYSTEM TASK TIB   @D61NDIS 08640201
         LA    RF,140             SET FUNCTION CODE            @D61RDIS 08664101
         L     RD,ADISPRT               GET DISP ROUTINE ADDR. @D61RDIS 08699901
         BASSM RE,RD              CALL ROUTINE                 @D14BDFR 08735801
         LM    R0,RF,TDSAVE       RESTORE REGISTERS            @D14BDFR 08759701
         MEXIT                                                 @D61NDIS 08783801
.* --------------- SWOWNER END, OPTION=SYSOWNER --------------          08793901
.*                                                                      08804001
.* --------------- ACTIVATE START ----------------------------          08814101
.ACTIVAT ANOP                                                           08824201
&INDEX   SETC  'ACTS&SYSNDX'                                   @D61NDIS 08834301
         AIF   ('&OPTION' EQ 'INDIRECT').INDACT                @D61PDIS 08844401
         AIF   ('&OPTION' EQ 'DIRECT').NOSYS1                  @D61PDIS 08854501
         AIF   ('&OPTION' EQ 'USER').USER                      @D61PDIS 08859501
         AIF   ('&OPTION' EQ 'PMRUSER').PMRUSER                @D61PDIS 08862001
         AIF   ('&OPTION' NE 'SYSOWNER').OPTERR                @D61PDIS 08864601
.* --------------- ACTIVATE START, OPTION=SYSOWNER -----------          08874701
*        ACTIVATE SYSTEM TASK                                  @D14BDFR 08894901
.* --------------- OPTION=SYSOWNER START ---------------------          08955501
*        SET SYSTEM TASK TO ITS OWN SERVICE OWNER              @D14BDFR 08965601
         STM   R0,RF,TDSAVE       SAVE REGISTERS               @D61RDIS 08976501
         LA    R1,&STASK.TID      GET TASK ID                  @D61NDIS 08987501
         L     R8,A&STASK.TIB     POINTER TO SYSTEM TASK TIB   @D61NDIS 09002101
         LA    RF,144             SET FUNCTION CODE            @D61RDIS 09009401
         L     RD,ADISPRT               GET DISP ROUTINE ADDR. @D61RDIS 09020301
         BASSM RE,RD              CALL ROUTINE                 @D14BDFR 09031301
         LM    R0,RF,TDSAVE       RESTORE REGISTERS            @D14BDFR 09038601
         MVI   RID+1,USERTID      IDENTIFY TASKS OWN PROCESSING@D61NDIS 09045901
         MEXIT                                                          09053201
.*                                                                      09061401
.* --------------- ACTIVATE START, OPTION=DIRECT -------------          09067101
.NOSYS1  ANOP                                                  @D14BDRP 09072801
         AIF   (T'&WR2 EQ 'O').ERROR                           @D14BDFR 09078501
         AIF   ('&WR1' EQ '&WR2').REGERR                       @D14BDFR 09084201
*        ACTIVATE SYSTEM TASK, SERVICE OWNER IS CURRENT TASK   @D14BDFR 09089901
         STM   R0,RF,TDSAVE       SAVE REGISTERS               @D61RDIS 09101401
         LA    R1,&STASK.TID      GET SYSTEM TASK ID           @D61NDIS 09113001
         LH    R2,TID             GET TASK ID                  @D61NDIS 09120701
         L     R8,A&STASK.TIB     POINTER TO SYSTEM TASK TIB   @D61NDIS 09136101
         LA    RF,148             SET FUNCTION CODE            @D61RDIS 09143801
         L     RD,ADISPRT               GET DISP ROUTINE ADDR. @D61RDIS 09155301
         BASSM RE,RD              CALL ROUTINE                 @D14BDFR 09166901
         LM    R0,RF,TDSAVE       RESTORE REGISTERS            @D14BDFR 09174601
         MVI   RID+1,USERTID      IDENTIFY TASKS OWN PROCESSING@D61NDIS 09182301
         MEXIT                                                          09190001
.*                                                                      09198201
.* --------------- ACTIVATE START, OPTION=INDIRECT -----------          09203901
.INDACT  ANOP                                                           09209601
*        ACTIVATE SYSTEM TASK                                  @D14BDFR 09215301
*        SET SERVICE OWNER USING TASK ID IN WR1                @D14BDFR 09221001
         AIF   (T'&WR2 EQ 'O').ERROR                           @D14BDFR 09226701
         AIF   ('&WR1' EQ '&WR2').REGERR                       @D14BDFR 09232401
         STM   R0,RF,TDSAVE       SAVE REGISTERS               @D61RDIS 09282401
         LA    R1,&STASK.TID      GET SYSTEM TASK ID           @D61NDIS 09382401
         LR    R2,&WR1            GET TASK ID                  @D61NDIS 09432401
         L     R8,A&STASK.TIB     POINTER TO SYSTEM TASK TIB   @D61NDIS 09532401
         LA    RF,152             SET FUNCTION CODE            @D61RDIS 09582401
         L     RD,ADISPRT               GET DISP ROUTINE ADDR. @D61RDIS 09632401
         BASSM RE,RD              CALL ROUTINE                 @D14BDFR 09732401
         LM    R0,RF,TDSAVE       RESTORE REGISTERS            @D14BDFR 09782401
         MVI   RID+1,USERTID      IDENTIFY TASKS OWN PROCESSING@D61NDIS 10212201
         MEXIT                                                          10212401
.* --------------- ACTIVATE START, OPTION=USER ---------------          10212601
.USER    ANOP                                                           10212801
*        SETUP LOW CORE FIELDS FOR SERVICE OWNER                        10213001
*        POST SERVICE OWNER TASK                                        10213101
*        INPUT: WR1 - SERVICE OWNER'S TID                               10213201
*        WORK:  WR2                                                     10213401
*------------------------------------------------------------           10213601
         STM   R0,RF,TDSAVE       SAVE REGISTERS               @D61RDIS 10213701
         LA    R1,&STASK.TID      GET SYSTEM TASK ID           @D61RDIS 10213901
         LR    R2,&WR1            GET SERVICE OWNER'S TID      @D61NDIS 10214401
         LA    RF,156             SET FUNCTION CODE            @D61RDIS 10214501
         L     RD,ADISPRT               GET DISP ROUTINE ADDR. @D61RDIS 10214601
         BASSM RE,RD              CALL ROUTINE                 @D14BDFR 10214801
         LM    R0,RF,TDSAVE       RESTORE REGISTERS            @D14BDFR 10214901
         MEXIT                                                          10215001
.* --------------- ACTIVATE START, OPTION=PMRUSER ------------          10215101
.PMRUSER ANOP                                                           10215201
*        SETUP LOW CORE FIELDS FOR SERVICE OWNER                        10215301
*        INPUT: WR1 - SYSTEM TASK TIB                                   10215401
*------------------------------------------------------------           10215501
         STM   R0,RF,TDSAVE       SAVE REGISTERS               @D61RDIS 10215601
         LA    R1,&STASK.TID      GET SYSTEM TASK ID           @D61NDIS 10215701
         L     R8,A&STASK.TIB     POINTER TO SYSTEM TASK TIB   @D61NDIS 10215801
         LA    RF,168             SET FUNCTION CODE            @D61RDIS 10215901
         L     RD,ADISPRT               GET DISP ROUTINE ADDR. @D61RDIS 10216001
         BASSM RE,RD              CALL ROUTINE                 @D14BDFR 10216201
         LM    R0,RF,TDSAVE       RESTORE REGISTERS            @D14BDFR 10216301
         MEXIT                                                          10216401
.* --------------- ACTIVATE END ------------------------------          10224701
.*                                                                      10233001
.* --------------- UNPOST START ------------------------------          10241301
.UNPOST  ANOP                                                           10250000
*        DEACTIVATE SYSTEM TASK                                @D14BDFR 10300000
         LR    RF,R6              SETUP EXIT ADDR.FOR SYSDEACT @D14BDFR 10350000
         LA    R5,SYSDEACT-DISP(,R6)   GET ROUTINE ADDRESS     @D52VDIS 10375001
*        AMODESW RETURN,REG=(5)   GOTO DEACTIVATE SYSTEM TASK  @D52VDIS 10400001
         AMODESW RETURN,REG=(5)   GOTO DEACTIVATE SYSTEM TASK  @D52VDIS 10425001
         MEXIT                                                          10450000
.ERROR   MNOTE 5,'REQUIRED OPERAND MISSING - MACRO IGNORED'             10500000
         MEXIT                                                          10550000
.REGERR  MNOTE 5,'SAME REGISTER SPECIFIED TWICE - MACRO IGNORED'        10600000
         MEXIT                                                 @D14BDFR 10650000
.* --------------- UNPOST END  -------------------------------          10666601
.*                                                                      10683201
.* --------------- UPDTIME START ----------------------------- @D51IDIS 10700000
.* UPDTIME HOOKS|OPTION  |NEXTPCB |REGISTER      |EXIT         @D51IDIS 10750000
.* -------------+--------+--------+--------------+------------ @D51IDIS 10800000
.* DISP         |  --    |AALBPCB |WR1, WR2, WR3 |             @D51IDIS 10850000
.* SGNUC        |  --    |ASYSPCB |WR1, WR2, WR3 |             @D51IDIS 10900000
.* SGSCHED      |  --    |  ---   |WR1, WR2, WR3 |             @D51IDIS 10950000
.* SGTSLICE     |  --    |  ---   |WR1, WR2, WR3 |             @D51IDIS 11000000
.* SGTSLICE     |SETCPUT |  ---   |WR1, WR2      |             @D51IDIS 11050000
.* SGTINF       |  --    |  ---   |WR1, WR2, WR3 |             @D51IDIS 11100000
.* DISP         |SERVER  |  ---   |WR1, WR2, WR3 |YES          @D51IDIS 11150000
.* DISP         |OWNER   |PCBJAPTR|WR1, WR2, WR3 |             @D51IDIS 11200000
.* MCRAS        |OWNER   |ASYSPCB |WR1, WR2, WR3 |             @D51IDIS 11250000
.* SGIOS        |OWNER   |ASYSPCB |WR1, WR2, WR3 |             @D51IDIS 11300000
.* SGPCK        |OWNER   |ASYSPCB |WR1, WR2, WR3 |             @D51IDIS 11350000
.* SGPLLEV      |OWNER   |ASYSPCB |WR1, WR2, WR3 |YES          @D51IDIS 11400000
.* SGPFIX       |OWNER   |ASYSPCB |WR1, WR2, WR3 |             @D51IDIS 11450000
.* IOINTER      |IOINTER |  ---   |WR1, WR2, WR3 |             @D51IDIS 11500000
.* ----------------------------------------------------------- @D51IDIS 11550000
.UPDTIME AIF   (T'&WR1 EQ 'O').ERROR                           @D14NDFG 11600000
         AIF   (T'&WR2 EQ 'O').ERROR                           @D14NDFG 11650000
         AIF   ('&OPTION' EQ 'SETCPUT').UPDT01                 @D51IDIS 11700000
         AIF   (T'&WR3 EQ 'O').ERROR                           @D51IDIS 11750000
.UPDT01  ANOP                                                  @D51IDIS 11800000
&EXIT1   SETC  'ACCX&SYSNDX'                                   @D14BDFR 11850000
         AIF   (T'&EXIT EQ 'O').TEST0                          @D14BDFR 11900000
&EXIT1   SETC  '&EXIT'                                         @D14BDFR 11950000
         AIF   ('&EXIT'(1,1) NE '(').TEST0                     @D14BDFR 12000000
&BR      SETC  'R'                                             @D14BDFR 12050000
&EXIT1   SETC  '&EXIT(1)'                                      @D14BDFR 12100000
.* ----------------------------------------------------------- @D51IDIS 12150000
.TEST0   ANOP                                                  @D14BDFR 12200000
&INDEX   SETC  'ACCT&SYSNDX'                                   @D61RDIS 12225001
         AIF   ('&OPTION' EQ 'SETCPUT').TEST01                 @D51IDIS 12250000
         AIF   (T'&OPTION NE 'O').OPTION                       @D14BDFR 12300000
         TM    SUPVFLAG,PBALACT+JAACT ACCOUNTING WANTED        @D14BDFR 12400000
         BZ&BR &EXIT1             NO, DO NOT ACCOUNT TIME      @D14BDFR 12450001
.TEST01  ANOP                                                  @D51IDIS 12600000
         L     &WR1,ARUNTIME      GET CURR.ACCOUNTING POINTER  @D14BDFR 12650000
         AIF   (T'&NEXTPCB EQ 'O').CONT1                       @D14BDFR 12700000
         MVC   ARUNTIME,&NEXTPCB  SET NEXT ACCOUNTING POINTER  @D14BDFR 12750000
         AGO   .CONT1                                          @D14BDFR 12800000
.* ----------------------------------------------------------- @D51IDIS 12850000
.* OPTION SPECIFIED                                            @D51IDIS 12900000
.* ----------------------------------------------------------- @D51IDIS 12950000
.OPTION  AIF   ('&OPTION' EQ 'IOINTER').CONT1                  @D14BDFR 13000000
         AIF   (T'&NEXTPCB EQ 'O').SERVER                      @D14BDFR 13050000
         L     &WR2,&NEXTPCB      LOAD ACCOUNTING POINTER      @D14BDFR 13100000
.SERVER  AIF   ('&OPTION' NE 'SERVER').OWNER                   @D14BDFR 13150000
         TM    TIBFLAG2,OVHIND    TASK IS WORKING FOR OVERHEAD @D14BDFR 13250000
         BO    &INDEX             YES,DO NOT USE OWNERS PTR.   @D14BDFR 13300000
         L     &WR2,PIBPCB        POINT TO SERV.OWNERS PCB     @D51IDIS 13350000
         L     &WR2,PCBJAPTR      NEW ACCOUNTING POINTER       @D14BDFR 13400000
&INDEX   DS    0H                                              @D14BDFR 13450000
.OWNER   C     &WR2,ARUNTIME      IS IT THE SAME ACCT.POINTER  @D14BDFR 13500000
         BE&BR &EXIT1             YES, DO NOT ACCOUNT YET      @D14BDFR 13550000
.CONT0   ANOP                                                  @D14BDFR 13600000
         L     &WR1,ARUNTIME      GET CURR.ACCOUNTING POINTER  @D14BDFR 13650000
         ST    &WR2,ARUNTIME      STORE NEW ACCOUNTING POINTER @D14BDFR 13700000
.* -----------------------------------------------------------          13750001
.* .CONT1   - COMMON ACCOUNTING ROUTINE                                 13800001
.*            &WR1 = PCB ADDRESS TO BE ACCOUNTED                        13850001
.* -----------------------------------------------------------          13900001
.CONT1   ANOP                                                  @D61RDIS 13950001
         STM   R0,RF,TDSAVE       SAVE REGISTERS               @D61RDIS 14000001
         LR    R5,&WR1            GET PCB ADDRESS              @D61RDIS 14050001
         LA    RF,100             SET FUNCTION CODE            @D61RDIS 14100001
         L     RD,ADISPRT               GET DISP ROUTINE ADDR. @D61RDIS 14150001
         BASSM RE,RD              CALL ROUTINE                 @D14BDFR 14250001
         LM    R0,RF,TDSAVE       RESTORE REGISTERS            @D14BDFR 14300001
         AIF   (T'&EXIT EQ 'O').ACCNOX                         @D14BDFR 15100000
         B&BR  &EXIT1             EXIT                         @D14BDFR 15150000
         MEXIT                                                 @D14BDFR 15200000
.ACCNOX  ANOP                                                  @D14BDFR 15250000
&EXIT1   DS    0H                                              @D14BDFR 15300000
         MEXIT                                                 @D14BDFR 15350000
.* --------------- UPDTIME END ------------------------------- @D51IDIS 15400000
.*                                                                      15425001
.LRA     AIF   (T'&WR1 EQ 'O').ERROR                           @D14NDFG 15450000
&BR      SETC  ' '                                             @D14BDRP 15550000
&EXIT1   SETC  '&EXIT'                                         @D14BDRP 15600000
         AIF   ('&EXIT'(1,1) NE '(').LRANREG                   @D14BDRP 15650000
&BR      SETC  'R'                                             @D14BDRP 15700000
&EXIT1   SETC  '&EXIT(1)'                                      @D14BDRP 15750000
.LRANREG ANOP                                                  @D14BDRP 15800000
.* --------------- LRA START --------------------------------- @D52VDIS 15860001
* ------------- LRA HAS TO BE CALLED IN 31 BIT MODE ---------- @D52VDIS 15880001
&INDEX   SETC  'LRA&SYSNDX'                                    @D14BDRP 15900000
&INDEX1  SETC  'AND&SYSNDX'                                    @D14ADVB 15950000
&INDEX2  SETC  'VAL&SYSNDX'                                    @D51GDRP 16000000
&ISKMSK  SETC  'ISKMASK'                                       @D14BDRP 16050000
         AIF   ('&OPTION' NE 'STRKF').NOSTRKF                  @D14BDRP 16100000
&ISKMSK  SETC  'ISKFMASK'                                      @D14BDRP 16150000
.NOSTRKF ANOP                                                  @D14BDRP 16200000
         CL    &WR1,AVPBEG        VIRT. ADDR IN VPOOL          @D51GDRP 16250000
         BL    &INDEX2            NO,  BRANCH                  @D51GDRP 16300000
         CL    &WR1,AVPEND        VIRT. ADDR IN VPOOL          @D51GDRP 16350000
         BNH&BR &EXIT1            YES, ADDR INVALID            @D51GDRP 16400000
&INDEX2  DS    0H                                              @D51GDRP 16450000
         AIF   ('&OPTION' NE 'STRKREAL').NOREAL1               @D14NDFG 16500000
         SLR   &WR2,&WR2          INDICATE PAGE NOT IN STORAGE @D14NDFG 16550000
.NOREAL1 ANOP                                                  @D14NDFG 16600000
         LRA   &WR1,0(&WR1)       CHECK PAGE STATUS            @D14NDFG 16650000
         AIF   ('&OPTION'(1,4)  EQ 'STRK').STRK                @D14NDFG 16700000
         BC&BR 7,&EXIT1           PAGE NOT ADDRESSABLE         @D14NDFG 16750000
         MEXIT                                                 @D14NDFG 16800000
.STRK    ANOP                                                  @D14NDFG 16850000
         BC    7,&INDEX           PAGE NOT ADDRESSABLE ---->   @D14NDFG 16900000
         AIF   ('&OPTION' NE 'STRKREAL').NOREAL2               @D14NDFG 16950000
         LR    &WR2,&WR1          SAVE REAL ADDRESS            @D14NDFG 17000000
.NOREAL2 ANOP                                                  @D14NDFG 17050000
         ISKE  &WR1,&WR1          ... STORAGE KEY FOR PAGE     @D51GDTP 17300000
         B     &INDEX1            --->                         @D14ADVB 17650000
&INDEX   BC&BR 5,&EXIT1           INVALID ADDRESS ---->        @D14NDFG 17700000
*        AMODESW SET,DAT=OFF                                   @D52VDIS 17710001
         AMODESW SET,DAT=OFF                                   @D52VDIS 17720001
.* ------------------------------------------------------------         17725001
.* DO NOT CHANGE THE FOLLOWING SEQUENCE, BECAUSE OF THE                 17730001
.* TM CONDITION CODE                                                    17740001
.* ------------------------------------------------------------         17745001
         TM    PTESTAT-PTE(&WR1),PTEINVAD                      @D51GDTP 17750000
         IC    &WR1,PTEKEY-PTE(&WR1) GET STORAGE KEY           @D51GDTP 17850000
*        AMODESW SET,DAT=ON                                    @D52VDIS 17866601
         AMODESW SET,DAT=ON                                    @D52VDIS 17883201
         BO&BR &EXIT1             INVALID ADDRESS ---->        @D52VDIS 17899801
&INDEX1  DS    0H                                              @D52VDIS 17916401
         N     &WR1,&ISKMSK       GET STORAGE KEY ALONE        @D52VDIS 17933001
         MEXIT                                                 @D14NDFG 17950000
.* --------------- LRA  END   ---------------------------------         17962501
.*                                                                      17975001
.*                                                                      19851401
.* --------------- SETSCB / RESETSCB START--------------------          19851601
.* SET   FUNCTION: SWITCH TO ADDRESS SPACE GIVEN BY NEXTSCB             19852001
.* RESET FUNCTION: SWITCH BACK TO ORIGINAL ADDRESS SPACE                19852401
.*                                                                      19852801
.* SYNTAX:      SGSETUP <SETSCB|RESETSCB>,WR1=...,WR2=...               19853201
.*                      <<,NEXTSCB=...>>                                19853601
.*                      <<,OPTION=<SAVE|NOSAVE>>>                       19854001
.*              WHERE <<...>> MEANS PARAMETER IS OPTIONAL               19854401
.*                    DEFAULT IS TAKEN                                  19854801
.*                                                                      19855201
.*                    NEXTSCB     - REGISTER CONTAINING SCB ADDRESS     19855601
.*                                  OF SPACE TO BE SWICHED TO           19856001
.*                    OPTION=SAVE - SCB ADDRESS HAS TO BE SAVED         19856401
.*                    OPTION=NOSAVE - NO SAVING NECESSARY, DEFAULT      19856801
.*                                                                      19857201
.*                  | WR1 | WR2 | NEXTSCB |    OPTION     |             19857601
.*       -----------+-----+-----+---------+---------------|             19858001
.*          SETSCB  |  R  |  R  |    R    |      O        |             19858401
.*        RESETSCB  |  R  |  R  |    -    |      -        |             19858801
.*                                                                      19859201
.*       WHERE  R = REQUIRED, - = NOT APPLIC., O = OPTIONAL             19859601
.*                                                                      19860001
.* CALLER:      PAGE MANAGER FUNCTIONS                                  19860401
.* INVOCATION:  AMODE ANY, DAT ON                                       19860801
.* CONVENTIONS: NEXTSCB SPECIFIES A REGISTER THAT CONTAINS THE          19861201
.*              ADDRESS OF THE SCB. THIS REGISTER HAS TO BE             19861601
.*              UNEQUAL TO REGISTERS WR1 AND WR2.                       19862001
.*              AFTER FUNCTION SET, A RESET HAS TO FOLLOW               19862401
.*              BEFORE THE NEXT SET TO ANOTHER ADDRESS SPACE            19862801
.*              IS POSSIBLE. EXCEPTION: SET TO THE ACTIVE SPACE         19863201
.* PCBPSCB = SCB OF SPACE WHERE PARTITION IS INITIALLY ALLOCATED        19863601
.* PCBASCB = SCB OF ACTIVE SPACE FOR PARITION AFTER START               19864001
.* TIBSCB  = CURRENT SCB OF TASK                                        19864401
.* ASSUMPTIONS                                                          19864600
.* PMR USES ASN TRANSLATION FOR PAGE FAULTS THAT OCCUR WHEN A USER      19864800
.* SPACE IS SET UP. FOR PAGE FAULTS THAT OCCUR WHEN A PMR SPACE IS      19865000
.* SET UP NO ASN TRANSLATION CAN BE USED. THE REASON IS THAT A PMR      19865200
.* SPACE HAS NO ASN.                                                    19865400
.* THEREFORE, WHEN A SWITCH TO THE PMR SPACE IS DONE                    19865600
.* THERE IS NO NEED TO SET UP THE ASN. IT REMAINS AS IT WAS             19865800
.* BEFORE. SO WHEN THE SWITCH IS DONE BACK TO THE USER SPACE THE ASN    19866000
.* IS STILL CORRECT.                                                    19866200
.* THERE WILL NEVER OCCUR A SWITCH USER <-> USER                        19866400
.* ONLY THE FOLLOWING SITUATION CAN OCCUR:                              19866600
.* USER <-> PMR <-> PMR                                                 19866800
.* THE ASSUMPTIONS ARE IMPORTANT FOR:                                   19867000
.* 1) NO ASN NEEDS TO BE SET UP                                         19867200
.* 2) WHEN THE SPACE WHICH IS TO BE SET UP NEXT IS ALREADY ACTIVE       19867400
.*    (CAN ONLY BE THE PMR SPACE) HW AND SW IS IDENTICAL                19867600
.* --------------- SETSCB / RESETSCB START--------------------          19867801
.RSSCB   ANOP                                                  @D52VDIS 19868001
         AIF   ('&FUNCT' EQ 'RESETSCB').RRSETF                 @D64XDIS 19868203
         AIF   (T'&OPTION EQ 'O').RSETOK                       @D52VDIS 19869201
         AIF   ('&OPTION' EQ 'NOSAVE').RSETOK                  @D52VDIS 19869601
         AIF   ('&OPTION' NE 'SAVE').OPTERR                    @D52VDIS 19870001
.RSETOK  ANOP                                                  @D52VDIS 19870401
         STM   R0,RF,TDSAVE       SAVE REGISTERS               @D64XDIS 19871703
         LR    R4,&NEXTSCB        SCB ADDRESS                  @D64XDIS 19873003
         AIF   ('&OPTION' NE 'SAVE').SETNSA                    @D64XDIS 19874303
         LA    R1,0               INDICATE SETSCB,SAVE         @D64XDIS 19875603
         AGO   .SETSOK                                         @D64XDIS 19876903
.SETNSA  ANOP                                                  @D64XDIS 19878203
         LA    R1,4               INDICATE SETSCB,NOSAVE       @D64XDIS 19879503
         AGO   .SETSOK                                         @D64XDIS 19880803
.RRSETF  ANOP                                                  @D52VDIS 19882801
         STM   R0,RF,TDSAVE       SAVE REGISTERS               @D64XDIS 19883903
         LA    R1,8               INDICATE RESETSCB            @D64XDIS 19885003
.SETSOK  ANOP                                                  @D64XDIS 19886103
         LA    RF,236             SET FUNCTION CODE            @D64XDIS 19887203
         L     RD,ADISPRT               GET DISP ROUTINE ADDR. @D64XDIS 19888303
         BASSM RE,RD              CALL ROUTINE                 @D64XDIS 19889403
         LM    R0,RF,TDSAVE       RESTORE REGISTERS            @D64XDIS 19890503
         MEXIT                                                 @D52VDIS 19891701
.* --------------- SETLCTL START -----------------------------          19891801
.* FUNCTION:                                                            19891901
* ------------------------------------------------------------          19892001
*          - SET UP CONTROL REGISTERS                                   19892101
.*                                                                      19892201
.* SYNTAX:      SGSETUP SETLCTL,<NEXTSCB=DISP|                          19942200
.*                               NEXTSCB=CURRENT,WR1=..,WR2=..,         19992201
.*                               OPTION=<POWER|OWNER>>                  20042201
.*                                                                      20092201
.*    NEXTSCB=DISP                                                      20142201
.*                                                                      20192201
* >>>>>>>>> NO LONGER SUPPORTED                                         20242200
.*                                                                      21342201
.*    NEXTSCB=CURRENT,WR1=..,WR2=..,OPTION=<POWER|OWNER>                21392201
.*                                                                      21442201
.*            INPUT:  R6  = DISPATCHER BASE ADDRESS                     21492201
.*            OUTPUT: NONE                                              21542201
.*            WORK:   WR1, WR2                                          21592201
.*            ENVIRONMENT: MODE=ESA                                     21730701
.*                                                                      21736201
.*            CALLER:      SVC00 / SVC03 / SVC90 / SVC91                21742201
.*                         BEFORE / AFTER CALL OF POWER APPENDAGE       21792201
.*            INVOCATION:  AMODE 24, DAT ON                             21842201
.* --------------- SETLCTL START -----------------------------          21892201
.SLCTL   ANOP                                                  @D52GDIS 21942201
&INDEX   SETC  'LOW0&SYSNDX'                                   @D52GDIS 21992201
         AIF   (T'&NEXTSCB EQ 'O').ERROR                       @D52GDIS 22042201
         AIF   ('&NEXTSCB' EQ 'CURRENT').SLCTLCU               @D52GDIS 22092201
         AIF   ('&NEXTSCB' EQ 'DISP').SLCTLOK                  @D61NDIS 22142201
         MNOTE 5,'WRONG NEXTSCB SPECIFICATION - MACRO IGNORED' @D61PDIS 22275501
.* ------ NEXTSCB=DISP ---------------------------------------          22308801
.SLCTLOK ANOP                                                  @D52GDIS 22342201
         MEXIT                                                 @D52GDIS 24742201
.* ------ NEXTSCB=CURRENT ------------------------------------          24792201
.SLCTLCU ANOP                                                  @D52GDIS 25092201
         AIF   (T'&OPTION EQ 'O').OPTERR                       @D61NDIS 25142201
         AIF   ('&OPTION' NE 'OWNER').SLCTLCP                  @D52GDIS 25392201
.* ------ NEXTSCB=CURRENT,OPTION=OWNER -----------------------          25407800
         STM   R0,RF,TDSAVE       SAVE REGISTERS               @D64XDIS 25423403
         LA    RF,240             SET FUNCTION CODE            @D64XDIS 25439003
         L     RD,ADISPRT               GET DISP ROUTINE ADDR. @D64XDIS 25454603
         BASSM RE,RD              CALL ROUTINE                 @D64XDIS 25470203
         LM    R0,RF,TDSAVE       RESTORE REGISTERS            @D64XDIS 25485803
         MEXIT                                                 @D64XDIS 25501403
.* ------ NEXTSCB=CURRENT,OPTION=POWER -----------------------          25517201
.SLCTLCP ANOP                                                  @D64XDIS 25567203
         AIF   ('&OPTION' NE 'POWER').OPTERR                   @D64XDIS 25617203
         STM   R0,RF,TDSAVE       SAVE REGISTERS               @D64XDIS 25667203
         LA    RF,244             SET FUNCTION CODE            @D64XDIS 25717203
         L     RD,ADISPRT               GET DISP ROUTINE ADDR. @D64XDIS 25767203
         BASSM RE,RD              CALL ROUTINE                 @D64XDIS 25817203
         LM    R0,RF,TDSAVE       RESTORE REGISTERS            @D64XDIS 25867203
         MEXIT                                                 @D64XDIS 25917203
.* -----------------------------------------------------------          26392201
         MEND                                                           26542201
