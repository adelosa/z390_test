         MACRO                                                          00050000
         DTSSVCIN                                                       00100000
         GBLB  &BGVM              VM LINKAGE                   @D35ZDUL 00150000
************************************************************** @D368DUL 00200000
*                                                            * @D368DUL 00250000
.*      IBM DISK OPERATING SYSTEM                            * @D368DUL 00300000
*       SUPERVISOR - DTSSVCIN - 5686-007-06-C44              * @D368DUL 00350000
.*                                                           * @D368DUL 00400000
*       THIS MACRO IS "RESTRICTED MATERIALS OF IBM"          * @D41TDKY 00450000
*       5686-007 (C) COPYRIGHT IBM CORP. 1979, 1988          * @D41TDKY 00500000
*       ALL RIGHTS RESERVED                                  * @D41TDKY 00550000
*       LICENSED MATERIALS-PROPERTY OF IBM                   * @D368DUL 00600000
.*      SEE COPYRIGHT INSTRUCTIONS, G120-2083                * @D41TDKY 00650000
*                                                            * @D368DUL 00700000
************************************************************** @D368DUL 00750000
          SPACE 5                                              @D368DUL 00800000
************************************************************** @D35ZDUL 00850000
*                                                            * @D35ZDUL 00900000
*    MACRO NAME =  DTSSVCIN                                  * @D35ZDUL 00950000
*                                                            * @D35ZDUL 01000000
*    DESCRIPTIVE NAME =  SPECIAL ICCF SVC                    * @D36ZDUL 01050000
*                                                            * @D35ZDUL 01100000
*                                                            * @D35ZDUL 01150000
*    FUNCTION =                                              * @D35ZDUL 01200000
*       THIS ROUTINE HANDLES SPECIAL ICCF SUPERVISOR         * @D36ZDUL 01250000
*       SERVICES.                                            * @D35ZDUL 01300000
*       IT ENABLES ICCF MONITOR CALLS AT ICCF INITIALIZATION * @D36ZDUL 01350000
*       AND IT DISABLES THEM AGAIN AT ICCF TERMINATION.      * @D35ZDUL 01400000
*       ALL OTHER REQUESTS ARE PASSED TO ICCF.               * @D35ZDUL 01450000
*       R0   19 --->   ENABLE ICCF MONITOR CLASS             * @D36ZDUL 01500000
*       R0   20 --->   DISABLE ICCF MONITOR CLASS            * @D36ZDUL 01550000
*       ALL QTHER CODES ARE PASSED TO ICCF CONTROL PROGRAM   * @D35ZDUL 01600000
*                                                            * @D35ZDUL 01650000
*      REGISTER CONVENTIONS =                                * @D35ZDUL 01700000
*         REGISTER  5 - ADDRESS OF ICCF VECTOR TABLE         * @D36ZDUL 01750000
*         REGISTER  6 - ADDRESS OF DISPATCHER SET BY GENENT  * @D35ZDUL 01800000
*         REGISTER 10 - ADDRESS OF REQUESTOR'S TCB           * @D36ZDUL 01850000
*         REGISTER 12 - WORK REGISTER                        * @D35ZDUL 01900000
*                     - ADDRESS OF REQUESTOR'S PCB           * @D36ZDUL 01950000
*                     - RETURN ADDRESS FROM ICCF APPENDAGE   * @D36ZDUL 02000000
*         REGISTER 13 - ENTRY ADDRESS OF ICCF SVC ROUTINE    * @D36ZDUL 02050000
*                     - MODULE BASE REGISTER                 * @D36ZDUL 02100000
*                                                            * @D35ZDUL 02150000
*    EXIT-NORMAL =                                           * @D35ZDUL 02200000
*                    ---> DISPATCHER                         * @D35ZDUL 02250000
*    EXIT-ERROR  =                                           * @D35ZDUL 02300000
*                    ---> ERR21                              * @D35ZDUL 02350000
*                                                            * @D35ZDUL 02400000
*                                                            * @D35ZDUL 02450000
************************************************************** @D35ZDUL 02500000
SVC82    DS    0H                                              @D35ZDUL 02550000
         BALR  RD,R0              MODULE BASE                  @D36ZDUL 02600000
         USING *,RD                                            @D36ZDUL 02650000
         USING DTSVECDS,R5                                     @D35ZDUL 02700000
         USING DISP,R6                                         @D35ZDUL 02750000
         USING TCBADR,RA                                       @D36ZDUL 02800000
         L     R5,IJBETSS         ADDRESS OF ICCF VECTOR TABLE @D35ZDUL 02850000
         LA    RC,ICFMCEN         REQUEST FOR ENABLING MC CALL @D35ZDUL 02900000
         CR    R0,RC              HAS ICCF MON CLASS TO BE EN? @D35ZDUL 02950000
         BL    SVC82ICF           NO, PASS REQ TO ICCF -->     @D35ZDUL 03000000
         LA    RC,1(RC)           REQUST FOR DISABLING MC CALL @D35ZDUL 03050000
         CR    R0,RC              HAS ICCF MON CL TO BE DISAB? @D35ZDUL 03100000
         BH    SVC82ICF           NO, PASS REQ TO ICCF -->     @D35ZDUL 03150000
         CLC   DTSITPIK(2),PIK    REQUEST FROM ICCF INIT/TERM? @D36ZDUL 03200000
         BNE   ERR21              NO, INVALID SVC -->          @D35ZDUL 03250000
         STCTL R8,R8,SVC82SMC     STORE MONITOR CLASS CONTROL  @D35ZDUL 03300000
         CR    R0,RC              IS IT CODE 20 ?              @CU93002 03312553
         BE    MC4DIS             YES  --> GO AND DISABLE      @CU93002 03325053
         OI    SVC82SMC+2,ICFMCTL NO   --> IT IS 19 (ENABLE)   @CU93002 03337553
         B     MC4DONE                                         @CU93002 03350053
MC4DIS   DS    0H                                              @CU93002 03362553
         NI    SVC82SMC+2,255-ICFMCTL  DISABLE MC4             @CU93002 03375053
MC4DONE  DS    0H                                              @CU93002 03387553
         LCTL  R8,R8,SVC82SMC     RELOAD MONITOR CLASS CONTROL @D35ZDUL 03400000
         BR    R6                 RETURN TO DISPATCHER         @D35ZDUL 03450000
SVC82ICF EQU   *                                               @D35ZDUL 03500000
         L     RC,PCBPTR          CURRENT PARTITION CONTREL BL @D36ZDUL 03550000
         USING PCBADR,RC          PCB ADDRESSABILITY           @D36ZDUL 03600000
         TM    PCBSSFLG,ICCF      REQUEST FROM ICCF PARTITION? @D36ZDUL 03650000
         BZ    ERR21              NO, INVALID SVC --->         @D36ZDUL 03700000
         TM    DTSFLG2,DTSCANCL   IS ICCF PARTITION TO BE CANC @D35ZDUL 03750000
         BO    ERR21              BETTER CANCEL REQUESTOR      @D35ZDUL 03800000
         MVC   DTSSVCC(L2),INTINFO+2 PASS SVC CODE TO ICCF     @D36ZDUL 03850000
         MVC   DTSCUSA+D1(L3),TCBSAVE+1 PASS REQUESTOR SAVE AR @D36ZDUL 03900000
         L     RD,DTSINSVC        ADDRESS OF ICCF SVC ROUTINE  @D35ZDUL 03950000
         MVI   RID+1,ETSSID       SET ID FOR DISABLED ICCF RTN @D35ZDUL 04000000
         BALR  RC,RD              TO ICCF SVC ROUTINE          @D35ZDUL 04050000
*        ICCF RETURNS REGISTERS 6 - 12 UNCHANGED               @D36ZDUL 04100000
*        RD ---> 0                EXIT TO DISPATCHER           @D35ZDUL 04150000
*        RD ---> 4                EXIT TO ERR21                @D35ZDUL 04200000
*        RD ---> 8                EXIT TO SVC FLIH             @D35ZDUL 04250000
*        RD ---> 12               EXIT TO ERR0C                @D35ZDUL 04300000
*        RC ---> RETURN FROM ICCF APPENDAGE (SVC82RET)         @D35ZDUL 04350000
SVC82RET MVI   RID+1,SYSTEMID     RESET ID TO SYSTEM RID       @D35ZDUL 04400000
         L     RC,SVC82EXT-SVC82RET(RC,RD) EXIT VIA RD DISPLMT @D35ZDUL 04450000
         L     R5,IJBETSS         GET VECTOR TABLE ONCE AGAIN  @D36ZDUL 04500000
         TM    DTSFLG2,DTSRESVC   SVC TO BE REEXECUTED?        @D36ZDUL 04550000
         BZR   RC                 NO, RETURN                   @D36ZDUL 04600000
         MVC   INTINFO+2(L2),DTSSVCC RESTORE SVC CODE IN TCB   @D36ZDUL 04650000
         BR    RC                 RETURN                       @D35ZDUL 04700000
SVC82EXT DC    A(DISP)            TO DISPATCHER                @D35ZDUL 04750000
         DC    A(ERR21)           TO ERROR ROUTINE - INV SVC   @D35ZDUL 04800000
         DC    A(ICCFVCX)         LET SUPERVISOR HANDLE SVC    @D35ZDUL 04850000
         DC    A(ERR0C)           TO ERROR ROUTINE - ICCF FAIL @D35ZDUL 04900000
         DROP  R5,R6,RA,RC,RD                                  @D36ZDUL 04950000
         DS    0F                                              @D35ZDUL 05000000
SVC82SMC DC    F'0'               CONTROL REGISTER SAVE AREA   @D35ZDUL 05050000
ICFMCEN  EQU   19                 SPECIAL SVC CODE FOR MC ENAB @D35ZDUL 05100000
ICFMCTL  EQU   X'08'              CNTL R VALUE FOR MON CLASS 4 @D35ZDUL 05150000
         MEND                                                  @D35ZDUL 05200000
