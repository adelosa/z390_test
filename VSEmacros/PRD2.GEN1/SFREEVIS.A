         MACRO                                                          00050000
&LABEL   SFREEVIS &ERROR,&ADDRESS=,&LENGTH=,&SPID=,            @D14CDJB*00100000
               &SAVE=,&SPACE=,&PMSPACE=                        @D52VDMZ 00150001
         LCLB  &BERR               ERROR INDICATOR                      00200000
         LCLB  &BLPMSP             PMR SPACE FREEVIS REQUEST   @D52VDMZ 00225001
         LCLB  &BLSPID             SUBPOOL INDICATOR                    00250000
         LCLB  &BLSAVE             SAVE INDICATOR              @D14CDJB 00300000
         LCLB  &BLSPCE             SPACE REQUEST               @D51IDMZ 00350000
         LCLB  &BLSVA              SVA REQUEST                 @D51IDMZ 00400000
         LCLB  &BLROUT             SPACE=ROUTED REQUEST        @D51IDMZ 00450000
         LCLB  &BLROS              SPACE=ROS390 REQUEST        @DY45712 00460002
         LCLC  &A                  INSTRUCTION MODIFIER        @D14CDJB 00500000
         LCLC  &SAVEAR             SAVE AREA ADDRESS           @D14CDJB 00550000
         LCLC  &SAVEPTR            SAVE AREA PTR               @D14CDJB 00600000
***************************************************************         00650000
.*       IBM DISK OPERATING SYSTEM                            *         00700000
*        SUPERVISOR - SFREEVIS - 5686-032-06                  *@D149DJB 00750000
         AGO   .ZZZ                                           *         00800000
*        LICENSED MATERIALS - PROPERTY OF IBM                 *         00850000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"          *@D31UDMZ 00900000
*        5686-032 (C) COPYRIGHT IBM CORP. 1980, 1993          *@D52VDMZ 00950001
*        SEE COPYRIGHT INSTRUCTIONS                           *@D31UDMZ 01000000
.*                                                                      01050000
.*    CHANGE ACTIVITIES                                                 01100000
.*      AF 5.1.0 MORE PARTITION SUPPORT                       *@D51IDMZ 01150000
.*      AF 5.2.0 31 BIT SUPPORT                               *@D52VDMZ 01175001
.*                                                                      01200000
.ZZZ     ANOP                                                 *         01250000
.* A000000-999999                                             *@D14CDJB 01300000
***************************************************************         01350000
         AIF   (T'&ERROR EQ 'O').F0                            @D14CDJB 01400000
         MNOTE 8,'POSITIONAL PARAMETER SPECIFIED, MACRO IGNORED'        01450000
         MEXIT                                                 @D14CDJB 01500000
.F0      ANOP                                                  @D14CDJB 01550000
&BLSVA   SETB  1                  SVA REQUEST IS DEFAULT       @D51IDMZ 01600000
         AIF   (T'&LABEL EQ 'O').PRSP                                   01650000
&LABEL   DS    0H                                                       01700000
.PRSP    ANOP                                                           01750000
.**************************************************************         01752701
.*  PROCESS PMSPACE PARAMETER                                  @D52VDMZ 01755401
.**************************************************************         01758101
         AIF   (T'&PMSPACE EQ 'O').SP0                         @D52VDMZ 01760801
         AIF   ('&PMSPACE' EQ 'NO').SP0                        @D52VDMZ 01763501
         AIF   ('&PMSPACE' EQ 'YES').PMSCH0                    @D52VDMZ 01766201
         MNOTE 8,'INVALID PMSPACE PARAMETER SPECIFIED - MACRO IGNORED'  01768901
         MEXIT                                                 @D52VDMZ 01771601
.PMSCH0  ANOP                                                  @D52VDMZ 01774301
&BLSVA   SETB  0                  NO SVA REQUEST               @D52VDMZ 01777001
&BLPMSP  SETB  1                  PMR SPACE REQUEST            @D52VDMZ 01779701
         AIF   (T'&SPACE EQ 'O').ZERO                          @D52VDMZ 01782401
         AIF   ('&SPACE' EQ 'NO').ZERO                         @D52VDMZ 01785101
         MNOTE 8,'SPACE PARAMETER MUST NOT BE SPECIFIED WITH PMSPACE PA*01787801
               RAMER - MACRO IGNORED'                                   01790501
         MEXIT                                                 @D51IDMZ 01793201
.SP0     ANOP                                                  @D52VDMZ 01795901
.**************************************************************         01800000
.*  PROCESS SPACE PARAMETER                                    @D51IDMZ 01850000
.**************************************************************         01900000
         AIF   (T'&SPACE EQ 'O').ZERO                          @D51IDMZ 01950000
         AIF   ('&SPACE' EQ 'NO').ZERO                         @D51IDMZ 02000000
&BLSVA   SETB  0                  NO SVA REQUEST               @D51IDMZ 02050000
         AIF   ('&SPACE' EQ 'YES').SPCHEK3                     @D51IDMZ 02100000
         AIF   ('&SPACE' EQ 'ROUTED').SPCHEK0                  @D51IDMZ 02150000
         AIF   ('&SPACE' EQ 'ROS390').SPCHK00                  @DY45712 02160002
         MNOTE 8,'INVALID SPACE PARAMETER SPECIFIED - MACRO IGNORED'    02200000
         MEXIT                                                 @D51IDMZ 02250000
.SPCHK00 ANOP                                                           02260001
&BLROS   SETB  1                                               @DY45712 02270002
.SPCHEK0 ANOP                                                           02300000
&BLROUT  SETB  1                                               @D51IDMZ 02350000
&BLSPCE  SETB  1                                               @D51IDMZ 02400000
.* NO OTHER OPERAND IS ALLOWED FOR SPACE=ROUTED                @D51IDMZ 02450000
         AIF   (T'&LENGTH EQ 'O').SPCHEK1                      @D51IDMZ 02500000
         MNOTE 8,'LENGTH PARAMETER NOT ALLOWED FOR SPACE=ROUTED|ROS390 *02550001
                MACRO IGNORED'                                          02600001
         MEXIT                                                 @D51IDMZ 02650000
.SPCHEK1 ANOP                                                           02700000
         AIF   (T'&SPID EQ 'O').SPCHEK2                        @D51IDMZ 02750000
         MNOTE 8,'SPID PARAMETER NOT ALLOWED FOR SPACE=ROUTED|ROS390 - *02760002
                MACRO IGNORED'                                          02810002
         MEXIT                                                 @D51IDMZ 02900000
.SPCHEK2 ANOP                                                           02950000
         AIF   (T'&ADDRESS EQ 'O').ZERO                        @D51IDMZ 03000000
         MNOTE 8,'ADDRESS PARAMETER NOT ALLOWED FOR SPACE=ROUTED|ROS390*03050001
                 MACRO IGNORED'                                         03100001
         MEXIT                                                 @D51IDMZ 03150000
.SPCHEK3 ANOP                                                           03200000
&BLSPCE  SETB  1                                               @D51IDMZ 03250000
.**************************************************************@D149DJB 03300000
.*  PROCESS SAVE PARAMETER                                     @D149DJB 03350000
.**************************************************************@D149DJB 03400000
.ZERO    ANOP                                                           03450000
         AIF   (T'&SAVE EQ 'O').PRSPID    PARAMETER SPECIFIED ?@D14CDJB 03500000
         AIF   ('&SAVE'(1,1) NE '(').SVADD     REG NOTATION    @D14CDJB 03550000
&SAVEAR  SETC  '16&SAVE'          GET SAVE AREA OFFSET         @D14CDJB 03600000
&SAVEPTR SETC  '&SAVE(1)'         GET SAVE AREA POINTER        @D14CDJB 03650000
&A       SETC  'R'                GET INSTRUCTION MODIFIER     @D14CDJB 03700000
         AGO   .SVCOM                                          @D14CDJB 03750000
.SVADD   ANOP                                                  @D14CDJB 03800000
         AIF   (T'&SAVE NE 'N').SVLA      VALID SYMBOL         @D14CDJB 03850000
         MNOTE 8,'SAVE PARAMETER INVALID - MACRO IGNORED'      @D14CDJB 03900000
         MEXIT                                                 @D14CDJB 03950000
.SVLA    ANOP                                                  @D14CDJB 04000000
&SAVEAR  SETC  '&SAVE+16'         GET SAVE AREA OFFSET         @D14CDJB 04050000
&SAVEPTR SETC  '&SAVE'            GET SAVE AREA POINTER        @D14CDJB 04100000
&A       SETC  'A'                GET INSTRUCTION MODIFIER     @D14CDJB 04150000
.SVCOM   ANOP                                                  @D14CDJB 04200000
&BLSAVE  SETB  1                                               @D14CDJB 04250000
.**************************************************************         04300000
.*  PROCESS SPID PARAMETER                                    *         04350000
.**************************************************************         04400000
.PRSPID  ANOP                                                  @D14CDJB 04450000
         AIF   (&BLROUT).ELEVEN     NO FURTHER PARAMETERS SPEC.@D51IDMZ 04500000
         AIF   (T'&SPID EQ 'O').PRADDR    PARAMETER SPECIFIED           04550000
         AIF   ('&SPID'(1,1) NE '(').SPADD     REG NOTATION             04600000
         AIF   ('&SPID(1)' EQ '1').SPCOM   SKIP FOR RIGHT REG           04650000
         LR    1,&SPID(1)         GET SUBPOOL ID FIELD ADDR.            04700000
         AGO   .SPCOM                                                   04750000
.SPADD   AIF   (T'&SPID NE 'N').SPLA      VALID SYMBOL                  04800000
         MNOTE 8,'SPID PARAMETER INVALID - MACRO IGNORED'               04850000
         MEXIT                                                          04900000
.SPLA    LA    1,&SPID            GET SUBPOOL ID FIELD ADDR.            04950000
.SPCOM   ANOP                                                           05000000
&BLSPID  SETB  1                                                        05050000
         AIF   (T'&ADDRESS EQ 'O' AND                                  *05100000
               T'&LENGTH EQ 'O').ELEVEN  ANYTHING ELSE SPEC.?           05150000
         MNOTE 1,'ADDRESS AND/OR LENGTH OPERAND IGNORED WHEN SPID SPECI*05200000
               FIED'                                                    05250000
         AGO   .ELEVEN                                                  05300000
.**************************************************************         05350000
.*  PROCESS ADDRESS AND LENGTH PARAMETER                      *         05400000
.**************************************************************         05450000
.PRADDR  ANOP                                                           05500000
         AIF   (T'&ADDRESS EQ 'O').FR1 ADDRESS SPECIFIED ?              05550000
         AIF   ('&ADDRESS'(1,1) EQ '(').FR1 YES, REGISTER NOTAT.?       05600000
         AIF   (T'&ADDRESS NE 'N').FR1 NO, SYMBOL SPECIFIED ?           05650000
         MNOTE 4,'ADDRESS PARAMETER INVALID - MACRO IGNORED'            05700000
&BERR    SETB  1                                                        05750000
.FR1     ANOP                                                           05800000
         AIF   (T'&LENGTH EQ 'O').FR2  LENGTH SPECIFIED ?               05850000
         AIF   ('&LENGTH'(1,1) EQ '(').FR2 YES, REGISTER NOTAT.?        05900000
         AIF   (T'&LENGTH NE 'N').FR2  NO, SYMBOL SPECIFIED ?           05950000
         MNOTE 4,'LENGTH PARAMETER INVALID - MACRO IGNORED'             06000000
         MEXIT                                                          06050000
.FR2     ANOP                                                           06100000
         AIF   (NOT &BERR).FR3         ERROR ?                          06150000
         MEXIT                                                          06200000
.FR3     ANOP                                                           06250000
         AIF   (T'&ADDRESS EQ 'O').ONE                                  06300000
         AIF   ('&ADDRESS'(1,1) NE '(').THREE                           06350000
         AIF   ('&ADDRESS(1)' NE '1').THREE                             06400000
.ONE     AIF   (T'&LENGTH EQ 'O').ELEVEN                                06450000
         AIF   ('&LENGTH'(1,1) NE '(').TWO                              06500000
         AIF   ('&LENGTH(1)' EQ '0').ELEVEN                             06550000
         LR    0,&LENGTH(1)       LOAD LENGTH INTO REGISTER 0           06600000
         AGO   .ELEVEN                                                  06650000
.TWO     L     0,&LENGTH          LOAD LENGTH INTO REGISTER 0           06700000
         AGO   .ELEVEN                                                  06750000
.THREE   AIF   (T'&LENGTH EQ 'O').FOUR                                  06800000
         AIF   ('&LENGTH'(1,1) NE '(').FOUR                             06850000
         AIF   ('&LENGTH(1)' NE '0').SEVEN                              06900000
.FOUR    AIF   ('&ADDRESS'(1,1) NE '(').FIVE                            06950000
         LR    1,&ADDRESS(1)      LOAD ADDRESS INTO REGISTER 1          07000000
         AGO   .SIX                                                     07050000
.FIVE    L     1,&ADDRESS         LOAD ADDRESS INTO REGISTER 1          07100000
.SIX     AIF   (T'&LENGTH EQ 'O').ELEVEN                                07150000
         AIF   ('&LENGTH'(1,1) EQ '(').ELEVEN                           07200000
         L     0,&LENGTH          LOAD LENGTH INTO REGISTER 0           07250000
         AGO   .ELEVEN                                                  07300000
.SEVEN   AIF   ('&ADDRESS'(1,1) EQ '(').EIGHT                           07350000
         LR    0,&LENGTH(1)       LOAD LENGTH INTO REGISTER 0           07400000
         L     1,&ADDRESS         LOAD ADDRESS INTO REGISTER 1          07450000
         AGO   .ELEVEN                                                  07500000
.EIGHT   AIF   ('&ADDRESS(1)' EQ '0').NINE                              07550000
         LR    0,&LENGTH(1)       LOAD LENGTH INTO REGISTER 0           07600000
         LR    1,&ADDRESS(1)      LOAD ADDRESS INTO REGISTER 1          07650000
         AGO   .ELEVEN                                                  07700000
.NINE    AIF   ('&LENGTH(1)' EQ '1').TEN                                07750000
         LR    1,&ADDRESS(1)      LOAD ADDRESS INTO REGISTER 1          07800000
         LR    0,&LENGTH(1)       LOAD LENGTH INTO REGISTER 0           07850000
         AGO   .ELEVEN                                                  07900000
.**************************************************************@D149DJB 07950000
.*  GENERATE CODE                                              @D149DJB 08000000
.**************************************************************@D149DJB 08050000
.TEN     LR    15,0               SAVE REGISTER 0                       08100000
         LR    0,1                LOAD LENGTH INTO REGISTER 0           08150000
         LR    1,15               LOAD ADDRESS INTO REGISTER 1          08200000
.ELEVEN  ANOP                                                           08250000
         CNOP  0,4                                             @D52VDMZ 08270001
         BAL   15,*+8                                          @D52VDMZ 08290001
         DC    B'00000000000000&BLROS&BLROUT.0&BLPMSP.0000&BLSPCE.00000*08303301
               0&BLSVA&BLSPID.0'                                        08316601
         L     15,0(,15)                                       @D52VDMZ 08330001
         AIF   (&BLSAVE).ALTSRA   ALTERNATE SAVE AREA ?        @D14CDJB 08350000
         STM   9,8,SVCSV3-TCBADR(10)    SAVE ALL REGISTERS     @D14CDJB 08400001
         LA    13,SVCSV3-16-TCBADR(10)  SAVE AREA POINTER      @D14CDJB 08450001
         AGO   .GENBR                                          @D14CDJB 08500000
.ALTSRA  ANOP                                                  @D14CDJB 08550000
         STM   9,8,&SAVEAR              SAVE ALL REGISTERS     @D14CDJB 08600001
         L&A   13,&SAVEPTR              GET SAVE AREA POINTER  @D14CDJB 08650001
.GENBR   ANOP                                                  @D14CDJB 08700000
         ASYSCOM 7                                             @D14NDFG 08750001
         L     7,IJBSPAVT-SYSCOM(7)     SUPERV. ADDRESS VECTOR @D14NDFG 08800001
         L     7,ASFREVIS-SUPAVT(7)     ROUTINE ADDRESS        @D14NDFG 08850001
         BALR  7,7                      INTERNAL FREEVIS ROUT. @D14CDJB 08900001
         AIF   (&BLSAVE).ALTSRB         ALTERNATE SAVE AREA ?  @D14CDJB 08950000
         LM    9,8,SVCSV3-TCBADR(10)    RESTORE ALL REGISTERS           09000001
         MEXIT                                                 @D14CDJB 09050000
.ALTSRB  ANOP                                                  @D14CDJB 09100000
         LM    9,8,16(13)               RESTORE ALL REGISTERS  @D14CDJB 09150001
         MEND                                                           09200000
