         MACRO                                                          00050061
&NAME    SGCHKINT &WR1=,&FUNC=ENABLE                                    00100061
         LCLC  &LR                                                      00150061
.*       IBM DISK OPERATING SYSTEM                            *         00200061
         SPACE 1                                                        00250061
**************************************************                      00300061
*        SUPERVISOR - SGCHKINT - 5686-066                     *@D62MPGL 00350061
         AGO   .ZZZ                                           *         00400061
*        LICENSED MATERIALS - PROPERTY OF IBM                 *         00450061
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"          *         00500061
*        5686-066 (C) COPYRIGHT IBM CORP. 1996                *@D62MPGL 00550061
*        SEE COPYRIGHT INSTRUCTIONS                           *         00600061
.ZZZ     ANOP                                                 *         00650061
.* /* START OF SPECIFICATIONS ********************************          00700061
.*01  MODULE TYPE      = SUPERVISOR GENERATION MACRO                    00750061
.*01  DESCRIPTIVE NAME = ENABLE FOR INTERRUPTS                          00800061
.*01                   = DISABLE INTERRUPTS AGAIN                       00850061
.*01  NOTES            = CHANGE ACTIVITY                                00900061
.*    MP PROTOTYPE                                             @D62MPGL 00950061
.*    A000000-999999                                                    01000061
.*02  PROCESSOR        = ASSEMBLER                                      01050061
.*END OF SPECIFICATIONS       *******************************/          01100061
         MEXIT                                                 @D....MK 01125061
         AIF   ('&FUNC' EQ 'ENABLE').ENTIOPR                            01150061
         AIF   ('&FUNC' NE 'RETURN').FUNCERR                            01200061
         AGO   .RETURNF                                        @D62MPGL 01250061
.FUNCERR ANOP                                                  @D62MPGL 01300061
         MNOTE 5,'INVALID FUNCTION SPECIFIED'                  @D62MPGL 01350061
         MEXIT                                                 @D62MPGL 01400061
.REGERR  ANOP                                                  @D62MPGL 01450061
         MNOTE 5,'NOT ENOUGH WORK REGISTERS SPECIFIED'         @D62MPGL 01500061
         MEXIT                                                 @D62MPGL 01550061
.* -----------------------------------------------------------          01600061
.*       - GO AND CHECK FOR INTERRUPTS HERE                             01650061
.* -----------------------------------------------------------          01700061
.ENTIOPR AIF   (T'&WR1 EQ 'O').REGERR                                   01750061
&NAME    DS    0H                 SUPPLY LABEL NAME            @D62MPGL 01800061
         L     &WR1,IJBMPATB      TAKE MPATAB   POINTER        @D62MPGL 01850061
         L     &WR1,0(,&WR1)      TAKE MPCOMREG POINTER        @D62MPGL 01900061
         TM    MPCFLAG3-MPCOMREG(&WR1),MPCIO  ANY IO QUEUED??  @D62MPGL 01950061
         BNO   IJBI&SYSNDX        SKIP REST IF NOT             @D62MPGL 02000061
         L     &WR1,MPCPUPTR      GET CPU'S CONTROL BLOCK      @D62MPGL 02050061
         L     &WR1,MCPUWRK-MCPUADR(,&WR1) GET WORK AREA ADDR. @D62MPGL 02100061
         STM   R0,RF,MWSAVI-MWCPADR(&WR1) SAVE REGISTERS       @D62MPGL 02150061
         ASYSCOM RD               GET SYSCOM ADDRESS           @D62MPGL 02200061
         L     RD,IJBSPAVT-SYSCOM(,RD)                         @D62MPGL 02250061
         L     RD,AENTIOPR-SUPAVT(,RD) ADDRESS OF ENTIOPRE     @D62MPGL 02300061
         BAS   R6,0(,RD)          TAKE RETURN ADDRESS          @D62MPGL 02350061
         L     &WR1,MPCPUPTR                0 RESTORE ALL      @D62MPGL 02400061
         L     &WR1,MCPUWRK-MCPUADR(,&WR1)  4 REGISTERS EXCEPT @D62MPGL 02450061
         LM    R0,RF,MWSAVI-MWCPADR(&WR1)   8 WORK REG         @D62MPGL 02500061
IJBI&SYSNDX DS 0H     R6+12 = RETURN POINT OF SIM IO INTERR.   @D62MPGL 02550061
         MEXIT                                                          02600061
.* -----------------------------------------------------------          02650061
.*       - SET RETURN OFFSET FOR IOINTER                                02700061
.* -----------------------------------------------------------          02750061
.RETURNF AIF   (T'&WR1 EQ 'O').REGERR                                   02800061
         LA    &WR1,12  TAKE OFFSET OF SIM IO INTERR. RET.POINT@D62MPGL 02850061
         MEND                                                           02900061
