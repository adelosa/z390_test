         MACRO                                                          00050061
&NAME    SGCUIR                                                         00100061
         GBLB  &SGCUIR      SGCUIR PRINT CONTROL SWITCH                 00196461
         GBLA  &AG1         NUMBER OF PUBS (MORE DEVICES)               00244661
         GBLB  &IJBSGEN     SUPERVISOR GENERATION                               
         AIF (&IJBSGEN).SUPVR                                                   
***************************************************************         01064061
*                                                                       01112261
*        LICENSED MATERIALS-PROPERTY OF IBM                             01160461
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM'                    01208661
*        5686-066 (C) COPYRIGHT IBM CORP. 1994, 2002                    01256800
*        SEE COPYRIGHT INSTRUCTIONS                                     01305061
*                                                                       01353261
***************************************************************         01401461
.* /* START OF SPECIFICATIONS ***************************************** 01450061
.*                                                                    * 01500061
.*01* MODULE-TYPE = SUPERVISOR GENERATION MACRO                       * 01550061
.*                                                                    * 01600061
.*01* DESCRIPTIVE NAME = QUIESCE RESUME AND TPA MIM/SIM PROCESSING    * 01650000
.*                                                                    * 01700061
.*01* FUNCTIONAL-DESCRIPTION =                                        * 01750061
.*    THIS MODULE IS THE CODE RUN THRU BY THE SERVICE TASK,           * 01788461
.*    IF THE CENTRAL WORK TO DO FLAG OF THIS TASK INDICATES           * 01826861
.*    THAT THERE POSSIBLY IS SOME WORK TO DO.                         * 01865200
.*    THIS FLAG IS ALWAYS SET ON, IF THE SUPERVISOR RECEIVES          * 01903661
.*    AN ATTENTION INTERRUPT. THE MODULE IS ENTERED AT LABEL          * 01942061
.*    QRBASE WITHOUT ANY SPECIAL PARAMETERS. ROUTINE QRBASE           * 01980461
.*    WILL ISSUE THE IO TO THE DEVICES ASKING FOR ATTENTION           * 02018861
.*    MESSAGES AND SEE IF THESE MESSAGES ARE RECONFIGURATION          * 02057261
.*    REQUEST RECORDS.                                                * 02064800
.*    IF THEY ARE NOT, THE ATTENTION INTERRUPT MIGHT BE PRESENTED     * 02072400
.*    BY A TPA TAPE DEVICE THAT WANTS TO HAVE PROCESSED A MEDIA       * 02080000
.*    OR SERVICE INFORMATION MESSAGE. IN SUCH A CASE THE ISSUED       * 02087600
.*    IO TO THE DEVICE HAD LED TO A UNIT CHECK WITH ASSOCIATED        * 02095200
.*    SENSE DATA CONTAINING THE MIM OR SIM WHICH HAS THEN TO BE       * 02102800
.*    HANDLED BY TAPE ERROR RECOVERY. THE ERP RETRY RECOVERY          * 02110400
.*    ACTION RETURNS A NO-MESSAGE MESSAGE AND ATTENTION               * 02122800
.*    PROCESSING IS COMPLETE.                                         * 02135200
.*    IN THE OTHER CASE, THE RECONFIGURATION REQUEST IS PASSED        * 02147600
.*    TO ROUTINE QRMAIN, WHICH HANDLES THEM COMPLETELY.               * 02160000
.*    A NUMBER OF MESSAGES MAY BE GENERATED IN THE COURSE OF          * 02172461
.*    THIS PROCESSING.                                                * 02210861
.*                                                                    * 02250061
.*    THE MODULE FIRST FINDS OUT, WHAT KIND OF REQUEST IT IS.         * 02300061
.*    THE FOLLOWING REQUESTS ARE HONORED.                             * 02350061
.*                                                                    * 02400061
.*      1.) QUIESCE (DEVICE OR) PATH                                  * 02450000
.*      2.) RESUME (DEVICE OR) PATH                                   * 02500000
.*      3.) (IDENTIFY SYSTEM)                                         * 02550000
.*                                                                    * 02600061
.*    THE STEPS THAT HAVE TO BE TAKEN TO PROCESS THE RECONFIG.        * 02700061
.*    REQUEST ARE DESCRIBED IN ARCHITECTURE AR-4903-01-POK.           * 02725061
.*                                                                    * 02750061
.*    BASICLY THE PROCESSING TAKES PLACES IN TWO STEPS. WE            * 02764061
.*    FIRST IDENTIFY THE SCOPE OF THE REQUEST, MEANING WHAT           * 02778061
.*    DEVICE PATH PAIRS ARE IMPACTED. THEN WE EXECUTE THE             * 02792061
.*    DESIRED OPERATION AND RETURN AN ANSWER TO THE CONTROL           * 02806061
.*    UNIT THAT INITIATED THE WHOLE PROCESSING.                       * 02820061
.*                                                                    * 02834061
.*    THE SCOPE IS RETURNED TO THE MAIN ROUTINE IN A LIST             * 02848061
.*    CONTAINING THE DEVICE PATH PAIRS. THIS LIST IS SET UP BY        * 02862061
.*    THE QRGSCOPE ROUTINE WHICH DOES THE FOLLOWING PROCESSING.       * 02876061
.*    THE PROCESSING IS BASED ON SO CALLED CONFIGURATIN DATA          * 02890061
.*    RECORDS. THESE ARE RECORDS THAT CONSIST OF 32 BYTE ENTRIES      * 02904061
.*    WHICH CAN EITHER BE NED (NODE ELEMENT DESCRIPTOR) OR NEQ        * 02918061
.*    (NODE ELEMENT QUALIFIER). THERE IS ONE GENERAL NEQ AT THE       * 02932061
.*    END OF EACH CONFIGURATION DATA RECORD. IN ADDITION THERE IS     * 02946061
.*    ONE NED WHICH IS FLAGGED TO BE THE SO CALLED TOKEN NED.         * 02960061
.*    THE CODE RELIES ON THESE PRECONDITIONS: THE GNEQ IS ALWAYS      * 02974061
.*    THE LAST ENTRY IN THE CONFIGURATION DATA RECORD AND THERE       * 02988061
.*    IS ALWAYS ONE AND ONLY ONE TOKEN NED IN THE CDR.                * 03002061
.*                                                                    * 03016061
.*    1.) WE HAVE TO FIND A RECORD CALLED THE BASE CONFIGURATION      * 03030061
.*        DATA RECORD. THIS RECORD IS A CONFIGURATION DATA RECORD.    * 03044061
.*        THE NEDS AND THE GNEQ OF IT WILL HELP IN DETERMINING        * 03058061
.*        THE REST OF THE SCOPE. IF THE SELECTOR BYTE IN THE          * 03072061
.*        RECONFIGURATION REQUEST IS ZERO, THE CDR FROM THE           * 03086061
.*        DEVICE PATH PAIR THAT WE READ THE RECONFIGURATION REQUEST   * 03100061
.*        RECORD FROM IS THE BASE CDR.                                * 03114061
.*        IF THE SELECTOR BYTE IS NOT ZERO HOWEVER WE HAVE TO         * 03128061
.*        READ ALL THE CDR'S THAT HAVE THE SAME TOKEN NED AND         * 03142061
.*        FIND OUT THE FIRST CDR THAT HAS THE SAME SELECTOR BYTE      * 03156061
.*        VALUE IN ITS GNEQ (GENERAL NEQ) .                           * 03170061
.*        THIS IS THE BASE CONFIGURATION DATA RECORD THEN.            * 03184061
.*                                                                    * 03200061
.*    2.) GIVEN THIS BASE CONFIGURATION DATA RECORD, A NUMBER         * 03250061
.*        OF RECORDS IS IDENTIFIED, WHICH IS CALLED THE               * 03300061
.*        SET OF RELATED CONFIGURATION RECORDS.                       * 03350061
.*        THE RELATED CONFIGURATION DATA RECORDS ARE ALL CDR'S        * 03380061
.*        THAT HAVE THE SAME TOKEN NED.                               * 03410061
.*        OUT OF THESE WE WILL FILTER THE SCOPE NOW. THERE IS         * 03440061
.*        A NED MAP AND AN NEQ MAP IN THE RECONFIGURATION             * 03470061
.*        REQUEST RECORD. THESE ARE THE TWO FIELDS THAT HELP          * 03500061
.*        TO IDENTIFY THE SET OF CONFIGURATION DATA RECORDS           * 03530061
.*        MEANT BY THE RECONFIGURATION REQUEST. THE DEVICE PATH       * 03560061
.*        PAIRS THAT THESE CDR'S HAVE BEEN READ FROM MAKE UP          * 03590061
.*        THE SCOPE OF THE REQUEST.                                   * 03620061
.*                                                                    * 03650061
.*        THERE IS A BIT IN SOME NEDS OF THE CDR THAT IDENTIFIES      * 03677761
.*        THESE NEDS AS RECONFIGURATION NED. EACH BIT IN THE NED      * 03705461
.*        MAP OF THE RECONFIGURATION REQUEST RECORD IS RELATED TO     * 03733161
.*        ONE OF THESE RECONFIGURATION NEDS. IF THE BIT IS ON,        * 03760861
.*        THE RECONFIGURATION NED HAS TO BE EQUAL TO THE              * 03788561
.*        CORRESPONDING RECONFIGURATION NED IN THE BASE               * 03816261
.*        CONFIGURATION DATA RECORD. IF THE BIT IS OFF THEY MAY       * 03843961
.*        BE DIFFERENT.                                               * 03871661
.*                                                                    * 03900061
.*        IF THE NEQ MAP IS NOT ZERO, EACH BIT REFERENCES ONE         * 03950061
.*        BYTE IN THE LAST 24 BYTES OF THE GENERAL NEQ IN THE         * 04000061
.*        BASE CONFIGURATION DATA RECORD. ALL THESE RECORDS           * 04050061
.*        ARE PART OF THE SUBSET, THAT SHARE THE SAME VALUES          * 04100061
.*        IN THE REFERENCED BYTES.                                    * 04150061
.*                                                                    * 04200061
.*        IF BOTH THE NED MAP AND THE NEQ MAP ARE ZERO, THERE         * 04250061
.*        IS A BUG IN THE CONTROL UNIT THE MAPS STEM FROM.            * 04300061
.*        WE RETURN AN ERROR INDICATION TO THE CONTROL UNIT.          * 04350061
.*                                                                    * 04450061
.*    IN ORDER TO SAVE TIME FOR READING CONFIGURATION DATA RECORDS    * 04500061
.*    THEY ARE KEPT IN AN AREA IN SYSTEM GETVIS. THEY ARE NOT         * 04550061
.*    KEPT PERMANENTLY BUT AT LEAST AS LONG AS THE SERVICE TASK       * 04600061
.*    IS PROCESSING ANY REMAINING RECONFIGURATION REQUEST RECORD.     * 04650061
.*    THE FORMAT OF THE TABLE IS MUCH SIMPLER THAN THAT OF MVS.       * 04700061
.*    BUT SUBROUTINE QRGETCDR CAN BE USED TO RETRIEVE RECORDS FROM    * 04750061
.*    EITHER THIS TABLE OR BY ISSUEING IO TO THE ACTUAL DEVICE/PATH.  * 04800061
.*                                                                    * 04841661
.*01* AMODE CONSIDERATIONS:                                           * 04883261
.*    THIS MACRO IS INCLUDED IN TWO PHASES TO SAVE SPACE BELOW THE    * 04924861
.*    16 MEGABYTE BOUNDARY. THE INVOCATION IN THE SUPERVISOR ONLY     * 04966461
.*    EXPANDS THE GLOBAL DATA AREA >QRSGLOB<. THIS WAS REQUIRED       * 05008061
.*    TO HAVE ALL CCBS AND CCW CHAINS BELOW 16 MB. ALL OTHER          * 05049661
.*    DATA ARE ALSO CONTAINED IN THIS AREA WHICH FACILITATES          * 05091261
.*    DEBUGGING.                                                      * 05132861
.*    THE INVOCATION IN PHASE $IJBCUIR EXPANDS THE CODE, THE          * 05174461
.*    STACK AREA AND A WRAP AROUND DEBUG AREA. THE CODE RUNS          * 05216061
.*    IN AMODE 31 ONLY. IF THE CODE DETECTS A FATAL PROCESSING        * 05257661
.*    ERROR IT DOES NOT FORCE HARD WAIT BUT USES A ROUTINE IN         * 05307600
.*    QRSGLOB TO RETURN TO THE SERVICE TASK MAINLINE PROCESSING.      * 05357600
.*    THE DATA IN THE STACK, GLOBAL AND WRAP AROUND DEBUG AREA        * 05424061
.*    REMAIN UNCHANGED FOR INSPECTION. CURRENTLY HELD GETVIS          * 05465661
.*    AREAS ARE FREED.                                                * 05515600
.*                                                                    * 06200000
*                                                                       06505761
*   GENERAL LAYOUT OF ONE ENTRY IN A CONFIGURATION DATA REC.            06507661
*                                                                       06509561
MAPCDR   DSECT                                                          06503861
CDRENTRY DS    0XL32                                                    06511461
CDRENFLG DS    X         CDR-ID                                         06513361
CDRENFUS EQU   X'00'                                                    06515261
CDRENFSN EQU   X'40'     SPECIFIC NEQ                                   06517161
CDRENFGN EQU   X'80'     GENERAL  NEQ                                   06519061
CDRENFND EQU   X'C0'     NED                                            06520961
CDRENFTO EQU   X'E0'     TOKEN NED                                      06522861
         DS    XL31      DATA BYTES                                     06524761
CDRENEXT DS    0H                                                       06526661
*                                                                       06528561
*                                                                       06532361
*   LAYOUT OF GENERAL NEQ IN CONFIGURATION DATA RECORD                  06534261
*                                                                       06536161
         ORG   CDRENTRY                                                 06530461
CDRGNEQ  DS    0XL32                                                    06538061
CDRGNFLG DS    X       FLAG BYTE                                        06539961
CDRGNSEL DS    X       RECORD SELECTOR BYTE                             06541861
CDRGNIID DS    XL2     INTERFACE ID                                     06543761
CDRGNATT DS    XL2     ATTRIBUTES                                       06545661
         DS    XL2     RESERVED BYTES, ZERO                             06547561
CDRGNQAL DS    0XL24   GNEQ QUALIFIER BYTES                             06549461
*                                                                       06551361
*                                                                       06555161
*   LAYOUT OF SPECIFIC NEQ IN CONFIGURATION DATA RECORD                 06557061
*                                                                       06558961
         ORG   CDRENTRY                                                 06553261
CDRSNEQ  DS    0XL32                                                    06560861
CDRSNFLG DS    X                                                        06562761
         DS    XL7                                                      06564661
CDRSNQAL DS    XL24    QUALIFIER BYTES OF SPECIFIC NEQ                  06566561
*                                                                       06568461
*                                                                       06572261
*   LAYOUT OF ONE NODE ELEMENT DESCRIPTOR                               06574161
*                                                                       06576061
         ORG   CDRENTRY                                                 06570361
CDRNED   DS    0XL32   COMPLETE NED                                     06577961
CDRNDFLG DS    X                                                        06579861
CDRNDFTO EQU   X'20'   THIS NED IS A TOKEN NED                          06581761
CDRNDFRC EQU   X'04'   THIS IS A RECONFIGURATION NED                    06583661
CDRNDTYP DS    X       TYPE OF ELEMENT DESCRIBED BY THIS NED            06585561
CDRNDTIO EQU   X'01'     NED BELONGS TO I/O DEVICE                      06587461
CDRNDTCU EQU   X'02'     NED BELONGS TO CONTROL UNIT                    06589361
CDRNDCLS DS    X                                                        06591261
         DS    X       RESERVED                                         06593161
&SYSECT  CSECT                                                          06595061
.*01* ENTRY-POINT = QRBASE                                              06596961
.*                                                                      06600061
.*01* NOTES = CHANGE ACTIVITY                                           06650061
.*    NEW MODULE FIRST RELEASE IS 2.1 (3990-6 CUIR)                     06700000
.*    ATTENTION PROCESSING FOR TPA TAPE DEVICES                @D61SDJK 06750000
.*                                                                      06900061
.**** END OF SPECIFICATIONS ******************************************* 09700061
         SPACE 3                                                        09750061
         DC    CL8'SGCUIR  '                                            09950061
         DC    CL8'&SYSDATE'      DATE                                  10000000
         DC    CL8'VSE 2.7 '      CHANGE ACTIVITY CODE                  10000100
*                                                                       10050061
***************************************************************         10100061
*  CODEBLOCK QRGSCOPE                                         *         10150061
*  FUNCTION: DETERMINE THE SCOPE FOR A GIVEN RECONFIGURATION  *         10200061
*    REQUEST RECORD (RRR).                                    *         10250061
*                                                             *         10300061
*  ON ENTRY: (RC,RE,RD = STANDARD USE, SEE ABOVE)             *         10350061
*    RA = QRSGLOB POINTER WITH INPUT AS PROVIDED BY SGSER     *         10400061
*    R4 = PUBX PTR OF DEVICE IN QUESTION                      *         10450061
*  ON EXIT:                                                   *         10500061
*    R2 = POINTS TO AN AREA IN GETVIS, WHICH IS MAPPED BY     *         10550061
*         THE DSECT QRSCDSCT. THE AREA HAS TO BE FREED BY     *         10600061
*         THE CALLER OF THIS SUBROUTINE.                      *         10650061
QRSCDSCT DSECT                                                          10700061
QRSCGVFL DS    0X   GETVIS INDICATOR.                                   10750061
QRSCGVIS EQU   X'80'  AREA IS NOT TAKEN FROM GETVIS                     10800061
QRSCLNG  DS    XL4  LENGTH OF THIS AREA                                 10850061
QRSCENTN DS    F    NUMBER OF ENTRIES THAT FOLLOW                       10900061
QRSCENTR DS    0F   PLACE OF FIRST ENTRY                                10950061
QRSCPU   DS    XL2  PUB INDEX OF DEVICE IN QUESTION                     11000061
QRSCPIDM DS    X    CHANNEL PATH ID. ZERO FOR DEVICE REQUESTS.          11050061
*                   FOR 370 MODE: X'4000' IF PRIMARY PATH,              11100061
*                                 X'8000' IF ALTERNATE PATH,            11150061
         DS    X    RESERVED                                            11200061
QRSCNEXT DS    0XL4 NEXT ENTRY                                          11250061
&SYSECT  CSECT                                                          11300061
         TITLE 'QUIESCE/RESUME PROCESSING  RECONFIG. SCOPE PROCESSING ' 68689361
         DC    C'QRGSCOPE'                                                      
         USING *,RC              SUBROUTINE BASE                                
         USING QRSGLOB,RA        GLOBAL WORK AREA BASE                          
         USING PBXADR,R4         PUBX BASE POINTER                              
*SGLINK  QRGSCOPE INPUT=(R4,RA,RC),OUTPUT=R9                                    
QRGSCOPE DS    0H                DETERMINE CUIR SCOPE                           
         BAL   R5,QRHARDWT       THATS THE END                                  
Q$NSCOPE BE    *-4               SAVE AREA STACK IS FULL                        
         LA    RD,16*4(,RD)      ADVANCE SAVE AREA POINTER                      
         ST    RE,RE*4(,RD)      SAVE RETURN REGISTER                           
         OI    QRST+04,X'80'     FOOTPRINT                                      
         LH    R3,QRIPUBND       GET PUB INDEX                          11966861
         SLR   R2,R2             CLEAR REGISTER                         12008561
         IC    R2,QRICPIDM       GET PATH MASK                          12050261
         STH   R3,QRSDVFLD+8     SAVE PUB INDEX                         12091961
         STC   R2,QRSDVFLD+10    SAVE PATH MASK                         12133661
         LR    R5,R4             COPY PUBX POINTER                      12175361
         SLR   RB,RB             INDICATE DONT HAVE CDR                 12217061
         CLI   16*4(RD),X'FF'    SET CONDITION CODE                             
         STM   R0,RF,16*4(RD)    SAVE REGISTERS                                 
         DC    0H'0',X'58C0',S(*+6) LOAD SUBROUTINE BASE                        
         BAL   RE,4(,RC)         GET CONFIGURATION DATA RECORD                  
         DC    AL4(QRGETCDR)     SUBROUTINE ADDRESS                             
*SGLINK  QRGETCDR INPUT=(R2,R3,R5,RB),OUTPUT=RB                                 
         B     QRSC0005       0  ERROR (NO CDR AVAILABLE)                       
         B     QRSC0010       4  OK RETURN                                      
QRSC0005 DS    0H                NO CDR WAS READ                                
         LM    R0,RF,0(RD)       RESTORE REGISTERS                              
         MVI   16*4(RD),X'00'    INDICATE SAVE AREA FREE                        
         B     8(,RE)            ERROR RETURN TO CALLER                         
         SPACE 1                                                                
QRSC0010 DS    0H                CDR WAS SUCCESSFULLY READ              12508961
         OI    QRST+04,X'40'     FOOTPRINT                                      
         LR    RF,RB             COPY CDR AREA ADDRESS                  12592361
         SLR   RB,RB             CLEAR REGISTER                         12634061
         CLI   QRIRRSEL,X'00'    IS SELECTOR BYTE OF RRR ZERO           12675761
         BE    QRSC1000          YES,                                   12717461
         OI    QRST+04,X'20'     FOOTPRINT                                      
         SLR   R3,R3             CLEAR REGISTER                         12800861
         BCTR  R3,0              MAKE IT NEGATIVE                       12842561
         SLR   R2,R2             RESET CHANNEL MASK BITS                12884261
QRSC0030 DS    0H                                                       12925961
         OI    QRST+04,X'10'     FOOTPRINT                                      
         CLI   16*4(RD),X'FF'    CC=00 WHEN NO MORE SAVE AREA                   
         STM   R0,RF,16*4(RD)    SAVE REGISTERS                                 
         DC    0H'0',X'58C0',S(*+6) LOAD QRGETNXT BASE ADDRESS                  
         BAL   RE,4(,RC)         GET NEXT RECORD                                
         DC    AL4(QRGETNXT)     SUBROUTINE BASE ADDRESS                        
*SGLINK  QRGETNXT INPUT=(R2,R3,R4,RF),OUTPUT=(R2,R3,R5,RB)                      
         B     QRSC0900        0 NO MORE CDR LEFT                               
         LA    R6,4(,RB)       4 SKIP HEADER BYTES                      12967661
         USING CDRENTRY,R6       CDRENTRY BASE                          13217861
QRSC0060 DS    0H                PROCESS ALL CDR'S                      13259561
         LA    R6,CDRENEXT       BUMP TO NEXT ENTRY                     13301261
         TM    CDRENFLG,CDRENFGN IS THIS THE GENERAL NEQ                13342961
         BNO   QRSC0060          NO,                                    13384661
         TM    CDRENFLG,CDRENFSN IS IT A SPECIFIC NEQ                   13426361
         BO    QRSC0060          YES, ITS NOT THE GENERAL NEQ           13468061
         OI    QRST+04,X'08'     FOOTPRINT                                      
         CLC   QRIRRSEL,CDRGNSEL IS SELECTOR BYTE THE SAME              13593161
         BNE   QRSC0030          NO,                                    13634861
         OI    QRST+04,X'04'     FOOTPRINT                                      
         DROP  R6                RELEASE CDRENTRY BASE                  13718261
         STH   R3,QRSDVFLD+8     SAVE PUB INDEX                         13759961
         STC   R2,QRSDVFLD+10    SAVE PATH MASK                         13801661
         LR    RF,RB             SAVE CDR ADDRESS                       13843361
         B     QRSC1000          WE ARE DONE                            13885061
QRSC0900 MVI   QRERRIND,QRERR#01 NO MORE CDR TO PROCESS                 13968461
         LM    R0,RF,0(RD)       RESTORE CALLERS REGISTERS                      
         MVI   16*4(RD),X'00'    INDICATE SAVE AREA AVAILABLE                   
         B     8(,RE)            TAKE ERROR RETURN                              
         SPACE 1                                                                
QRSC1000 DS    0H                SELECTOR BYTE WAS ZERO                 14051861
         OI    QRST+04,X'02'     FOOTPRINT                                      
         STH   R3,QRBASCDX       BASE CDR PUBINDEX                              
         STC   R2,QRBASCDC       BASE CDR CHPID                                 
         CLI   16*4(RD),X'FF'    CC=00 NO MORE SAVE AREA                        
         STM   R0,RF,16*4(RD)    SAVE REGISTERS                                 
         DC    0H'0',X'58C0',S(*+6) LOAD QRSTRACE BASE ADDRESS                  
         BAL   RE,4(,RC)         SET UP A TYPE-2 ENTRY                          
         DC    AL4(QRSTRACE)     SUBROUTINE BASE ADDRESS                        
         DC    AL4(2)            TRACE ENTRY TYPE-2                             
         CLI   QRIRRCOD,QRIRRCQD IS DEVICE TO BE QUIESCED                       
         BE    QRSC1010          YES,                                           
         CLI   QRIRRCOD,QRIRRCRD IS DEVICE TO BE RESUMED                        
         BNE   QRSC2000          NO,                                            
QRSC1010 DS    0H                DEVICE QUIESCE PROCESSING              14468861
         OI    QRST+04,X'01'     FOOTPRINT                                      
         SLR   R0,R0             CLEAR REGISTER                                 
         ICM   R0,8,QRIRRNED     GET ASSOCIATED NED                             
         BZ    QRSC2000          THERE IS NONE                                  
         OC    QRIRRNEQ,QRIRRNEQ IS THE NEQ MAP IS ZERO                         
         BNZ   QRSC2000          NO,                                            
         OI    QRST+05,X'80'     FOOTPRINT                                      
         LA    R5,1              RESET BIT COUNTER                              
         B     QRSC1040          JOIN MAIN PATH                                 
QRSC1020 SLL   R0,1              SHIFT NED INFO                         14927561
         LA    R5,1(,R5)         ADJUST BIT COUNT                       14969261
QRSC1040 LTR   R0,R0             IS THIS BIT ON                         15010961
         BNM   QRSC1020          NO                                     15052661
         SLL   R0,1              SHIFT OUT THIS BIT                     15136061
         LTR   R0,R0             WAS THIS THE ONLY BIT                  15177761
         BNZ   QRSC2000          NO,                                    15219461
         OI    QRST+05,X'40'     FOOTPRINT                                      
         LA    R7,4(,RF)         PREVIOUS CDRENTRY ADDRESS                      
         USING CDRENTRY,R7       CDRENTRY BASE POINTER                          
         B     QRSC1120          FIRST TIME THROUGH                             
QRSC1100 LA    R7,CDRENEXT       BUMP TO NEXT ENTRY                             
QRSC1120 OI    QRST+05,X'20'     FOOTPRINT                                      
         TM    CDRENFLG,CDRENFND IS THIS A NED                                  
         BO    QRSC1140          YES,                                           
         TM    CDRENFLG,CDRENFGN IS IT THE GENERAL NEQ                          
         BNO   QRSC1100          NO,                                            
         OI    QRST+05,X'10'     FOOTPRINT                                      
         MVI   QRERRIND,QRERR#02 SET ERROR INDICATOR                            
         LM    R0,RF,0(RD)       RESTORE CALLERS REGISTERS                      
         MVI   16*4(RD),X'00'    INDICATE WORK AREA FREE                        
         B     8(,RE)            TAKE ERROR RETURN                              
QRSC1140 DS    0H                NED FOUND PROCESSING                   15803261
         TM    CDRNDFLG,CDRNDFRC IS IT A RECONFIGURATION NED                    
         BNO   QRSC1100          NO,                                            
         BCT   R5,QRSC1100       CHECK NEXT NED                                 
         OI    QRST+05,X'08'     FOOTPRINT                                      
         CLI   CDRNDTYP,CDRNDTIO IS THIS A DEVICE TYPE NED                      
         BNE   QRSC2000          NO,                                            
         OI    QRST+05,X'04'     FOOTPRINT                                      
         LA    R9,QRSDVFLD       SET ADDRESS POINTER                            
         ST    R9,R9*4(,RD)      SAVE ADDRESS FOR CALLER                        
         LM    R0,RF,0(RD)       RESTORE CALLERS REGISTERS                      
         MVI   16*4(RD),X'00'    FREE SAVE AREA                                 
         B     4(,RE)            RETURN TO CALLER                               
         DROP  R7                RELEASE CDRENTRY BASE                          
*                                                                       16428761
*  LENGTH OF GETVIS FOR SCOPE LIST IS NUMBER OF POSSIBLE DEV.           16470461
*  ON A CONTROL UNIT X NUMBER OF PATHS X LENGTH OF ENTRY +              16512161
*  LENGTH OF SCOPE LIST HEADER.                                         16553861
*                                                                       16595561
QRSC2000 DS    0H                GET US THE REQUIRED GETVIS             16387061
         LR    R9,RF             PRESERVE BASE CDR ADDRESS              16637261
*HERE WE MAY HAVE TO SUPPORT MORE DEVICES                                       
         LA    R0,64*4*8+8       LENGTH OF REQUIRED GETVIS              16678961
         GETVIS ADDRESS=(1),LENGTH=(0),LOC=ANY,SVA=YES,                X16762361
               SPID=QRSGVSPN,PFIX=YES                                   16804061
         LTR   RF,RF             DID WE GET THE GETVIS AREA             16887461
         BZ    QRSC2020          YES,                                   16929161
         OI    QRST+05,X'02'     FOOTPRINT                                      
         MVI   QRERRIND,QRERR#03 SET ERROR INDICATOR                            
         LM    R0,RF,0(RD)       RESTORE CALLERS REGISTERS                      
         MVI   16*4(RD),X'00'    INDICATE SAVE AREA FREE                        
         B     8(,RE)            TAKE ERROR RETURN                              
QRSC2020 DS    0H                WE GOT THE GETVIS STORAGE              17095961
         LR    RF,R9             RESTORE BASE CDR ADDRESS               17137661
         LR    R9,R1             TAKE SCOPE LIST ADDRESS                17179361
         USING QRSCDSCT,R9       QRSCDSCT BASE POINTER                  17221061
         ST    R0,QRSCLNG        SAVE LENGTH                            17262761
         MVC   QRSCPU(4),QRSDVFLD+8 SAVE PUB AND CHPID INFO             17304461
         LA    R1,1              THERE IS ONE IN LIST                   17346161
         ST    R1,QRSCENTN       SAVE ENTRY COUNTER                     17387861
         LA    R8,QRSCNEXT       ADVANCE POINTER TO NEXT ENTRY          17429561
         USING QRSCENTR,R8       QRSCENTR BASE POINTER                  17471261
         SLR   R3,R3             CLEAR REGISTER                         17512961
         BCTR  R3,0              FORCE PUB INDEX TO FFFFFFFF            17554661
         SLR   R2,R2             CLEAR CHPID MASK BYTE                  17596361
QRSC2040 DS    0H                LOOP                                   17638061
         OI    QRST+05,X'01'     FOOTPRINT                                      
         CLI   16*4(RD),X'FF'    CC=00 WHEN NO MORE SAVE AREA                   
         STM   R0,RF,16*4(RD)    SAVE CURENT REGISTERS                          
         DC    0H'0',X'58C0',S(*+6) LOAD QRGETNXT BASE ADDRESS                  
         BAL   RE,4(,RC)         PROCESS NEXT DEVICE                            
         DC    AL4(QRGETNXT)     SUBROUTINE BASE ADDRESS                        
*SGLINK  QRGETNXT INPUT=(R2,R3,R4,RF),OUTPUT=(R2,R3,R5,RB)                      
         B     QRSC6000        0 NO MORE CDR LEFT                               
         CLM   R3,3,QRBASCDX   4 IS THIS THE BASE DEVICE                17888261
         BNE   QRSC2060          NO,                                    17929961
         CLI   QRIRRCOD,QRIRRCQD IS IT A DEVICE QUIESCE REQUEST         17971661
         BE    QRSC5010          YES,                                   18013361
         CLI   QRIRRCOD,QRIRRCRD IS IT A DEVICE RESUME REQUEST          18055061
         BE    QRSC5010          YES,                                   18096761
         CLM   R2,1,QRBASCDC     IS THIS A PATH REQUEST                 18138461
         BE    QRSC2040          YES,                                   18180161
QRSC2060 DS    0H                NON BASE DEVICE PROCESSING             18221861
         OI    QRST+06,X'80'     FOOTPRINT                                      
         SLR   R0,R0             CLEAR REGISTER                                 
         ICM   R0,8,QRIRRNED     IS NED ALL ZEROS                               
         BZ    QRSC4000          YES,                                           
         OI    QRST+06,X'40'     FOOTPRINT                                      
         LA    R7,4(,RF)         POINT AHEAD OF 1ST                             
         S     R7,=A(L'CDRENTRY) CDR ENTRY ADDRESS                              
         LA    R6,4(,RB)         POINT AHEAD OF 1ST                             
         S     R6,=A(L'CDRENTRY) CDR ENTRY ADDRESS                              
         B     QRSC2100          PROCESS ALL AFFECTED NEDS                      
QRSC2080 LTR   R0,R0             ALL NEDS PROCESSED                     18680561
         BZ    QRSC4000          YES,                                   18722261
         OI    QRST+06,X'20'     FOOTPRINT                                      
         USING CDRENTRY,R7       CDRENTRY BASE POINTER                          
QRSC2100 LA    R7,CDRENEXT       NEXT CDR ENTRY ADDRESS                 18847361
         TM    CDRENFLG,CDRENFGN IS THIS A GENERAL NEQ                  18889061
         BNO   QRSC2100          NO,                                    18930761
         TM    CDRENFLG,CDRENFSN IS THIS A SPECIFIC NEQS                18972461
         BNO   QRSC3000          NO,                                    19014161
         TM    CDRNDFLG,CDRNDFRC IS THIS A RECONFIGURATION NED          19055861
         BNO   QRSC2100          NO,                                    19097561
         DROP  R7                RELEASE CDRENTRY BASE                  19139261
         USING CDRENTRY,R6       CDRENTRY BASE POINTER                  19180961
QRSC2200 LA    R6,CDRENEXT       BUMP  TO NEXT ENTRY                    19222661
         TM    CDRENFLG,CDRENFGN IS IT A GENERAL NEQ                    19264361
         BNO   QRSC2200          NO,                                    19306061
         TM    CDRENFLG,CDRENFSN IS IT A SPECIFIC NEQ                   19347761
         BNO   QRSC2040          NO,                                    19389461
         TM    CDRNDFLG,CDRNDFRC RECONFIGURATION REQUEST NED            19431161
         BNO   QRSC2200          NO,                                    19472861
         DROP  R6                RELEASE CDRENTRY BASE                  19514561
         OI    QRST+06,X'10'     FOOTPRINT                                      
         LTR   R0,R0             MORE NEDS TO PROCESS                   19597961
         SLL   R0,1              BUMP TO NXT NEDMAP BIT                 19639661
         BNM   QRSC2080          THE NED MAP IS OFF                     19681361
         CLC   0(L'CDRENTRY,R6),0(R7) ARE THE NEDS THE SAME             19723061
         BE    QRSC2080          YES,                                   19764761
         OI    QRST+06,X'08'     FOOTPRINT                                      
         B     QRSC2040          PROCESS NEXT                           19848161
         B     QRSC4000          ???????????????????????????            19931561
QRSC3000 DS    0H                ERROR PROCESSING                       19973261
         OI    QRST+06,X'04'     FOOTPRINT                                      
         MVI   QRERRIND,QRERR#04 SET ERROR INDICATOR                            
         LM    R0,RF,0(RD)       RESTORE CALLERS REGISTERS                      
         MVI   16*4(RD),X'00'    INDICATE SAVE AREA FREE                        
         B     8(,RE)            ERROR RETURN                                   
         SPACE 1                                                                
QRSC4000 DS    0H                                                       20181761
         SLR   R0,R0             CLEAR REGISTERS                        20223461
         ICM   R0,14,QRIRRNEQ    GET NEQ-MAP                            20265161
         BZ    QRSC5000          IS ALL ZEROS                           20306861
         OI    QRST+06,X'02'     FOOTPRINT                                      
         LA    R6,4(RB)          POINT TO NEW CDR                       20390261
         USING CDRENTRY,R6       CDRENTRY BASE POINTER                  20431961
QRSC4010 DS    0H                PROCESS NEXT CDR                       20473661
         LA    R6,CDRENEXT       POINT TO NEXT ENTRY                    20515361
         TM    CDRENFLG,CDRENFGN IS THIS A GENERAL NEQ                  20557061
         BNO   QRSC4010          NO,                                    20598761
         TM    CDRENFLG,CDRENFSN IS IT A SPECIFIC NEQ                   20640461
         BO    QRSC4010          YES,                                   20682161
         DROP  R6                RELEASE CDRENTRY BASE                  20723861
         LA    R7,4(RF)          POINT TO NEW CDR                       20765561
         USING CDRENTRY,R7       NEW CDR-ENTRY BASE                     20807261
QRSC4020 DS    0H                PROCESS NEXT CDR                       20848961
         LA    R7,CDRENEXT       BUMP TO NEXT ENTRY                     20890661
         TM    CDRENFLG,CDRENFGN IS THIS A GENERAL NEQ                  20932361
         BNO   QRSC4020          NO,                                    20974061
         TM    CDRENFLG,CDRENFSN IS IT A SPECIFIC NEQ                   21015761
         BO    QRSC4020          YES,                                   21057461
         DROP  R7                RELEASE NEXT CDRENTRY BASE             21099161
         USING CDRGNEQ,R6        CDRGNEQ BASE POINTER                   21140861
         LA    R6,CDRGNQAL-1     POINT TO QUALIFIER BYTE                21182561
         DROP  R6                RELEASE CDRGNEQ BASE                   21224261
         USING CDRGNEQ,R7        CDRGNEQ NEW BASE POINTER               21265961
         LA    R7,CDRGNQAL-1     POINT TO QUALIFIER BYTE                21307661
         DROP  R7                DROP CDRGNEQ NEW BASE                  21349361
         SRL   R0,1              SHIFT OFF ONE BIT                      21391061
QRSC4100 DS    0H                PROCESS NEQMAP                         21432761
         OI    QRST+06,X'01'     FOOTPRINT                                      
         LTR   R0,R0             WAS THIS THE LAST MAP                  21516161
         BZ    QRSC5000          YES,                                   21557861
         LA    R6,1(,R6)         BUMP TO NEXT QUALIF.B.                 21599561
         LA    R7,1(,R7)         BUMP TO NEXT QUALIF.B.                 21641261
         SLL   R0,1              SHIFT OFF ONE BIT                      21682961
         LTR   R0,R0             DO WE NEED TO PROCESS THIS ONE         21724661
         BNM   QRSC4100          NO,                                    21766361
         CLC   0(1,R7),0(R6)     IS THIS A MATCHING GNEQ                21808061
         BE    QRSC4100          YES,                                   21849761
         OI    QRST+07,X'80'     FOOTPRINT                                      
         B     QRSC2040          PROCESS NEXT ENTRY                     21933161
QRSC5000 DS    0H                                                       22016561
         STH   R3,QRSCPU         SAVE PUB INDEX                         22058261
         STC   R2,QRSCPIDM       SAVE CHPID MASK                        22099961
         L     R1,QRSCENTN       NUMBER OF ENTRIES                      22141661
         LA    R1,1(,R1)         INCREASE COUNTER                       22183361
         ST    R1,QRSCENTN       SAVE UPDATED COUNTER                   22225061
         LA    R8,L'QRSCENTR(,R8) BUMP TO NEXT FREE ENTRY               22266761
QRSC5002 DS    0H                                                       22308461
         CLI   QRIRRCOD,QRIRRCQD DEVICE TO BE QUIESCED                  22350161
         BE    QRSC5010          YES,                                   22391861
         CLI   QRIRRCOD,QRIRRCRD DEVICE TO BE RESUMED                   22433561
         BNE   QRSC2040          NO                                     22475261
QRSC5010 SLR   R2,R2             SET PATH MASK TO ZERO                  22516961
         OI    QRST+07,X'40'     FOOTPRINT                                      
         B     QRSC2040          PROCESS NEXT                           22642061
QRSC6000 DS    0H                RETURN                                         
         ST    R9,R9*4(,RD)      SET RECORD ADDRESS IN SAVE                     
         LM    R0,RF,0(RD)       RESTORE CALLERS REGISTERS                      
         MVI   16*4(RD),X'00'    INDICATE SAVE AREA FREE                        
         B     4(,RE)            RETURN TO CALLER                               
         DROP  ,                                                        22767161
QRSDVFLD DC    X'80',X'00000C'   RESERVED AND FIELD LENGTH              23017361
         DC    X'00000001'       NUMBER OF ENTRIES                      23059061
         DC    X'00000000'       FIRST AND ONLY ENTRY                   23100761
         LTORG                                                          23142461
***************************************************************         23200061
*  CODEBLOCK QRGETNXT                                         *         23250061
*  FUNCTION: BUMP TO NEXT PUB AND CHANNEL PATH MASK           *         23300061
*    THAT SHALL BE TESTED FOR A CONFIGURATION DATA RECORD.    *         23350061
*  ON ENTRY: (STANDARD REGS SEE ABOVE)                        *         23400061
*    R2 = CHANNEL PATH ID MASK FROM WHICH CDR IS TO BE READ   *         23500061
*    R3 = PUB INDEX OF LAST DEVICE THAT CDR WAS READ FROM     *         23550061
*    R4 = PUBX OF THE DEVICE THE BASE CDR WAS TAKEN FROM      *         23600061
*    RF = POINTER TO AREA WITH BASE CDR                       *         23650061
*  ON EXIT:                                                   *         23700061
***************************************************************         23750061
         DC    C'QRGETNXT'                                                      
         USING *,RC              QRGETNXT BASE POINTER                          
         USING QRSGLOB,RA        GLOBAL AREA BASE                               
         USING PBXADR,R4         PUBX BASE POINTER                              
*SGLINK  QRGETNXT INPUT=(R2,R3,R4,RF),OUTPUT=(R2,R3,R5,RB)                      
QRGETNXT DS    0H                NEXT PUB PROCESSING                            
         BAL   R5,QRHARDWT       THATS THE END                                  
Q$NETNXT BE    *-4               SAVE AREA IS FULL                              
         LA    RD,16*4(,RD)      UPDATE SAVE AREA POINTER                       
         ST    RE,RE*4(,RD)      SAVE RETURN REGISTER                           
         OI    QRST+08,X'80'     FOOTPRINT                                      
         LA    R7,4(,RF)         POINT TO FIRST NED                             
         USING CDRENTRY,R7       CDRENTRY BASE POINTER                          
         B     QRGNXT04          FIRST TIME THROUGH                             
QRGNXT02 LA    R7,CDRENEXT       BUMP TO NEXT NED                       24404561
QRGNXT04 TM    CDRENFLG,CDRENFTO IS THIS A TOKEN NED                    24448061
         BNO   QRGNXT02          NO,                                    24491561
         DROP  R7                RELEASE CDRENTRY BASE                  24535061
         LTR   R2,R2             ANY PATH LEFT                          24622061
         BNZ   QRGNXT40          YES,                                   24665561
QRGNXT30 DS    0H                PROCESS NEXT PUB                       24752561
         OI    QRST+08,X'40'     CODE COVERAGE POINT                            
         LA    R3,1(,R3)         NEXT PUB INDEX                         24839561
         LR    R1,R3             COPY PUB INDEX                         24883061
         SLL   R1,PUBSLEN        MULTIPLY WITH PUB-LENGTH               24926561
         L     R6,QRPUBPTR       GET ADDRESS OF PUBTAB                  24970061
         AH    R1,0(,R6)         ADDRESS OF OUR PUB                     25013561
         USING PUBADR,R1         PUB BASE POINTER                       25057061
         CLI   PUBCHANN,X'FF'    IS THIS A VALID PUB                    25100561
         BE    QRGNXTX0          NO, WE ARE DONE                        25144061
         DROP  R1                RELEASE PUB BASE POINTER               25187561
         LR    R5,R3             COPY PUB INDEX                         25231061
         SLL   R5,2              OFFSET WITHIN PUBX ADDR. TABLE         25274561
         L     R6,QRPBXPTR       ADDRESS OF PUBXTAB                     25318061
         A     R5,0(,R6)         ADDRESS OF PUBX ADDRESS                25361561
         L     R5,0(,R5)         ADDRESS OF PUBX ENTRY                  25405061
         CLC   PBXCUTYP(3),PBXCUTYP-PBXADR(R5) MATCHING CU-TYPE         25448561
         BNE   QRGNXT30          NO, PROCESS NEXT PUB                   25709561
         DROP  R4                RELEASE OLD PUBX BASE                          
         USING PBXADR,R5         PUBX BASE POINTER                              
         OI    QRST+08,X'20'     FOOTPRINT                                      
         LA    R2,X'100'         INITIALIZE CHPID MASK                  25840061
QRGNXT40 DS    0H                PROCESS CHANNEL PATH                   25927061
         OI    QRST+08,X'10'     FOOTPRINT                                      
         SRL   R2,1              DROP PROCESSED CHPID                   26014061
         LTR   R2,R2             ANY MORE CHPIDS TO PROCESS             26057561
         BZ    QRGNXT30          NO, PROCESS NEXT PUB                   26101061
         IC    R1,PBXNOQSD       NOT OPERATIONAL QUIESCED MASK          26144561
         O     R1,PBXLPM-3       ADD LPM                                26188061
         SLR   R0,R0             CLEAR REGISTER                         26318561
         IC    R0,PBXNOPVF       NOT OPERATIONAL VERIFY MASK            26362061
         X     R0,=X'000000FF'   TURN THEM OFF                          26405561
         NR    R1,R0             RESULTING CHPIDS THAT REMAIN           26449061
         IC    R0,PBXNOERP       NOT OPERATIONAL BY ERP MASK            26492561
         X     R0,=X'000000FF'   TURN THEM OFF                          26536061
         NR    R1,R0             RESULTING CHPIDS THAT REMAIN           26579561
         IC    R0,PBXNOOPR       NOT OPERATIONAL BY OPER. MASK          26623061
         X     R0,=X'000000FF'   TURN THEM OFF                          26666561
         NR    R1,R0             RESULTING CHPIDS TO PROCESS            26710061
         NR    R1,R2             IS OUR PATH STILL ACCESSABLE           26753561
         BZ    QRGNXT40          NO, IT IS NOT                          26797061
         OI    QRST+08,X'08'     FOOTPRINT                                      
         CLI   16*4(RD),X'FF'    SET CONDITION CODE                             
         STM   R0,RF,16*4(RD)    SAVE REGISTERS                                 
         DC    0H'0',X'58C0',S(*+6) LOAD SUBROUTINE ADDRESS                     
         BAL   RE,4(,RC)         GET CONFIGURATION DATA RECORD                  
         DC    AL4(QRGETCDR)     ADDRESS OF SUBROUTINE                          
*SGLINK  QRGETCDR INPUT=(R2,R3,R5,RB),OUTPUT=RB                                 
         B     QRGNXT40          PROCESS NEXT CHPID                             
         LA    R6,4(,RB)         NEXT CDR ADDRESS                               
         USING CDRENTRY,R6       CDRENTRY BASE POINTER                          
         B     QRGNXT52          FIRST TIME PATH                                
QRGNXT50 LA    R6,CDRENEXT       BUMP TO NEXT ENTRY                     27188561
QRGNXT52 TM    CDRENFLG,CDRENFTO IS THIS A TOKEN NED                    27232061
         BNO   QRGNXT50          NO, PROCESS NEXT ENTRY                 27275561
         CLC   0(L'CDRENTRY,R6),0(R7) SAME TOKEN NED                    27319061
         BNE   QRGNXT40          NO, PROCESS NEXT CHPID                 27362561
         OI    QRST+08,X'04'     FOOTPRINT                                      
QRGNXTX4 DS    0H                RETURN, FOUND ONE                              
         ST    R2,R2*4(,RD)      SAVE CHANNEL PATH ID                           
         ST    R3,R3*4(,RD)      SAVE PUB INDEX                                 
         ST    R5,R5*4(,RD)      SAVE PUBX ADDRESS                              
         ST    RB,RB*4(,RD)      SAVE CDR ADDRESS                               
         LM    R0,RF,0(RD)       RESTORE CALLERS REGISTERS                      
         MVI   16*4(RD),X'00'    INDICATE SAVE AREA FREE                        
         B     8(,RE)            RETURN TO CALLER                               
QRGNXTX0 DS    0H                NO MORE PUBS TO PROCESS                        
         LM    R0,RF,0(RD)       RESTORE CALLERS REGISTERS                      
         MVI   16*4(RD),X'00'    INDICATE SAVE AREA FREE                        
         B     4(,RE)            RETURN TO CALLER                               
         DROP  ,                                                        27710561
         LTORG                                                          27754061
***************************************************************         27800061
*  CODEBLOCK QRMSGWRT                                         *         27850061
*  FUNCTION: WRITE A MESSAGE TO THE OPERATOR CONSOLE          *         27900061
*    IF A RECONFIGURATION REQUEST WAS SUCCESSFUL OR NOT.      *         27950061
.*   THE TECHNIQUE USED FOR WRITING THE MESSAGE IS BASED      *         28000061
.*   ON THE FOLLOWING DSECT:                                  *         28050061
.*   EACH MESSAGE CONSISTS OF LINES, REPRESENTED BY A LINE    *         28100061
.*   POINTER VECTOR. THE VECTOR ENDS WITH THE FIRST NEGATIVE  *         28150061
.*   POINTER.                                                 *         28200061
.*   THE LINES AGAIN CONSIST OF PARTS. ALL PARTS MUST NOT     *         28250061
.*   EXCEED ONE LINE ON THE SCREEN IN LENGTH.                 *         28300061
.*   THE PARTS ARE REPRESENTED BY A TWO BYTE TOKEN,           *         28350061
.*   WHICH CONTAINS IN THE FIRST BYTE THE FOLLOWING:          *         28400061
.*   BIT 0: IF SET THE LINE IS FINISHED WITH THIS PART        *         28450061
.*   BIT 1: IF SET ADD VALUE IN BYTE 1 TO CURRENT POINTER     *         28500061
.*          SERVES TO TRANSFER IN LINE TEXT..                 *         28550061
.*   BIT 2-7: ACTION CODE FOR WHAT SUBROUTINE TO SELECT       *         28600061
.*          TO WRITE DATA TO A BUFFER.                        *         28650061
.*                                                            *         28700061
.*   BYTE 1 CONTAINS THE INDEX IN THE TEXT TABLE, FOR THE     *         28750061
.*   TEXT TO BE PRINTED.                                      *         28800061
.*                                                            *         28850061
.*                                                            *         28900061
*  ON ENTRY: (STANDARD REGS SEE ABOVE)                        *         28950061
*    R1 = INDEX IN MESSAGE TABLE FOR MESSAGE TO BE WRITTEN.   *         29000061
*  ON EXIT:                                                   *         29050061
***************************************************************         29100061
         DC    C'QRMSGWRT'                                                      
         USING *,RC              QRMSGWRT BASE POINTER                          
         USING QRSGLOB,RA        GLOBAL AREA BASE POINTER                       
*SGLINK  QRMSGWRT INPUT=(R1,RA,RC)                                              
QRMSGWRT DS    0H                WRITE MESSAGE TO SYSLOG                        
         BAL   R5,QRHARDWT       THATS THE END                                  
Q$NSGWRT BE    *-4               SAVE AREA FULL CONDITION                       
         LA    RD,16*4(,RD)      POINT TO NEXT SAVE AREA                        
         ST    RE,RE*4(,RD)      SAVE RETURN ADDRESS                            
         OI    QRST+16,X'80'     FOOTPRINT                                      
         B     QRMSGWTB(R1)      POINT TO MESSAGE DESCRIPTOR            29529161
QRMSGWTB DS    0H                                                       29547061
QR#1H40I EQU   (*-QRMSGWTB)                                             29564961
         BAL   RB,QRMSGW00       QUIESCE RESUME MSG RECEIVED            29582861
         DC    AL2(QRX1H40I-QRMSGWRT-4)                                 29600761
         DC    C'40'                                                    29618661
         DC    S(QRRRDEVN)                                              29636561
         DC    S(QRRRCPID)                                              29654461
         DC    S(QRIRRR)                                                29672361
QR#1H41I EQU   (*-QRMSGWTB)                                             29690261
         BAL   RB,QRMSGW00       DEVICE IS QUIESCED                     29708161
         DC    AL2(QRX1H41I-QRMSGWRT-4)                                 29726061
         DC    C'41'                                                    29743961
         DC    S(QRMSDEVN)                                              29761861
QR#1H42I EQU   (*-QRMSGWTB)                                             29779761
         BAL   RB,QRMSGW00       DEVICE NOT QUIESCED                    29797661
         DC    AL2(QRX1H42I-QRMSGWRT-4)                                 29815561
         DC    C'42'                                                    29833461
         DC    S(QRMSDEVN)                                              29851361
QR#1H43I EQU   (*-QRMSGWTB)                                             29869261
         BAL   RB,QRMSGW00       CUU ... OWNED BY ..                    29887161
         DC    AL2(QRX1H43I-QRMSGWRT-4)                                 29905061
         DC    C'43'                                                    29922961
         DC    S(QRMSOWNR)                                              29940861
         DC    S(QRMSDEVN)                                              29958761
QR#1H44I EQU   (*-QRMSGWTB)                                             29976661
         BAL   RB,QRMSGW00       DEVICE IS RESUMED                      29994561
         DC    AL2(QRX1H44I-QRMSGWRT-4)                                 30012461
         DC    C'44'                                                    30030361
         DC    S(QRMSDEVN)                                              30048261
QR#1H45I EQU   (*-QRMSGWTB)                                             30066161
         BAL   RB,QRMSGW00       CUU NEED NOT BE RESUMED                30084061
         DC    AL2(QRX1H45I-QRMSGWRT-4)                                 30101961
         DC    C'45'                                                    30119861
         DC    S(QRMSDEVN)                                              30137761
QR#1H46I EQU   (*-QRMSGWTB)                                             30155661
         BAL   RB,QRMSGW00       DEVICE RESUMED, BUT STILL DOWN         30173561
         DC    AL2(QRX1H46I-QRMSGWRT-4)                                 30191461
         DC    C'46'                                                    30209361
         DC    S(QRMSDEVN)                                              30227261
QR#1H47I EQU   (*-QRMSGWTB)                                             30245161
         BAL   RB,QRMSGW00       PATH IS QUIESCED                       30263061
         DC    AL2(QRX1H47I-QRMSGWRT-4)                                 30280961
         DC    C'47'                                                    30298861
         DC    S(QRMSCPID)                                              30316761
         DC    S(QRMSDEVN)                                              30334661
QR#1H48I EQU   (*-QRMSGWTB)                                             30352561
         BAL   RB,QRMSGW00       PATH NOT QUIESCED (LAST)               30370461
         DC    AL2(QRX1H48I-QRMSGWRT-4)                                 30388361
         DC    C'48'                                                    30406261
         DC    S(QRMSCPID)                                              30424161
         DC    S(QRMSDEVN)                                              30442061
QR#1H49I EQU   (*-QRMSGWTB)                                             30459961
         BAL   RB,QRMSGW00       PATH IS RESUMED                        30477861
         DC    AL2(QRX1H49I-QRMSGWRT-4)                                 30495761
         DC    C'49'                                                    30513661
         DC    S(QRMSCPID)                                              30531561
         DC    S(QRMSDEVN)                                              30549461
QR#1H50I EQU   (*-QRMSGWTB)                                             30567361
         BAL   RB,QRMSGW00       PATH TO CUU WAS RESUMED ALREAD         30585261
         DC    AL2(QRX1H50I-QRMSGWRT-4)                                 30603161
         DC    C'50'                                                    30621061
         DC    S(QRMSCPID)                                              30638961
         DC    S(QRMSDEVN)                                              30656861
QR#1H51I EQU   (*-QRMSGWTB)                                             30674761
         BAL   RB,QRMSGW00       PATH RESUMED, BUT STILL DOWN.          30692661
         DC    AL2(QRX1H51I-QRMSGWRT-4)                                 30710561
         DC    C'51'                                                    30728461
         DC    S(QRMSCPID)                                              30746361
         DC    S(QRMSDEVN)                                              30764261
QR#1H52I EQU   (*-QRMSGWTB)                                             30782161
         BAL   RB,QRMSGW00       IDENTIFIED MYSELF MESSAGE              30800061
         DC    AL2(QRX1H52I-QRMSGWRT-4)                                 30817961
         DC    C'52'                                                    30835861
         DC    S(QRRRSYID)                                              30853761
         DC    S(QRRRDEVN)                                              30871661
         DC    S(QRRRCPID)                                              30889561
QR#1H53I EQU   (*-QRMSGWTB)                                             30907461
         BAL   RB,QRMSGW00       UNSUPP. TYPE OF RECONF. RECORD         30925361
         DC    AL2(QRX1H53I-QRMSGWRT-4)                                 30943261
         DC    C'53'                                                    30961161
QR#1H54I EQU   (*-QRMSGWTB)                                             30979061
         BAL   RB,QRMSGW00       END OF REQUEST PROCESSING              30996961
         DC    AL2(QRX1H54I-QRMSGWRT-4)                                 31014861
         DC    C'54'                                                    31032761
         DC    S(QRRRANSP)                                              31050661
         DC    S(QRMSDEVN)                                              31068561
         DC    S(QRMSCPID)                                              31086461
QR#1H55I EQU   (*-QRMSGWTB)                                             31104361
         BAL   RB,QRMSGW00       ANSWER RECEIVED DISASTER ERROR         31122261
         DC    AL2(QRX1H55I-QRMSGWRT-4)                                 31140161
         DC    C'55'                                                    31158061
         DC    S(QRRRANSP)                                              31175961
         DC    S(QRMSDEVN)                                              31193861
         DC    S(QRMSCPID)                                              31211761
         DC    S(QRPSFCCB)                                              31229661
         DC    S(QRSNSDT0)                                              31247561
         DC    S(QRSNSDT1)                                              31265461
QR#1H56I EQU   (*-QRMSGWTB)                                             31283361
         BAL   RB,QRMSGW00       DISASTER ERROR ON READ ATTNMSG         31301261
         DC    AL2(QRX1H56I-QRMSGWRT-4)                                 31319161
         DC    C'56'                                                    31337061
         USING PBXADR,R4                                                31354961
         DC    S(PBXCUU)                                                31372861
         DROP  R4                                                       31390761
         DC    S(QRMSCPID)                                              31408661
         DC    S(QRRRRCCB)                                              31426561
         DC    S(QRSNSDT0)                                              31444461
         DC    S(QRSNSDT1)                                              31462361
QR#1H57I EQU   (*-QRMSGWTB)                                             31480261
         BAL   RB,QRMSGW00       ATT.RECEIV. ON IMPOSSIBLE PATH         31498161
         DC    AL2(QRX1H57I-QRMSGWRT-4)                                 31516061
         DC    C'57'                                                    31533961
         DC    S(QRRRRCCC)                                              31551861
         USING PBXADR,R4                                                31569761
         DC    S(PBXCUU)                                                31587661
         DC    S(PBXNOERP)                                              31605561
         DC    S(PBXNOPVF)                                              31623461
         DC    S(PBXNOOPR)                                              31641361
         DC    S(PBXNOQSD)                                              31659261
         DC    S(PBXPIM)                                                31677161
         DC    S(PBXPAM)                                                31695061
         DC    S(PBXLPM)                                                31712961
         DC    S(PBXCHPID)                                              31730861
         DROP  R4                                                       31748761
QR#1H58I EQU   (*-QRMSGWTB)                                             31766661
         BAL   RB,QRMSGW00       INVALID CDR FOUND IN QRTSTCDR          31784561
         DC    AL2(QRX1H58I-QRMSGWRT-4)                                 31802461
         DC    C'58'                                                    31820361
         DC    S(QRMSCPID)                                              31838261
         DC    S(QRMSDEVN)                                              31856161
         DC    S(QRERRIND)                                              31874061
         DC    S(QRMSREGC)                                              31891961
         USING QRCDKRRL,RB                                              31909861
         DC    S(QRCDKRRL)                                              31927761
         DROP  RB                                                       31945661
QR#1H59I EQU   (*-QRMSGWTB)                                             31963561
         BAL   RB,QRMSGW00       COULD NOT OBTAIN GETVIS                31981461
         DC    AL2(QRX1H59I-QRMSGWRT-4)                                 31999361
         DC    C'59'                                                    32017261
*        DC    S(QRMSREGC)                                   @D61NDMK   32035100
QR#1H60I EQU   (*-QRMSGWTB)                                             32053061
         BAL   RB,QRMSGW00       INTERNAL PROCESSING ERROR              32070961
         DC    AL2(QRX1H60I-QRMSGWRT-4)                                 32088861
         DC    C'60'                                                    32106761
         DC    S(QRERRIND)                                              32124661
*                                                                       32142561
QRMSGW00 LH    R6,0(,RB)         TAKE MESSAGE NR - OFFSET               32160461
         LA    R6,QRMSGWRT(R6)   COMPRESSED MSG ADDRESS                 32178361
         MVC   QRMSGNO(2),2(RB)  INSERT MESSAGE NUMBER                  32196261
         LA    RB,4(,RB)         BUMP TO FIRST DATA ADDRESS             32214161
         LA    R7,QRMSGBUF-QRMSGLEN INITIALIZE BUFFER POINTER           32232061
         LA    R8,QRMSGCCW-L'CCWTOT INITIALIZE CCW POINTER              32249961
QRMSGW10 LA    R6,4(,R6)         ADVANCE TO NEXT LINE INFO              32267861
         LA    R7,QRMSGLEN(,R7)  POINT TO NEXT PRINT BUFFER             32285761
         CL    R7,QREMSGBU       DOES MESSAGE FIT IN BUFFER             32303661
         BL    QRMSGW20          YES,                                   32321561
         BAL   R5,QRHARDWT       THATS THE END                          32339461
QRMSGW20 DS    0H                PROVIDE FOR NEXT LINE PRINT            32357361
         LA    R8,L'CCWTOT(,R8)  POINT TO NEXT CCW                      32375261
         SLR   R3,R3             RESET ACCUMUL. LINE LENGTH             32393161
         L     R9,0(,R6)         POINT TO FIRST PART OF LINE            32411061
         LA    R9,0(,R9)         CLEAR HIGH ORDER BYTE                  32428961
         S     R9,=A(2)          ADJUST FOR 1ST TIME THROUGH            32446861
QRMSGW30 LA    R9,2(,R9)         POINT TO NEXT ENTRY                    32464761
QRMSGW31 TM    0(R9),X'40'       MESSAGE TO BE CONTINUED                32482661
         BNO   QRMSGW32          NO                                     32500561
         SLR   R1,R1             CLEAR REGISTER                         32518461
         IC    R1,1(,R9)         GET CONTINUATION OFFSET                32536361
         LA    R9,1(R1,R9)       GET THE CONTINUATION ENTRY             32554261
         B     QRMSGW31          PROCESS NEXT ENTRY                     32572161
QRMSGW32 DS    0H                                                       32590061
         LA    R5,0(R3,R7)       NEXT POSITION ADDRESS                  32607961
         TM    0(R9),X'0F'       IS THIS PLAIN TEXT                     32625861
         BZ    QRMSGW60          YES,                                   32643761
         SLR   R1,R1             CLEAR REGISTER                         32661661
         ICM   R1,3,0(RB)        GET RELATIVE ADDRESS                   32679561
         SRL   R1,12             LEAVE REGISTER ONLY                    32697461
         SLL   R1,2              MULTIPL WITH 4                         32715361
         L     R1,0(R1,RD)       RETRIEVE VALUE FROM SAVE AREA          32733261
         LH    R0,0(,RB)         RELOAD RELATIVE ADDRESS                32751161
         SLL   R0,20             ELIMINATE REGISTER                     32769061
         SRL   R0,20             OFFSET BASED ON REGISTER               32786961
         AR    R1,R0             ABSOLUTE ADDRESS                       32804861
         LA    RB,2(,RB)         POINT TO NEXT ENTRY                    32822761
         SLR   R2,R2             CLEAR REGISTER                         32840661
         IC    R2,1(,R9)         GET NEXT TEXT ENTRY                    32858561
         TM    0(R9),X'02'       IS THIS A VARIABLE EBCIDIC             32876461
         BO    QRMSGW50          YES,                                   32894361
QRMSGW40 LA    R3,2(,R3)         ADVANCE OFFSET POINTER                 32930161
         EX    R0,QRMSGUNP       UNPAK DATA                             32948061
         EX    R0,QRMSGTR        TRANSLATE TO PRINTABLE                 32965961
         LA    R1,1(,R1)         POINT TO NEXT BYTE                     32983861
         LA    R5,0(R3,R7)       UPDATE TARGET POINTER                  33001761
         BCT   R2,QRMSGW40       PROCESS NEXT BYTE                      33019661
         MVI   0(R5),C' '        BLANK UNUSED POSITION                  33037561
         LA    R5,1(,R5)         ADJUST TARGET POINTER                  33055461
         LA    R3,1(,R3)         ADJUST OFFSET POINTER                  33073361
         B     QRMSGW70          JOIN MAIN LINE                         33091261
QRMSGW50 DS    0H                EBCDIC DATA PROCESSING                 33109161
         LA    R3,0(R2,R3)       UPDATE OFFSET POINTER                  33127061
         BCTR  R2,0              ADJUST FOR EXECUTE                     33144961
         EX    R2,QRMSGMVC       MOVE DATA TO TARGET                    33162861
         LA    R5,0(R3,R7)       UPDATE TARGET POINTER                  33180761
         MVI   0(R5),C' '        ADD A BLANK                            33198661
         LA    R3,1(,R3)         ACCOUNT FOR THIS BLANK                 33216561
         B     QRMSGW70          JOIN MAIN LINE                         33234461
QRMSGW60 DS    0H                MOVE TEXT PROCESSING                   33252361
         SLR   R1,R1             CLEAR REGISTER                         33270261
         IC    R1,1(,R9)                                                33288161
         SLR   R2,R2             CLEAR REGISTER                         33306061
         IC    R2,QRMSGTTB(R1)   LENGTH OF TEXT                         33323961
         L     R1,QRMSGTTB(R1)   GET TEXT ADDRESS                       33341861
         ICM   R1,8,=A(0)        CLEAR LENGTH FIELD                     33359761
         LA    R1,QRMSGTTB(R1)   CLEAR HIGH-ORDER BYTE                  33377661
         AR    R3,R2             ADD LENGTH TO OFFSET POINTER           33395561
         BCTR  R2,0              ADJUST FOR EXECUTE                     33413461
         EX    R2,QRMSGMVC       MOVE TEXT TO TARGET                    33431361
QRMSGW70 DS    0H                                                       33449261
         TM    0(R9),X'80'       WAS THIS THE LAST ENTRY                33467161
         BNO   QRMSGW30          NO,                                    33485061
         USING CCWADR,R8         CCW BASE POINTER                       33520861
         OI    CCWCHAIN,CC       ENSURE COMMAND CHAINING                33538761
         STH   R3,CCWLNG         STORE MESSAGE LENGTH                   33556661
         STCM  R7,7,CCWBUF+1     SAVE BUFFER ADDRESS                    33574561
         TM    0(R6),X'80'       WAS THIS THE LAST LINE                 33592461
         BNO   QRMSGW10          NO, PROCESS NEXT LINE                  33610361
         NI    CCWCHAIN,X'FF'-CC RESET COMMAND CHAINING                 33646161
         XC    QRSNSDAT,QRSNSDAT PURGE OLD SENSE DATA                   33664061
         LA    R1,QRMSGCCB       CCB ADDRESS                            33681961
         SYSIO (1)               ISSUE SYSLOG I/O                       33699861
QRMSGWEX DS    0H                RETURN                                         
         LM    R0,RF,0(RD)       RESTORE USERS REGISTERS                        
         MVI   16*4(RD),X'00'    INDICATE SAVE AREA FREE                        
         B     4(,RE)            RETURN TO CALLER                               
*                                                                       33753561
QRMSGUNP UNPK  0(3,R5),0(2,R1)   UNPACK HEX DATA                        33771461
QRMSGTR  TR    0(2,R5),QRMSGTRT-QRMSGWRT-X'F0'(RC)                      33789361
QRMSGMVC MVC   0(0,R5),0(R1)     MOVE TEXT BYTES                        33807261
QRMSGTRT DC    C'0123456789ABCDEF' TRANSLATE TABLE                      33825161
*                                                                       33843061
*                                                                       33860961
*   THE MESSAGE TABLE FOR OPERATOR MESSAGES.                            33878861
*                                                                       33896761
QRMSGTAB DS     0F                                                      33914661
QRX1H60I EQU   *                      INTERNAL ERROR                    33932561
         DC    AL4(X'80000000'+QRT60I00)                                33950461
QRX1H40I EQU   *                      REQUEST RECEIVED MSG              33968361
         DC    AL4(X'00000000'+QRT40I00)                                33986261
         DC    AL4(X'80000000'+QRT40I10)                                34004161
QRX1H42I EQU   *                      DEVICE OWNED BY                   34022061
         DC    AL4(X'80000000'+QRT42I00)                                34039961
QRX1H43I EQU   *                      DEVICE NOT QUIESCED               34057861
         DC    AL4(X'80000000'+QRT43I00)                                34075761
QRX1H48I EQU   *                      LAST PATH NOT QUIESCED            34093661
         DC    AL4(X'80000000'+QRT48I00)                                34111561
QRX1H47I EQU   *                      PATH IS QUIESCED                  34129461
         DC    AL4(X'80000000'+QRT47I00)                                34147361
QRX1H49I EQU   *                      PATH TO CUU RESUMED               34165261
         DC    AL4(X'80000000'+QRT49I00)                                34183161
QRX1H44I EQU   *                      DEVICE RESUMED                    34201061
         DC    AL4(X'80000000'+QRT44I00)                                34218961
QRX1H51I EQU   *                      PATH TO CUU RESUMED BUT..         34236861
         DC    AL4(X'80000000'+QRT51I00)                                34254761
QRX1H46I EQU   *                      DEVICE RESUMED BUT..              34272661
         DC    AL4(X'80000000'+QRT46I00)                                34290561
QRX1H59I EQU   *                      INSUFFICIENT GETVIS               34308461
         DC    AL4(X'80000000'+QRT59I00)                                34326361
QRX1H56I EQU   *                      ERROR READING ATTN MSG            34344261
         DC    AL4(X'00000000'+QRT56I00)                                34362161
         DC    AL4(X'80000000'+QRTDTI00)                                34380061
QRX1H53I EQU   *                      RECONF.RECORD FORMAT UNKN         34397961
         DC    AL4(X'80000000'+QRT53I00)                                34415861
QRX1H55I EQU   *                      QRES RESPONSE REJECTED            34433761
         DC    AL4(X'00000000'+QRT55I00)                                34451661
         DC    AL4(X'80000000'+QRTDTI00)                                34469561
QRX1H54I EQU   *                      QRES REQUEST FINISHED             34487461
         DC    AL4(X'80000000'+QRT54I00)                                34505361
QRX1H52I EQU   *                      SYSTEM ID SENT                    34523261
         DC    AL4(X'80000000'+QRT52I00)                                34541161
QRX1H41I EQU   *                      DEVICE IS QUIESCED                34559061
         DC    AL4(X'80000000'+QRT41I00)                                34576961
QRX1H57I EQU   *                      ATTN. ON IMPOSSIBLE PATH          34594861
         DC    AL4(X'00000000'+QRT57I00)                                34612761
         DC    AL4(X'80000000'+QRT57I10)                                34630661
QRX1H58I EQU   *                      INVALID CDR FOUND                 34648561
         DC    AL4(X'00000000'+QRT58I00)                                34666461
         DC    AL4(X'80000000'+QRT58I10)                                34684361
QRX1H50I EQU   *                      PATH ALREADY RESUMED              34702261
         DC    AL4(X'80000000'+QRT50I00)                                34720161
QRX1H45I EQU   *                      CUU  ALREADY RESUMED              34738061
         DC    AL4(X'80000000'+QRT45I00)                                34755961
*                                                                       34773861
*    THE LINES FOR PRINTING:                                            34791761
*                                                                       34809661
QRT60I00 DC    AL1(X'00',QR@1H40I)        0H60I                         34827561
         DC    AL1(X'00',QR@INTER)        INTERNAL                      34845461
         DC    AL1(X'00',QR@PROCE)        PROCESSING                    34863361
         DC    AL1(X'00',QR@ERROR)        ERROR                         34881261
         DC    AL1(X'81',1)               ..                            34899161
*                                                                       34917061
QRT40I00 DC    AL1(X'00',QR@1H40I)        0H40I                         34934961
         DC    AL1(X'00',QR@QRES#)        QRES                          34952861
         DC    AL1(X'00',QR@REQ##)        REQ.                          34970761
         DC    AL1(X'00',QR@RECEI)        RECEIVED                      34988661
         DC    AL1(X'00',QR@FROM#)        FROM                          35006561
         DC    AL1(X'00',QR@CUU##)        CUU                           35024461
         DC    AL1(X'01',2)               ....                          35042361
         DC    AL1(X'00',QR@PATH#)        PATH                          35060261
         DC    AL1(X'81',1)               ..                            35078161
QRT40I10 DC    AL1(X'00',QR@BLANK)                                      35096061
         DC    AL1(X'00',QR@DATA#)        DATA                          35113961
         DC    AL1(X'81',L'QRIRRR)        ...                           35131861
*                                                                       35149761
QRT42I00 DC    AL1(X'00',QR@1H40I)        0H42I                         35167661
         DC    AL1(X'00',QR@CUU##)        CUU                           35185561
         DC    AL1(X'01',2)               ....                          35203461
         DC    AL1(X'00',QR@IS###)        IS                            35221361
         DC    AL1(X'00',QR@NOT##)        NOT                           35239261
         DC    AL1(X'80',QR@QUIES)        QUIESCED                      35257161
*                                                                       35275061
QRT43I00 DC    AL1(X'00',QR@1H40I)        0H43I                         35292961
         DC    AL1(X'02',2)               ..                            35310861
         DC    AL1(X'00',QR@IS###)        IS                            35328761
         DC    AL1(X'00',QR@OWNER)        OWNER                         35346661
         DC    AL1(X'00',QR@OF###)        OF                            35364561
         DC    AL1(X'00',QR@CUU##)        CUU                           35382461
         DC    AL1(X'81',2)               ....                          35400361
*                                                                       35418261
QRT48I00 DC    AL1(X'00',QR@1H40I)        0H48I                         35436161
         DC    AL1(X'00',QR@PATH#)        PATH                          35454061
         DC    AL1(X'01',1)               ..                            35471961
         DC    AL1(X'00',QR@TO###)        TO                            35489861
         DC    AL1(X'00',QR@CUU##)        CUU                           35507761
         DC    AL1(X'01',2)               ...                           35525661
         DC    AL1(X'00',QR@NOT##)        NOT                           35543561
         DC    AL1(X'00',QR@QUIES)        QUIESCED                      35561461
         DC    AL1(X'00',QR@ISLAS)        IS LAST                       35579361
         DC    AL1(X'00',QR@PATH#)        PATH                          35597261
         DC    AL1(X'00',QR@TO###)        TO                            35615161
         DC    AL1(X'80',QR@CUU##)        CUU                           35633061
*                                                                       35650961
QRT47I00 DC    AL1(X'00',QR@1H40I)        0H47I                         35668861
         DC    AL1(X'00',QR@QUIES)        QUIESCED                      35686761
         DC    AL1(X'40',QRT49I01-*)      ->                            35704661
*                                                                       35722561
QRT49I00 DC    AL1(X'00',QR@1H40I)        0H49I                         35740461
         DC    AL1(X'00',QR@RESUM)        RESUMED                       35758361
QRT49I01 DC    AL1(X'00',QR@PATH#)        PATH                          35776261
         DC    AL1(X'01',1)               ..                            35794161
         DC    AL1(X'00',QR@TO###)        TO                            35812061
         DC    AL1(X'00',QR@CUU##)        CUU                           35829961
         DC    AL1(X'81',2)               ...                           35847861
*                                                                       35865761
QRT44I00 DC    AL1(X'00',QR@1H40I)        0H44I                         35883661
         DC    AL1(X'00',QR@CUU##)        CUU                           35901561
         DC    AL1(X'01',2)               ...                           35919461
         DC    AL1(X'80',QR@RESUM)        RESUMED                       35937361
*                                                                       35955261
QRT51I00 DC    AL1(X'00',QR@1H40I)        0H51I                         35973161
         DC    AL1(X'00',QR@PATH#)        PATH                          35991061
         DC    AL1(X'01',1)               ..                            36008961
         DC    AL1(X'00',QR@TO###)        TO                            36026861
         DC    AL1(X'00',QR@CUU##)        CUU                           36044761
         DC    AL1(X'01',2)               ...                           36062661
         DC    AL1(X'00',QR@RESUM)        RESUMED                       36080561
         DC    AL1(X'80',QR@BUTST)        BUT STILL OFFLINE             36098461
*                                                                       36116361
QRT46I00 DC    AL1(X'00',QR@1H40I)        0H46I                         36134261
         DC    AL1(X'00',QR@CUU##)        CUU                           36152161
         DC    AL1(X'01',2)               ...                           36170061
         DC    AL1(X'00',QR@RESUM)        RESUMED                       36187961
         DC    AL1(X'80',QR@BUTST)        BUT STILL OFFLINE             36205861
*                                                                       36223761
QRT59I00 DC    AL1(X'00',QR@1H40I)        0H59I                         36241661
         DC    AL1(X'00',QR@QRES#)        QRES                 @D61NDMK 36259500
         DC    AL1(X'00',QR@PROCE)        PROCESSING                    36277461
         DC    AL1(X'00',QR@STOPP)        STOPPED                       36295361
         DC    AL1(X'80',QR@INSUF)        INSUFFICIENT GETVIS           36313261
*                                                                       36331161
QRT56I00 DC    AL1(X'00',QR@1H40I)        0H56I                         36349061
         DC    AL1(X'00',QR@ERROR)        ERROR                         36366961
         DC    AL1(X'00',QR@READI)        READING                       36384861
         DC    AL1(X'00',QR@ATTEN)        ATTENTION                     36402761
         DC    AL1(X'80',QR@MESSA)        MESSAGE                       36420661
*                                                                       36438561
QRT53I00 DC    AL1(X'00',QR@1H40I)        0H53I                         36456461
         DC    AL1(X'00',QR@QRES#)        QRES                          36474361
         DC    AL1(X'00',QR@REQ##)        REQ.                          36492261
         DC    AL1(X'00',QR@FORMA)        FORMAT                        36510161
         DC    AL1(X'80',QR@UNKNO)        UNKNOWN                       36528061
*                                                                       36545961
QRT55I00 DC    AL1(X'00',QR@1H40I)        0H55I                         36563861
         DC    AL1(X'00',QR@QRES#)        QRES                          36581761
         DC    AL1(X'00',QR@RESPO)        RESPONSE                      36599661
         DC    AL1(X'01',L'QRRRANSP)      ....                          36617561
         DC    AL1(X'80',QR@REJEC)        REJECT                        36635461
*                                                                       36653361
QRT54I00 DC    AL1(X'00',QR@1H40I)        0H54I                         36671261
         DC    AL1(X'00',QR@QRES#)        QRES                          36689161
         DC    AL1(X'00',QR@RESPO)        RESPONSE                      36707061
         DC    AL1(X'01',L'QRRRANSP)      ....  (ANSWER)                36724961
         DC    AL1(X'40',QRT52I01-*)      ->                            36742861
*                                                                       36760761
QRT52I00 DC    AL1(X'00',QR@1H40I)        0H52I                         36778661
         DC    AL1(X'00',QR@SYSID)        SYSID                         36796561
         DC    AL1(X'01',L'QRRRSYID)      ........                      36814461
QRT52I01 DC    AL1(X'00',QR@SENT#)        SENT                          36832361
         DC    AL1(X'00',QR@TO###)        TO                            36850261
         DC    AL1(X'00',QR@CUU##)        CUU                           36868161
         DC    AL1(X'01',2)               ....                          36886061
         DC    AL1(X'00',QR@PATH#)        PATH                          36903961
         DC    AL1(X'81',1)               ..                            36921861
*                                                                       36939761
QRT41I00 DC    AL1(X'00',QR@1H40I)        0H41I                         36957661
         DC    AL1(X'00',QR@CUU##)        CUU                           36975561
         DC    AL1(X'01',2)               ...                           36993461
         DC    AL1(X'80',QR@QUIES)        QUIESCED                      37011361
*                                                                       37029261
QRT57I00 DC    AL1(X'00',QR@1H40I)        0H57I                         37047161
         DC    AL1(X'00',QR@ATTEN)        ATTENTION                     37065061
         DC    AL1(X'00',QR@RECEI)        RECEIVED                      37082961
         DC    AL1(X'00',QR@FROM#)        FROM                          37100861
         DC    AL1(X'00',QR@DISAB)        DISABLED                      37118761
         DC    AL1(X'40',QRT58I01-*)      ->                            37136661
QRT57I10 DC    AL1(X'00',QR@BLANK)                                      37154561
         DC    AL1(X'00',QR@DATA#)        DATA                          37172461
         DC    AL1(X'01',1)               PBXNOERP                      37190361
         DC    AL1(X'01',1)               PBXNOPVF                      37208261
         DC    AL1(X'01',1)               PBXNOOPR                      37226161
         DC    AL1(X'01',1)               PBXNOQSD                      37244061
         DC    AL1(X'01',1)               PBXPIM                        37261961
         DC    AL1(X'01',1)               PBXPAM                        37279861
         DC    AL1(X'01',1)               PBXLPM                        37297761
         DC    AL1(X'81',8)               PBXCHPID                      37315661
*                                                                       37333561
QRT58I00 DC    AL1(X'00',QR@1H40I)        0H57I                         37351461
         DC    AL1(X'00',QR@INVAL)        INVALID                       37369361
         DC    AL1(X'00',QR@CONFI)        CONFIG.DATA RECORD            37387261
         DC    AL1(X'00',QR@FROM#)        FROM                          37405161
QRT58I01 DC    AL1(X'00',QR@PATH#)        PATH                          37423061
         DC    AL1(X'00',QR@MASK#)        MASK                          37440961
         DC    AL1(X'01',1)               ..                            37458861
         DC    AL1(X'00',QR@CUU##)        CUU                           37476761
         DC    AL1(X'81',2)               ....                          37494661
QRT58I10 DC    AL1(X'00',QR@BLANK)                                      37512561
         DC    AL1(X'00',QR@DATA#)        DATA                          37530461
         DC    AL1(X'01',1)               QRERRIND                      37548361
         DC    AL1(X'01',4)               RB                            37566261
         DC    AL1(X'81',24)              RECORD OFF RB                 37584161
*                                                                       37602061
QRT45I00 DC    AL1(X'00',QR@1H40I)        0H45I                         37619961
         DC    AL1(X'40',QRT50I01-*)      ->                            37637861
*                                                                       37655761
QRT50I00 DC    AL1(X'00',QR@1H40I)        0H50I                         37673661
         DC    AL1(X'00',QR@PATH#)        PATH                          37691561
         DC    AL1(X'01',1)               ..                            37709461
         DC    AL1(X'00',QR@TO###)        TO                            37727361
QRT50I01 DC    AL1(X'00',QR@CUU##)        CUU                           37745261
         DC    AL1(X'01',2)               ..                            37763161
         DC    AL1(X'00',QR@IS###)        IS                            37781061
         DC    AL1(X'00',QR@RESUM)        RESUMED                       37798961
         DC    AL1(X'80',QR@ALREA)        ALREADY                       37816861
*                                                                       37834761
QRTDTI00 DC    AL1(X'00',QR@BLANK)        BLANK                         37852661
         DC    AL1(X'00',QR@DATA#)        DATA                          37870561
         DC    AL1(X'01',2)               .. CUU                        37888461
         DC    AL1(X'01',1)               .. CHPID                      37906361
         DC    AL1(X'01',9)               .. CCB (1ST PART)             37924261
         DC    AL1(X'01',L'QRSNSDT0)      .. SENSEDATA 0-8              37942161
         DC    AL1(X'81',L'QRSNSDT1)      .. SENSEDATA 22-27            37960061
*                                                                       37977961
*                                                                       37995861
*   AND THE TABLE FOR THE TEXTS OF THE PARTS                            38013761
*                                                                       38031661
QRMSGTTB DS    0F                                                       38049561
QR@1H40I EQU   *-QRMSGTTB                                               38067461
         DC    AL1(L'QRMTXT00),AL3(QRMTXT00-QRMSGTTB)                   38085361
QR@BLANK EQU   *-QRMSGTTB                                               38103261
         DC    AL1(L'QRMTXT01),AL3(QRMTXT01-QRMSGTTB)                   38121161
QR@QRES# EQU   *-QRMSGTTB                                               38139061
         DC    AL1(L'QRMTXT02),AL3(QRMTXT02-QRMSGTTB)                   38156961
QR@PROCE EQU   *-QRMSGTTB                                               38174861
         DC    AL1(L'QRMTXT03),AL3(QRMTXT03-QRMSGTTB)                   38192761
QR@ERROR EQU   *-QRMSGTTB                                               38210661
         DC    AL1(L'QRMTXT04),AL3(QRMTXT04-QRMSGTTB)                   38228561
QR@REQ## EQU   *-QRMSGTTB                                               38246461
         DC    AL1(L'QRMTXT05),AL3(QRMTXT05-QRMSGTTB)                   38264361
QR@FROM# EQU   *-QRMSGTTB                                               38282261
         DC    AL1(L'QRMTXT06),AL3(QRMTXT06-QRMSGTTB)                   38300161
QR@CUU## EQU   *-QRMSGTTB                                               38318061
         DC    AL1(L'QRMTXT07),AL3(QRMTXT07-QRMSGTTB)                   38335961
QR@PATH# EQU   *-QRMSGTTB                                               38353861
         DC    AL1(L'QRMTXT08),AL3(QRMTXT08-QRMSGTTB)                   38371761
QR@NOT## EQU   *-QRMSGTTB                                               38389661
         DC    AL1(L'QRMTXT09),AL3(QRMTXT09-QRMSGTTB)                   38407561
QR@QUIES EQU   *-QRMSGTTB                                               38425461
         DC    AL1(L'QRMTXT0A),AL3(QRMTXT0A-QRMSGTTB)                   38443361
QR@OF### EQU   *-QRMSGTTB                                               38461261
         DC    AL1(L'QRMTXT0B),AL3(QRMTXT0B-QRMSGTTB)                   38479161
QR@TO### EQU   *-QRMSGTTB                                               38497061
         DC    AL1(L'QRMTXT0C),AL3(QRMTXT0C-QRMSGTTB)                   38514961
QR@ISLAS EQU   *-QRMSGTTB                                               38532861
         DC    AL1(L'QRMTXT0D),AL3(QRMTXT0D-QRMSGTTB)                   38550761
QR@RESUM EQU   *-QRMSGTTB                                               38568661
         DC    AL1(L'QRMTXT0E),AL3(QRMTXT0E-QRMSGTTB)                   38586561
QR@BUTST EQU   *-QRMSGTTB                                               38604461
         DC    AL1(L'QRMTXT0F),AL3(QRMTXT0F-QRMSGTTB)                   38622361
QR@INTER EQU   *-QRMSGTTB                                               38640261
         DC    AL1(L'QRMTXT10),AL3(QRMTXT10-QRMSGTTB)                   38658161
QR@INSUF EQU   *-QRMSGTTB                                               38676061
         DC    AL1(L'QRMTXT11),AL3(QRMTXT11-QRMSGTTB)                   38693961
QR@FORMA EQU   *-QRMSGTTB                                               38711861
         DC    AL1(L'QRMTXT12),AL3(QRMTXT12-QRMSGTTB)                   38729761
QR@UNKNO EQU   *-QRMSGTTB                                               38747661
         DC    AL1(L'QRMTXT13),AL3(QRMTXT13-QRMSGTTB)                   38765561
QR@RESPO EQU   *-QRMSGTTB                                               38783461
         DC    AL1(L'QRMTXT14),AL3(QRMTXT14-QRMSGTTB)                   38801361
QR@REJEC EQU   *-QRMSGTTB                                               38819261
         DC    AL1(L'QRMTXT15),AL3(QRMTXT15-QRMSGTTB)                   38837161
QR@OWNER EQU   *-QRMSGTTB                                               38855061
         DC    AL1(L'QRMTXT16),AL3(QRMTXT16-QRMSGTTB)                   38872961
QR@STOPP EQU   *-QRMSGTTB                                               38890861
         DC    AL1(L'QRMTXT17),AL3(QRMTXT17-QRMSGTTB)                   38908761
QR@READI EQU   *-QRMSGTTB                                               38926661
         DC    AL1(L'QRMTXT18),AL3(QRMTXT18-QRMSGTTB)                   38944561
QR@ATTEN EQU   *-QRMSGTTB                                               38962461
         DC    AL1(L'QRMTXT19),AL3(QRMTXT19-QRMSGTTB)                   38980361
QR@MESSA EQU   *-QRMSGTTB                                               38998261
         DC    AL1(L'QRMTXT1A),AL3(QRMTXT1A-QRMSGTTB)                   39016161
QR@SENT# EQU   *-QRMSGTTB                                               39034061
         DC    AL1(L'QRMTXT1B),AL3(QRMTXT1B-QRMSGTTB)                   39051961
QR@DATA# EQU   *-QRMSGTTB                                               39069861
         DC    AL1(L'QRMTXT1C),AL3(QRMTXT1C-QRMSGTTB)                   39087761
QR@OPENP EQU   *-QRMSGTTB                                               39105661
         DC    AL1(L'QRMTXT1D),AL3(QRMTXT1D-QRMSGTTB)                   39123561
QR@CLOSP EQU   *-QRMSGTTB                                               39141461
         DC    AL1(L'QRMTXT1E),AL3(QRMTXT1E-QRMSGTTB)                   39159361
QR@MASK# EQU   *-QRMSGTTB                                               39177261
         DC    AL1(L'QRMTXT1F),AL3(QRMTXT1F-QRMSGTTB)                   39195161
QR@DISAB EQU   *-QRMSGTTB                                               39213061
         DC    AL1(L'QRMTXT20),AL3(QRMTXT20-QRMSGTTB)                   39230961
QR@RECEI EQU   *-QRMSGTTB                                               39248861
         DC    AL1(L'QRMTXT21),AL3(QRMTXT21-QRMSGTTB)                   39266761
QR@INVAL EQU   *-QRMSGTTB                                               39284661
         DC    AL1(L'QRMTXT22),AL3(QRMTXT22-QRMSGTTB)                   39302561
QR@CONFI EQU   *-QRMSGTTB                                               39320461
         DC    AL1(L'QRMTXT23),AL3(QRMTXT23-QRMSGTTB)                   39338361
QR@SYSID EQU   *-QRMSGTTB                                               39356261
         DC    AL1(L'QRMTXT24),AL3(QRMTXT24-QRMSGTTB)                   39374161
QR@IS### EQU   *-QRMSGTTB                                               39392061
         DC    AL1(L'QRMTXT25),AL3(QRMTXT25-QRMSGTTB)                   39409961
QR@ALREA EQU   *-QRMSGTTB                                               39427861
         DC    AL1(L'QRMTXT26),AL3(QRMTXT26-QRMSGTTB)                   39445761
*                                                                       39463661
QRMSGNO  EQU   *+2             OVERWRITE XX OF MESSAGE NR.              39481561
QRMTXT00 DC    C'1HXXI '                                                39499461
QRMTXT01 DC    C'       '                                               39517361
QRMTXT02 DC    C'QRES '                                                 39535261
QRMTXT03 DC    C'PROCESSING '                                           39553161
QRMTXT04 DC    C'ERROR '                                                39571061
QRMTXT05 DC    C'REQUEST '                                     @D61NDMK 39588900
QRMTXT06 DC    C'FROM '                                                 39606861
QRMTXT07 DC    C'CUU '                                                  39624761
QRMTXT08 DC    C'PATH '                                                 39642661
QRMTXT09 DC    C'NOT '                                                  39660561
QRMTXT0A DC    C'QUIESCED '                                             39678461
QRMTXT0B DC    C'OF '                                                   39696361
QRMTXT0C DC    C'TO '                                                   39714261
QRMTXT0D DC    C', IS LAST '                                            39732161
QRMTXT0E DC    C'RESUMED '                                              39750061
QRMTXT0F DC    C', BUT STILL OFFLINE'                                   39767961
QRMTXT10 DC    C'INTERNAL '                                             39785861
QRMTXT11 DC    C', INSUFFICIENT GETVIS'                                 39803761
QRMTXT12 DC    C'FORMAT '                                               39821661
QRMTXT13 DC    C'UNKNOWN '                                              39839561
QRMTXT14 DC    C'RESPONSE '                                             39857461
QRMTXT15 DC    C'REJECT '                                               39875361
QRMTXT16 DC    C'OWNER '                                                39893261
QRMTXT17 DC    C'STOPPED '                                              39911161
QRMTXT18 DC    C'READING '                                              39929061
QRMTXT19 DC    C'ATTENTION '                                            39946961
QRMTXT1A DC    C'MESSAGE '                                              39964861
QRMTXT1B DC    C'SENT '                                                 39982761
QRMTXT1C DC    C'DATA '                                                 40000661
QRMTXT1D DC    C'('                                                     40018561
QRMTXT1E DC    C')'                                                     40036461
QRMTXT1F DC    C'MASK '                                                 40054361
QRMTXT20 DC    C'DISABLED '                                             40072261
QRMTXT21 DC    C'RECEIVED '                                             40090161
QRMTXT22 DC    C'INVALID '                                              40108061
QRMTXT23 DC    C'CONFIG.DATA RECORD '                                   40125961
QRMTXT24 DC    C'SYSTEM ID '                                            40143861
QRMTXT25 DC    C'IS '                                                   40161761
QRMTXT26 DC    C'ALREADY '                                              40179661
*                                                                       40215461
         DROP  ,                                                                
         LTORG                                                          40251261
*                                                                       40269161
***************************************************************         40287061
*  CODEBLOCK QRGETCDR                                         *         40304961
*  FUNCTION: GET A CONFIGURATION DATA RECORD FOR A GIVEN      *         40322861
*    PUBINDEX AND CHANNEL PATH ID. AN OUTPUT AREA CAN         *         40340761
*    BE PROVIDED FOR REUSE OR IT WILL BE GETVISED OTHERWISE   *         40358661
*                                                             *         40376561
*    THE CONFIGURATION DATA RECORD IS EITHER READ FROM A      *         40394461
*    A KEPT AREA IN SYSTEM GETVIS OR, IF IT IS NOT AVAILABLE  *         40412361
*    YET (EITHER BECAUSE THE AREA DOES NOT EXIST OR           *         40430261
*    BECAUSE THE CDR IS READ THE FIRST TIME) IT IS TAKEN      *         40448161
*    FROM THE DEVICE / CHPID COMBINATION BY ISSUEING IO.      *         40466061
*    IF THIS WAS SUCCESSFUL, AN ATTEMPT IS MADE TO INCLUDE    *         40483961
*    THE NEWLY GAINED CONFIGURATION DATA RECORD IN THE CDR    *         40501861
*    KEEP AREA IN SYSTEM GETVIS.                              *         40519761
*                                                             *         40537661
*  THE FORMAT OF THE CDR KEEP AREA IN SYSTEM GETVIS:          *         40555561
QRCDKPDS DSECT         KEEP AREA DSECT                                  40573461
QRCDKPLN DS    F       LENGTH OF KEEP AREA                              40591361
QRCDKPNX DS    A       PTR TO NEXT PART OF KEEP IN GETVIS OR            40609261
*                      ZERO IF NO SUCH AREA.                  *         40627161
QRCDKPNO DS    F       NO OF ENTRIES IN THIS KEEP AREA                  40645061
QRCDKPUN DS    A       PTR TO START OF UNUSED AREA IN KEEP              40662961
QRCDKPR1 EQU   *       START OF FIRST RECORD IN KEEP          *         40680861
*                                                             *         40698761
*  EACH ENTRY FOR A CDR CONSISTS OF THE FOLLOWING:            *         40716661
*                                                             *         40734561
QRCDKRPX DS    H       PUB INDEX OF DEVICE                    *         40752461
QRCDKRCP DS    X       CHPID TAKEN FOR READ OF CDR            *         40770361
         DS    X       RESERVED                                         40788261
QRCDKRHD EQU   *-QRCDKRPX   HEADER LENGTH                               40806161
QRCDKRRL DS    F       LENGTH OF COMPACTED CDR                *         40824061
QRCDKRDT EQU   *       START OF COMPACTED DATA                *         40841961
&SYSECT  CSECT                                                          40859861
*                                                             *         40877761
*  ON ENTRY: (STANDARD REGS SEE ABOVE)                        *         40895661
*    R3 = PUB INDEX OF DEVICE IN QUESTION                     *         40913561
*    R5 = PTR TO PUBX OF SAME DEVICE                          *         40931461
*    R2 = CHANNEL PATH ID MASK                                *         40949361
*    RB = ZERO OR POINTER TO AN AREA, THAT MUST CONTAIN       *         40967261
*         THE AREA LENGTH IN THE FIRST FULLWORD.              *         40985161
*  ON EXIT:                                                   *         41003061
*    RB = POINTS TO EITHER THE USER PROVIDED AREA OR, IF      *         41020961
*         RB HAD BEEN ZERO ON ENTRY TO AN AREA IN GETVIS      *         41038861
*         IN BOTH CASES IT CONTAINS:                          *         41056761
*         <1ST FULLWORD: LENGTH OF AREA>                      *         41074661
*         <REST: CONFIGURATION DATA RECORD>                   *         41092561
***************************************************************         41110461
         DC    C'QRGETCDR'                                                      
         USING *,RC              QRGETCDR BASE POINTER                          
         USING QRSGLOB,RA        GLOBAL AREA BASE POINTER                       
         USING PBXADR,R5         PUBX BASE POINTER                      41289461
QRGETCDR DS    0H                GET CONFIGURATION DATA RECORD                  
         BAL   R5,QRHARDWT       THATS THE END                                  
Q$NETCDR BE    *-4               SAVE AREA FULL CONDITION                       
         LA    RD,16*4(,RD)      UPDATE SAVE AREA POINTER                       
         ST    RE,RE*4(,RD)      SAVE RETURN REGISTER                           
         OI    QRST+09,X'80'     FOOTPRINT                                      
         CLI   PBXRCDCC,X'FF'    DOES DEVICE SUPPORT RCD COMMAND        41307361
         BNE   QRGC0010          YES,                                   41325261
         LM    R0,RF,0(RD)       RESTORE USERS REGISTERS                        
         MVI   16*4(RD),X'00'    INDICATE SAVE AREA FREE                        
         B     4(,RE)            RETURN TO CALLER                               
QRGC0010 DS    0H                ISSUE RCD COMMAND                      41361061
         OI    QRST+09,X'40'     FOOTPRINT                                      
         MVC   QRMSDEVN,PBXCUU   SAVE CUU ADDRESS                       41396861
         STC   R2,QRMSCPID       SAVE CHPID MASK                        41414761
         MVC   QRRCDCCW(1),PBXRCDCC GET RCD COMMAND CODE                41432661
         XR    R0,R0             CLEAR REGISTER                         41450561
         ICM   R0,3,PBXRCDLN     LENGTH OF RCD RECORD                   41468461
         STH   R0,QRRCDCCW+6     SAVE LENGTH IN RCD CCW                 41486361
         A     R0,=A(4)          ACCOUNT FOR AREA LENGTH                41504261
         LR    R6,R0             PRESERVE THIS VALUE                    41522161
         LTR   R1,RB             DID CALLER PROVIDE AREA                41557961
         BZ    QRGC0020          NO,                                    41575861
         C     R0,0(,RB)         IS AREA LARGE ENOUGH                   41593761
         BNH   QRGC0022          YES, IT IS                             41611661
         L     R0,0(RB)          GET AREA LENGTH                        41629561
         FREEVIS ADDRESS=(1),LENGTH=(0),SVA=YES  FREE THE AREA          41665361
         OI    QRST+09,X'20'     FOOTPRINT                                      
QRGC0020 DS    0H                GET THE REQUIRED STORAGE               41736961
         OI    QRST+09,X'10'     FOOTPRINT                                      
         GETVIS ADDRESS=(RB),LENGTH=(R6),LOC=ANY,SVA=YES,              X41772761
               PFIX=YES,SPID=QRSGVSPN GET THE REQUIRED AREA             41790661
         LTR   RF,RF             DID WE GET THE REQUIRED STORAGE        41826461
         BZ    QRGC0021          YES,                                   41844361
         OI    QRST+09,X'08'     FOOTPRINT                                      
         MVI   QRERRIND,QRERR#05 SET ERROR INDICATOR                            
         LM    R0,RF,0(RD)       RESTORE CALLERS REGISTERS                      
         MVI   16*4(RD),X'00'    INDICATE SAVE AREA FREE                        
         B     4(,RE)            RETURN TO CALLER (ERROR)                       
QRGC0021 ST    R6,0(,RB)         SAVE AREA LENGTH                       41915961
QRGC0022 DS    0H                                                       41933861
         ICM   R4,15,QRCDKEEP    DID WE KEEP AN AREA                    41951761
         BZ    QRGC1020          NO,                                    41969661
         OI    QRST+09,X'04'     FOOTPRINT                                      
         USING QRCDKPDS,R4       QRCDKPDS BASE POINTER                  42005461
         LA    R7,QRCDKPR1       ADDRESS OF 1ST RECORD                  42023361
         USING QRCDKPR1,R7       QRCDKPR1 BAASE POINTER                 42041261
         B     QRGC1023          JOIN MAIN PATH                         42059161
QRGC1022 CLM   R3,3,QRCDKRPX     IS THIS RECORD FOR OUR PUB             42077061
         BNE   QRGC1021          NO,                                    42094961
         CLM   R2,1,QRCDKRCP     IS IT FOR OUR CHPID                    42112861
         BE    QRGC1024          YES,                                   42130761
QRGC1021 A     R7,QRCDKRRL       POINT TO NEXT CD-RECORD                42148661
         LA    R7,QRCDKRHD(,R7)  ADD HEADER LENGTH                      42166561
QRGC1023 CL    R7,QRCDKPUN       DID WE REACH FREE POSITION             42184461
         BL    QRGC1022          NO, KEEP GOING                         42202361
         ICM   R4,15,QRCDKPNX    IS NEXT PART AVAILABLE                 42220261
         BZ    QRGC1020          NO,                                    42238161
         DROP  R7                RELEASE QRCDKPR1 BASE                  42256061
         LA    R7,QRCDKPR1       ADDRESS OF 1ST RECORD                  42273961
         B     QRGC1022          CHECK THIS ENTRY                       42291861
QRGC1024 DS    0H                ENTRY FOUND PROCESSING                 42309761
         OI    QRST+09,X'02'     FOOTPRINT                                      
         LA    R7,QRCDKRHD(,R7)  POINT TO DECOMPRESSION INFO            42345561
         CLI   16*4(RD),X'FF'    SET CONDITION CODE                             
         STM   R0,RF,16*4(RD)    SAVE REGISTERS                                 
         DC    0H'0',X'58C0',S(*+6) LOAD SUBROUTINE ADDRESS                     
         BAL   RE,4(,RC)         DECOMPACT THE RECORD                           
*SGLINK  QRDECOMP INPUT=(R7,RB)                                                 
         DC    AL4(QRDECOMP)     ADDRESS OF SUBROUTINE                          
         B     QRGC1020          ERROR IN DECOMPACTION                          
         OI    QRST+09,X'01'     FOOTPRINT                                      
         BALR  R5,0              PREPARE IN CASE OF HARD WAIT                   
         CLI   16*4(RD),X'FF'    IS SAVE AREA FREE                              
         STM   R0,RF,16*4(RD)    SAVE REGISTERS                                 
         DC    0H'0',X'58C0',S(*+6) LOAD SUBROUTINE ADDRESS                     
         BAL   RE,4(,RC)         TEST CONFIG. DATA RECORD                       
*SGLINK  QRTSTCDR INPUT=RB                                                      
         DC    AL4(QRTSTCDR)     SUBROUTINE ADDRESS                             
         B     QRHARDWT          ERROR RETURN                                   
         OI    QRST+10,X'80'     FOOTPRINT                                      
         ST    RB,RB*4(,RD)      STORE RECORD ADDRESS FOR CALLER                
         LM    R0,RF,0(RD)       RESTORE CALLERS REGISTERS                      
         MVI   16*4(RD),X'00'    INDICATE SAVE AREA FREE                        
         B     8(,RE)            RETURN TO CALLER                               
QRGC1020 DS    0H                ENDIF                                  42649861
         OI    QRST+10,X'40'     FOOTPRINT                                      
         DROP  R4                RELEASE QRCDKPDS BASE POINTER          42005461
         STC   R2,QRRCDCCB+8     STORE CHANNEL PATH MASK                42703561
         STH   R3,QRRCDCCB+CCBCLS-CCBADR SAVE PUB INDEX                 42721461
         OI    QRRCDCCB+CCBCLS-CCBADR,CCBPHYAD+XCPR SET BITS            42739361
         NI    QRRCDCCB+12,X'FF'-USENSE RESET USER WANTS SENSE          42748200
         XC    QRSNSDAT,QRSNSDAT PURGE SENSE DATA                       42757261
         LA    R1,QRRCDCCB       ADDRESS OF CCB                         42775161
QRGC1025 DS    0H                PHYSICAL I/O                           42793000
         SYSIO (1)               EXECUTE CHANNEL PROGRAM                42810961
         CLI   16*4(RD),X'FF'    SET CONDITION CODE                             
         STM   R0,RF,16*4(RD)    SAVE REGISTERS                                 
         DC    0H'0',X'58C0',S(*+6) LOAD SUBROUTINE BASE                        
         BAL   RE,4(,RC)         MAKE A TRACE ENTRY                             
         DC    AL4(QRSTRACE)     SUBROUTINE ADDRESS                             
         DC    AL4(3)            TRACE ENTRY TYPE-3                             
         TM    QRRCDCCB+CCBCOM1-CCBADR,DISERR ANY ERROR                         
         BNO   QRGC0030          NO,                                            
         TM    QRRCDCCB+12,USENSE DID WE ASK FOR SENSE                          
         BNZ   QRGC1027          YES,                                           
         OI    QRRCDCCB+12,USENSE INDICATE SENSE REQUIRED                       
         B     QRGC1025          TRY AGAIN                                      
QRGC1027 DS    0H                EXAMINE SENSE DATA                     42914900
         LM    R0,RF,0(RD)       RESTORE REGISTERS                              
         MVI   16*4(RD),X'00'    INDICATE SAVE AREA FREE                        
         B     4(,RE)            RETURN TO CALLER                               
QRGC0030 DS    0H                READ COMPLETED SUCCESSFULLY            42936261
         OI    QRST+10,X'20'     FOOTPRINT                                      
         LA    R4,4(,RB)         TARGET AREA ADDRESS                    42972061
         L     R5,0(,RB)         AREA LENGTH                            42989961
         LR    R0,R6             GET REQUIRED LENGTH                    43007861
         L     R6,QRRRRDAT       RRSD RECORD ADDRESS                    43025761
         L     R7,QRRRRDAL       GET RECORD LENGTH                      43043661
         MVCL  R4,R6             MOVE DATA TO AREA                      43061561
         LR    R6,R0             RELOAD ACTUAL LENGTH REG               43079461
         CLI   16*4(RD),X'FF'    SET CONDITION CODE                             
         STM   R0,RF,16*4(RD)    SAVE USERS REGISTER                            
         DC    0H'0',X'58C0',S(*+6) LOAD SUBROUTINE ADDRESS                     
         BAL   RE,4(,RC)         TEST CONFIGURATION RECORD                      
*SGLINK  QRTSTCDR INPUT=RB                                                      
         DC    AL4(QRTSTCDR)     ADDRESS OF SUBROUTINE                          
         B     QRGC2002          ERROR RETURN                                   
         B     QRGC2004          JOIN MAIN LINE                                 
QRGC2002 DS    0H                ERROR PROCESSING                       43186861
         OI    QRST+10,X'10'     FOOTPRINT                                      
         LM    R0,RF,0(RD)       RESTORE USERS REGISTERS                        
         MVI   16*4(RD),X'00'    INDICATE SAVE AREA FREE                        
         B     4(,RE)            RETURN TO CALLER                               
QRGC2004 DS    0H                *                                      43240561
         LA    R4,QRCDKEEP-(QRCDKPNX-QRCDKPDS)  POINT TO DUMMY          43258461
         USING QRCDKPDS,R4       QRCDKPDS BASE POINTER                  43276361
QRGC2000 DS    0H                                                       43294261
         OI    QRST+10,X'08'     FOOTPRINT                                      
         ICM   R7,15,QRCDKPNX    IS THERE ANOTHER RECORD                43330061
         BNZ   QRGC2020          YES,                                   43347961
         OI    QRST+10,X'04'     FOOTPRINT                                      
         L     R9,QRCDKNDX       GET GETVIS COUNT                       43383761
         LA    R9,1(,R9)         INCREASE GETVIS COUNT                  43401661
         ST    R9,QRCDKNDX       STORE NEW GETVIS COUNT                 43419561
         BCTR  R9,0              RESTORE OLD COUNT                      43437461
         LA    R0,7              GET COUNTER MASK                       43455361
         NR    R9,R0             RESET ALL BUT 3-BIT COUNTER            43473261
         IC    R9,QRGCTABL(R9)   GET SHIFT VALUE                        43491161
         LR    R0,R6             SAVE AREA LENGTH                       43509061
         SLL   R0,0(R9)          MULTIPLY                               43526961
         LR    R8,R0             SAVE GETVIS LENGTH                     43544861
         GETVIS ADDRESS=(1),LENGTH=(0),LOC=ANY,SVA=YES,                X43580661
               PFIX=YES,SPID=QRSGVSPN                                   43598561
         LTR   RF,RF             DID WE GET THE STORAGE                 43634361
         BZ    QRGC2010          YES,                                   43652261
         OI    QRST+10,X'02'     FOOTPRINT                                      
         ST    RB,RB*4(,RD)      SAVE RECORD ADDRESS FOR USER                   
         LM    R0,RF,0(RD)       RESTORE CALLERS REGISTER                       
         MVI   16*4(RD),X'00'    INDICATE SAVE AREA FREE                        
         B     8(,RE)            ERROR RETURN                                   
QRGC2010 DS    0H                INITIALIZE AREA                        43723861
         OI    QRST+10,X'01'     FOOTPRINT                                      
         ST    R8,0(,R1)         SAVE GETVIS LENGTH                     43759661
         ST    R1,QRCDKPNX       SAVE GETVIS ADDRESS                    43777561
         LR    R7,R1             NUMBER OF STORED CDRS                  43795461
         DROP  R4                RELEASE QRCDKPDS BASE                  43813361
         USING QRCDKPDS,R7       QRCDKPDS BASE POINTER                  43831261
         XC    QRCDKPNO,QRCDKPNO CLEAR ENTRY COUNTER                    43849161
         LA    R0,QRCDKRPX-QRCDKPDS(,R1) ADDRESS OF 1ST RECORD          43867061
         ST    R0,QRCDKPUN       SET UNUSED RECORD ADDRESS              43884961
         DROP  R7                RELEASE QRCDKPDS BASE                  43813361
QRGC2020 DS    0H                                                       43920761
         OI    QRST+11,X'80'     FOOTPRINT                                      
         LR    R4,R7             NEW KEEP RECORD BASE                   43956561
         USING QRCDKPDS,R4       QRCDKPDS BASE POINTER                  43831261
         L     R0,QRCDKPUN       GET UNUSED ADDRESS                     43992361
         SR    R0,R4             SUBTRACT START ADDRESS                 44010261
         LNR   R0,R0             PROVIDE FOR SUBTRACTION                44028161
         A     R0,QRCDKPLN       FREE AREA                              44046061
         CLR   R0,R6             DOES OUR RECORD FIT                    44063961
         BL    QRGC2100          NO,                                    44081861
         OI    QRST+11,X'40'     FOOTPRINT                                      
         LA    R7,QRCDKRHD       GET HEADER LENGTH                      44117661
         SR    R0,R7             SUBTRACT HEADER LENGTH                 44135561
         L     R7,QRCDKPUN       UNUSED ENTRY ADDRESS                   44153461
         USING QRCDKPR1,R7       QRCDKPR1 BASE POINTER                  44171361
         ST    R0,QRCDKRRL       SAVE COMPACTED LENGTH                  44189261
         LA    R7,QRCDKRHD(,R7)  SKIP HEADER BYTES                      44207161
         CLI   16*4(RD),X'FF'    SET CONDITION CODE                             
         STM   R0,RF,16*4(RD)    SAVE CALLERS REGISTERS                         
         DC    0H'0',X'58C0',S(*+6) LOAD SUBROUTINE BASE                        
         BAL   RE,4(,RC)         COMPRESS THE DATA                              
*SGLINK  QRCOMPRS INPUT=(R7,RB)                                                 
         DC    AL4(QRCOMPRS)     SUBROUTINE ADDRESS                             
         B     QRGC2000          ERROR RETURN                                   
         OI    QRST+11,X'20'     FOOTPRINT                                      
         L     R7,QRCDKPUN       GET NEXT UNUSED ADDRESS                44314561
         STH   R3,QRCDKRPX       SAVE PUB INDEX                         44332461
         STC   R2,QRCDKRCP       SAVE CHPID INFORMATION                 44350361
         L     R1,QRCDKRRL       GET COMPACTED LENGTH                   44368261
         A     R1,QRCDKPUN       ADD CURRENT UNUSED                     44386161
         LA    R1,QRCDKRHD(,R1)  ADD HEADER BYTES                       44404061
         ST    R1,QRCDKPUN       STORE NEW UNUSED POINTER               44421961
         B     QRGC2300          JOIN MAIN LINE                         44439861
QRGC2100 DS    0H                                                       44457761
         B     QRGC2000                                                 44475661
QRGC2300 DS    0H                                                       44493561
         OI    QRST+11,X'10'     FOOTPRINT                                      
         ST    RB,RB*4(,RD)      RETURN RECORD ADDRESS                          
         LM    R0,RF,0(RD)       RESTORE CALLERS REGISTERS                      
         MVI   16*4(RD),X'00'    INDICATE SAVE AREA FREE                        
         B     8(,RE)            RETURN TO CALLER                               
         DROP  ,                                                        44565161
         SPACE 1                                                        44583061
QRGCTABL DC    AL1(3,2,1,1,1,1,1,1)                                     44600961
         LTORG                                                          44618861
***************************************************************         44700061
*  CODEBLOCK QRDECOMP                                         *         44750061
*  FUNCTION: COMPACT OR DECOMPACT A SOURCE INTO A TARGET.     *         44800061
*    IN CASE OF COMPACTION, THE SOURCE IS COMPACTED IN        *         44850061
*    THE FOLLOWING FORMAT: THE COMPACTED FORMAT CONSISTS      *         44900061
*    OF STRINGS, STARTING WITH A FLAG BYTE. IF BIT 0 OF THE   *         44950061
*    FLAG BYTE IS ON BIT 1 TO 7 GIVES THE NUMBER OF           *         45000061
*    CONSECUTIVE EQUAL BYTE VALUES. THE NEXT BYTE IS THE      *         45050061
*    VALUE THAT ALL THESE BYTES HAD.                          *         45100061
*    IF BIT 0 IS OFF, BIT 1 TO 7 GIVE THE NUMBER OF UNCHANGED *         45150061
*    BYTES THAT FOLLOW, UNTIL THE NEXT FLAGBYTE IS REACHED.   *         45200061
*    EXAMPLE:                                                 *         45250061
*    X'030A0B0C850084FF' IS THE COMPACTED FORMAT OF           *         45300061
*    X'0A0B0C0000000000FFFFFFFF'                              *         45350061
*                                                             *         45400061
*    THERE SHOULD USUALLY BE MANY ZEROES IN THE CDR'S ..      *         45450061
*                                                             *         45500061
*  ON ENTRY: (STANDARD REGS SEE ABOVE)                        *         45550061
*    R7 = POINTER TO AN AREA OF THE FOLLOWING FORMAT:         *         45600061
*         <4BYTE LNG OF AREA><SOURCE FOR DECOMPACTION>        *         45650061
*    RB = POINTER TO AN AREA OF THE FOLLOWING FORMAT:         *         45700061
*         <4BYTE LNG OF AREA><TARGET FOR DECOMPACTION>        *         45750061
*  ON EXIT (TO RETURN+4)                                      *         45800061
*    NO REGISTERS CHANGED. JUST DATA DECOMPACTED IN TARGET    *         45850061
*    AREA.                                                    *         45900061
*  ON EXIT (TO RETURN+0)                                      *         45950061
*    NO REGISTERS CHANGED, BUT DECOMPACTED DATA DID NOT FIT   *         46000061
*    INTO TARGET AREA.                                        *         46050061
***************************************************************         46100061
         DC    C'QRDECOMP'                                                      
         USING *,RC              QRDECOMP BASE POINTER                          
         USING QRSGLOB,RA        GLOBAL AREA BASE                               
QRDECOMP DS    0H                DECOMPRESSION ROUTINE                          
         BAL   R5,QRHARDWT       THATS THE END                                  
Q$NECOMP BE    *-4               SAVE AREA FULL CONDITION                       
         LA    RD,16*4(,RD)      UPDATE SAVE AREA POINTER                       
         ST    RE,RE*4(,RD)      SAVE RETURN REGISTER                           
         OI    QRST+14,X'80'     FOOTPRINT                                      
         LA    R2,4(,R7)         START OF SOURCE ADDRESS                46667961
         LA    R4,4(,RB)         START OF TARGET ADDRESS                46707261
         L     R3,0(,R7)         LENGTH OF SOURCE STRING                46746561
         L     R5,0(,RB)         LENGTH OF TARGET STRING                46785861
         LA    R0,4              LOAD CONSTANT                          46825161
         SLR   R5,R0             ACCOUNT FOR LENGTH FIELD               46864461
         SLR   R3,R0             ACCOUNT FOR LENGTH FIELD               46903761
QRDEC000 DS    0H                LOOP                                   46943061
         OI    QRST+14,X'40'     FOOTPRINT                                      
         TM    0(R2),X'80'       IS THIS COMPACTED DATA                 47021661
         BO    QRDEC100          YES,                                   47060961
         OI    QRST+14,X'20'     FOOTPRINT                                      
         SLR   R6,R6             CLEAR REGISTER                         47139561
         IC    R6,0(,R2)         GET BYTE-TO-FOLLOW COUNT               47178861
         CR    R6,R5             DOES IT FIT IN TARGET FIELD            47218161
         BH    QRDEC300          NO,                                    47257461
         BCTR  R6,0              ADJUST FOR EXECUTE                     47296761
         EX    R6,QRDECMV1       MOVE DATA INTO TARGET FIELD            47336061
         LA    R6,1(,R6)         RESTORE LENGTH COUNT                   47375361
         LA    R2,1(R2,R6)       POINT TO NEXT FLAG BYTE                47414661
         LA    R4,0(R4,R6)       ADJUST TARGET POINTER                  47453961
         SLR   R3,R6             SUBTRACT FROM SOURCE LENGTH            47493261
         BCTR  R3,0              ACCOUNT FOR FLAG BYTE                  47532561
         SLR   R5,R6             REMAINING TARGET LENGTH                47571861
         B     QRDEC200          JOIN MAIN PATH                         47611161
QRDEC100 DS    0H                PROCESS COMPACTED DATA                 47650461
         OI    QRST+14,X'10'     SET FOOT PRINT                                 
         LA    R0,X'7F'          GET RESET MASK                         47729061
         IC    R6,0(,R2)         LENGTH OF COMPACTED DATA               47768361
         NR    R6,R0             TURN OFF FLAG BYTE                     47807661
         CR    R6,R5             DOES IT FIT IN TARGET FIELD            47846961
         BH    QRDEC300          NO,                                    47886261
         MVC   0(1,R4),1(R2)     MOVE REPLICATION VALUE                 47925561
         BCTR  R6,0              ACCOUNT FOR IT                         47964861
         BCTR  R6,0              ADJUST FOR EXECUTE                     48004161
         EX    R6,QRDECMV2       REPLICATE DATA TO TARGET               48043461
         LA    R6,2(,R6)         RESTORE LENGTH COUNT                   48082761
         LA    R2,2(,R2)         UPDATE SOURCE POINTER                  48122061
         LA    R4,0(R4,R6)       UPDATE TARGET POINTER                  48161361
         SLR   R5,R6             REMAINING TARGET LENGTH                48200661
         BCTR  R3,0              ACCOUNT FOR FLAG BYTE                  48279261
         BCTR  R3,0              ACCOUNT FOR REPLICATION BYTE           48239961
QRDEC200 DS    0H                MAIN PATH PROCESSING                   48318561
         LTR   R3,R3             MORE BYTES TO PROCESS                  48357861
         BNP   QRDEC310          NO,                                    48397161
         B     QRDEC000          PROCESS NEXT FIELD                     48436461
QRDEC300 DS    0H                DATA WOULD NOT FIT IN TARGET           48475761
         OI    QRST+14,X'08'     LEAVE FOOT PRINT                               
         LM    R0,RF,0(RD)       RESTORE REGISTERS                              
         MVI   16*4(RD),X'00'    FREE SAVE AREA                                 
         B     4(,RE)            RETURN TO CALLER                               
QRDEC310 DS    0H                DATA DID NOT FIT IN TARGET                     
         OI    QRST+14,X'04'     SET FOOT PRINT                                 
         LM    R0,RF,0(RD)       RESTORE REGISTERS                              
         MVI   16*4(RD),X'00'    FREE SAVE AREA                                 
         B     8(,RE)            RETURN TO CALLER (ERROR)                       
         DROP  ,                                                        48711561
         SPACE 1                                                        48750861
QRDECMV1 MVC   0(0,R4),1(R2)     MOVE SOURCE DATA TO TARGET             48790161
QRDECMV2 MVC   1(0,R4),0(R4)     REPLICATE THE DATA TO TARGET           48829461
         LTORG ,                                                        48868761
*                                                                       48908061
***************************************************************         48950061
*  CODEBLOCK QRCOMPRS                                         *         49000061
*  FUNCTION: COMPRESS DATA INTO A FORMAT THAT CAN BE          *         49050061
*    DECOMPACTED BY QRDECOMP. FOR DESCRIPTION SEE ABOVE       *         49100061
*                                                             *         49150061
*  ON ENTRY: (STANDARD REGS SEE ABOVE)                        *         49200061
*    R7 = POINTER TO AN AREA OF THE FOLLOWING FORMAT:         *         49250061
*         <4BYTE LNG OF AREA><TARGET FOR COMPACTION>          *         49300061
*    RB = POINTER TO AN AREA OF THE FOLLOWING FORMAT:         *         49350061
*         <4BYTE LNG OF AREA><SOURCE FOR COMPACTION>          *         49400061
*  ON EXIT (TO RETURN+4)                                      *         49450061
*    NO REGISTERS CHANGED. JUST DATA COMPACTED IN TARGET      *         49500061
*    AREA AND LENGTH OF AREA ADJUSTED TO REALLY USED          *         49550061
*    AMOUNT.                                                  *         49600061
*  ON EXIT (TO RETURN+0)                                      *         49650061
*    NO REGISTERS CHANGED, BUT COMPACTED   DATA DID NOT FIT   *         49700061
*    INTO TARGET AREA.                                        *         49750061
***************************************************************         49800061
         DC    C'QRCOMPRS'                                                      
QRCOMPRS DS    0H                QRCOMPRS BASE POINTER                          
         USING QRSGLOB,RA        GLOBAL AREA BASE                               
         USING *,RC              COMPRESSION ROUTINE BASE                       
         BAL   R5,QRHARDWT       THATS THE END                                  
Q$NOMPRS BE    *-4               SAVE AREA FULL CONDITION                       
         LA    RD,16*4(,RD)      UPDATE SAVE AREA POINTER                       
         ST    RE,RE*4(,RD)      SAVE RETURN REGISTER                           
         OI    QRST+12,X'80'     FOOTPRINT                                      
         LA    R2,4(,RB)         POINT TO START OF SOURCE               50377561
         LA    R4,4(,R7)         POINT TO START OF TARGET               50420061
         L     R3,0(,RB)         LENGTH OF SOURCE STRING                50462561
         L     R5,0(,R7)         LENGTH OF TARGET FIELD                 50505061
         LA    R0,4              LOAD CONSTANT                          50547561
         SLR   R3,R0             REAL SOURCE LENGTH                     50590061
         SLR   R5,R0             REAL TARGET LENGTH                     50632561
         LTR   R3,R3             SOURCE LENGTH VALID                    50675061
         BNP   QRCOMERX          NO,                                    50717561
QRCOM000 DS    0H                COMPRESS SOURCE DATA                   50760061
         OI    QRST+12,X'40'     LEAVE FOOT PRINT                               
         LA    R1,1              INITIALISE BYTE COUNTER                50845061
         LA    R0,127            SET BYTE COUNT LIMIT                   50887561
         LR    R6,R2             COPY SOURCE ADDRESS                    50930061
QRCOM010 DS    0H                                                       50972561
         OI    QRST+12,X'20'     LEAVE FOOT PRINT                               
         CR    R1,R3             STILL WITHIN SOURCE FIELD              51057561
         BNL   QRCOM300          NO,                                    51100061
         CLC   0(1,R6),1(R6)     NEXT VALUE SAME AS CURRENT             51142561
         BE    QRCOM020          YES, DO COMPRESSION                    51185061
         CR    R1,R0             MORE THAN MAXIMUM                      51227561
         BNL   QRCOM300          YES,                                   51270061
         LA    R1,1(,R1)         UPDATE BYTE COUNTER                    51312561
         LA    R6,1(,R6)         INCREASE SOURCE ADDRESS                51355061
         B     QRCOM010          PROCESS NEXT BYTE                      51397561
QRCOM020 DS    0H                REPLICATED DATA FOUND                  51440061
         OI    QRST+12,X'10'     SET FOOT PRINT                                 
         C     R1,=A(1)          DID WE START A NEW STRING              51567561
         BE    QRCOM030          YES,                                   51610061
         OI    QRST+12,X'08'     SET FOOT PRINT                                 
         CR    R1,R5             GREATER THAN TARGET FIELD              51695061
         BH    QRCOMERX          YES,                                   51737561
         BCTR  R1,0              LENGTH OF PREVIOUS STRING              51780061
         STC   R1,0(,R4)         SAVE LENTH IN FLAG BYTE                51822561
         BCTR  R1,0              ADJUST FOR EXECUTE                     51865061
         EX    R1,QRCOMEM1       MOVE PREVIOUS STRING TO TARGET         51907561
         LA    R1,1(,R1)         RESTORE BYTE COUNT                     51950061
         LA    R4,1(R4,R1)       NEXT TARGET ADDRESS                    51992561
         LA    R2,0(R2,R1)       NEXT SOURCE ADDRESS                    52035061
         SR    R3,R1             REMAINING SOURCE LENGTH                52077561
         SR    R5,R1             REMAINING TARGET LENGTH                52120061
         BCTR  R5,0              ACCOUNT FOR FLAG BYTE                  52162561
QRCOM030 DS    0H                REPLICATED DATA FOUND PROCESS          52205061
         LA    R1,2              WE ONLY NEED 2 BYTES                   52247561
         LA    R6,1(,R2)         ADVANCE SOURCE POINTER                 52290061
         LA    R0,127            RESTORE BYTE COUNT LIMIT               52332561
QRCOM040 DS    0H                START A NEW REPLICATED STRING          52375061
         CR    R1,R3             SOURCE STRING EXHAUSTED                52417561
         BNL   QRCOM042          YES,                                   52460061
         CLC   0(1,R6),1(R6)     DOES NEXT BYTE MATCH CURRENT           52502561
         BNE   QRCOM042          NO,                                    52545061
         CR    R1,R0             DID WE REACH COUNT LIMIT               52587561
         BNL   QRCOM042          YES,                                   52630061
         LA    R1,1(,R1)         UPDATE REPLICATION COUNT               52672561
         LA    R6,1(,R6)         UPDATE SOURCE ADDRESS                  52715061
         B     QRCOM040          CHECK NEXT CHARACTER                   52757561
QRCOM042 DS    0H                END OF STRING PROCESSING               52800061
         C     R5,=A(2)          AT LEAST 2 BYTES AVAILABLE             52842561
         BL    QRCOMERX          NO,                                    52885061
         STC   R1,0(,R4)         SET REPLICATION COUNT                  52927561
         MVC   1(1,R4),0(R2)     MOVE REPLICATION VALUE                 52970061
         OI    0(R4),X'80'       INCICATE COMPRESSED DATA               53012561
         LA    R4,2(,R4)         UPDATE TARGET POINTER                  53055061
         LA    R2,0(R1,R2)       UPDATE SOURCE POINTER C                53097561
         BCTR  R5,0              ACCOUNT FOR TARGET FLAG                53140061
         BCTR  R5,0              ACCOUNT FOR TARGET VALUE               53182561
         SR    R3,R1             REMAINING SOURCE LENTH                 53225061
         B     QRCOM400          JOIN MAIN PATH                         53267561
QRCOM300 DS    0H                UNUSUAL CONDITION PROCESSING           53310061
         OI    QRST+12,X'04'     SET FOOT PRINT                                 
         CR    R1,R5             TARGET FIELD EXHAUSTED                 53395061
         BNL   QRCOMERX          YES,                                   53437561
         STC   R1,0(,R4)         SET LENGTH FIELD IN TARGET             53480061
         BCTR  R1,0              ADJUST FOR EXECUTE                     53522561
         EX    R1,QRCOMEM1       MOVE DATA TO TARGET                    53565061
         LA    R1,1(,R1)         INCREASE BYTE COUNT                    53607561
         LA    R4,1(R4,R1)       ADJUST TARGET POINTER                  53650061
         LA    R2,0(R2,R1)       ADJUST SOURCE POINTER                  53692561
         SR    R3,R1             REMAINING SOURCE LENGTH                53735061
         SR    R5,R1             REMAINING TARGET LENGTH                53777561
         BCTR  R5,0              ACCOUNT FOR FLAGT BYTE                 53820061
QRCOM400 DS    0H                NEXT FIELD PROCESSING                  53905061
         LTR   R3,R3             MORE SOURCE DATA TO PROCESS            53947561
         BP    QRCOM000          YES, DO IT ALL OVER AGAIN              53990061
         SR    R4,R7             LENGTH OF COMPRESSED TARGET            54032561
         ST    R4,0(,R7)         SAVE COMPRESSED TARGET LENGTH          54075061
         OI    QRST+12,X'02'     SET FOOT PRINT                                 
         LM    R0,RF,0(RD)       RELOAD CALLERS REGISTERS                       
         MVI   16*4(RD),X'00'    FREE THE SAVE AREA                             
         B     8(,RE)            RETURN TO CALLER                               
QRCOMERX DS    0H                ERROR RETURN                                   
         OI    QRST+12,X'01'     SET FOOT PRINT                                 
         LM    R0,RF,0(RD)       RELOAD CALLERS REGISTERS                       
         MVI   16*4(RD),X'00'    FREE SAVE AREA                                 
         B     4(,RE)            RETURN TO CALLER (ERROR)                       
         DROP  ,                                                        54372561
QRCOMEM1 MVC   1(0,R4),0(R2)     MOVE DATA TO TARGET FIELD              54457561
         LTORG ,                                                        54500061
***************************************************************         54550061
*  CODEBLOCK QRGCHPID                                         *         54600061
*  FUNCTION: GET CHANNEL PATH ID FROM CHANNEL PATH ID MASK    *         54650061
*  ON ENTRY:                                                  *         54700061
*       R2        CHANNEL PATH ID MASK                        *         54750061
*       R4        PUBX POINTER                                *         54800061
*  ON EXIT:                                                   *         54850061
*       R0        CHANNEL PATH ID                             *         54900061
***************************************************************         54950061
         DC    C'QRGCHPID'                                                      
         USING QRSGLOB,RA        QRSGLOB BASE POINTER                           
         USING PBXADR,R4         PUBX BASE POINTER                      55434161
*        USING CHPID-MASK,R2     CHPID MASK                             55434161
         USING *,RC              SUBROUTINE BASE                                
*SGLINK  QRGCHPID INPUT=(R2,R4,RA),OUTPUT=R0                                    
QRGCHPID DS    0H                CONVERT MASK TO CHPID-ID                       
         BAL   R5,QRHARDWT       ENTER WAIT ROUTINE                             
Q$NCHPID BE    *-4               NO MORE SAVE AREA PRESENT                      
         LA    RD,16*4(,RD)      POINT TO NEXT SAVE AREA                        
         ST    RE,RE*4(,RD)      SAVE RETURN REGISTER                           
         OI    QRST+17,X'80'     LEAVE FOOTPRINT                                
         SLL   R2,24             MOVE MASK BIT TO UPMOST BYTE           55478861
         SLR   R1,R1             CLEAR INDEX                            55523561
         BCTR  R1,0              -1 (FIRSAT PATH THROUGH)               55568261
QRGCH020 LA    R1,1(,R1)         INCREASE BIT COUNT                     55612961
         SLL   R2,1              PROVIDE NEXT MASK BIT                  55657661
         LTR   R2,R2             DOES THIS PATH EXIST                   55702361
         BP    QRGCH020          NO,                           @DAOM    55747061
         OI    QRST+17,X'40'     LEAVE FOOTPRINT                                
         SLR   R0,R0             CLEAR REGISTER                         55836461
         IC    R0,PBXCHPID(R1)   GET CHPID-ID                           55881161
         ST    R0,R0*4(,RD)      SAVE CHPID IN SAVE AREA                        
         LM    R0,RF,0(RD)       RESTORE CALLERS REGISTERS                      
         MVI   16*4(RD),X'00'    INDICATE SAVE AREA FREE                        
         B     4(,RE)            RETURN TO CALLER                               
         DROP  ,                                                        56015261
         LTORG ,                                                        56059961
***************************************************************         56150061
*  CODEBLOCK QRTSTCDR                                         *         56150861
*  FUNCTION: TEST A CONFIGURATION DATA RECORD FOR VALIDITY    *         56151661
*    SINCE THERE MUST BE A TOKEN NED IN IT AND WE NEED A      *         56152461
*    GENERAL NEQ AT THE END.                                  *         56153261
*  ON ENTRY: (STANDARD REGS SEE ABOVE)                        *         56154061
*    RB = PTR TO AREA WITH <LENGTH><CDR DATA>                 *         56154861
*  ON EXIT: TO RETURN                                         *         56155661
*    RECORD IS TESTED AND WAS BAD                             *         56156461
*  ON EXIT: TO RETURN4                                        *         56157261
*    RECORD IS TESTED AND OK.                                 *         56158061
***************************************************************         56158861
         DC    C'QRTSTCDR'                                                      
QRTSTCDR DS    0H                TEST CONFIGURATION DATA RECORD                 
         USING *,RC              QRTSTCDR BASE POINTER                          
         USING QRSGLOB,RA        GLOBAL AREA BASE                               
         BAL   R5,QRHARDWT       TERMINATE PROCESSING                           
Q$NSTCDR BE    *-4               SAVE AREA FULL CONDITION                       
         LA    RD,16*4(,RD)      UPDATE SAVE AREA POINTER                       
         ST    RE,RE*4(,RD)      SAVE RETURN REGISTER                           
         OI    QRST+15,X'80'     SET FOOT PRINT                                 
         L     R1,0(,RB)         GET RCD RECORD LENGTH                  56167861
         LA    R1,0(R1,RB)       POINT TO NEXT LOGICAL RECORD           56168461
         LA    R6,4(,RB)         POINT TO DATA RECORD                   56169061
         USING CDRENTRY,R6       CDRENTRY BASE POINTER                  56169661
QRTSTC40 TM    CDRENFLG,CDRENFTO IS THIS A TOKEN NED                    56175061
         BO    QRTSTC60          YES,                                   56176261
QRTSTC50 LA    R6,CDRENEXT       BUMP TO NEXT ENTRY                     56170861
         CR    R6,R1             DID WE REACH THE END                   56171461
         BNH   QRTSTC40          NO,                                    56172061
         OI    QRST+15,X'40'     SET FOOT PRINT                                 
         MVI   QRERRIND,QRERR#06 SET ERROR INDICATOR                    56173261
         B     QRTSTC80          TERMINATE LOOP PROCESSING              56173861
QRTSTC60 LA    R6,CDRENEXT       BUMP TO NEXT ENTRY                     56176861
         CR    R6,R1             STILL WITHIN RECORD AREA               56177461
         BNH   QRTSTC70          YES,                                   56178061
         OI    QRST+15,X'20'     SET FOOT PRINT                                 
         MVI   QRERRIND,QRERR#07 INDICATE NO GNEQ PRESENT               56179261
         B     QRTSTC80          TERMINATE LOOP PROCESSING              56179861
QRTSTC70 TM    CDRENFLG,CDRENFSN IS THIS A SPECIFIC NEQ                 56181061
         BO    QRTSTC60          YES,                                   56181661
         TM    CDRENFLG,CDRENFGN IS THIS A GENERAL NEQ                  56182261
         BNO   QRTSTC60          NO,                                    56182861
         OI    QRST+15,X'10'     SET FOOT PRINT                                 
         LM    R0,RF,0(RD)       RELOAD USERS REGISTERS                         
         MVI   16*4(RD),X'00'    INDICATE SAVE AREA FREE                        
         B     8(,RE)            RETURN TO CALLER                               
QRTSTC80 ST    RB,QRMSREGC       PASS ALONG RECORD ADDRESS              56184661
         LA    R1,QR#1H58I       SET MSG NUMBER                         56185261
         CLI   16*4(RD),X'FF'    TEST FOR SAVE AREA                             
         STM   R0,RF,16*4(RD)    SAVE CALLERS REGISTERS                         
         DC    0H'0',X'58C0',S(*+6) LOAD SUBROUTINE ADDRESS                     
         BAL   RE,4(,RC)         WRITE 1H58I MESSAGE                            
         DC    AL4(QRMSGWRT)     ADDRESS OF MESSAGE WRITER                      
*SGLINK  QRMSGWRT INPUT=(R1,RA,RC)                                              
         OI    QRST+15,X'08'     SET FOOT PRINT                                 
         LM    R0,RF,0(RD)       RELOAD CALLERS REGISTERS                       
         MVI   16*4(RD),X'00'    FREE SAVE AREA                                 
         B     4(,RE)            RETURN TO CALLER (ERROR)                       
         DROP  ,                                                        56188861
         LTORG ,                                                        56189461
***************************************************************         56194061
*  FUNCTION                                                             56194661
*    WRITE TRACE ENTRIES INTO A TRACE AREA. THIS FUNCTION               56195261
*    SHALL PROVIDE THE NECESSARY INFORMATION FOR DEBUGGING              56195861
*                                                                       56196461
*  INPUT                                                                56197061
*    - A TRACE TYPE INDICATION AT RETURN+0                              56197661
*                                                                       56198261
*  OUTPUT ON RETURN TO RETURN+4                                         56198861
*    - NONE. A TRACE TABLE ENTRY IS WRITTEN.                            56199461
*                                                                       56200061
***************************************************************         56200661
         DC    C'QRSTRACE'                                                      
         USING QRSGLOB,RA         QRSGLOB BASE                                  
         USING *,RC               QRSTRACE BASE POINTER                         
QRSTRACE DS    0H                 BUILD A TRACE ENTRY                           
         BAL   R5,QRHARDWT        THATS THE END                                 
Q$NTRACE BE    *-4                SAVE AREA FULL CONDITION                      
         LA    RD,16*4(,RD)       ADVANCE SAVE AREA POINTER                     
         ST    RE,RE*4(,RD)       SAVE RETURN REGISTER                          
         L     R6,=A(QRSDBGMS)    QRSDBGMS BASE ADDRESS                 56206061
         USING QRSDBGMS,R6        QRSDBGMS BASE POINTER                 56206661
         LH    R4,QRSDBGAC        LAST TRACE ENTRY LENGTH               56207261
         LA    R4,QRSDEBUG(R4)    ADD START ADDRESS                     56207861
         MVC   0(2,R4),QRSDBGNR   SET I/O TRACE NUMBER                  56208461
         L     R5,4(,RE)          GET DEBUG ENTRY TYPE                  56209061
         STC   R5,2(,R4)          SAVE IT IN TRACE ENTRY                56209661
         SLL   R5,2               MULTIPLY WITH FOUR                    56210261
         C     R5,=A(QRSTRANR)    IS TRACE ENTRY NUMBER VALID           56210861
         BL    QRSTRATB(R5)       YES,                                  56211461
         BAL   R5,QRHARDWT        THATS THE END                         56212061
QRSTRATB B     QRSTR000         0 ROUTINE FOR ENTRY TYPE 0              56212661
         B     QRSTR100         1 RECEIVED RRR                          56213261
         B     QRSTR200         2 BASE CDR VALUES                       56213861
         B     QRSTR300         3 IO DONE TO READ CDR                   56214461
         B     QRSTR400         4                                       56215061
         B     QRSTR500         5                                       56215661
QRSTRANR EQU   (*-QRSTRATB)       LOWEST INVALID TRACE TYPE             56216261
         SPACE 1                                                        56216861
QRSTR000 SLR   R5,R5              CLEAR REGISTER                        56217461
         ICM   R5,3,QRSDBGNR      GET LAST ENTRY NUMBER                 56218061
         LA    R5,1(,R5)          INCREASE ENTRY NUMBER                 56218661
         STH   R5,QRSDBGNR        SAVE UPDATED VALUE                    56219261
         B     QRSTRAEX           TAKE GENERAL SUBROUTINE EXIT          56219861
*                                                                       56220461
* LAYOUT FOR TYPE 1   (RECEIVED RRR FOR PROCESSING)                     56221061
*                                                                       56221661
QRSTR100 DS    0H                                                       56222261
         MVC   4(14,R4),QRIRRR    SAVE RRR INFORMATION                  56222861
         MVC   18(9,R4),QRRRRCCB  SAVE CCB DATA                         56223461
         B     QRSTR888           JOIN MAIN PATH                        56224061
*                                                                       56224661
* LAYOUT FOR TYPE 2   (DISPLAY BASE CDR)                                56225261
*                                                                       56225861
QRSTR200 DS    0H                                                       56226461
         MVC   3(1,R4),QRBASCDC   GET CHPID INFORMATION                 56227061
         MVC   4(2,R4),QRBASCDX   PUT BUB INDEX                         56227661
         B     QRSTR888           JOIN MAIN PATH                        56228261
*                                                                       56228861
* LAYOUT FOR TYPE 3   (DID IO TO READ CDR)                              56229461
*                                                                       56230061
QRSTR300 DS    0H                                                       56230661
         MVC   4(9,R4),QRRCDCCB   SAVE CCB DATA                         56231261
         MVC  13(9,R4),QRSNSDAT   SAVE SENSE DATA                       56231861
         MVC  22(6,R4),QRSNSDAT+22 SAVE REST OF SENSE                   56232461
         B     QRSTR888           JOIN MAIN PATH                        56233061
*                                                                       56233661
* LAYOUT FOR TYPE 4   (DISPLAY OF SCOPE LIST)                           56234261
*                                                                       56234861
QRSTR400 DS    0H                                                       56235461
         MVC   4(28,R4),0(R9)     SAVE CUIR SCOPE INFORRMATION          56236061
         B     QRSTR888           JOIN MAIN PATH                        56236661
*                                                                       56237261
* LAYOUT FOR TYPE 5   (EXIT INFORMATION)                                56237861
*                                                                       56238461
QRSTR500 DS    0H                                                       56239061
         MVC   4(28,R4),QRST      DISPLAY IT                            56239661
         OC    QRSTACCU,QRST      ACCUMULATE IT                         56240261
         XC    QRST,QRST          PURGE IT                              56240861
         B     QRSTR888                                                 56241461
*                                                                       56242061
QRSTR888 DS    0H                 COMPLETE THE TRACE ENTRY              56242661
         LH    R4,QRSDBGAC        ADVANCE TO THE NEXT ENTRY             56243261
         LA    R4,32(,R4)         PROVIDE FOR ANOTHER ENTRY             56243861
         N     R4,QRSDBGMS        DONT USE MORE THAN 8K                 56244461
         STH   R4,QRSDBGAC        STORE NEXT ENTRY OFFSET               56245061
QRSTRAEX DS    0H                 RETURN TO CALLER                              
         LM    R0,RF,0(RD)        RELOAD CALLERS REGISTERS                      
         MVI   16*4(RD),X'00'     INDICATE SAVE AREA FREE                       
         B     8(,RE)             RETURN TO CALLER                              
         DROP  ,                  DROP ALL REGISTERS                    56246261
         LTORG                                                          56246861
         TITLE 'QUIESCE/RESUME PROCESSING  MAIN PROCESS VALIDATION    ' 68689361
***************************************************************         56248061
*  CODEBLOCK QRMAIN                                           *         56248661
*  FUNCTION: RECONFIGURATION REQUEST RECORD PROCESSING (RRR)  *         56250061
*            CALLED BY  QRBASE ROUTINE.                       *         56300061
*  ON ENTRY:                                                  *         56350061
*            RA        ADDRESS OF QRSGLOB                     *         56400061
*            QRSINPUT  ATTENTION-MSG DATA                     *         56400061
*  ON EXIT:                                                   *         57550061
*            RRR HAS BEEN PROCESSED                           *         57600061
***************************************************************         57650061
         DC    C'QRMAIN'                                                        
         USING QRSGLOB,RA        GLOBAL WORK AREA BASE                          
         USING PBXADR,R4         PBXADR BASE POINTER                            
         USING *,RC              QRMAIN BASE POINTER                            
*SGLINK  QRMAIN,INPUT=(R4,RA,RC)                                                
QRMAIN   DS    0H                START OF CODEBLOCK                             
         BAL   R5,QRHARDWT       HARD-WAIT CONDITION                            
Q$NAIN   BE    *-4               ENTRY POINT                                    
         LA    RD,16*4(,RD)      ADVANCE SAVE AREA POINTER                      
         ST    RE,RE*4(,RD)      SAVE RETURN ADDRESS                            
         OI    QRST+00,X'80'     FOOTPRINT                                      
         CLI   16*4(RD),X'FF'    SET CONDITION CODE                             
         STM   R0,RF,16*4(RD)    SAVE CALLERS REGISTERS                         
         DC    0H'0',X'58C0',S(*+6) LOAD SUBROUTINE BASE                        
         BAL   RE,4(,RC)         MAKE A TRACE ENTRY                             
         DC    AL4(QRSTRACE)     TRACE SUBROUTINE BASE                          
         DC    AL4(0)            TRACE ENTRY TYPE-0                             
         ICM   R1,15,QRIRRMID    GET MESSAGE-ID                         58353161
         ST    R1,QRRRMSID       STORE MSG-ID IN RESPONSE AREA          58385261
         STIDP QRRRSYTP          GET CPUID                     @D61NDMK 58395900
         UNPK  QRRRSYTC(9),QRRRSYTP(5) UNPAK CPU-ID NUMBER     @D61NDMK 58406600
         L     R2,QRRRTRT        ADDRESS OF TRANSLATE TABLE    @DAOM    58417300
         TR    QRRRSYTC(8),0(R2) MAKE IT PRINTABLE             @DAOM    58428000
         MVC   QRRRSYID(8),QRRRSYTC SAVE DATA IN RESPONSE AREA @D61NDMK 58438700
         MVC   QRRRDEVN(2),PBXCUU PROVIDE CUU ADDRESS                   58674161
         MVI   QRERRIND,0        RESET ERROR INDICATOR                  58706261
         IC    R1,QRICPID        GET CHPID MASK                         58770461
         STC   R1,QRRRCPID       SAVE CHPID-ID IN RESP. BUFFER                  
         LA    R1,QR#1H40I       ADDRESS OF 1H40I MESSAGE                       
         CLI   16*4(RD),X'FF'    CC=00 WHEN NO MORE SAVE AREA                   
         STM   R0,RF,16*4(RD)    SAVE REGISTERS                                 
         DC    0H'0',X'58C0',S(*+6) GET QRMSGWRT BASE ADDRESS                   
         BAL   RE,4(,RC)         WRITE 1H40I MESSAGE                            
         DC    AL4(QRMSGWRT)     SUBROUTINE BASE ADDRESS                        
*SGLINK  QRMSGWRT INPUT=(R1,RA,RC)                                              
         EJECT ,                                                                
         CLI   16*4(RD),X'FF'    CC=== WHEN NO MORE SAVE AREA                   
         STM   R0,RF,16*4(RD)    SAVE REGISTERS                                 
         DC    0H'0',X'58C0',S(*+6) GET QRSTRACE BASE ADDRESS                   
         BAL   RE,4(,RC)         SET UP TRACE-1 ENTRY                           
         DC    AL4(QRSTRACE)     SUBROUTINE BASE ADDRESS                        
         DC    AL4(1)            TRACE ENTRY TYPE-1                             
         MVI   QRRRCODE,QRRRCDUR INDICATE UNKNOWN REQUEST TYPE          59155661
         CLC   QRIRRLEN,=X'0010' IS THIS SUPPORTED LENGTH               59187761
         BH    QRUNSUP           NO,                                    59219861
         OI    QRST+00,X'40'     FOOTPRINT                                      
         CLI   QRIRRCOD,QRIRRCIS SYSTEM IDENTIFICATION REQUEST @DAOM    59320400
         BE    QRMAIN70          YES,                          @DAOM    59334100
         OI    QRST+00,X'20'     FOOTPRINT                                      
         MVI   QRRRCODE,QRRRCDIR ASSUME INVALID REQUEST TYPE            59380361
         CLI   QRIRRNED,0        ANY NED PROVIDED                       59412461
         BNE   QRMAINI5          YES,                                   59444561
         OC    QRIRRNEQ,QRIRRNEQ IS NEQ PROVIDED                        59476661
         BZ    QRUNSUP           NO,                                    59508761
QRMAINI5 CLI   QRIRRCOD,QRIRRCQD DEVICE TO BE QUIESCED         @DAOM    59562100
         BE    QRMAIN00          YES,                          @DAOM    59588800
         CLI   QRIRRCOD,QRIRRCRD DEVICE TO BE RESUMED          @DAOM    59615500
         BE    QRMAIN00          YES,                          @DAOM    59642200
         CLI   QRIRRCOD,QRIRRCQP PATH TO BE QUIESCED                    59669261
         BE    QRMAIN00          YES                                    59701361
         CLI   QRIRRCOD,QRIRRCRP PATH TO BE RESUMED                     59733461
         BE    QRMAIN00          YES,                                   59765561
QRMAINC0 MVI   QRRRCODE,QRRRCDUR INDICATE UNSUPPORTED REQUEST           59797661
         B     QRUNSUP           GIVE NON-SUPPORT MESSAGE               59829761
         SPACE 1                                                                
QRRRSYTP DC    D'0'              CPU-ID INFORMATION            @DAOM    59838700
QRRRSYTC DC    CL8' '            CPU-ID NO. IN CHAR-FORMAT     @D61NDMK 59843200
QRRRARAC DC    XL1'00',C'0123456789ABCDEF' PRINTABLE CHARACTERS@D61NDMK 59847700
QRRRTRTA EQU   QRRRARAC+1-240    BEGIN OF TRANSLATE TABLE      @D61NDMK 59852200
QRRRTRT  DC    A(QRRRTRTA)       BEGIN OF TRANSLATE TABLE      @D61NDMK 59856700
         TITLE 'QUIESCE/RESUME PROCESSING  MAIN PROCESSING            ' 68689361
QRMAIN00 DS    0H                QUIESCE/RESUME REQUEST PROC.           59861861
         OI    QRST+00,X'10'     FOOTPRINT                                      
         MVI   QRRRCODE,QRRRCDCS ASSUME SUCCESSFUL COMPLETION           59926061
         CLI   16*4(RD),X'FF'    CC=00 WHEN NO MORE SAVE AREA                   
         STM   R0,RF,16*4(RD)    SAVE ALL REGISTERS                             
         DC    0H'0',X'58C0',S(*+6) LOAD QRGSCOPE BASE ADDRESS                  
         BAL   RE,4(,RC)         DETERMINE FAILURE SCOPE                        
         DC    AL4(QRGSCOPE)     SUBROUTINE BASE ADDRESS                        
         B     QRMAIN02        0 OK, JOIN MAIN LINE                             
*SGLINK  QRGSCOPE,INPUT=(R4,RA,RC),OUTPUT=R9                                    
         LA    R1,QR#1H60I     4 INDICATE INTERNAL ERROR                60118661
         CLI   16*4(RD),X'FF'    CC=00 WHEN NO MORE SAVE AREA                   
         STM   R0,RF,16*4(RD)    SAVE REGISTERS                                 
         DC    0H'0',X'58C0',S(*+6) LOAD QRMSGWRT BASE ADDRESS                  
         BAL   RE,4(,RC)         ISSUE MSG 1H60I                                
         DC    AL4(QRMSGWRT)     SUBROUTINE BASE ADDRESS                        
*SGLINK  QRMSGWRT INPUT=(R1,RA,RC)                                              
         MVI   QRRRCODE,QRRRCDIR ASSUME INVALID REQUEST TYPE                    
         OI    QRST+00,X'08'     FOOTPRINT                                      
         B     QRRESPPR          DO RESPONSE PROCESSING                         
QRMAIN02 DS    0H                SCOPE HAS BEEN VALIDATED               60343361
         CLI   16*4(RD),X'FF'    CC=00 WHEN NO MORE SAVE AREA                   
         STM   R0,RF,16*4(RD)    SAVE REGISTERS                                 
         DC    0H'0',X'58C0',S(*+6) LOAD QRSTRACE BASE ADDRESS                  
         BAL   RE,4(,RC)         SET UP TYPE-4 ENTRY                            
         DC    AL4(QRSTRACE)     SUBROUTINE BASE                                
         DC    AL4(4)            TRACE TYPE-4 ENTRY                             
         USING QRSCDSCT,R9       SCOPE GETVIS BASE                      60439661
         L     R5,QRSCENTN       NO. OF ENTRIES IN GETVIS AREA          60471761
QRMAIN04 DS    0H                LOOP FOR ALL ENTRIES                   60503861
         OI    QRST+00,X'04'     FOOTPRINT                                      
         L     R1,QRSCENTN       GET RESORDS ENTRY NUMBER               60568061
         SR    R1,R5             ENTRY NUMBER IN BUFFER                 60600161
         SLL   R1,2              MULTIPLY WITH FOUR                     60632261
         LA    R1,QRSCENTR(R1)   SCOPE RECORD ADDRESS                   60664361
         USING QRSCENTR,R1       QRSCENTR BASE POINTER                  60696461
         LH    R3,QRSCPU         GET PUB INDEX                          60728561
         LR    R4,R3             COPY PUB INDEX                         60792761
         SLL   R4,2              OFFSET OF PUBX ADDRESS ENTRY           60824861
         L     R2,QRPBXPTR       ADDRESS OF PBX-ADDRESS VECTOR          60856961
         A     R4,0(,R2)         ADDRESS OF PUBX ADDRESS                60889061
         L     R4,0(,R4)         ADDRESS OF PUBX ENTRY                  60921161
         XC    QRMSCPID,QRMSCPID RESET MESSAGE CHPID                    60953261
         CLI   QRSCPIDM,0        IS CHPID MASK PRESENT                  61017461
         BE    QRMAINA4          NO,                                    61049561
         SLR   R2,R2             CLEAR REGISTER                         61081661
         IC    R2,QRSCPIDM       GET CHPID MASK                         61113761
         DROP  R1                RELEASE QRSCENTR BASE                  61145861
         CLI   16*4(RD),X'FF'    CC=00 IF NO MORE SAVE AREA                     
         STM   R0,RF,16*4(RD)    SAVE REGISTERS                                 
         DC    0H'0',X'58C0',S(*+6) LOAD QRGCHPID BASE ADDRESS                  
         BAL   RE,4(,RC)         CONVERT CHPID MASK TO CHPID-ID                 
         DC    AL4(QRGCHPID)     SUBROUTINE BASE                                
*SGLINK  QRGCHPID INPUT=(R2,R4),OUTPUT=R0                                       
         STC   R0,QRMSCPID       SAVE CHPID-ID FOR MSG                          
         OI    QRST+00,X'02'     FOOTPRINT                                      
QRMAINA4 DS    0H                DEVICE QUIESCE PROCESSING              61338461
         MVC   QRMSDEVN(2),PBXCUU PROVIDE CUU ADDRESS                   61370561
         CLI   QRIRRCOD,QRIRRCQD IS DEVICE TO BE QUIESCED               61434761
         BNE   QRMAIN30          NO, CHECK FOR PATH QUIESCE             61466861
         OI    QRST+00,X'01'     FOOTPRINT                                      
         GETFLD FIELD=OWNER,PU=(3) GET OWNER INFORMATION                61563161
         LTR   RF,RF             IS OWNER PRESENT                       61627361
         BZ    QRMAIN20          NO,                                    61659461
         OI    QRST+01,X'80'     FOOTPRINT                                      
         LA    R1,QR#1H42I       GET MESSAGE ADDRESS                    61723661
         CLI   16*4(RD),X'FF'    CC=00 WHEN NO MORE SAVE AREA                   
         STM   R0,RF,16*4(RD)    SAVE REGISTERS                                 
         DC    0H'0',X'58C0',S(*+6) LOAD QRMSGWRT BASE ADDRESS                  
         BAL   RE,4(,RC)         ISSUE 1H42I MESSAGE                            
         DC    AL4(QRMSGWRT)     SUBROUTINE BASE ADDRESS                        
*SGLINK  QRMSGWRT INPUT=(R1,RA,RC)                                              
         SLR   R2,R2             SET PIK TO SYSTEM                              
QRMAIN05 DS    0H                DEVICE OWNER PROCESSING                61884161
         OI    QRST+01,X'40'     FOOTPRINT                                      
         GETFLD FIELD=NXTOWNER,PART=(2),PU=(3) GET NEXT OWNER           61948361
         LTR   RF,RF             IS THERE AN OWNER                      61980461
         BNZ   QRMAIN10          YES,                                   62012561
         LA    R2,X'10'(,R1)     GET NEXT PIK                           62044661
         GETFLD FIELD=LOGID,PART=(1) GET PARTITION SYSLOG-ID            62076761
         STH   R1,QRMSOWNR       SAVE SYSLOG-ID                         62108861
         LA    R1,QR#1H43I       GET MESSAGE ADDRESS                    62140961
         CLI   16*4(RD),X'FF'    CC=00 WHEN NO MORE SAVE AREA                   
         STM   R0,RF,16*4(RD)    SAVE REGISTERS                                 
         DC    0H'0',X'58C0',S(*+6) LOAD QRMSGWRT BASE ADDRESS                  
         BAL   RE,4(,RC)         ISSUE 1H43I MSG                                
         DC    AL4(QRMSGWRT)     SUBROUTINE BASE ADDRESS                        
*SGLINK  QRMSGWRT INPUT=(R1,RA,RC)                                              
         B     QRMAIN05          PROCESS NEXT OWNER (IF ANY)                    
QRMAIN10 DS    0H                NO DEVICE-OWNWER PROCESSING            62301461
         MVC   QRRRDEVN(2),PBXCUU SAVE CUU ADDRESS                      62333561
         MVI   QRRRCODE,QRRRCDIU INDICATE DEVICE NOT QUIESCED           62365661
         B     QRMAIN62          JOINB MAIN LINE                        62397761
QRMAIN20 DS    0H                COMPLETED OWNER PROCESSING             62429861
         OI    QRST+01,X'10'     FOOTPRINT                                      
         SLR   R0,R0             CLEAR REGISTER                         62494061
         IC    R0,PBXPIM         GET PATH INSTALLED MASKS      @DAOM    62526161
         STC   R0,PBXNOQSD       SET ALL PATHES QUIESCED                62943461
         MVI   PBXLPM,X'00'      DON'T ALLOW ANY ACCESS                 62975561
         LA    R1,QR#1H41I       GET MESSAGE ADDRESS                    63007661
         CLI   16*4(RD),X'FF'    CC=00 WHEN NO MORE SAVE AREA                   
         STM   R0,RF,16*4(RD)    SAVE REGISTERS                                 
         DC    0H'0',X'58C0',S(*+6) LOAD QRMSGWRT BASE ADDRESS                  
         BAL   RE,4(,RC)         ISSUE IH41I MSG                                
         DC    AL4(QRMSGWRT)     SUBROUTINE BASE ADDRESS                        
*SGLINK  QRMSGWRT INPUT=(R1,RA,RC)                                              
         B     QRMAIN60          JOIN MAIN LINE                                 
         SPACE 2                                                                
QRMAIN30 CLI   QRIRRCOD,QRIRRCQP PATH TO BE QUIESCED                    63168161
         BNE   QRMAIN40          NO,                                    63200261
         OI    QRST+01,X'08'     FOOTPRINT                                      
         LR    R0,R2             COPY CHPID MASK                        63264461
         SLR   R1,R1             CLEAR REGISTER                                 
         IC    R1,PBXNOQSD       GET ALREADY QUIESCED MASK                      
         NR    R0,R1             DID WE QUIESCE IT ALREADY                      
         BNZ   QRMAIN32          YES,                                   63392861
         CLM   R2,1,PBXLPM       IS THIS OUR LAST PATH                  63424961
         BNE   QRMAIN32          NO,                                    63457061
         OI    QRST+01,X'04'     FOOTPRINT                                      
         LA    R1,QR#1H48I       LOAD MESSAGE ADDRESS                   63521261
         CLI   16*4(RD),X'FF'    CC=00 WHEN NO MORE SAVE AREA                   
         STM   R0,RF,16*4(RD)    SAVE REGISTERS                                 
         DC    0H'0',X'58C0',S(*+6) LOAD QRMSGWRT BASE ADDRESS                  
         BAL   RE,4(,RC)         ISSUE 1H48I MSG                                
         DC    AL4(QRMSGWRT)     SUBROUTINE BASE ADDRESS                        
*SGLINK  QRMSGWRT INPUT=(R1,RA,RC)                                              
         MVC   QRRRDEVN,QRMSDEVN PROVIDE CUU ADDRESS                    63649661
         MVC   QRRRCPID,QRMSCPID PROVIDE CHPID-ID                       63681761
         MVI   QRRRCODE,QRRRCDLP INDICATE LAST PATH AVAILABLE           63713861
         B     QRMAIN62          JOIN MAIN LINE                         63745961
QRMAIN32 DS    0H                                                       63778061
         OI    QRST+01,X'02'     FOOTPRINT                                      
         LR    R0,R2             GET PATH TO BE QUIESCED MASK           63842261
         SLR   R1,R1             CLEAR REWGISTER               @DAOM            
         BCTR  R1,0              MAKE IT FFFFFFFF              @DAOM            
         XR    R0,R1             TURN OFF QUIESCED MASK        @DAOM            
         SLR   R1,R1             CLEAR REWGISTER               @DAOM            
         IC    R1,PBXLPM         GET LOGICAL PATH MASK         @DAOM            
         NR    R0,R1             TURN OFF THE QUIESCED PATH    @DAOM            
         STC   R0,PBXLPM         USE AS LPM NOW                         64002761
         LR    R0,R2             COPY QUIESCED PATH MASK                64034861
         IC    R1,PBXNOQSD       GET QUIESCED PATHES           @DAOM    64066961
         OR    R0,R1             NEW QUIESCED MASK             @DAOM            
         STC   R0,PBXNOQSD       SAVE NEW QUIESCED MASK                 64163261
         LA    R1,QR#1H47I       LOAD MESSAGE ADDRESS                   64195361
         CLI   16*4(RD),X'FF'    CC=00 WHEN NO MORE SAVE AREA                   
         STM   R0,RF,16*4(RD)    SAVE REGISTERS                                 
         DC    0H'0',X'58C0',S(*+6) LOAD QRMSGWRT BASE ADDRESS                  
         BAL   RE,4(,RC)         ISSUE 1H47I MSG                                
         DC    AL4(QRMSGWRT)     SUBROUTINE BASE ADDRESS                        
*SGLINK  QRMSGWRT INPUT=(R1,RA,RC)                                              
         B     QRMAIN60          JOIN MAIN LINE                                 
QRMAIN40 CLI   QRIRRCOD,QRIRRCRP PATH TO BE RESUMED                     64355861
         BNE   QRMAIN50          NO, MUST BE DEVICE RESUME              64387961
         OI    QRST+01,X'01'                                                    
         LR    R0,R2                      TAKE THE BIT IF IT IS         64484261
         N     R0,0                       *  SET IN THE MASK            64516361
         ORG   *-2                        *  FOR QUIESCED PATHS         64548461
         DC    S(PBXNOQSD-3)              *                             64580561
         LTR   R0,R0                      IF (PATH WAS NOT              64612661
         BNZ   QRMAIN52                   *** QUIESCED BEFORE)          64644761
         LA    R1,QR#1H50I                  TAKE MSG NR                 64676861
         B     QRMAIN53                 ELSE DEVICE RESUME              64741061
QRMAIN50 DS    0H                DEVICE RESUME PROCESSING               64773161
         OI    QRST+02,X'80'                                                    
         IC    R2,PBXNOQSD                TAKE ALL QUIESCED             64837361
         LTR   R0,R2                      IF (NOTHING WAS               64869461
         BNZ   QRMAIN52                   *** QUIESCED BEFORE)          64901561
         LA    R1,QR#1H45I                  TAKE MSG NR                 64933661
         B     QRMAIN53                   ENDIF                         64965761
QRMAIN52 DS    0H                       IF (SOMETHING TO DO)            65029961
         IC    R1,PBXNOERP                BUILD A MASK WITH ALL         65062061
         X     R1,=X'FFFFFFFF'            *  BITS SET, THAT ARE         65094161
         NR    R0,R1                      *  TO BE RESUMED, BUT         65126261
         IC    R1,PBXNOPVF                *  THAT ARE NOT SET           65158361
         X     R1,=X'FFFFFFFF'            *  IN ONE OF THE OTHE         65190461
         NR    R0,R1                      *  NOT OPERATIONAL            65222561
         IC    R1,PBXNOOPR                *  MASKS                      65254661
         X     R1,=X'FFFFFFFF'            *                             65286761
         NR    R0,R1                      *                             65318861
         IC    R1,PBXLPM                  SET ALL THESE BITS            65350961
         OR    R0,R1                      *   ON IN THE LPM             65383061
         STC   R0,PBXLPM                  *                             65415161
         LR    R0,R2                      TAKE BIT ONCE AGAIN           65447261
         X     R0,=X'FFFFFFFF'            SET THE BIT OFF IN            65479361
         N     R0,0                       *  THE QUIESCED               65511461
         ORG   *-2                        *  MASK                       65543561
         DC    S(PBXNOQSD-3)              *                             65575661
         STC   R0,PBXNOQSD                RESET IN NOQSD MASK           65607761
         XR    R1,R1                      RESET MSG NR                  65639861
QRMAIN53 DS    0H                       ENDIF                           65671961
         CLI   PBXLPM,X'00'             IF (THERE IS NO PATH)           65704061
         BNE   QRMAIN56                 ***                             65736161
         OI    QRST+02,X'40'                                                    
         CLI   QRIRRCOD,QRIRRCRP          IF (PATH RESUME)              65800361
         BNE   QRMAIN54                   ***                           65832461
         OI    QRST+02,X'20'                                                    
         LA    R1,QR#1H51I                  IND. PARTIAL SUCC.          65896661
         B     QRMAIN59                   ELSE                          65928761
QRMAIN54 DS    0H                         ***                           65960861
         OI    QRST+02,X'10'                                                    
         LA    R1,QR#1H46I                  IND. PARTIAL SUCC.          66025061
         B     QRMAIN59                 ELSE                            66089261
QRMAIN56 DS    0H                       ***                             66121361
         OI    QRST+02,X'08'                                                    
         CLI   QRIRRCOD,QRIRRCRP          IF (PATH RESUME)              66185561
         BNE   QRMAIN58                   ***                           66217661
         LTR   R1,R1                        IF (NO ALREADY              66249761
         BNZ   QRMAIN59                     *** RESUMED MSG)            66281861
         OI    QRST+02,X'04'                                                    
         LA    R1,QR#1H49I                    IND. PATH RESUMED         66346061
         B     QRMAIN59                   ELSE                          66410261
QRMAIN58 DS    0H                         ***                           66442361
         LTR   R1,R1                        IF (NO ALREADY              66474461
         BNZ   QRMAIN59                     *** RESUMED MSG)            66506561
         OI    QRST+02,X'02'                                                    
         LA    R1,QR#1H44I                    IND. CUU RESUMED          66570761
QRMAIN59 DS    0H                       ENDIF                           66667061
         CLI   16*4(RD),X'FF'           *                                       
         STM   R0,RF,16*4(RD)           CALL, WRITE MSG:                        
         DC    0H'0',X'58C0',S(*+6)       .                                     
         BAL   RE,4(,RC)                  .                                     
         DC    AL4(QRMSGWRT)              .                                     
*SGLINK  QRMSGWRT INPUT=(R1,RA,RC)                                              
QRMAIN60 DS    0H                     ENDSELECT                         66795461
         BCT   R5,QRMAIN04          UNTIL (NO DEVICE LEFT)              66827561
QRMAIN62 DS    0H                   DO CLEANUP PROCESSING               66891761
         B     QRRESPPR          DO RESPONSE PROCESSING                 66923861
QRMAIN70 DS    0H                SYSTEM IDENTIFICATION REQUEST          66955961
         OI    QRST+02,X'01'                                                    
         LA    R1,QR#1H52I          TELL IDENTIFIED MYSELF              67020161
         CLI   16*4(RD),X'FF'       *                                           
         STM   R0,RF,16*4(RD)       CALL, WRITE MSG:                            
         DC    0H'0',X'58C0',S(*+6)   .                                         
         BAL   RE,4(,RC)              .                                         
         DC    AL4(QRMSGWRT)          .                                         
*SGLINK  QRMSGWRT INPUT=(R1,RA,RC)                                              
         MVI   QRRRCODE,QRRRCDCS    INDICATE SUCCESSFUL COMPL.                  
         B     QRRESPPR           DO RESPONSE PROCESSING                        
QRUNSUP  DS    0H                 UNSUPPORTED FORMAT PROCESSING         67212761
         OI    QRST+03,X'80'      FOOTPRINT                                     
         LA    R1,QR#1H53I        LOAD MESSAGE ADDRESS                          
         CLI   16*4(RD),X'FF'     CC=00 WHEN NO MORE SAVE AREA                  
         STM   R0,RF,16*4(RD)     SAVE REGISTERS                                
         DC    0H'0',X'58C0',S(*+6) GET QRMSGWRT ADDRESS                        
         BAL   RE,4(,RC)          ISSUE MSG 1H53I MESSAGE                       
         DC    AL4(QRMSGWRT)      SUBROUTINE ADDRESS                            
*SGLINK  QRMSGWRT INPUT=(R1,RA,RC)                                              
*        B     QRRESPPR           DO RESPONSE PROCESSING                        
         TITLE 'QUIESCE/RESUME PROCESSING  CUIR RESPONSE PROCESSING   ' 68689361
QRRESPPR DS    0H                 CUIR RESPONSE PROCESSING              67437461
         OI    QRST+03,X'40'                                                    
         NI    QRPSFCCB+12,X'FF'-USENSE LET ERP HANDLE ERROR   @D61SDJK 67485500
         XC    QRSNSDAT,QRSNSDAT  PURGE OLD SENSE DATA                  67501661
         LA    R1,QRPSFCCB        POINT TO ANSWER CCB                   67533761
QRRESP20 DS    0H                 SEND CUIR-REQUEST RESPONSE   @D61SDJK 67565800
         SYSIO (1)                REPONSE TO REQUEST                    67597961
         ICM   R4,15,QRIPBXAD     GET PUBX ADDRESS             @DAOM    67662161
         MVC   QRMSDEVN(2),PBXCUU PROVIDE CUU ADDRESS                   67854761
         SLR   R2,R2              CLEAR REGISTER                        67886861
         IC    R2,QRICPIDM        GET CHPID MASK                        67918961
         CLI   16*4(RD),X'FF'     CC=00 WHEN NO MORE SAVE AREA                  
         STM   R0,RF,16*4(RD)     SAVE REGISTERS                                
         DC    0H'0',X'58C0',S(*+6) LOAD QRGCHPID BASE ADDRESS                  
         BAL   RE,4(,RC)          CONVERT MASK TO CHPID-ID                      
         DC    AL4(QRGCHPID)      SUBROUTINE BASE ADDRESS                       
*SGLINK  QRGCHPID INPUT=(R2,R4),OUTPUT=R0                                       
         STC   R0,QRMSCPID        STORE CHPID-ID INTO RECORD            68047361
         OI    QRST+03,X'20'      FOOTPRINT                                     
         TM    QRPSFCCB+2,DISERR  ANY ERROR                             68111561
         BNO   QRRESP60           NO,                                   68143661
         TM    QRPSFCCB+12,USENSE DID ERP TRY IT ALREADY       @D61SDJK 68148900
         BO    QRRESP40           NO,                          @D61SDJK 68154200
         OI    QRPSFCCB+12,USENSE ASK FOR SENSE BYTES          @D61SDJK 68159500
         B     QRRESP20           RETRY WITHOUT ERP NOW        @D61SDJK 68164800
         SPACE 1                                                                
QRRESP40 DS    0H                 ISSUE 1H55I MESSAGE          @D61SDJK 68170100
         OI    QRST+03,X'10'      FOOTPRINT                                     
         LA    R1,QR#1H55I        LOAD MESSAGE ADDRESS                  68207861
         B     QRRESP80           JOIN MAIN LINE                        68239961
QRRESP60 DS    0H                 ISSUE 1H54I MESSAGE                   68272061
         OI    QRST+03,X'08'      FOOTPRINT                                     
         LA    R1,QR#1H54I        LOAD MESSAGE ADDRESS                  68336261
QRRESP80 DS    0H                 PROVIDE MESSAGE NOW                   68368361
         CLI   16*4(RD),X'FF'     CC=00 WHEN NO MORE SAVE AREA                  
         STM   R0,RF,16*4(RD)     SAVE REGISTERS                                
         DC    0H'0',X'58C0',S(*+6) GET QRMSGWRT BASE ADDRESS                   
         BAL   RE,4(,RC)          ISSUE MESSAGE                                 
         DC    AL4(QRMSGWRT)      SUBROUTINE BASE ADDRESS                       
*SGLINK  QRMSGWRT INPUT=(R1,RA,RC)                                              
         CLI   16*4(RD),X'FF'     CC=00 WHEN NO MORE SAVE AREA                  
         STM   R0,RF,16*4(RD)     SAVE REGISTERS                                
         DC    0H'0',X'58C0',S(*+6) LOAD QRSTRACE BASE ADDRESS                  
         BAL   RE,4(,RC)          MAKE A TYPE-5 ENTRY                           
         DC    AL4(QRSTRACE)      SUBROUTINE ADDRESS                            
         DC    AL4(5)             TRACE ENTRY TYPE-5                            
         DS    0H                 RETURN TO QRBASE LOOP                         
         LM    R0,RF,0(RD)        RELOAD CALLERS REGISTERS                      
         MVI   16*4(RD),X'00'     FREE SAVE AREA                                
         B     4(,RE)             RETURN TO QRBASE LOOP                         
         DROP  ,                                                        68593061
         LTORG                                                          68657261
         TITLE 'QUIESCE/RESUME PROCESSING  INITIALISATION/TERMINATION ' 68689361
***************************************************************         68750061
*  CODEBLOCK QRBASE                                           *         68800061
*  FUNCTION: DO SOME INITIALISATION WORK AND EXECUTE          *         68850061
*            QRMAIN (CONFIGURATION DATA PROCESSING) ROUTINE   *         68900061
*            ATTENTION MESSAGE HAS BE READ BY THE AOM TASK    *         68950000
*            AND PASSED TO IJBCUIR (RUNNING AS SVT-TASK)      *         69000000
*            FOR FURTHER PROCESSING.                          *         69050000
*                                                             *         69150061
*  ON ENTRY:                                                  *         69200061
*    RC = ADDRESS OF QRBASE                                   *         69250061
*    RE = RETURN ADDRESS                                      *         69300061
*                                                             *         69150061
*  ON EXIT:                                                   *         69350061
***************************************************************         69400061
         DC    C'QRBASE  '                                              69433861
QRBASE   DS    0H                 MAIN ROUTINE                          69467661
         USING QRBASE,RC          QRBASE BASE POINTER                   69501461
         USING QRSGLOB,RF         QRSGLOB TEMP BASE POINTER             69535261
         STM   R0,RF,QRHWSAVE     SAVE FOR EMERGENCY ACTIONS            69569061
         STM   R0,RF,QRSTACK      SAVE CALLERS REGISTERS                69602861
         LR    RA,RF              COPY GLOBAL-WORK-AREA ADDRESS         69670461
         DROP  RF                 RELEASE QRSGLOB TEMP BASE             69636661
         USING QRSGLOB,RA         QRSGLOB PERM BASE POINTER             69704261
         LA    RD,QRSTACK         NEXT SAVE AREA ADDRESS                69738061
         L     R1,=A(QRGSCOPE)    GET SCOPE ADDRESS                     69771861
         ST    R1,Q@GSCOPE        SET SCOPE ADDRESS                     69805661
         L     R1,=A(QRSFINIS)    GET END ADDRESS                       69839461
         ST    R1,Q@SFINIS        SET END ADDRESS                       69873261
         L     R1,=A(QRSDBGST)    GET DEBUG START ADDRESS               69907061
         ST    R1,Q@SDBGST        SET DEBUG START ADDRESS               69940861
         AMODESW SET,AMODE=31,WR=(1)  SWITCH TO 31 BIT ADDR.            69974661
         XC    QRRRRDAT,QRRRRDAT  PURGE PTRS TO GETVIS                  70008461
         XC    QRCDKEEP,QRCDKEEP  PURGE KEEP INFORMATION                70042261
         XC    QRSNSDAT,QRSNSDAT  PURGE SENSE INFORMATION               70076061
         B     QRBASC00           FIRST TIME THROUGH           @DAOM    70143661
QRBAS000 NC    0(1,R1),QRNOWORK   RESET SGSER WORK-TO-DO SWITCH@DAOM    70177461
         ICM   R5,15,QRSWTDCH     ANY WORK TO DO               @DAOM            
         BZ    QRBASC00           NO, WE ARE DONE              @DAOM            
         CLC   4(L'QRCNCUIR,R5),QRCNCUIR RIGHT INPUT           @DAOM            
         BNE   QRBASC00           NO, TERMINATE PROCESSING     @DAOM            
         MVC   QRSWTDCH,0(R5)     DEQUEUE REQUEST FROM CHAIN   @DAOM            
         MVC   QRSINPUT(L'QRSINPUT),8(R5) SAVE MSG DATA        @DAOM    75484061
         LR    R1,R5              COPY AOM BUFFER ADDRESS      @DAOM            
         LA    R0,L'QRSINPUT+8    GET LENGTH OF BUFFER         @DAOM            
         FREEVIS ADDRESS=(1),LENGTH=(0),SVA=YES FREE AOM BUFFER@DAOM    41665361
         EJECT ,                                               @DAOM            
         ICM   R4,15,QRIPBXAD     PUBX ENTRY ADDRESS           @DAOM    70211261
         USING PBXADR,R4          PUBX BASE POINTER            @DAOM    70380261
         SLR   R2,R2              CLEAR REGISTER               @DAOM    58738361
         IC    R2,QRICPIDM        GET CHPID MASK               @DAOM    58770461
         CLI   16*4(RD),X'FF'     CC=00 WHEN NO MORE SAVE AREA @DAOM            
         STM   R0,RF,16*4(RD)     SAVE REGISTERS               @DAOM            
         DC    0H'0',X'58C0',S(*+6) GET QRGCHPID BASE ADDRESS  @DAOM            
         BAL   RE,4(,RC)          CONVERT CHPID MASK TO CHPID-ID@DAOM           
         DC    AL4(QRGCHPID)      SUBROUTINE BASE ADDRESS      @DAOM            
*SGLINK  QRGCHPID INPUT=(R2,R4),OUTPUT=R0                      @DAOM            
         STC   R0,QRICPID         SAVE CHPID-ID IN INPUT BUFFER@DAOM            
         SLR   R0,R0              CLEAR REGISTER               @DAOM            
         SLR   R1,R1              CLEAR REGISTER               @DAOM            
         IC    R0,PBXNOERP        GET IN ERROR PATHES          @DAOM            
         OR    R1,R0              SAVE IN ERROR MASK           @DAOM    71123861
         IC    R0,PBXNOPVF        GET PATH ERRORS              @DAOM            
         OR    R1,R0              SAVE IN ERROR MASK           @DAOM    71123861
         IC    R0,PBXNOOPR        GET OFFLINED PATHES          @DAOM            
         OR    R0,R1              ALL DOWN PATHES              @DAOM    71123861
         SLR   R1,R1              CLEAR REGISTER               @DAOM            
         BCTR  R1,0               MAKE IT FFFFFFFF             @DAOM            
         XR    R1,R0              FORCE GATED PATH MASK = ZERO @DAOM            
         IC    R0,QRICPIDM        GET CHPID MASK               @DAOM    70921061
         NR    R0,R1              RESET GATED PATHES           @DAOM            
         LTR   R0,R0              IS PATH ACCESSABLE           @DAOM    71597061
         BNZ   QRBAS020           YES, KEEP GOING              @DAOM    71630861
         LA    R1,QR#1H57I        GET MESSAGE ADDRESS          @DAOM    71664661
         CLI   16*4(RD),X'FF'     CC=00 WHEN NO MORE SAVE AREA @DAOM            
         STM   R0,RF,16*4(RD)     SAVE CURRENT REGISTER SETTING@DAOM            
         DC    0H'0',X'58C0',S(*+6) LOAD SUBROUTINE BASE       @DAOM            
         BAL   RE,4(,RC)          ISSUE 1H57I MESSAGE          @DAOM            
         DC    AL4(QRMSGWRT)      ADDRESS OF MESSAGE ROUTINE   @DAOM            
*SGLINK  QRMSGWRT INPUT=(R1,RA,RC)                             @DAOM            
         B     QRBAS000           PROCESS NEXT CUIR REQUEST    @DAOM            
         EJECT ,                                               @DAOM            
QRBAS020 DS    0H                 WE FOUND A VALID CUIR REQUEST@DAOM    71833661
         MVC   QRRRRCCB+CCBCLS-CCBADR(2),QRIPUBND GET PUB INDEX@DAOM            
         OI    QRRRRCCB+CCBCLS-CCBADR,CCBPHYAD+XCPR PHYS.ADDR. @DAOM    72036461
         MVC   QRRRRCCB+CCBCCW31-CCBADR(1),QRICPIDM GET MASK   @DAOM            
         MVC   QRPSFCCB+CCBCLS-CCBADR(3),QRRRRCCB+CCBCLS-CCBADR DAOM            
         MVI   QRPSFCCW,PSF       ASSUME DASD PSF CCW          @DAOM    73192800
         TM    PBXFLAG,PBXDASD    IT IS A DASD DEVICE          @DAOM    73200000
         BO    QRBAS030           YES,                         @DAOM    73207200
         MVI   QRPSFCCW,X'77'     SET UP TAPE PSF CCW          @DAOM    73214400
QRBAS030 MVC   QRRRRCC1(1),QRPSFCCW ADJUST CCW OPCODE          @DAOM    73228800
         ICM   R1,15,QRRRRDAT     RRR BUFFER ALREADY ALLOCATED @DAOM    72104061
         BNZ   QRBASB00           YES,                         @DAOM    72171661
         L     R0,QRRRRDAL        GET BUFFER LENGTH            @DAOM    72205461
         GETVIS ADDRESS=(1),LENGTH=(0),LOC=ANY,SVA=YES,                X72273061
               SPID=QRSGVSPN,PFIX=YES                          @DAOM    72306861
         LTR   RF,RF              DID WE GET THE BUFFER SPACE  @DAOM    72374461
         BZ    QRBAS040           YES,                         @DAOM    72408261
         ST    RF,QRMSREGC        SAVE RETURN CODE             @DAOM    72442061
         LA    R1,QR#1H59I        GET MESSAGE NUMBER           @DAOM    72475861
         CLI   16*4(RD),X'FF'     CC=00 WHEN NO MORE SAVE AREA @DAOM            
         STM   R0,RF,16*4(RD)     SAVE REGISTER CONTENTS       @DAOM            
         DC    0H'0',X'58C0',S(*+6) LOAD SUBROUTINE BASE       @DAOM            
         BAL   RE,4(,RC)          ISSUE 1H59I MESSAGE          @DAOM            
         DC    AL4(QRMSGWRT)      QRMSGWRT BASE ADDRESS        @DAOM            
*SGLINK  QRMSGWRT INPUT=(R1,RA,RC)                             @DAOM            
         B     QRBASC20           TAKE ERROR EXIT              @DAOM            
         SPACE 1                                                                
QRBAS040 DS    0H'0'              RRR BUFFER ALLOCATED         @DAOM            
         ST    R1,QRRRRDAT        SAVE BUFFER ADDRESS          @DAOM    72712461
         OI    QRPRCFLG,QRPRCFGV  INDICATE GOT GETVIS          @DAOM    72678661
         LR    RF,R0              SAVE GETVIS LENGTH           @DAOM            
         AR    RF,R1              BUFFER END ADDDRESS          @DAOM            
         LR    R2,R1              BUFFER BEGIN ADDRESS         @DAOM            
         SLR   RE,RE              INITIALIZE IDAL OFFSET PNTR  @DAOM            
QRBAS050 DS    0H                 SET UP IDAL ENTRIES          @DAOM            
         LRA   R0,0(,R2)          GET REAL ADDRESS             @DAOM            
         ST    R0,QRIOIDAL(RE)    SAVE IDAW ADDRESS IN IDAL    @DAOM            
         LA    RE,4(,RE)          ADVANCE IDAL POINTER         @DAOM            
         LA    R2,2048(,R2)       NEXT 2K ADDRESS              @DAOM            
         SRL   R2,11              ENSURE PROPER                @DAOM            
         SLL   R2,11              2K BOUNDARY                  @DAOM            
         CLR   RF,R2              DID WE REACH BUFFER END      @DAOM            
         BL    QRBAS050           NO, PROCESS NEXT IDAL        @DAOM            
*        B     QRBASB00           YES, DO MAIN PROCESSING      @DAOM            
         EJECT ,                                                                
QRBASB00 DS    0H'0'              DO Q/R MAIN PROCESSING       @DAOM            
         CLI   16*4(RD),X'FF'     CC=00 WHEN NO MORE SAVE AREA @DAOM            
         STM   R0,RF,16*4(RD)     SAVE REGISTER CONTENTS       @DAOM            
         DC    0H'0',X'58C0',S(*+6) LOAD SUBROUTINE BASE       @DAOM            
         BAL   RE,4(,RC)          DO  RECONFIGURATION PROCESS  @DAOM            
         DC    AL4(QRMAIN)        QRMAIN BASE ADDRESS          @DAOM            
*SGLINK  QRMAIN,INPUT=(R4,RA,RC)                               @DAOM            
         SPACE 1                                               @DAOM            
QRBASC00 L     R1,QRRQTFLG        ADDRESS OF SERFLGPR FLAG     @DAOM    75010861
         SLR   R0,R0              CLEAR REGISTER               @DAOM    75044661
         ICM   R0,8,QRISWORK      GET WORK-TO-DO FLAG          @DAOM    75078461
         N     R0,0(,R1)          WORK TO DO                   @DAOM    75112261
         BNZ   QRBAS000           YES, PROCESS NEXT            @DAOM    75146061
QRBASC20 TM    QRPRCFLG,QRPRCFGV  GETVIS STORAGE ALLOCATED     @DAOM    75179861
         BZ    QRBASC40           NO,                          @DAOM    75213661
         FREEVIS SPID=QRSGVSPN,SVA=YES                         @DAOM    75281261
         NI    QRPRCFLG,X'FF'-QRPRCFGV WE FREED THE GETVIS     @DAOM    70109861
QRBASC40 LM    R0,RF,QRSTACK      RELOAD ALL REGISTERS         @DAOM    75382661
         SVC   33                 SAVE CURRENT STATUS          @DAOM    75416461
         AMODESW RETURN,REG=(RE)  RETURN ==================>>> @DAOM    75450261
         DROP  ,                                               @DAOM    75517861
QRCNCUIR DC    CL4'CUIR'          CONSTANT                     @DAOM            
         LTORG  ,                                              @DAOM    75596161
         EJECT ,                                               @DAOM            
* -----------------------------------------------------------           75624061
*                                                                       75651961
*        SAVE AREA STACK                                                75679861
*                                                                       75707761
QRSTACKL EQU   16*4          LENGTH OF ONE STACK ENTRY                  75735661
QRSTACKN EQU   9             NR OF ENTRIES IN STACK                     75763561
         DC    CL16'SAVE-AREA STACK'                                            
QRSTACK  DS    0F                                                       75791461
         DC    (QRSTACKN*QRSTACKL)X'00'                                 75819361
         DC    X'FF'         END INDICATOR FOR SECURITY CHECK           75847261
*                                                                       75875161
*-------------------------------------------------------------*         75903061
*   QUIESCE RESUME WRAP AROUND DEBUG AREA                               75930961
*-------------------------------------------------------------*         75958861
*                                                                       75986761
         ORG   ((((*+15-IJBCUIR)/16)*16)+IJBCUIR) 16 BYTE BOUND         76014661
QRSDBGST DC    CL16'CUIR DEBUG AREA'                                    76042561
QRSDBGMS DC    A(X'03FF')    MASK FOR WRAP AROUND WRITING               76070461
QRSDBGAC DC    H'0'          CURRENT OFFSET IN AREA                     76098361
QRSDBGNR DC    H'0'          NO OF REQUESTS  PROCESSED                  76126261
QRSTACCU DS    XL28          COVERAGE POINTS                            76154161
QRSDEBUG DC    64XL16'00'    DEBUG AREA                                 76182061
QRSFINIS DC    C'CUIREND '                                              76209961
* -----------------------------------------------------------           76237861
*  THE GLOBAL AREA (WHICH IS A HEAP AREA) IS A DSECT FOR                76265761
*  THE CODE LOCATED IN HIGH SVA AND A CSECT FOR THE CODE IN             76293661
*  THE SUPERVISOR.                                                      76321561
*                                                                       76349461
         DSECT                                                          76377361
SERFLGPR EQU    *         AVOID ASSEMBLY ERRORS IN SVA PHASE            76405261
SERRQTQR EQU    0         BY SUPPLYING DUMMIES FOR SUPERVISOR           76433161
APBXAREA EQU    *         ADDRESSES USED IN QRSGLOB                     76461061
YPUBTAB  EQU    *                                                       76488961
ASVASVDL EQU    *                                                       76516861
SVASVDL  EQU    *                                                       76544761
AIJBCUIR EQU    *                                                       76572661
.SUPVR   ANOP                                                           76600561
         AIF   (NOT &SGCUIR).NPRT010                                    09800061
         PRINT GEN                                                      09850061
.NPRT010 ANOP                                                           09900061
*                                                                       76628461
*  THE FOLLOWING AREA IS THE HEAP AREA FOR THE QUIESCE                  76656361
*  RESUME PROCESSING DONE IN PHASE $IJBCUIR IN THE SVA.                 76684261
*  IT IS ADDRESSED BY POINTER IN REGISTER RA ALL THE TIME.              76712161
*                                                                       76740061
QRSGLOB  DS    0D                                                       76767961
         DC    C'QRSGLOB '        SET EYECATCHER                        76795861
* -----------------------------------------------------------           76823761
*                                                                       76851661
*  PART 0: SOME INFORMATION TO FACILITATE DEBUGGING AND FOR             76879561
*          MESSAGE WRITING OF SGCUIR                                    76907461
*                                                                       76935361
Q@GSCOPE DC    A(0)     START ADDRESS OF QRGSCOPE                       76963261
Q@SFINIS DC    A(0)     END ADDRESS OF IJBCUIR                          76991161
Q@SDBGST DC    A(0)     START OF DEBUG AREA                             77019061
Q@SHARDW DC    A(0)     REASON FOR HARD WAIT                            77046961
QRBASCDX DS    H        PUB INDEX OF BASE CDR                           77074861
QRBASCDC DS    X        CHPID MASK OF BASE CDR                          77102761
QRMSCPID DS    X        CHPID VALUE FOR MESSAGING                       77130661
QRMSDEVN DS    XL2      CUU FOR MESSAGING                               77158561
QRMSOWNR DS    CL2      LOGID OF OWNER OF DEVICE                        77186461
QRMSREGC DC    A(0)     REG CONTENTS FOR MESSAGE PRINTING               77214361
QRERRIND DS    X        ERROR INDICATOR                                 77242261
QRERR#01 EQU   1          ERROR IN QRGSCOPE: NO BASE CDR                77270161
QRERR#02 EQU   2          ERROR IN QRGSCOPE: NEDMAP BIT FALSE?          77298061
QRERR#03 EQU   3          ERROR IN QRGSCOPE: NO GETVIS FOR LIST         77325961
QRERR#04 EQU   4          ERROR IN QRGSCOPE: INEXISTENT NED             77353861
QRERR#05 EQU   5          ERROR IN QRGETCDR: NO AREA FOR CDR            77381761
QRERR#06 EQU   6          ERROR IN QRTSTCDR: NO TOKEN NED               77409661
QRERR#07 EQU   7          ERROR IN QRTSTCDR: NO GNEQ AT END             77437561
QRPRCFLG DS    X        QUIESCE RESUME PROCESSING FLAGS                 77465461
QRPRCFGV EQU   X'80'      FLAG TO INDICATE WE RECEIVED GETVIS           77493361
QRST     DS    XL28     QUIESCE RESUME TRACE BITMAP                     77521261
* -----------------------------------------------------------           77549161
*                                                                       77577061
*  PART 1: CONSTANTS COMMON TO ALL SUBROUTINES                          77604961
*                                                                       77632861
QRSGVSPN DC    CL8'IJBQRS  '   QUIESCE RESUME GETVIS SUBPOOL            77660761
QRSWTDCH DC    A(0)        Q/R WORK TO DO CHAIN                                 
QRPUBPTR DC    A(YPUBTAB)  PTR TO PTR TO BEGIN OF PUB                   77688661
QRPBXPTR DC    A(APBXAREA) PTR TO PTR TO START OF PUBX                  77716561
QRRQTFLG DC    A(SERFLGPR) PTR TO WORK TO DO FLAGS                      77744461
QRNOWORK DC    AL1(X'FF'-SERRQTQR) MASK TO SET OFF WORK TO DO           77772361
QRISWORK DC    AL1(SERRQTQR)       MASK TO TEST IF WORK TO DO           77800261
*                                                                       77828161
* -----------------------------------------------------------           78246661
*                                                                       78274561
*  PART 2: THE INPUT DATA FOR THE MAIN ROUTINE                          78302461
*                                                                       78330361
QRSINPUT DS    0XL24   0     TOTAL INPUT FIELD FOR MAIN                 78358261
QRIRRR   DS    0XL16   0     RECONFIGURATION REQUEST RECORD             78386161
QRIRRLEN DS    XL2     0-1   LENGTH CODE. SHOULD BE X000E               78414061
*                            OR 0010 FOR TPA                                    
QRIRRTYP DS    X       2     01 = RECONFIGURATION MSG TYPE              78441961
QRIRRCOD DS    X       3     MESSAGE CODE                               78497761
QRIRRCQP EQU   X'01'         QUIESCE PATH REQUEST                       78525661
QRIRRCRP EQU   X'02'         RESUME  PATH REQUEST                       78553561
QRIRRCQD EQU   X'03'         QUIESCE DEVICE REQUEST                     78581461
QRIRRCRD EQU   X'04'         RESUME  DEVICE REQUEST                     78609361
QRIRRCIS EQU   X'06'         IDENTIFY SYSTEM REQUEST                    78637261
QRIRRMID DS    XL4     4-7   MESSAGE ID FOR THIS REQUEST RECORD         78665161
QRIRRFLG DS    X       8     FLAG BYTE                                  78693061
QRIRRFNS EQU   X'80'         INDICATE NON-SUPPORT              @DAOM            
QRIRRFSM EQU   X'40'         INDICATE SOLICITED MESSAGE        @DAOM            
QRIRRNEQ DS    XL3     9-11  NEQ MAP                                    78720961
QRIRRNED DS    X      12     NED MAP                                    78748861
QRIRRSEL DS    X      13     RECORD SELECTOR                            78776761
* HERE   END OF DASD CUIR MESSAGE                              @DAOM            
QRIRRSMS DS    X      14     RECORD SELECTOR MASK              @DAOM    78776761
         DS    X      15     RESERVED                          @DAOM    78916261
* HERE   END OF TAPE CUIR MESSAGE                                               
.*       THE FOLLOWING DATA IS APPENDED BY VSE                 @DAOM            
QRICPID  DS    XL1           FILLED IN BY QRBASE ROUTINE       @DAOM            
QRICPIDM DS    XL1           CHPID MASK                                 78944161
QRIPUBND DS    XL2           PUB INDEX                                  78972061
QRIPBXAD DS    XL4           PUBX ADDRESS                      @DAOM    78972061
* -----------------------------------------------------------           78999961
*                                                                       79027861
*  PART 3: THE DATA FOR SENDING THE RESPONSE MESSAGE TO THE             79055761
*          DEVICE, THAT ISSUED THE RECONFIGURATION REQUEST.             79083661
*                                                                       79111561
QRPSFCCB CCB   SYSRDR,QRPSFCCW,X'1400',QRSNSDAT                @DAOM    79139400
QRPSFCCW CCW   PSF,QRRRSTAT,X'00',QRRRLNG                               79167300
         SPACE 1                                                        79251061
QRRRSTAT DC    X'1A'  COMMAND CODE FOR'RESPOND TO QR REQUEST'           79278961
         DC    X'00'  FLAGS. RESERVED. MUST BE ZERO                     79306861
QRRRANSP DS    0XL10  PART OF ANSWER PRINTED IN MESSAGE                 79334761
QRRRCODE DC    X'00'  RESPONSE CODE.                                    79362661
QRRRCDCS EQU   X'01'  COMPLETED SUCCESSFULLY                            79390561
QRRRCDNS EQU   X'02'  REQUEST REJECTED, QUIESCE/RES.NOT SUPP.           79418461
QRRRCDIR EQU   X'03'  INVALID RECONFIGURATION REQUEST                   79446361
QRRRCDOD EQU   X'04'  OPERATOR DENIED REQUEST                           79474261
QRRRCDLP EQU   X'05'  REJ.QUIESCE PATH, LAST PATH AFFECTED              79502161
QRRRCDIU EQU   X'06'  REJ.QUIESCE DEVICE, DEVICE IS IN USE              79530061
QRRRCDVP EQU   X'07'  RECONF.FAILED DURING VARY PROCESSING              79557961
QRRRCDPF EQU   X'08'  RECONF.FAILED DUE TO PROGRAM FAILURE              79585861
QRRRCDUR EQU   X'09'  RECONF.REQUEST, UNKNOWN REQUEST TYPE              79613761
QRRRCPID DC    X'00'  CHANNEL PATH ID                                   79641661
QRRRDEVN DC    XL2'00'  DEVICE NUMBER                                   79669561
         DC    XL2'00'  RESERVED. MUST BE ZERO                          79697461
QRRRMSID DC    XL4'00'  MESSAGE ID.                                     79725361
QRRRSYID DC    XL8'00'  SYSTEM ID. CPUID IN OUR CASE                    79753261
QRRRLNG  EQU   *-QRRRSTAT                                               79781161
* -----------------------------------------------------------           79809061
*                                                                       79836961
*  PART 4: DATA FOR RETRIEVAL OF A CONFIGURATION DATA RECORD            79864861
*          FROM A DEVICE.                                               79892761
*                                                                       79920661
QRCDKEEP DC    A(0)   KEEPAREA FOR CONFIGURATION DATA RECS.             79948561
QRCDKNDX DC    A(0)   NR OF TIMES WE FETCHED GETVIS FOR IT.             79976461
         DS    0D     ENSURE DOUBLEWORD ALIGNMENT                       80004361
QRRCDCCB CCB   SYSRDR,QRRCDCCW,X'1400',QRSNSDAT                @DAOM    80032200
QRRCDCCW CCW   X'00',QRIOIDAL,IDAL,0                                    80143861
QRSNSDAT DS    XL32'00'                                                 80171761
         ORG   QRSNSDAT+0                                               80199661
QRSNSDT0 DS    XL9               SENSE BYTES  0- 8                      80227561
         ORG   QRSNSDAT+22                                              80255461
QRSNSDT1 DS    XL6               SENSE BYTES 22-27                      80283361
         ORG   ,                                                        80311261
* -----------------------------------------------------------           80339161
*                                                                       80367061
*  PART 5: DATA FOR READ OF RECONFIGURATION REQUEST RECORD              80394961
*          FROM A DEVICE/PATH PAIR                                      80422861
*                                                                       80450761
QRRRRDAT DS    A         VIRT.ADDRESS OF BUFFER FOR RSSD COM.           80478661
QRRRRDAL DC    A(4096)   LENGTH OF AREA ADDRESSED BY QRRRRDAT           80506561
         DS    0D                                                       80534461
QRRRRCCP DS    0XL16    CCB FIELDS FOR PRINTING                         80562361
QRRRRCCC EQU   QRRRRCCB+8   CHPID MASK IN CCB                           80590261
QRRRRCCB CCB   SYSRDR,QRRRRCC1,X'1400',QRSNSDAT                @DAOM    80618100
QRRRRCC1 CCW   PSF,QRPSFDAT,X'60',L'QRPSFDAT                            80729700
QRRRRCC2 CCW   RSSD,QRIOIDAL,IDAL,4096                                  80757661
QRPSFDAT DC    XL12'180000000000030000000000'   READ ATTN MSG           80813461
QRIOIDAL DC    5A(0)                                                    80841361
* -----------------------------------------------------------           80869261
*                                                                       80897161
*  PART 6: CCB AND CCWS FOR MESSAGES OF QUIESCE RESUME                  80925061
*                                                                       80952961
         DS    0D                                                       80980861
QRMSGCCB CCB   SYSLOG,QRMSGCCW,X'1500'                                  81008761
QRMSGCCW CCW   X'09',0,SLI,0                                            81036661
         CCW   X'09',0,SLI,0                                            81064561
QRMSGNUM EQU   (*-QRMSGCCW)/8    NUMBER OF LINES                        81092461
QRMSGLEN EQU   64                LENGTH OF ONE LINE                     81120361
QRMSGBUF DC    (QRMSGNUM*QRMSGLEN)X'00'                                 81148261
QREMSGBU DC    A(*)              PTR TO 1ST BYTE BEHIND BUFF.           81176161
*                                                                       81177300
*    THE ROUTINE TO STOP PROCESSING IF THE ROUTINE NOTICED              81178500
*    AN INTERNAL ERROR.                                                 81179700
*                                                                       81180900
.*   SINCE INLINE CODE GENERATION FOR AMODESW RETURN IN THE             81182100
.*   SUPERVISOR IS DIFFERENT FROM $IJBCUIR GENERATION, THIS             81183300
.*   ROUTINE MUST BE LOCATED AT THE END OF DSECT QRSGLOB.               81184500
.*                                                                      81185700
QRHWSAVE DC    16A(0)               EMERGENCY SAVE AREA                 81186900
         USING QRSGLOB,RA           REGISTER RA IS ALWAYS SET           81188100
QRHARDWT ST    R5,Q@SHARDW          STORE REASON FOR THIS      @D61SDJK 81189300
         TM    QRPRCFLG,QRPRCFGV    DO WE OWN ANY GETVIS       @D61SDJK 81191700
         BZ    QRHARD00             NO,                        @D61SDJK 81192900
         FREEVIS SPID=QRSGVSPN,SVA=YES                         @D61SDJK 81195300
         NI    QRPRCFLG,X'FF'-QRPRCFGV IND.WE RELEASED GETVIS  @DAOM    70109861
QRHARD00 DS    0H                   MEND                       @D61SDJK 81196500
         LM    R0,RF,QRHWSAVE       RESTORE ALL REGS                    81197700
         DROP  RA                   RELEASE QRSGLOB BASE                81200100
         SVC   33                   RESTORE TCB SAVE AREA      @D61SDJK 81198900
         AMODESW RETURN,REG=(RE)    RETURN ================>>>          81201300
*                                                                       81202500
.*                                                                      81204061
* -----------------------------------------------------------           81231961
*                                                                       81259861
*  SUPERVISOR PRINT CONTROL                                             81287761
*                                                                       81315661
         AIF   (NOT &SGCUIR).NPRT020                                    81343561
         PRINT NOGEN                                                    81371461
.NPRT020 ANOP                                                           81399361
         MEND                                                           91950061
