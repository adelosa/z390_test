         MACRO                                                          00050000
         DTSMCIC                                                        00100000
   TITLE 'DOS SUPERVISOR - MACRO DTSMCIC           ICCF MONITOR CALL IN*00150000
               TERRUPTION INTERCEPT.'                          @D368DUL 00200000
*****************************************************          @D31BDHB 00500000
*                                                   *          @D31BDHB 00550000
*   LICENSED MATERIALS - PROPERTY OF IBM            *          @D31BDHB 00600000
*   THIS MACRO IS "RESTRICTED MATERIALS OF IBM"     *          @D31BDHB 00650061
*   5686-066 (C) COPYRIGHT IBM CORP. 1977, 1995     *          @D32BDHB 00700061
*   SEE COPYRIGHT INSTRUCTIONS                      *          @D31BDHB 00750000
*                                                   *          @D31BDHB 00800000
*****************************************************          @D31BDHB 00850000
*                                                            * @D368DUL 00900000
************************************************************** @D368DUL 00950000
          SPACE 5                                              @D368DUL 01000000
************************************************************** @D35ZDUL 01050000
*                                                            * @D35ZDUL 01100000
*                                                            * @D35ZDUL 01150000
*    MACRO NAME =  DTSMCIC                                   * @D35ZDUL 01200000
*                                                            * @D35ZDUL 01250000
*    DESCRIPTIVE NAME =  ICCF MONITOR CALL INTERRUPTION      * @D35ZDUL 01300000
*                        INTERCEPT ROUTINE                   * @D35ZDUL 01350000
*                                                            * @D35ZDUL 01400000
*                                                            * @D35ZDUL 01450000
*    FUNCTION =                                              * @D35ZDUL 01500000
*       THIS ROUTINE HANDLES MONITOR CALLS ISSUED BY         * @D35ZDUL 01550000
*       ICCF INTERACTIVE PARTITIONS.                         * @D35ZDUL 01600000
*       NON-ICCF MONITOR CALLS ARE PASSED BACK TO THE        * @D35ZDUL 01650000
*       PROGRAM CHECK HANDLER, AND ICCF MONITOR CALLS        * @D35ZDUL 01700000
*       WHICH DO NOT COME FROM THE ICCF PARTITION ARE        * @D35ZDUL 01750000
*       IGNORED.                                             * @D35ZDUL 01800000
*       VALID ICCF INTERACTIVE PARTITION MONITOR CALLS ARE:  * @D35ZDUL 01850000
*       MONITOR CLASS: 4                                     * @D35ZDUL 01900000
*       MONITOR CODES: 0-1023                                * @D35ZDUL 01950000
*       CODE  0 --->   SET PSW KEY ZERO,                     * @D35ZDUL 02000000
*                      HAVE SLA REQUEST CHECKED FOR VALIDITY * @D36ZDUL 02050000
*       CODE  1 --->   SET PSW KEY TO SPECIFIED VALUE - FAST * @D37EDUL 02100000
*                      PATH RESERVED FOR ICCF FILE ROUTINE   * @D37EDUL 02150000
*       CODE  2 --->   CANCEL INTERACTIVE PARTITION BECAUSE  * @D36ZDUL 02200000
*                      OF JOBCOM REQUEST (NOT SUPPORTED)     * @D36ZDUL 02250000
*       CODE  3 --->   SET PSW KEY TO INTERACTIVE PARTN KEY, * @D35ZDUL 02300000
*                      PASS ON TO ICCF IF RETURN CODE IN-    * @D36ZDUL 02350000
*                      DICATES THAT A LABEL WAS TOO LONG FOR * @D35ZDUL 02400000
*                      THE LABEL ROUTINE SVA BUFFER.         * @D35ZDUL 02450000
*       CODE  4 --->   PASS TO ICCF WITHOUT GOING THRU GENENT* @D36ZDUL 02500000
*                      BECAUSE REQUEST IS FROM SUPERVISOR.   * @D36ZDUL 02550000
*                      PASS BACK INTERACTIVE PARTITION COMREG* @D36ZDUL 02600000
*                      IN REGISTER OF PRECEDING INSTRUCTION  * @D36ZDUL 02650000
*       CODE  5 --->   SAME AS MC 4, BUT REQUEST IS ISSUED   * @D36ZDUL 02700000
*                      BY PROBLEM PROGRAM. (NORMAL SAVE CON- * @D36ZDUL 02750000
*                      VENTIONS)                             * @D36ZDUL 02800000
*       CODES 6 - 1023 ARE PASSED TO ICCF CONTROL PROGRAM    * @D35ZDUL 02850000
*                                                            * @D35ZDUL 02900000
*      REGISTER CONVENTIONS =                                * @D35ZDUL 02950000
*         REGISTER  6 - ADDRESS OF DISPATCHER SET BY GENENT  * @D368DUL 03000000
*         REGISTER  9 - ADDRESS OF ICCF VECTOR TABLE         * @D368DUL 03050000
*         REGISTER 10 - ADDRESS OF REQUESTOR'S TIB AND TCB   * @D368DUL 03100000
*         REGISTER 11 - ADDRESS OF LOW CORE SAVE AREA 'ERA'  * @D368DUL 03150000
*                     - ADDRESS OF REQUESTOR'S SAVE AREA     * @D368DUL 03200000
*                     - WORK REGISTER                        * @D368DUL 03250000
*         REGISTER 12 - MONITOR CALL CODE                    * @D35ZDUL 03300000
*                     - RETURN ADDRESS FROM ICCF APPENDAGE   * @D368DUL 03350000
*         REGISTER 13 - MODULE BASE REGISTER                 * @D368DUL 03400000
*                     - ENTRY ADDRESS OF ICCF APPENDAGE      * @D368DUL 03450000
*                     - AT RETURN FROM ICCF A BRANCH OFFSET  * @D368DUL 03500000
*                                                            * @D35ZDUL 03550000
*    EXIT-NORMAL =                                           * @D35ZDUL 03600000
*      IF ICCF MONITOR CALL CODE                             * @D35ZDUL 03650000
*           ISSUED FROM A INTERACTIVE PARTITION              * @D35ZDUL 03700000
*                               ---> VIA DISPATCHER TO CALLER* @D35ZDUL 03750000
*           ISSUED FOR A INTERACTIVE PARTITION               * @D35ZDUL 03800000
*           FROM THE SUPERVISOR ---> TO CALLER DIRECTLY      * @D35ZDUL 03850000
*                                                            * @D37EDUL 03900000
*           MONITOR CALL CODE 1 ---> TO CALLER DIRECTLY      * @D37EDUL 03950000
*                                    ( FAST PATH MC)         * @D37EDUL 04000000
*                                                            * @D35ZDUL 04050000
*      IF NOT ICCF MONITOR CALL ---> PROGRAM CHECK HANDLER   * @D35ZDUL 04100000
*                                                            * @D35ZDUL 04150000
*      CHANGE ACTIVITY:                                      * @DA41654 04166652
*             92/06/16  HARDWAIT DUE TO RID 00 AFTER MC      * @DA41654 04183252
*                                                            * @D35ZDUL 04200000
************************************************************** @D35ZDUL 04250000
         USING DTSMCENT,RD                                     @D61RDOW 04266661
DTSMCENT DS    0H                                              @D61RDOW 04283261
         CLI   MONCLASS+1,MCETSS  IS IT ICCF MONITOR CLASS ?   @D35ZDUL 04300000
         BNE   DTSMCEX            NO, RETURN TO PGM CHCK HANDL @D35ZDUL 04350000
         L     RA,TIBPTR          REQUESTOR'S TIB POINTER      @D37EDUL 04400000
         USING TIBADR,RA                                       @D61RDOW 04425061
         L     RC,MONCADR         GET MONITOR CALL CODE        @D35ZDUL 04450000
         BCT   RC,DTSMC           FALL THROUGH FOR CODE 1      @D37EDUL 04500000
         SPACE 3                                               @D37EDUL 04550000
*        FAST PATH MONITOR CALL                                @D37EDUL 04600000
DTSMC1   EQU   *                  CODE 1 --> FAST PATH KEY CHG @D37EDUL 04650000
         TM    TIBFLAG2,ICCFPP    MC FROM INTERACTIVE PARTITN? @D37EDUL 04750000
         BZ    DTSMCRTS           NO,TREAT MONITOR CALL AS NOP @D37EDUL 04800000
         STH   R0,MCSAVE          SAVE SPECIFIED PROTECTION KY @D37EDUL 04850000
         MVZ   MCSAVE(1),PCOLDKYS   SAVE CURRENT PSW KEY       @D14NDHB 04900000
         MVZ   PCOLDKYS(1),MCSAVE+1 SET SPECIFIED PSW KEY      @D14NDHB 04950000
         ICM   R0,2,MCSAVE        PASS BACK CURRENT PSW KEY    @D37EDUL 05000000
DTSMCRTS LM    R9,RD,ERA          RESTORE CALLER'S REGISTERS   @D37EDUL 05050000
         LPSW  PCOLDPS            RETURN TO CALLER DIRECTLY    @D14NDHB 05100000
         SPACE 3                                               @D37EDUL 05150000
DTSMC    EQU   *                                               @D37EDUL 05200000
         LA    RC,1(RC)           RESTORE MONITOR CALL CODE    @D37EDUL 05250000
         C     RC,ICCFMCX         IS IT AN ICCF RESERVED CODE? @D35ZDUL 05300000
         BH    DTSMCEX            NO, RETURN TO PGM CHCK HANDL @D35ZDUL 05350000
         TM    TIBFLAG2,ICCFPP    MC FROM INTERACTIVE PARTITN? @D36ZDUL 05450000
         DROP  RA                                              @D36ZDUL 05500000
         L     RA,TCBPTR          REQUESTOR'S TASK BLOCK       @D36ZDUL 05550000
         USING TCBADR,RA                                       @D36ZDUL 05600000
         BO    DTSMCICF           YES, HANDLE MONITOR CALL     @D36ZDUL 05650000
DTSMCRT  L     RB,IJBSMCOM        STOR. MANAGMNT COMM. AREA    @D31BDRP 05700000
         USING SMCOM,RB                                        @D31BDRP 05750000
         CLC   PCOLDPS+5(3),SMCSSEND+1 REQUEST FROM SUPERVISOR?@D31BDRP 05800000
         DROP  RB                                              @D31BDRP 05850000
         BNH   DTSMCRTS           YES, RETURN VIA LPSW         @D36ZDUL 05900000
DTSMCRTD EQU   *                  RETURN VIA DISPATCHER        @D36ZDUL 05950000
         CLI   RID+D1,ZERO        INTERR STATUS ALREADY SAVED  @DA36572 06000000
         BER   R6                 YES                          @DA36572 06050000
         LA    RC,PCOLDPS         IDENTIFY PGM CHECK PSW       @D14NDHB 06100000
         XR    RB,RB              CLEAR REG FOR IDUMP          @D14NDHB 06150000
         L     RD,AFLIH           BASE FOR GENENT              @D61RDOW 06166661
         USING FLIH,RD                                         @D61RDOW 06183261
         BAL   R9,GENENT          SAVE INTERRUPT STATUS        @D36ZDUL 06200000
         USING DTSMCENT,RD                                     @D61RDOW 06225061
         BR    R6                                              @D35ZDUL 06250000
         SPACE 3                                               @D35ZDUL 06350000
DTSMCICF LA    RB,MCTABLN         CODES HANDLED BY SUPV RTN    @D36ZDUL 06400000
         CR    RC,RB              IS CODE TO BE PASSED TO ICCF?@D36ZDUL 06450000
         BNL   DTSMC2             YES, GO TO ICCF APPENDAGE    @D37EDUL 06500000
         SLL   RC,2               CODE * 4 IS TABLE OFFSET     @D35ZDUL 06550000
         L     RC,MCTAB(RC)       GET ADDRESS OF MON CALL RTN  @D35ZDUL 06600000
         BR    RC                 BRANCH TO APPROPRIATE RTN    @D35ZDUL 06650000
         SPACE 3                                               @D35ZDUL 06700000
DTSMC0   EQU   *                  CODE 0 --> ZERO PSW KEY      @D35ZDUL 06750000
         L     R9,IJBETSS         ADDRESS OF ICCF VECTOR TABLE @D36ZDUL 06800000
         USING DTSVECDS,R9                                     @D36ZDUL 06850000
         MVZ   DTSPKEY,PCOLDKYS   SAVE CURRENT PSW KEY         @D14NDHB 06900000
         DROP  R9                                              @D61RDOW 06925061
         NI    PCOLDKYS,X0F       ZERO PSW KEY                 @D14NDHB 06950000
         SPACE 3                                               @D35ZDUL 07000000
DTSMC2   EQU   *                  CODE 2 --> CANCEL PP         @D35ZDUL 07050000
DTSMC3   EQU   *                  CODE 3 --> SET PSW TO PP KEY @D35ZDUL 07100000
DTSMC5   EQU   *                  CODE 5 --> RETURN PP COMR PT @D35ZDUL 07150000
         LA    RC,PCOLDPS         IDENTIFY PGM CHECK PSW       @D14NDHB 07200000
         L     RD,AFLIH           BASE FOR GENENT              @D61RDOW 07216661
         USING FLIH,RD                                         @D61RDOW 07233261
         BAL   R9,GENENT          SAVE INTERRUPT STATUS        @D35ZDUL 07250000
         DROP  RD                 SETUP TEMP BASE              @D61RDOW 07258361
         USING *,R9               ...                          @D61RDOW 07266661
         L     RD,ADTSMCEN        LOAD BASE                    @D61RDOW 07274961
         DROP  R9                                              @D61RDOW 07283261
         USING DTSMCENT,RD                                     @D61RDOW 07291561
         SPACE 3                                               @D35ZDUL 07300000
DTSMC4   EQU   *                  CODE 4 --> RETURN PP COMR PT @D35ZDUL 07350000
         L     R9,IJBETSS         ADDRESS OF ICCF VECTOR TABLE @D35ZDUL 07400000
         USING DTSVECDS,R9                                     @D61RDOW 07425061
         TM    DTSFLG2,DTSCANCL   PGM CHECK IN ICCF SUPVR CDE? @D35ZDUL 07450000
         BO    DTSMCRTD           YES, IGNORE ANY SUBSEQNT MC  @DA41654 07500052
         XC    DTSFLAGS,DTSFLAGS  CLEAR ANY OLD INFORMATION    @D35ZDUL 07550000
         MVC   DTSMCC(2),MONCADR+2  PASS MONITOR CALL CODE     @D35ZDUL 07600000
         L     RB,TCBSAVE         SAVE AREA ADDR OF PSEUDO PRT @D36ZDUL 07650000
         STCM  RB,M7,DTSCUSA+1    PASS CURRENT SAVE AREA ADDR  @D36ZDUL 07700000
         L     RD,DTSMCAP         ADDRESS OF ICCF M C APPENDGE @D36ZDUL 07750000
         DROP  R9                                              @D61RDOW 07766661
         DROP  RD                                              @D61RDOW 07783261
         MVI   RID+D1,ETSSID      SET ID FOR DISABLED ICCF RTN @D35ZDUL 07800000
         LA    RB,ERA             PASS ERA ADDRESS TO ICCF     @D35ZDUL 07850000
         BALR  RC,RD              TO ICCF MON CALL APPENDAGE   @D36ZDUL 07900000
*        ALL REGISTERS WILL BE PRESERVED EXCEPT R9-R13         @D36ZDUL 07950000
*        BY THE ICCF MONITOR CALL INTERCEPT ROUTINE            @D35ZDUL 08000000
*        RD --> 0    RETURN TO SUPERVISOR ROUTINE              @D36ZDUL 08050000
*        RD --> 4    RETURN TO CALLER VIA DISPATCHER           @D36ZDUL 08100000
DTSMCRET MVI   RID+D1,SYSTEMID    RESET ID TO PFFLIH RID       @D36ZDUL 08150000
         L     RD,DTSMCBR-DTSMCRET(RC,RD) LOAD BRANCH ADDRESS  @D36ZDUL 08200000
         BR    RD                 RETURN TO APPROPRIATE RTN    @D36ZDUL 08250000
         DS    0F                                              @D37EDUL 08300000
DTSMCBR  DC    A(DTSMCRTS)        RETURN TO SUPERVISOR RTN     @D36ZDUL 08350000
         DC    A(DISP)            RETURN TO CALLER VIA DISP    @D36ZDUL 08400000
         DROP  RA                                              @D36ZDUL 08450061
ICCFMCX  DC    F'1023'            HIGHEST ICCF MON CALL CODE   @D35ZDUL 08500000
MCTAB    DC    A(DTSMC0)          ADDRESS OF MC CODE 0 ROUTINE @D35ZDUL 08550000
         DC    A(DTSMC1)          ADDRESS OF MC CODE 1 ROUTINE @D35ZDUL 08600000
         DC    A(DTSMC2)          ADDRESS OF MC CODE 2 ROUTINE @D35ZDUL 08650000
         DC    A(DTSMC3)          ADDRESS OF MC CODE 3 ROUTINE @D35ZDUL 08700000
         DC    A(DTSMC4)          ADDRESS OF MC CODE 4 ROUTINE @D35ZDUL 08750000
         DC    A(DTSMC5)          ADDRESS OF MC CODE 5 ROUTINE @D35ZDUL 08800000
MCTABLN  EQU   (*-MCTAB)/4        HIGHEST MC CODE HANDLED IN SV@D35ZDUL 08850000
ADTSMCEN DC    A(DTSMCENT)        BASE OF ICCF HOOK IN SGPCK   @D61RDOW 08875061
MCSAVE   DC    H'0'               PSW KEY SETTING WORK AREA    @D37EDUL 08900000
DTSMCEX  DS    0H                 RETURN POINT TO PGM CHCK HDL @D35ZDUL 08950000
         L     RD,AFLIH           BASE OF PROGRAM CHECK HANDLER@D61RDOW 08980061
         USING FLIH,RD                                         @D61RDOW 09010061
         B     PCKDTSRT           RETURN TO SGPCK              @D61RDOW 09040061
         DROP  RD                                              @D61RDOW 09070061
         MEND                                                  @D35ZDUL 09100000
