         MACRO                                                          00050000
         SUPVR &ID=1,                                          @D51GDMZ*00100000
               &MICR=,                                         @D52VDMZ*00133301
               &MODE=,       | OBSOLETE SINCE AF 6.1.0         @D61NDMZ*00166601
               &NPARTS=,     | OBSOLETE SINCE AF 5.1.0         @D51GDMZ*00200000
               &NTASKS=,     | OBSOLETE SINCE REL.4            @D14BDFG*00250000
               &VM=,         | OBSOLETE SINCE REL.4            @D14BDFG*00300000
               &TP=,         | OBSOLETE SINCE REL.3            @D14ZDFG*00350000
               &SEGMENT=     | OBSOLETE                        @D36ZDAZ 00400000
***************************************************************         00450002
.*       IBM DISK OPERATING SYSTEM                            *         00550002
*        SUPERVISOR - SUPVR - 5686-CF7-06                     *         00600003
.*                                                            *         00650002
*        LICENSED MATERIALS - PROPERTY OF IBM                 *         00700002
*        "RESTRICTED MATERIALS OF IBM"                        *         00750002
*        5686-CF7                                             *         00800003
*        (C) COPYRIGHT IBM CORP. 1977, 2004                   *         00810003
***************************************************************         00860002
*                                                                       00900002
.* CHANGE ACTIVITIES                                          *@D31UDMZ 00950000
.*  AF 5.1.0                                                  *@D51GDMZ 01000000
.*  AF 6.1.0                                                  *@D61NDMZ 01025001
.*  MORE THAN 255 TASKS                                       *@D66CDOW 01026001
.*                                                            *         01050002
.**************************************************************         01100002
         GBLA  &AG15              CHECK SEQU.OF REQUIRED MACROS@D349DEM 01150000
         GBLA  &NPART             NUMBER OF PART.SPECIFIED     @D36ZDEM 01200000
         GBLB  &BG35,&BG36        MICR GEN./1419D IN ADDITION  @D349DEM 01300000
         GBLB  &BG370             370-MODE                     @D35CDEM 01350000
         GBLB  &IJBSGEN           SET FOR SUPGEN TO CALL SGEND @DM08813 01400000
         GBLB  &BGVM              VM LINKAGE                   @D35NDEM 01450000
         GBLB  &BGXA              ESA/VMESA SUPPORT            @D51GDTP 01500000
         GBLB  &BGINV             STOP SUPERVISOR GENERATION   @D51GDMZ 01550000
         GBLC  &P(13)             PARTITION IDENTIFIERS        @D61NDMZ 01583301
         GBLC  &CGP(12)           PARTITION IDENTIFIERS        @D61NDMZ 01616601
         GBLC  &CGSEGM                                         @D34RDEM 01650000
         LCLA  &X                                                    JS 01700000
         LCLC  &CLID              TEMP. CODE                   @DLSUPID 01750000
&IJBSGEN SETB  1                  SET TO 1 FOR SEND MACRO      @DM08813 01800000
         AIF   (&AG15 EQ 0).DISK                                        01850000
         MNOTE 7,'MACRO SEQUENCE ERROR'                                 01900000
         MEXIT                                                          01950000
.DISK    ANOP                                                           02000000
&AG15    SETA  1                                                        02050000
&CGP(1)    SETC  'BG'                                                JS 02100000
&CGP(2)    SETC  'F1'                                                JS 02150000
&CGP(3)    SETC  'F2'                                                JS 02200000
&CGP(4)    SETC  'F3'                                                JS 02250000
&CGP(5)    SETC  'F4'                                                JS 02300000
&CGP(6)    SETC  'F5'                                          @D34RDEM 02350000
&CGP(7)    SETC  'F6'                                          @D34RDEM 02400000
&CGP(8)    SETC  'F7'                                          @D34RDEM 02450000
&CGP(9)    SETC  'F8'                                          @D34RDEM 02500000
&CGP(10)   SETC  'F9'                                          @D34RDEM 02550000
&CGP(11)   SETC  'FA'                                          @D34RDEM 02600000
&CGP(12)   SETC  'FB'                                          @D34RDEM 02650000
&NPART     SETA   12        SUPPORT 12 STATIC PARTITIONS       @D51GDMZ 02700000
&CGSEGM    SETC  'ALL'                                         @D51GDMZ 02750000
.************* CHECK MODE SPECIFICATION                        @D35EDEM 02800000
&BGXA    SETB  1                    ESA MODE                   @D53BDMZ 02807101
&BG370   SETB  1                    MUST BE SET WHEN ESA MODE  @D53BDMZ 02814201
         AIF   (T'&MODE EQ 'O').MODFIN                         @D61NDMZ 02821301
         AIF   ('&MODE' NE 'ESA').MNOT16                       @D61NDMZ 02828401
         MNOTE 1,'THE MODE SPECIFICATION IS OBSOLETE. THE FORMERLY MODEX02835501
               =ESA SUPERVISOR IS THE STANDARD SUPERVISOR.'             02842601
         AGO   .MODFIN                                         @D53DDMZ 02849701
.MNOT16  ANOP                                                  @D53DDMZ 02856801
         MNOTE 16,'THE MODE SPECIFICATION IS OBSOLETE. THE FORMERLY MODX02863901
               E=ESA SUPERVISOR IS THE STANDARD SUPERVISOR. GENERATION X02871001
               IS SUPPRESSED.'                                          02878101
&BGINV   SETB  1                    STOP SUPERVISOR GENERATION @D51GDMZ 02885201
         MEXIT                                                 @D51GDMZ 02892301
.MODFIN  ANOP                                                  @D35CDEM 03800000
.*                                                             @D349DEM 03850000
.**** CHECK OBSOLETE PARAMETERS                                @D349DEM 03900000
.*                                                             @D349DEM 03950000
.************* CHECK NPARTS SPECIFICATION                      @D349DEM 04000000
         AIF   (T'&NPARTS EQ 'O').NPEX1                        @D51GDMZ 04050000
         MNOTE 1,'NPARTS SPECIFICATION IS OBSOLETE AND WILL BE IGNORED' 04100000
.NPEX1   ANOP                                                        JS 04150000
.************* CHECK NTASKS SPECIFICATION                      @D36IDEM 04200000
         AIF   (T'&NTASKS EQ 'O').TSK00                        @D14BDFG 04250000
         MNOTE 1,'NTASKS OPERAND IS OBSOLETE AND WILL BE IGNORED'       04300000
.TSK00   ANOP                                                  @D14BDFG 04350000
.************* CHECK VM SPECIFICATION                          @D349DEM 04600000
         AIF   (T'&VM EQ 'O').VMFIN                            @D14BDFG 04650000
         MNOTE 1,'VM OPERAND IS OBSOLETE AND WILL BE IGNORED'  @D51GDMZ 04700000
.VMFIN   ANOP                                                  @D349DEM 04750000
.************* CHECK SEGMENT SPECIFICATION                     @D36ZDAZ 04800000
         AIF   (T'&SEGMENT  EQ 'O').SEGMFIN                    @D51GDMZ 04850000
         MNOTE 1,'SEGMENT SPECIFICATION IS OBSOLETE AND WILL BE IGNOREDX04900000
               .'                                              @D51GDMZ 04950000
.SEGMFIN ANOP                                                  @D36ZDAZ 05000000
.************* CHECK OBSOLETE SPECIFICATIONS                   @D36EDEM 05050000
         AIF   (T'&TP EQ 'O').MNOTFIN                          @D36EDEM 05100000
         MNOTE 1,'SPECIFICATION OF TP=&TP IS OBSOLETE AND WILL BE IGNORX05150000
               ED.'                                            @D37DDEM 05200000
.MNOTFIN ANOP                                                  @D36EDEM 05250000
.************* MICR=NO IS STANDARD IN AF 5.2.0 FOR ALL MODES   @D52VDMZ 05275001
         AIF   (T'&MICR EQ 'O').MICRFIN                        @D52VDMZ 05300001
         MNOTE 3,'MICR IS NO LONGER SUPPORTED. MICR=NO IS STANDARD'     05350001
.MICRFIN ANOP                                                  @D349DEM 06000000
.*************                                                 @D349DEM 06050000
.*       12 STATIC PARTITIONS ARE SUPPORTED                             06100000
&P(1)    SETC  'BG'                                                  JS 06150000
&P(2)    SETC  'FB'                                            @D36IDEM 06200000
&P(3)    SETC  'FA'                                            @D36IDEM 06250000
&X       SETA  10                                              @D36IDEM 06300000
.NXTID   ANOP                                                        JS 06350000
&X       SETA  &X-1                                                  JS 06400000
         AIF   (&X EQ 0).ALLID                                       JS 06450000
&P(&NPART-&X+1) SETC  'F&X'                                          JS 06500000
         AGO   .NXTID                                                JS 06550000
.ALLID   ANOP                                                        JS 06600000
&P(13)   SETC  'AR'                                            @D61NDMZ 06625001
.************* CHECK ID SPECIFICATION                          @D349DEM 06650000
&CLID    SETC  '1'            DEFAULT ID = 1                   @DLSUPID 06700000
         AIF   (K'&ID NE 1).IDWRONG  INVALID ID                @D357DEM 06750000
&CLID    SETC  '&ID'              COPY ID                      @DLSUPID 06800000
         AGO   .IDX1                                           @DLSUPID 06850000
.IDWRONG MNOTE 3,'ID NOT 1 CHARACTER , - ID=1 ASSUMED - '      @DLSUPID 06900000
.IDX1    PUNCH '         PHASE $$A$SUP&CLID.,+0,NOAUTO'        @DLSUPID 06950000
.IDFIN   ANOP                                                  @D36ZDEM 07000000
         MEND                                                           07050000
