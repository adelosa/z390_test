         MACRO                                                 @D35EDKS 00001007
&L       MAPSVIPL &DSECT=                                      @D51GDUL 00002007
         GBLA  &AGMINSG           GETVIS SIZE FOR DUMPS        @D51IDUL 00003007
         GBLA  &AGMINSZ           MINIMUM PARTITION SIZE IN K  @D51GDUL 00004007
         GBLA  &AGMINGV           MINIMUM PARTITION GETVIS IN K@D51GDUL 00005007
         GBLA  &AGNPGR            SIZE OF PROGR.LUB POOL       @D51GDUL 00006007
         GBLA  &AGPSIZB           PAGE SIZE IN BYTES           @D51GDUL 00007007
         GBLA  &AGPHLSL           LENGTH OF LOAD TABLE/PART.   @D51GDUL 00008007
         GBLA  &AGSYSLB           NUMBER OF SYST.LUB'S/PART.   @D51GDUL 00009007
         GBLA  &NPART             NUMBER OF PART.SPECIFIED     @D51GDUL 00010007
         LCLA  &ABMIN             MIN BUFSIZE REQUIREMENT      @D51GDUL 00011007
         LCLA  &ABDEF             DEF BUFSIZE SETTING          @D51GDUL 00012007
         LCLA  &ACCWBL            LENGTH OF ONE COPY BUFFER    @D51GDUL 00013007
         LCLA  &ALDFPSZ           GETVIS SIZE FOR DASDFP       @D52VDUL 00014007
         LCLA  &ALJCLAR           SIZE OF JCL BUFFERS PER PART.@D51GDUL 00015007
         LCLA  &ALPADEF           DEFAULT SIZE OF PASIZE IN BY @D52VDUL 00016007
         LCLA  &ALPAMIN           INIT. TO 1M (-> 256M VSIZE)  @D61BDUL 00017007
         LCLA  &ALPFX24           SYSTEM PFIX COUNT / PAGES    @D52VDUL 00018007
         LCLA  &ALPFX31           SYSTEM PFIX COUNT / PAGES    @D52VDUL 00019007
         LCLA  &ALPSIZK           PAGE SIZE IN K               @D51GDUL 00020007
         LCLA  &ALRDEF            DEFAULT SIZE OF RSIZE IN BY  @D52VDUL 00021007
         LCLA  &ALSLAR            SIZE OF SLA SYSTEM AREA      @D51GDUL 00022007
         LCLA  &ALSPDEF           DEFAULT SIZE OF SPSIZE IN BY @D52VDUL 00023007
         LCLA  &ALSPMIN           MINIMUM SIZE OF SPSIZE IN BY @D52VDUL 00024007
         LCLA  &ALSVIS            24 BIT SYS GETVIS SIZE IN BY @D52VDUL 00025007
         LCLA  &ALSV31            31 BIT SYS GETVIS SIZE IN BY @D52VDUL 00026007
         LCLA  &ALVIOMX           MAXIMUM SIZE OF VIO IN K     @D52VDUL 00027007
         LCLA  &ALVPDEF           DEFAULT SIZE OF V-POOL IN K  @D52VDUL 00028007
         LCLA  &ALVSDEF           MINIMUM SIZE OF VSIZE IN K   @D52VDUL 00029007
         LCLA  &ALVSLIM           LIMIT VALUE OF VSIZE IN K    @D61BDUL 00030007
         LCLA  &ALVSMAX           MAXIMUM SIZE OF VSIZE IN K   @D52VDUL 00031007
         LCLA  &ALVSMIN           MINIMUM SIZE OF VSIZE IN K   @D52VDUL 00032007
         LCLA  &AL1,&AL2,&AL3                                  @D52VDUL 00033007
         LCLA  &CBBWPTR           OFFSET OF BACKWARD POINTER   @D51GDUL 00034007
         LCLA  &CBFWPTR           OFFSET OF FORWARD POINTER    @D51GDUL 00035007
         LCLB  &IPL               DSECT SWITCH                 @D51GDUL 00036007
         LCLC  &C,&S              DSECT/AREA GENERATION CONTROL@D51GDUL 00037007
&ACCWBL  SETA  72                 SET BUFFER LENGTH FOR IPL    @D51GDUL 00038007
&CBBWPTR SETA  &ACCWBL-8          SET OFFSET OF BW POINTER     @D51GDUL 00039007
&CBFWPTR SETA  &ACCWBL-4          SET OFFSET OF FW POINTER     @D51GDUL 00040007
         AIF   ('&DSECT' EQ 'NO').NODS1                        @D51GDUL 00041007
*************************************************************  @D368DPS 00042007
*                                                           *  @D368DPS 00043007
*        SUPERVISOR - MAPSVIPL                              *  @D31BDUL 00044007
*                                                           *  @D368DPS 00045007
*        LICENSED MATERIALS - PROPERTY OF IBM               *  @D31BDUL 00046007
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"        *  @D31BDUL 00047007
*        5686-CF7 (C) COPYRIGHT IBM CORP. 1979, 2004        *  @D68DDUL 00048014
*        SEE COPYRIGHT INSTRUCTIONS                         *  @D31BDUL 00049007
*                                                           *  @D368DPS 00050007
*************************************************************  @D368DPS 00051007
         SPACE 5                                               @D368DPS 00052007
*************************************************************  @D35EDKS 00053007
*                                                           *  @D35EDKS 00054007
*     DSECT FOR COMMUNICATION AREA OF IPL WITH SUPERVISOR   *  @D35EDKS 00055007
*                                                           *  @D35EDKS 00056007
*************************************************************  @D35EDKS 00057007
         SPACE 1                                               @D35EDKS 00058007
&IPL     SETB  1                  AVOID ASSEMBLY ERRORS        @D51GDUL 00059007
&C       SETC  'S'                SET TO 'DS' DEFINITION       @D51GDUL 00060007
&S       SETC  ' '                SET SEPARATOR                @D51GDUL 00061007
         AIF   ('&L' EQ '').GENDEF                             @D35EDKS 00062007
&L       DSECT .                                               @D35EDKS 00063007
         AGO   .GENDEF1                                        @D63JDUL 00064007
.GENDEF  ANOP  .                                               @D35EDKS 00065007
SVICOM   DSECT .                                               @D35EDKS 00066007
.GENDEF1 ANOP  .                                               @D63JDUL 00067007
IPLSVLVL IPLSVLVL DSECT=YES                                    @D63JDUL 00068007
         AGO   .GENSVI            GO GENERATE DSECT            @D51GDUL 00069007
.NODS1   ANOP                                                  @D51GDUL 00070007
         TITLE 'VSE/AF SUPERVISOR  MAPSVIPL     IPL COMMUNICATION AREA' 00071007
&C       SETC  'C'                SET TO 'DC' DEFINITION       @D51GDUL 00072007
&S       SETC  ''                 NO SEPARATOR                 @D51GDUL 00073007
&ALPSIZK SETA  &AGPSIZB/1024      PAGE SIZE IN K               @D51GDUL 00074007
.************************************************************* @D51GDUL 00075007
.*                                                           * @D51GDUL 00076007
.*      DEFAULT VPOOL :                   64K                * @D51GDUL 00077007
.*      MAXIMUM VIO :                    128M                * @D51GDUL 00078007
.*                                                           * @D51GDUL 00079007
.************************************************************* @D51GDUL 00080007
&ALVPDEF SETA  64                 DEFAULT SIZE OF V-POOL IN K  @D51GDUL 00081007
&ALVIOMX SETA  128*1024           MAXIMUM SIZE OF VIO IN K     @D51GDUL 00082007
.************************************************************* @D51GDUL 00083007
.*                                                           * @D51GDUL 00084007
.*      MINIMUM VSIZE - ESA      :        16M                * @D52VDUL 00085007
.*      MAXIMUM VSIZE - ESA      :        90G                * @D52VDUL 00086007
.*      DEFAULT VSIZE :                   32M                * @D61BDUL 00087007
.*                                                           * @D51GDUL 00088007
.************************************************************* @D51GDUL 00089007
&ALVSMIN SETA  16*1024            MINIMUM VSIZE IN K BLOCKS    @D52VDUL 00090007
&ALVSDEF SETA  32*1024            DEFAULT VSIZE IN K BLOCKS    @D61BDUL 00091007
&ALVSMAX SETA  90*1024*1024       ESA MAX VSIZE IN K BLOCKS    @D52VDUL 00092007
&ALVSLIM SETA  256*1024           LIMIT VSIZE IN K BLOCKS      @D61BDUL 00093007
*                                 LARGER VSIZE NEEDS 6M PASIZE @D61BDUL 00094007
.************************************************************* @D51GDUL 00095007
.*                                                           * @D51GDUL 00096007
.*      MINIMUM PARTITION SIZE:          128K                * @D51GDUL 00097007
.*      MINIMUM GETVIS SIZE:              48K                * @D51GDUL 00098007
.*                                                           * @D51GDUL 00099007
.************************************************************* @D51GDUL 00100007
&AGMINSZ SETA  80                 MINIMUM PARTITION SIZE IN K  @D51GDUL 00101007
&AGMINGV SETA  48                 MINIMUM PARTITION GETVIS IN K@D51GDUL 00102007
&ALJCLAR SETA  8*1024             SIZE OF JCL BUFFERS IN BYTES @D51IDUL 00103007
&ALSLAR  SETA  12*1024            SIZE OF SLA SYSTEM AREA BYT  @D51IDUL 00104007
.************************************************************* @D51GDUL 00105007
.*                                                           * @D51GDUL 00106007
.*      MINIMUM SPIZE :                    0 OR 128K         * @D52VDUL 00107007
.*      DEFAULT SPIZE :                    1M                * @D51GDUL 00108007
.*                                                           * @D51GDUL 00109007
.************************************************************* @D51GDUL 00110007
&ALSPDEF SETA  0                  DEFAULT SPSIZE IN BYTES      @D64ADUL 00111007
&ALSPMIN SETA  &AGMINSZ+&AGMINGV  MIN SPSIZE VALUE,K           @D51GDUL 00112007
.************************************************************* @D52VDUL 00113007
.*                                                           * @D52VDUL 00114007
.*   DEFAULT PASIZE                      6M                  * @D61BDUL 00115007
.*   MINIMUM PASIZE - VSIZE<=256M        1M                  * @D61BDUL 00116007
.*   MINIMUM PASIZE - VSIZE> 256M        6M                  * @D61BDUL 00117007
.*                                                           * @D52VDUL 00118007
.************************************************************* @D52VDUL 00119007
&ALPADEF SETA  6*1024*1024        6M - DEF PASIZE VALUE, BYTES @D52VDUL 00120007
&ALPAMIN SETA  1*1024*1024        1M - MIN PASIZE VALUE, BYTES @D52VDUL 00121007
.************************************************************* @D52VDUL 00122007
.*                                                           * @D52VDUL 00123007
.*      MINIMUM/DEFAULT RSIZE (ESA/370 ONLY):     64K        * @D52VDUL 00124007
.*                                                           * @D52VDUL 00125007
.************************************************************* @D52VDUL 00126007
&ALRDEF  SETA  64*1024            64K - MIN RSIZE VALUE, BYTES @D52VDUL 00127007
.************************************************************* @D51GDUL 00128007
.*                                                           * @D51GDUL 00129007
.*     CALCULATION OF SYSTEM GETVIS REQUIREMENTS, PART (1)   * @D51GDUL 00130007
.*                                                           * @D51GDUL 00131007
.*     THE SIZE OF THE SYSTEM GETVIS AREA IS THE SUM OF      * @D51GDUL 00132007
.*     THREE PARTS:                                          * @D149DFG 00133007
.*     (1) AF SPACE REQUIREMENTS ESTIMATED FROM SUPERVISOR   * @D149DFG 00134007
.*         GENERATION PARAMETERS.                            * @D149DFG 00135007
.*     (2) AF SPACE REQUIREMENTS ESTIMATED FROM IPL OPTIONS, * @D149DFG 00136007
.*         INCLUDING LIBRARIAN REQUIREMENTS FROM $INITCON.   * @D149DFG 00137007
.*     (3) SIZE SPECIFICATION FROM THE GETVIS PARAMETER OF   * @D149DFG 00138007
.*         THE SVA COMMAND.                                  * @D149DFG 00139007
.*                                                           * @D149DFG 00140007
.*     (1) IS CALCULATED AS FOLLOWS                          * @D149DFG 00141007
.*         (R(...) MEANS ROUNDING TO NEXT FULL PAGE)         * @D149DFG 00142007
.*                                                           * @D149DFG 00143007
.*         + SUBTASK CONTROL BLOCK SUBPOOL SPTCB             * @D52VDUL 00144007
.*           (6 SUBTASKS / PARTITION)                        * @D52VDUL 00145007
.*           CB LENGTH X'2CC'                                * @D61BDUL 00146007
.*                      R((6*NPART)/(PAGE_SIZE/CB_LN)) PAGES * @D52VDUL 00147007
.*         + SUBTASK SAVE AREA SUBPOOL                1 PAGE * @D52VDUL 00148007
.*           SA LENGTH X'78'                                 * @D52VDUL 00149007
.*                      R((6*NPART)/(PAGE_SIZE/SA_LN)) PAGES * @D52VDUL 00150007
.*         + XECB SUBPOOL                             1 PAGE * @D149DFG 00151007
.*         + LOCK SUBPOOL            R(10*(32+3*16)*NPART)   * @D149DFG 00152007
.*           (10 RES./PART., 3 OWNER EL./RES.)               * @D149DFG 00153007
.*         + LUBX, MSAT PART       4*(NPGR+&AGSYSLB*NPART)   * @D149DFG 00154007
.*         + XTENT SUBPOOL                            1 PAGE * @D52VDUL 00155007
.*         + MSAT SUBPOOL                             1 PAGE * @D149DFG 00156007
.*         + VIO SUBPOOL                              1 PAGE * @D149DFG 00157007
.*         + PARTITION SUBPOOLS                  1 PAGE/PART * @D52VDUL 00158007
.*         + DUMP SUBPOOL                               20 K * @D52VDUL 00159007
.*         + PARTITION CONTROL BLOCK ADDRESS TABLES  2 PAGES * @D52VDUL 00160007
.*           (PCBATAB, SMCBTAB, CLASS TABLE, ...)            * @D52VDUL 00161007
.*         + SECURITY AND LOGGING                     1 PAGE * @D14BDFG 00162007
.*         + SYSFIL ON FBA                            1 PAGE * @D149DFG 00163007
.*         + FETCH/LOAD TRACE TABLES        &AGPHLSL*NPART   * @D149DFG 00164007
.*         + PARAMETRIZED PROCEDURES      8+(11*NPART+1)/2 K * @D149DFG 00165007
.*         + REENTRANT DUMP WORK AREA                   72 K * @D52VDUL 00166007
.*                                                           * @D35DDUL 00167007
.************************************************************* @D35DDUL 00168007
&AL3     SETA  0                                               @D52VDUL 00169007
&AL1     SETA  &AGPSIZB/X'2CC'    SUBTASK CB'S PER PAGE        @D52VDUL 00170007
&AL1     SETA  (6*&NPART+&AL1-1)/&AL1                          @D52VDUL 00171007
&ALPFX24 SETA  &AL1               PFIX COUNT                   @D52VDUL 00172007
&AL2     SETA  &AL1*&AGPSIZB      SUBTASK CB SUBPOOL           @D52VDUL 00173007
&AL1     SETA  &AGPSIZB/X'78'     SAVE AREAS PER PAGE          @D52VDUL 00174007
&AL1     SETA  ((6*&NPART+&AL1-1)/&AL1)*&AGPSIZB               @D52VDUL 00175007
&AL2     SETA  &AL2+&AL1          SUBTASK SAVE AREA SUBPOOL    @D52VDUL 00176007
&AL2     SETA  &AL2+&AGPSIZB      XECB SUBPOOL SPXECB          @D14BDFG 00177007
&AL1     SETA  ((10*80*&NPART+&AGPSIZB-1)/&AGPSIZB)*&AGPSIZB   @D14BDFG 00178007
&AL2     SETA  &AL2+&AL1          LOCK SUBPOOL SPLOCK          @D14BDFG 00179007
&AL1     SETA  4*(&AGNPGR+&AGSYSLB*&NPART)  LUBX FOR MSAT      @D14BDFG 00180007
&AL2     SETA  &AL2+&AL1                                       @D14BDFG 00181007
&ALDFPSZ SETA  &AL1               FOR PART (2) IF DASDFP=YES   @D52VDUL 00182007
&AL1     SETA  ((240*&NPART+&AGPSIZB-1)/&AGPSIZB)*&AGPSIZB     @D14BDFG 00183007
&ALDFPSZ SETA  &ALDFPSZ+&AL1      EXTENT SUBPOOL SPXTNT        @D52VDUL 00184007
&AL2     SETA  &AL2+&AGPSIZB      MSAT SUBPOOL SPMSAT          @D14BDFG 00185007
&AL2     SETA  &AL2+&AGPSIZB      VIO SUBPOOL SPVIO            @D14BDFG 00186007
&AL2     SETA  &AL2+&AGPSIZB*&NPART  SUBPOOLS BGPOOL,FNPOOL    @D14BDFG 00187007
&AL2     SETA  &AL2+&AGMINSG*1024 GUARANTEED DUMP SUBPOOL      @D52VDUL 00188007
&ALPFX24 SETA  &ALPFX24+2         ADD 2 PAGES FOR PARTITION CB @D52VDUL 00189007
&AL2     SETA  &AL2+2*&AGPSIZB    ADD 2 PAGES FOR PART CTL BLKS@D52VDUL 00190007
&AL2     SETA  &AL2+&AGPSIZB      LOGGING AND SECURITY         @D14BDFG 00191007
&AL2     SETA  &AL2+&AGPSIZB      SPACE FOR FBA SYSFIL SUPPORT @D52VDUL 00192007
&AL2     SETA  &AL2+&AGPHLSL*&NPART  PARTITION LOAD TABLES     @D14BDFG 00193007
&AL1     SETA  (8+(11*&NPART+1)/2)*1024                        @D14BDFG 00194007
&AL2     SETA  &AL2+&AL1          SPACE FOR PARAM.PROCEDURES   @D14BDFG 00195007
&AL2     SETA  &AL2+72*1024       REENTRANT DUMP WORK AREA     @D52VDUL 00196007
&AL1     SETA  5                  5 PAGES FOR XPCC BUFFERS     @D52VDUL 00197007
&ALPFX31 SETA  &AL1               ADD 5 PAGES FOR XPCC BUFFERS @D52VDUL 00198007
&AL3     SETA  &AL1*&AGPSIZB      ADD XPCC BUFFERS             @D52VDUL 00199007
&ALSVIS  SETA  &AL2               24 BIT SYS GETVIS SIZE, BYTE @D52VDUL 00200007
&ALSV31  SETA  &AL3               31 BIT SYS GETVIS SIZE, BYTE @D52VDUL 00201007
.**************************************************************@D51GDUL 00202007
.*                                                            *@D51GDUL 00203007
.*       BUFSIZE VALUES:                                      *@D61BDUL 00204007
.*                                                            *@D51GDUL 00205007
.*             MINIMUM:      BUFSIZE=10                       *@D51GDUL 00206007
.*             DEFAULT:      BUFSIZE=1500                     *@D51GDUL 00207007
.*                                                            *@D51GDUL 00208007
.**************************************************************@D51GDUL 00209007
&ABMIN   SETA  10                 MINIMUM AND                  @D51GDUL 00210007
&ABDEF   SETA  1500               DEFAULT BUFSIZE              @D51GDUL 00211007
         SPACE 1                                               @D51GDUL 00212007
***************************************************************@D51GDUL 00213007
*                                                             *@D51GDUL 00214007
*        COMMUNICATION AREA OF SUPERVISOR WITH IPL            *@D51GDUL 00215007
*                                                             *@D51GDUL 00216007
***************************************************************@D51GDUL 00217007
         SPACE 1                                               @D51GDUL 00218007
         AIF   ('&L' EQ '').GENDEF2                            @D51GDUL 00219007
&L       DS    0F                                              @D51GDUL 00220007
         AGO   .GENSVIP                                        @D63JDUL 00221007
.GENDEF2 ANOP  .                                               @D51GDUL 00222007
SVIPL    DS    0F                                              @D51GDUL 00223007
.GENSVIP ANOP                                                  @D63JDUL 00224007
IPLSVLVL IPLSVLVL DSECT=NO                                     @D63JDUL 00225007
.GENSVI  ANOP                                                  @D51GDUL 00226007
SVISPGEN D&C   A&S.(IJBNUCND)   A(END OF GENERATED SUPV AREA)  @D61CDUL 00227007
SVISPCON D&C   A&S.(IJBNUCND)   A(END OF SUPERV WITH PHASES    @D61CDUL*00228007
                                LOADED ADJACENTLY) - WILL BE   @D61CDUL*00229007
                                  UPDATED BY IPL               @D61CDUL 00230007
*  THE FOLLOWING AREA USED DURING IPL BY CONSOLE AND SCSI SUPPORT       00230108
*  WILL BE 24-BIT SVA AFTER CONSOLE AND SCSI INITIALIZED IN 31-BIT SVA  00230208
SVISPEND D&C   A&S.(2*1024*1024) CONSOLE SUPPORT LOADED DOWNWRD@D68CDUL*00231008
                                 TOWARDS SUPERVICOR END        @D68DDUL 00231108
SVIPLFCP D&C   A&S.(2*1024*1024) FCP/SCSI LOADED UPWARDS       @D68CDUL*00231313
                                 TOWARDS PRIVATE AREA START    @D68DDUL 00231408
SVIPLWA  D&C   A&S.(2*1024*1024+128*1024)  FCP DYNAMIC STORAGE @D68CDUL*00232013
                                AREA DURING IPL (NO GETVIS)    @D68CDUL 00232109
SVICHTB  D&C   A&S.(CHNTAB)     A(CHANNEL CONTROL TABLE)       @D51GDUL 00233007
         D&C   A&S.(CHNTNXT-CHNTBL) L(CHANNEL CTL TABLE ENTRY) @D51GDUL 00234007
SVIAVRT  D&C   A&S.(AAVRTAB)    A(A(AVR-TABLE))                @D51GDUL 00235007
SVIACBQ  D&C   A&S.(FCBQ)       A(BEG OF 1ST COPY BUFFER AREA) @D61RDUL 00236007
SVIACBQ2 D&C   A&S.(0)          A(BEG OF 2ND COPY BUFFER AREA) @D61RDUL 00237007
SVIAICTL D&C   A&S.(INITCTLR)   A(INITIALIZE CTL-REGISTER)     @D51GDUL 00238007
SVIARCHQ D&C   A&S.(AHQRESCH)   A(A(RESERVED CHANQ ENTRIES))   @D51GDUL 00239007
.*                              (REL.3: A(A(EXTENT AREA)))     @D51GDUL 00240007
SVIRCHQ# D&C   A&S.((CHANQEND-HQRESCHQ)/CHQLEN) #(RESERVED CHQ)@D61BDUL 00241007
SVISVACO D&C   XL3&S.'000000'   SVA LOAD FLAGS                 @D51GDUL 00242007
.* FILL IN SUPEVISOR OPTIONS FROM RIGHT TO LEFT ==============>@D51GDUL 00243007
         D&C   B&S.'00011111'                                  @D52VDUL 00244007
SVILSEC  EQU   X'20'            SECURITY SVA LOAD FLAG         @D52VDUL 00245007
SVILNESM EQU   X'40'            EXTERNAL SECURITY SVA LOAD FLG @D62SDUL 00246007
SVILVMCF EQU   X'80'            VMCF SVA LOAD FLAG             @D61CDUL 00247007
SVIRQPMR D&C   A&S.(RQPMR)      A(PAGING RESOURCE QUEUES)      @D51GDUL 00248007
SVIRQTBE D&C   A&S.(ARQTABE)    A(A(END OF RQTAB)              @D51GDUL 00249007
SVIPASIZ D&C   F&S.'&ALPADEF'   DEFAULT PASIZE IN BYTES        @D61BDUL 00250007
SVIRSIZE D&C   F&S.'&ALRDEF'    DEFAULT RSIZE IN BYTES         @D52VDUL 00251007
* IPL VERSION OF SCSI SUPPORT LOADED BETWEEN 2M AND 4M BY      @D68DDUL 00252007
* MACHINE LOADER                                               @D68DDUL 00253007
SVIBGMIN D&C   A&S.(4*1024*1024) MINIMUM START OF IPL PARTITN  @D68DDUL 00254007
SVISC1   D&C   A&S.(INTTAB1)    A(CHANNEL SUBSYSTEM INFO)      @D68DDUL 00254211
         D&C   A&S.(0)          RESERVED                       @D61CDAP 00256007
SVIASSID D&C   A&S.(SSIDSUP)    A(SUPERVISOR SUBSID ENTRY)     @D51GDUL 00257007
SVIAISHR D&C   A&S.(INITDSHR)   A(DASD SHARING INIT ROUTINE)   @D51GDUL 00258007
SVIADLFT D&C   A&S.(DLFTABLE)   A(DASD SHARING COMM TABLE)     @D51GDUL 00259007
SVIVSIZE D&C   F&S.'&ALVSDEF'   DEFAULT VSIZE VALUE IN K       @D51GDUL 00260007
SVIVSMIN D&C   F&S.'&ALVSMIN'   MINIMUM VSIZE VALUE IN K       @D51GDUL 00261007
SVIVSMAX D&C   F&S.'&ALVSMAX'   MAXIMUM VSIZE VALUE IN K       @D51GDUL 00262007
SVISPSIZ D&C   F&S.'&ALSPDEF'   DEFAULT SPSIZE VALUE IN BYTES  @D51GDUL 00263007
SVISPMIN D&C   F&S.'&ALSPMIN'   MINIMUM SPSIZE VALUE, K        @D51GDUL 00264007
SVIACCW  D&C   A&S.(DASDCPT)    A(A(BEGIN OF CCW AREA))        @D51GDUL 00265007
SVIECCW  D&C   A&S.(DASDCPTA)   A(A(END OF CCW AREA + 1))      @D51GDUL 00266007
SVIADVCB D&C   A&S.(ADEVCB)     A(A(DEVICE CONTROL BLOCKS))    @D51GDUL 00267007
SVIVPOOL D&C   F&S.'&ALVPDEF'   DEFAULT VALUE OF VPOOL IN K    @D51GDUL 00268007
SVIVIOMX D&C   F&S.'&ALVIOMX'   MAXIMUM VALUE OF VIO IN K      @D51GDUL 00269007
SVIENDAT D&C   A&S.(ENABDAT)    A(INSTR MODIFIED BY INITDAT)   @D51GDUL 00270007
.*                              (REL.3:A(A(ATTENTION ROUTINE)))@D51GDUL 00271007
SVIJCBSZ D&C   H&S.'&ALJCLAR'   SIZE OF JC WORK AREA IN BYTES  @D51GDUL 00272007
SVISLBSZ D&C   H&S.'&ALSLAR'    SIZE OF SLA WORK AREA IN BYTES @D51GDUL 00273007
SVIXPYAV D&C   A&S.(XPYAPCVC)   A(A(APPC/VM VECTOR TABLE)      @D51GDUL 00274007
SVILPIDT D&C   AL2&S.(DSPIDELN) L(PATH ID TABLE ENTRY)         @D51GDUL 00275007
SVICBL   D&C   H&S.'&ACCWBL'    L(COPY-BUFFER)                 @D51GDUL 00276007
SVIACBE  D&C   A&S.(CBQADR)     A(ADDRESSES OF COPY BLK FRAGM) @D51GDUL 00277007
SVIBFMIN D&C   F&S.'&ABMIN'     MINIMUM BUFSIZE                @D51GDUL 00278007
SVIBFDFL D&C   F&S.'&ABDEF'     DEFAULT BUFSIZE                @D51GDUL 00279007
SVIAPUBX D&C   A&S.(APBXAREA)   A(A(PUBX VECTOR))              @D51GDUL 00280007
SVISGVIS D&C   F&S.'&ALSVIS'    GENERATED SYSTEM GETVIS, BYTES @D52VDUL 00281007
SVISGDFP D&C   F&S.'&ALDFPSZ'   SYSTEM GETVIS FOR DASDFP, BYTE @D52VDUL 00282007
SVISGV31 D&C   F&S.'&ALSV31'    31 BIT SYSTEM GETVIS           @D52VDUL 00283007
SVIACHFP D&C   A&S.(CHQFLPTR)   A(2 BYTE CHANQ FREE LIST PTR)  @D51GDUL 00284007
SVIAPUB2 D&C   A&S.(APB2AREA)   A(A(PUB2 VECTOR))              @D51GDUL 00285007
SVIAVPBG D&C   A&S.(AVPBEG)     A(A(VPOOL BEGIN))              @D52VDUL 00286007
SVIAVPND D&C   A&S.(AVPEND)     A(A(VPOOL END))                @D52VDUL 00287007
SVIPFX24 D&C   F&S.'&ALPFX24'   24 BIT SYSTEM PFIX             @D52VDUL 00288007
SVIPFX31 D&C   F&S.'&ALPFX31'   31 BIT SYSTEM PFIX             @D52VDUL 00289007
SVIPAMIN D&C   F&S.'&ALPAMIN'   MINIMUM PASIZE IN BYTES        @D61BDUL 00290007
SVIVSLIM D&C   F&S.'&ALVSLIM'   VSIZE >= THIS VALUE (IN K)     @D61BDUL 00291007
*                               REQUIRES LARGER PASIZE MINIMUM @D61BDUL 00292007
SVIASFT  D&C   A&S.(SFTADR)     ADDRESS OF MVS SFT             @D61BDUL 00293007
SVIAFCB  D&C   A&S.(AFCB)       ADDRESS OF COPY BUFFER FREECHN @D67ODUL 00294007
         DS    5F               RESERVED                       @D67ODUL 00295007
         AIF   (NOT &IPL).NOIPL1                               @D51GDUL 00296007
CBLFWPTR EQU   &CBFWPTR         OFFSET OF COPYBL FORWARD PTR   @D51GDUL 00297007
CBLBWPTR EQU   &CBBWPTR         OFFSET OF COPYBL BACKWARD PTR  @D51GDUL 00298007
.NOIPL1  ANOP                                                  @D51GDUL 00299007
         MEND                                                  @D51GDUL 00300007
