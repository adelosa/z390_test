*%GOTO PLXSYS;                       /* ASSEMBLER MACRO IN COMMENT      00050001
         MACRO                                                          00100001
&NAME    SYSSERV &ID=                                                   00150001
         GBLB  &IJBSGEN           SUPERVISOR GENERATION                 00200001
         LCLA  &CALLPGM                                                 00250001
.*       IBM DISK OPERATING SYSTEM                            *         00300001
*        SUPERVISOR - SYSSERV  - 5686-066                     *@D66GDFM 00350002
         AGO   .ZZZ                                           *         00400001
*        LICENSED MATERIALS - PROPERTY OF IBM                 *         00450001
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"          *         00500001
*        5686-066 (C) COPYRIGHT IBM CORP. 1995                *         00550001
.*       SEE COPYRIGHT INSTRUCTIONS                           *         00600001
.ZZZ     ANOP                                                 *         00650001
.**** START OF SPECIFICATIONS ***************************************** 00700001
.*                                                                      00750001
.*01* MODULE-TYPE = MACRO                                               00800001
.*                                                                      00850001
.*01* DESCRIPTIVE NAME = PROGRAM EXECUTION SERVICES                     00900001
.*                                                                      00950001
.*  STATUS           =  NEW MACRO - FIRST RELEASE SP 6.1.0              01000001
.*                                                                      01050001
.*  FUNCTION         =  CALLS A SUPERVISOR SERVICE, WHICH               01100001
.*                      PROVIDES SYSTEM SERVICES                        01150001
.*                      (JOB CONTROL USAGE ONLY)                        01200001
.*                                                                      01250001
.*                 SYSSERV ID=EQUCPGM                                   01300001
.*                       PROVIDE MAPPING FOR ID=CALLPGM                 01350001
.*                                                                      01400001
.*                 SYSSERV ID=CALLPGM                                   01450001
.*                      CALLS A SUPERVISOR SERVICE, WHICH               01500001
.*                      PROVIDES THE PROGRAM ENVIRONMENT                01550001
.*                      (E.G. CLEAR AR SAVE AREA, PROVIDE               01600001
.*                      LINKAGE STACK ENVIRONMENT)                      01650001
.*                      AND PROCESSES REQUESTED ACTIONS (E.G.           01700001
.*                      TRACE INITIALIZATION).                          01750001
.*                      INPUT: R7 = PARAMETER LIST ADDRESS              01800001
.*                             RF = PROGRAM ADDRESS INCLUDING           01850001
.*                                  AMODE IN HIGH ORDER BIT             01900001
.*                                                                      01950001
.*  NOTES            =  NONE                                            02000001
.*  DEPENDENCIES     =  NONE                                            02050001
.*  RESTRICTIONS     =  NONE                                            02100001
.*  REGISTER CONVENT.=  REGISTER  7 = PARAMETER LIST ADDRESS            02150001
.*                      REGISTER 15 = AMODE AND PROGRAM ADDRESS         02200001
.*  PATCH LABEL      =  NONE                                            02250001
.*  MACRO TYPE       =  ASSEMBLER                                       02300001
.*  PROCESSOR        =  ASM                                             02350001
.*  MACRO SIZE       =  SEE OUTPUT                                      02400001
.*  ATTRIBUTES       =  NONE                                            02450001
.*  ENTRY POINT      =  NONE                                            02500001
.*  PURPOSE          =  SEE FUNCTION                                    02550001
.*  LINKAGE          =  NONE                                            02600001
.*  CALLER           =  VSE/AF JOB CONTROL                              02650001
.*  INPUT            =  FUNCTION DEPENDENT                              02700001
.*  OUTPUT           =  FUNCTION DEPENDENT                              02750001
.*  EXIT NORMAL      =  MACRO EXPANSION                                 02800001
.*  EXIT ERROR       =  MACRO EXPANSION IGNORED                         02850001
.*  EXTERNAL REFEREN.=  NONE                                            02900001
.*  ROUTINES         =  NONE                                            02950001
.*  DATA AREAS       =  NONE                                            03000001
.*  CONTROL BLOCKS   =  NONE                                            03050001
.*  TABLES           =  NONE                                            03100001
.*  MACROS           =  NONE                                            03150001
.*  CHANGE ACTIVITIES=  NEW MACRO   FIRST RELEASE 6.1.0                 03200001
.*  MODIFIED REGISTER=  DEPENDENT ON FUNCTION                           03250001
.*                                                                      03300001
.** END OF SPECICATIONS *********************************************** 03350001
.*                                                                      03400001
.************  FUNCTION CODES  **********************************       03450001
&CALLPGM SETA  128          PROVIDE PROGRAM ENVIRONMENT AND CALL IT     03500001
.****************************************************************       03550001
         AIF   (T'&ID EQ 'O').IDERR                                     03600001
         AIF   ('&ID' NE 'EQUCPGM').NAM                                 03650001
.****************************************************************       03700001
.*   ID=EQUCPGM DSECT                                                   03750001
.****************************************************************       03800001
*------------------------------------------------------------------     03850001
* JCWEXEP  - EXEC PARAMETER FIELDS                                      03900001
*------------------------------------------------------------------     03950001
         AIF   (&IJBSGEN).SUP                                           04000001
JCWEXEP  DS    0CL20        START OF EXEC PARAMETER FIELDS     @D66GDFM 04050008
         AGO   .INL1                                                    04100001
.SUP     ANOP                                                           04150001
JCWEXEP  DSECT                                                          04200001
.INL1    ANOP                                                           04250001
JCWEPLNG DS    H            LENGTH OF JCWEXEP AREA                      04300001
JCWEFLAG DS    X            JCWEXEP FLAG BYTE                           04350001
JCWECPGF EQU   X'80'             0  TRACE REQUESTED                     04380001
JCWEDSPF EQU   X'40'             1  DSPACE SPECIFIED                    04410001
JCWENPAF EQU   X'20'             2  NON-PARALLEL APPLICATION            04440001
JCWEMVSF EQU   X'10'             3  MVS EMULATION MODE                  04470001
         DS    X            NOT USED                                    04500001
JCWEPHSN DS    CL8          PHASE NAME                                  04550001
JCWEDSPC DS    F            DSPACE VALUE                                04600001
JCWEMAXT DS    H            MAXIMAL NUMBER OF SUBTASKS         @D66GDFM 04610001
         DS    H            RESERVED, FORCE DWORD ALIGNMENT    @D66GDFM 04620008
JCWELNEQ EQU   *-JCWEXEP    LENGTH OF PARAMETER LIST                    04650001
         MEXIT                                                          04700001
.****************************************************************       04750001
.*   ID=CALLPGM                                                         04800001
.****************************************************************       04850001
.NAM     ANOP                                                           04900001
         AIF   ('&ID' NE 'CALLPGM').IDERR                               04950001
&NAME    DS    0H                                                       05000001
         SVC   133                                                      05050001
         MEXIT                                                          05100001
.****************************************************************       05150001
.*   ERROR MESSAGES                                                     05200001
.****************************************************************       05250001
.IDERR   ANOP                                                           05300001
         MNOTE 8,'PARAMETER MISSING OR INVALID. MACRO IGNORED'          05350001
         MEXIT                                                          05400001
         MEND                                                           05450001
* END OF PLX COMMENT */ %PLXSYS: ;                                      05500001
* /*-----------------------------------------------------------------*  05550001
*  *  JCWEXEP  - EXEC PARAMETER FIELDS                               *  05600001
*  *-----------------------------------------------------------------*/ 05650001
*      2 JCWEXEP   CHAR(20) BDY(WORD), /*EXEC PARAMETERS AREA@D66GDFM*/ 05700010
*       3 JCWEPLNG FIXED(16), /* LENGTH OF AREA              @D61HDIS*/ 05750001
*       3 JCWEFLAG BIT(8),    /* EXEC PARAMETER FLAG BYTE    @D61HDIS*/ 05800001
*        4 JCWECPGF BIT(1),   /* X'80' TRACE REQUESTED       @D61HDIS*/ 05850001
*        4 JCWEDSPF BIT(1),   /* X'40' DSPACE SPECIFIED      @D61HDIS*/ 05900001
*        4 JCWENPAF BIT(1),   /* X'20' NON PARALLEL APPL.    @D61HDIS*/ 05925001
*        4 JCWEMVSF BIT(1),   /* X'10' MVS EMULATION MODE    @D61HDIS*/ 05950001
*        4 *        BIT(4),   /* NOT USED                    @D61HDIS*/ 05975001
*       3 *        FIXED(8),  /* RESERVED                    @D61HDIS*/ 06000001
*       3 JCWEPHSN CHAR(8),   /* PHASE NAME                  @D61HDIS*/ 06050001
*       3 JCWEDSPC FIXED(31), /* DSPACE VALUE                @D61HDIS*/ 06100001
*       3 JCWEMAXT FIXED(15), /* MAXIMAL NUMBER OF SUBTASKS  @D66GDFM*/ 06110004
*       3 *        FIXED(15), /* RESERVED, FORCE DWORD ALIGNM@D66GDFM*/ 06120008
