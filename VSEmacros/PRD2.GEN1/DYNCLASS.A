         MACRO                                                          00050000
&NAME    DYNCLASS &ID=,&AREA=,&CLASS=,&RC=,&PART=                       00100000
         LCLA  &CHECK                                                   00150000
         LCLA  &DISABLE                                                 00200000
         LCLA  &DEFAULT                                                 00250000
         LCLA  &GET                                                     00300000
         LCLA  &INITIAL                                                 00350000
         LCLA  &LOAD                                                    00400000
         LCLA  &SAVE                                                    00450000
         LCLA  &ENABLE                                                  00500000
         LCLA  &RETURN                                                  00550000
         LCLA  &CLEAN                                                   00600000
         LCLA  &PREP                                                    00650000
         LCLA  &JACCT                                                   00700000
         LCLA  &FREE                                                    00750000
         LCLA  &HOLD                                                    00800000
         LCLA  &PURGE              DROPPED WITH 5.2.0          @D52VDIS 00850052
         LCLA  &JACCTP                                                  00875052
         LCLA  &FUNC                                                    00900000
         LCLB  &CLAR                                                    00950000
.*       IBM DISK OPERATING SYSTEM                            *         01000000
*        SUPERVISOR - DYNCLASS - 5686-032-06                  *@D52VDIS 01050052
         AGO   .ZZZ                                           *         01100000
*        LICENSED MATERIALS - PROPERTY OF IBM                 *         01150000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"          *         01200000
*        5686-032 (C) COPYRIGHT IBM CORP. 1990, 1992          *         01250052
.*       SEE COPYRIGHT INSTRUCTIONS                           *         01300000
.ZZZ     ANOP                                                 *         01350000
.**** START OF SPECIFICATIONS ***************************************** 01400000
.*                                                                      01450000
.*01* MODULE-TYPE = MACRO                                               01500000
.*                                                                      01550000
.*01* DESCRIPTIVE NAME = DYNAMIC CLASS SERVICES                         01600000
.*                                                                      01650000
.*  STATUS           =  NEW MACRO - FIRST RELEASE SP 5.1.0              01700000
.*                                                                      01750000
.*  FUNCTION         =  PROVIDES AN INTERFACE TO THE DYNAMIC CLASS      01800000
.*                      SERVICES PERFORMED BY SVC XXX.                  01850000
.*                      THE SERVICES ARE:                               01900000
.*                                                                      01950000
.*                       DYNCLASS ID=CHECK                              02000000
.*                            VALIDATE THE SPECIFIED CLASS              02050000
.*                       DYNCLASS ID=DISABLE                            02100000
.*                            DEACTIVATE ONE DYNAMIC CLASSES            02150000
.*                       DYNCLASS ID=DEFAULT                            02200000
.*                            GET CLASS/SYSTEM LIMITS/DEFAULTS          02250000
.*                       DYNCLASS ID=GET                                02300000
.*                            GET CLASS CHARACTERISTIC FOR ONE          02350000
.*                            OR ALL CLASSES FROM VSE/AF LIBRARY        02400000
.*                       DYNCLASS ID=INITIAL                            02450000
.*                            CALLED BY IPL TO UPDATE INTERNAL          02500000
.*                            CONTROL BLOCKS DEPENDENT ON NPARTS        02550000
.*                       DYNCLASS ID=SAVE                               02600000
.*                            STORES THE DYNAMIC CLASS INFORMATION      02650000
.*                            INTO THE VSE/AF LIBRARY                   02700000
.*                       DYNCLASS ID=ENABLE                             02750000
.*                            ACTIVATE ONE DYNAMIC CLASSES              02800000
.*                            DEFINED IN THE DYNAMIC CLASS TABLE        02850000
.*                       DYNCLASS ID=RETURN                             02900000
.*                            RETURN FROM SVA ROUTINE                   02950000
.*                       DYNCLASS ID=CLEANUP                            03000000
.*                            PROCESS CLEAN UP REQUEST                  03050000
.*                       DYNCLASS ID=PREPARE                            03100000
.*                            PROCESS PREPARE  REQUEST                  03150000
.*                       DYNCLASS ID=JACCT                              03200000
.*                            RETURN JOB ACCOUNTING DATE AND            03250000
.*                            PARTITION/CLASS CHARACTERISTICS           03300000
.*                       DYNCLASS ID=HOLD                               03350000
.*                            DO NOT DEALLOCATE THE PARTITION           03400000
.*                            UNTIL DYNCLASS ID=FREE OCCURS             03450000
.*                       DYNCLASS ID=FREE                               03500000
.*                            FREE A PARTITION THAT MAY BE UNPOSTED     03550000
.*                            BECAUSE OF A DYNCLASS ID=HOLD REQUEST     03600000
.*                       DYNCLASS ID=PURGE                              03650000
.*                            DELETE THE CLASS TABLE INFORMATION        03700000
.*                            OF THE LIBRARY MEMBER                     03750000
.*                                                                      03800000
.*  NOTES            =  NONE                                            03850000
.*  DEPENDENCIES     =  NONE                                            03900000
.*  RESTRICTIONS     =  NONE                                            03950000
.*  REGISTER CONVENT.=  REGISTER  0 = VALUE REQUESTED BY CLASS | PART   04000000
.*                   =  REGISTER  1 = AREA ADDRESS OR AREA CONTENTS     04050000
.*                      REGISTER 15 = FUNCTION CODE                     04100000
.*  PATCH LABEL      =  NONE                                            04150000
.*  MACRO TYPE       =  ASSEMBLER                                       04200000
.*  PROCESSOR        =  ASM                                             04250000
.*  MACRO SIZE       =  SEE OUTPUT                                      04300000
.*  ATTRIBUTES       =  NONE                                            04350000
.*  ENTRY POINT      =  NONE                                            04400000
.*  PURPOSE          =  SEE FUNCTION                                    04450000
.*  LINKAGE          =  NONE                                            04500000
.*  CALLER           =  VSE/SP COMPONENTS                               04550000
.*  INPUT            =  FUNCTION DEPENDENT                              04600000
.*  OUTPUT           =  FUNCTION DEPENDENT                              04650000
.*                      (ID=LOAD ONLY)                                  04700000
.*                   =  REG15 CONTAINS A RETURN CODE                    04750000
.*  EXIT NORMAL      =  MACRO EXPANSION                                 04800000
.*  EXIT ERROR       =  MACRO EXPANSION IGNORED                         04850000
.*  EXTERNAL REFEREN.=  NONE                                            04900000
.*  ROUTINES         =  NONE                                            04950000
.*  DATA AREAS       =  NONE                                            05000000
.*  CONTROL BLOCKS   =  NONE                                            05050000
.*  TABLES           =  NONE                                            05100000
.*  MACROS           =  NONE                                            05150000
.*  CHANGE ACTIVITIES=  NEW MACRO   FIRST RELEASE SP 5.1.0              05200000
.*  MODIFIED REGISTER=  DEPENDENT ON FUNCTION                           05250000
.*                                                                      05300000
.** END OF SPECICATIONS *********************************************** 05350000
.*                                                                      05400000
.************  FUNCTION CODES  **********************************       05450000
&CHECK   SETA  0            CHECK IF SPECIFIED CLASS IS CORRECT         05500000
&DISABLE SETA  1            DISABLE ONE DYNAMIC CLASS                   05550000
&DEFAULT SETA  2            RETRIEVE SYSTEM LIMITS                      05600000
&GET     SETA  3            GET CLASSES FROM LIBRARY                    05650000
&INITIAL SETA  4            INITIALIZE SYSTEM                           05700000
&LOAD    SETA  5            SET DYNAMIC CLASS TABLE ACTIVE              05750000
&SAVE    SETA  6            STORE CLASS TABLE INTO LIBRARY              05800000
&ENABLE  SETA  7            ENABLE ONE DYNAMIC CLASS                    05850000
&RETURN  SETA  8            RETURN FROM SVA ROUTINE                     05900000
&CLEAN   SETA  9            PROCESS CLEAN UP REQUEST                    05950000
&PREP    SETA  10           PROCESS PREPARE  REQUEST                    06000000
&JACCT   SETA  11           PROCESS JOB ACCOUNTING REQUEST              06050000
&HOLD    SETA  12           HOLD PARTITION DURING CLEAN UP PROCESS      06100000
&FREE    SETA  13           FREE A PARTITION                            06150000
.* &PURGE   SETA  14           DELETE CLASS TABLE INFO IN LIBRARY       06175052
&PURGE   SETA  14           NO LONGER SUPPORTED                @D52VDIS 06200052
&JACCTP  SETA  15          JOB ACCOUNTING REQUEST FOR PART.    @D52VDIS 06225052
.****************************************************************       06250000
         AIF   (T'&ID EQ 'O').IDERR                                     06300000
         AIF   ('&ID' NE 'EQU').NAM                                     06350000
.****************************************************************       06400000
.*   SVC 43 FUNCTION CODE EQUATES                                       06450000
.****************************************************************       06500000
SV43FCHK EQU   &CHECK     FC=0 - VALIDATE A GIVEN DYNAMIC CLASS         06550000
SV43FCLR EQU   &DISABLE   FC=1 - DISABLE  A GIVEN DYNAMIC CLASS         06600000
SV43FDEF EQU   &DEFAULT   FC=2 - RETRIEVE SYSTEM DEFAULTS               06650000
SV43FGET EQU   &GET       FC=3 - GET CLASS INFO FROM LIBRARY            06700000
SV43FINI EQU   &INITIAL   FC=4 - INITIALIZE SYSTEM                      06750000
SV43FLOD EQU   &LOAD      FC=5 - SET DYNAMIC CLASS TABLE ACTIVE         06800000
SV43FSAV EQU   &SAVE      FC=6 - SAVE CLASS TABLE INTO LIBRARY          06850000
SV43FSET EQU   &ENABLE    FC=7 - ENABLE A DYNAMIC CLASS                 06900000
SV43FRET EQU   &RETURN    FC=8 - RETURN FROM SVA ROUTINE                06950000
SV43FCLU EQU   &CLEAN     FC=9 - PROCESS CLEAN UP REQUEST               07000000
SV43FPRP EQU   &PREP      FC=10 - PROCESS PREPARE REQUEST               07050000
SV43FACC EQU   &JACCT     FC=11 - PROCESS JOB ACCOUNTING REQUEST        07100000
SV43FHLD EQU   &HOLD      FC=12 - HOLD A PARTITION                      07150000
SV43FFRE EQU   &FREE      FC=13 - FREE A PARTITION                      07200000
.* SV43FPUR EQU   &PURGE     FC=14 - PURGE LIBRARY CLASS TABLE INFO     07225052
SV43FPUR EQU   &PURGE     FC=14 - NO LONGER SUPPORTED          @D52VDIS 07250052
SV43FACP EQU   &JACCTP    FC=15 - JA REQUEST FOR PARTITION     @D52VDIS 07275052
         MEXIT                                                          07300000
.NAM     ANOP                                                           07350000
         AIF   (T'&NAME EQ 'O').SKIP                                    07400000
&NAME    DS    0H                                                       07450000
.SKIP    ANOP                                                           07500000
.************  INPUT PARAMETERS *********************************       07550000
.*    ID    | CLASS=  | AREA= | RC= | PART=                             07600000
.* --------------------------------------------------------------       07650000
.* CHECK    |   X     |   --- | --- | ---                               07700000
.* CLEANUP  |  ---    |   --- | --- | ---                               07750000
.* DEFAULT  |  ---    |    M  | --- | ---                               07800000
.* DISABLE  |   X     |   --- | --- | ---                               07850000
.* ENABLE   |   X     |   --- | --- | ---                               07900000
.* FREE     |  ---    |   --- | --- | ---                               07950000
.* GET      |  X/ALL  |    M  | --- | ---                               08000000
.* HOLD     |  ---    |   --- | --- |  M                                08050000
.* INITIAL  |  ---    |   --- | --- | ---                               08100000
.* JACCT    |  X/ALL  |    M  | --- |  P                                08150052
.* LOAD     |  ---    |    M  | --- | ---                               08200000
.* PURGE    |  ---    |    M  | --- | ---                               08250052
.* RETURN   |  ---    |   --- |  M  | ---                               08300000
.* SAVE     |  ---    |    M  | --- | ---                               08350000
.* --------------------------------------------------------------       08400000
.* M = MANDATORY                                                        08450000
.* P   CORRESPONDS TO PART=P, WHERE P = PIK                             08475052
.* X   CORRESPONDS TO CLASS=X, WHERE X = CLASS CHARACTER                08500000
.* ALL CORRESPONDS TO CLASS=ALL                                         08550000
.****************************************************************       08600000
.*   CHECK REQUEST TYPE                                                 08650000
.****************************************************************       08700000
         AIF   ('&ID' NE 'HOLD').JACCT                         @D52VDIS 08750052
&FUNC    SETA  &HOLD              FUNCTION CODE                         08800000
         AGO   .CONTPRT                                                 08814252
.JACCT   AIF   ('&ID' NE 'JACCT').TESTPA                       @D52VDIS 08828452
&FUNC    SETA  &JACCT             FUNCTION CODE                         08842652
&CLAR    SETB  1                                                        08856852
         AGO   .CONTPRT                                        @D52VDIS 08871052
.****************************************************************       08885252
.TESTPA  AIF   (T'&PART NE 'O').PARTERR                                 08900000
.****************************************************************       08950000
.CHECK   AIF   ('&ID' NE 'CHECK').DISABLE                               09000000
&FUNC    SETA  &CHECK             FUNCTION CODE                         09050000
         AGO   .CONTCL                                                  09100000
.DISABLE AIF   ('&ID' NE 'DISABLE').DEF                                 09150000
&FUNC    SETA  &DISABLE           FUNCTION CODE                         09200000
         AGO   .CONTCL                                                  09250000
.DEF     AIF   ('&ID' NE 'DEFAULT').GET                                 09300000
&FUNC    SETA  &DEFAULT           FUNCTION CODE                         09350000
         AGO   .CONTAR                                                  09400000
.GET     AIF   ('&ID' NE 'GET').INI                                     09450000
&FUNC    SETA  &GET               FUNCTION CODE                         09500000
&CLAR    SETB  1                                                        09550000
         AGO   .CONTCLA                                                 09600000
.INI     AIF   ('&ID' NE 'INITIAL').LOAD                                09650000
&FUNC    SETA  &INITIAL           FUNCTION CODE                         09700000
         AGO   .CONTNP                                                  09750000
.LOAD    AIF   ('&ID' NE 'LOAD').SAVE                                   09800000
&FUNC    SETA  &LOAD              FUNCTION CODE                         09850000
         AGO   .CONTAR                                                  09900000
.SAVE    AIF   ('&ID' NE 'SAVE').ENABLE                                 09950000
&FUNC    SETA  &SAVE              FUNCTION CODE                         10000000
         AGO   .CONTAR                                                  10050000
.ENABLE  AIF   ('&ID' NE 'ENABLE').RET                                  10100000
&FUNC    SETA  &ENABLE            FUNCTION CODE                         10150000
         AGO   .CONTCL                                                  10200000
.RET     AIF   ('&ID' NE 'RETURN').CLEAN                                10250000
&FUNC    SETA  &RETURN            FUNCTION CODE                         10300000
         AGO   .CONTRC                                                  10350000
.CLEAN   AIF   ('&ID' NE 'CLEANUP').PREP                                10400000
&FUNC    SETA  &CLEAN             FUNCTION CODE                         10450000
         AGO   .CONTNP                                                  10500000
.PREP    AIF   ('&ID' NE 'PREPARE').FREE                       @D52VDIS 10550052
&FUNC    SETA  &PREP              FUNCTION CODE                         10600000
         AGO   .CONTNP                                                  10650000
.FREE    AIF   ('&ID' NE 'FREE').PURGE                                  10900000
&FUNC    SETA  &FREE              FUNCTION CODE                         10950000
         AGO   .CONTNP                                                  11000000
.PURGE   ANOP                                                           11020052
         AGO   .IDERR                                          @D52VDIS 11040052
.* PURGE NO LONGER SUPPORTED, UNIQUE CODE USES LIBRM INSTEAD   @D52VDIS 11060052
.PURGEX  AIF   ('&ID' NE 'PURGE').IDERR                                 11080052
&FUNC    SETA  &PURGE             FUNCTION CODE                         11100000
         AGO   .CONTAR                                         @D52VDIS 11150052
.****************************************************************       11200000
.*   ERROR MESSAGES                                                     11250000
.****************************************************************       11300000
.IDERR   ANOP                                                           11350000
         MNOTE 8,'PARAMETER MISSING OR INVALID. MACRO IGNORED'          11400000
         MEXIT                                                          11450000
         MNOTE 8,'ID=&ID. INVALID. MACRO IGNORED'                       11500000
         MEXIT                                                          11550000
.CLERR   ANOP                                                           11600000
         MNOTE 8,'CLASS NOT ALLOWED FOR ID=&ID.. MACRO IGNORED'         11650000
         MEXIT                                                          11700000
.NOCLA   ANOP                                                           11750000
         MNOTE 8,'CLASS MISSING FOR ID=&ID.. MACRO IGNORED'             11800000
         MEXIT                                                          11850000
.ARERR   ANOP                                                           11900000
         MNOTE 8,'AREA NOT ALLOWED FOR ID=&ID.. MACRO IGNORED'          11950000
         MEXIT                                                          12000000
.RCERR   ANOP                                                           12050000
         MNOTE 8,'RC NOT ALLOWED FOR ID=&ID.. MACRO IGNORED'            12100000
         MEXIT                                                          12150000
.PARTERR ANOP                                                           12200000
         MNOTE 8,'PART NOT ALLOWED FOR ID=&ID.. MACRO IGNORED'          12250000
         MEXIT                                                          12300000
.ALLERR  ANOP                                                           12350000
         MNOTE 8,'CLASS=ALL NOT ALLOWED FOR ID=&ID.. MACRO IGNORED'     12400000
         MEXIT                                                          12450000
.LOERR   ANOP                                                           12500000
         MNOTE 8,'LOAD NOT ALLOWED FOR ID=&ID.. MACRO IGNORED'          12550000
         MEXIT                                                          12600000
.NOARE   ANOP                                                           12650000
         MNOTE 8,'AREA MISSING FOR ID=&ID.. MACRO IGNORED'              12700000
         MEXIT                                                          12750000
.NORC    ANOP                                                           12800000
         MNOTE 8,'RC MISSING FOR ID=&ID.. MACRO IGNORED'                12850000
         MEXIT                                                          12900000
.NOPART  ANOP                                                           12950000
         MNOTE 8,'PART MISSING FOR ID=&ID.. MACRO IGNORED'              13000000
         MEXIT                                                          13050000
.RLNERR  ANOP                                                           13100000
         MNOTE 8,'NO REGISTER NOTATION FOR ID=&ID.. MACRO IGNORED'      13150000
         MEXIT                                                          13200000
.****************************************************************       13250000
.*   CHECK FOR PART PARAMETER                                           13253552
.****************************************************************       13257052
.CONTPRT ANOP                                                  @D52VDIS 13260552
         AIF   (T'&PART EQ 'O').CHKMD                                   13261152
         AIF   (T'&CLASS NE 'O').CLERR                         @D52VDIS 13261752
         AIF   ('&PART'(1,1) NE '(').PKNAME       REGISTER NOTATION ?   13262352
         AIF   ('&PART(1)' EQ '0').CHKAD          - YES; REG. 1 ?       13262952
         LR    0,&PART(1)                          GET PIK              13263552
         AGO   .CHKAD                                                   13264152
.PKNAME  ANOP                                                           13264752
         LH    0,&PART                              GET PIK             13265352
.CHKAD   ANOP                                                  @D52VDIS 13265952
         AIF   ('&ID' EQ 'HOLD').CONT                          @D52VDIS 13266552
&FUNC    SETA  &JACCTP            FUNCTION CODE                @D52VDIS 13267552
         AGO   .CLADDOK                                        @D52VDIS 13281552
.CHKMD   ANOP                                                  @D52VDIS 13286152
         AIF   ('&ID' EQ 'HOLD').NOPART                        @D52VDIS 13290752
.****************************************************************       13295552
.*   CHECK FOR CLASS PARAMETER                                          13300000
.****************************************************************       13350000
.CONTCLA ANOP                                                           13400000
         AIF   (T'&RC NE 'O').RCERR                                     13450000
         AIF   (T'&AREA EQ 'O').NOARE                                   13500000
         AIF   ('&CLASS' NE 'ALL').NOALL        ALL SPECIFIED ?         13550000
.* --------------------------------------------------------------       13600000
.*   CLASS=ALL SPECIFIED                                                13650000
.* --------------------------------------------------------------       13700000
         SLR   0,0                                                      13750000
         BCTR  0,0                                  SET NEGATIVE        13800000
         AGO   .CLADDOK                                                 13850000
.CONTCL  ANOP                                                           13900000
.* --------------------------------------------------------------       13950000
.*   CLASS=.   SPECIFIED                                                14000000
.* --------------------------------------------------------------       14050000
         AIF   (T'&AREA NE 'O').ARERR                                   14100000
         AIF   ('&CLASS' EQ 'ALL').ALLERR       ALL SPECIFIED ?         14150000
         AIF   (T'&RC NE 'O').RCERR                                     14200000
.NOALL   ANOP                                                           14250000
         AIF   (T'&CLASS EQ 'O').NOCLA                                  14300000
         AIF   ('&CLASS'(1,1) NE '(').CLNAME       REGISTER NOTATION ?  14350000
         AIF   ('&CLASS(1)' EQ '0').CLADDOK        - YES; REG. 1 ?      14400000
         LR    0,&CLASS(1)                          GET CLASS           14450000
         AGO   .CLADDOK                                                 14500000
.CLNAME  ANOP                                                           14550000
         SLR   0,0                                                      14600000
         IC    0,&CLASS                             GET CLASS           14650000
.CLADDOK ANOP                                                           14700000
         AIF   (&CLAR).CONTAR1                    AREA MANDATORY ?      14750000
         AGO   .CONT                                                    14800000
.****************************************************************       14850000
.*   CHECK FOR AREA PARAMETER                                           14900000
.****************************************************************       14950000
.CONTAR  ANOP                                                           15000000
         AIF   (T'&CLASS NE 'O').CLERR                                  15050000
         AIF   (T'&AREA EQ 'O').NOARE                                   15100000
         AIF   (T'&RC NE 'O').RCERR                                     15150000
.CONTAR1 ANOP                                                           15200000
         AIF   ('&AREA'(1,1) NE '(').ARNAME         REGISTER NOTATION ? 15250000
         AIF   ('&AREA(1)' EQ '1').CONT             - YES; REG. 1 ?     15300000
         LR    1,&AREA(1)                           GET AREA ADDRESS    15350000
         AGO   .CONT                                                    15400000
.ARNAME  LA    1,&AREA                              GET AREA ADDRESS    15450000
         AGO   .CONT                                                    15500000
.****************************************************************       15550000
.*   CHECK FOR RC PARAMETER                                             15600000
.****************************************************************       15650000
.CONTRC  ANOP                                                           15700000
         AIF   (T'&CLASS NE 'O').CLERR                                  15750000
         AIF   (T'&AREA NE 'O').ARERR                                   15800000
         AIF   (T'&RC EQ 'O').NORC                                      15850000
         AIF   ('&RC(1)' EQ '1').CONT               - YES; REG.1 ?      15900000
         LR    1,&RC(1)                GET RETURN CODE                  15950000
         AGO   .CONT                                                    16000000
.****************************************************************       16050000
.*   CHECK FOR INITIAL REQUEST                                          16100000
.****************************************************************       16150000
.CONTNP  ANOP                                                           16800000
         AIF   (T'&RC NE 'O').RCERR                                     16850000
         AIF   (T'&CLASS NE 'O').CLERR                                  16900000
         AIF   (T'&AREA NE 'O').ARERR                                   16950000
.CONT    ANOP                                                           17000000
         LA    15,&FUNC               FUNCTION CODE                     17050000
         SVC   43                                                       17100000
*                                                                       17150000
         MEND                                                           17200000
