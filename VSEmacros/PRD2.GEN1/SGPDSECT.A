         MACRO                                                          00050000
         SGPDSECT                                                       00100000
         GBLB  &SGPMR             PRINT REQUIRED               @D14BDRP 00300000
         GBLB  &SGEND             PRINT OF DSECTS REQUIRED     @D14BDRP 00350000
         GBLA  &AGPNSFT           SHIFT ADDRESS TO PNR         @D14KDVB 00400000
         GBLA  &AGPTSPT           SHIFT SEGMENT# TO PTE-OFFSET @D14KDVB 00500000
         GBLA  &AGPNBLK           SHIFT PAGE# TO BLOCK#        @D51GDRP 00550000
         GBLA  &AGPABLK           PAGES / ALLOCATION UNIT      @D52VDVB 00575052
         GBLA  &AGSGBLK           SHIFT SEGMENT# TO BLOCK#     @D51GDRP 00600000
         GBLA  &AGPGPSG           NUMBER OF PAGE PER SEGMENT   @D14KDVB 00650000
         GBLA  &AGBKPSG           NUMBER OF BLOCKS PER SEGMENT @D51GDRP 00700000
         GBLA  &AGBSIZB           BLOCK SIZE ON PDS IN BYTES   @D51GDRP 00750000
         GBLA  &AGPSIZB           PAGE SIZE IN BYTES           @D14KDVB 00800000
         GBLA  &AGSGSIZ           SEGMENT SIZE                 @D14KDVB 00850000
         GBLA  &AGSGMSK           SEGMENT MASK                 @D14KDVB 00900000
         GBLA  &AGPGMSK           PAGE MASK                    @D14KDVB 00950000
         GBLA  &AGEPMSK           EPA  MASK                    @D14KDVB 01000000
         GBLA  &AGINVPT           INVALIDATION PATTERN PTE     @D14KDVB 01050000
         GBLA  &AGOFMSK           PAGE TABLE OFFSET MASK       @D14KDVB 01100000
         GBLA  &AGIBIT            IBIT MASK                    @D14KDVB 01150000
         LCLA  &PMPTSH            SHIFT VALUE PNR<-> PTE OFFSET@D51GDRP 01200000
         LCLA  &PMSGSH            SHIFT VALUE TO GET XA SEGMENT SIZE   *01250000
                                     OUT OF 370 SEGMENT SIZE   @D51GDRP 01300000
         ACTR  100                                             @D14BDRP 01350000
*                                                              @D52VDVB 01375061
.*  TO CHANGE THE ALLOCATION UNIT ON PDS (BLOCK) THE FOLLOWING @D51GDRP 01400000
.*  VALUES HAVE TO BE CHANGED:                                 @D51GDRP 01450000
.*    &AGBSIZB - BLOCK SIZE IN BYTES                           @D51GDRP 01500000
.*    &AGBKPSG - NUMBER OF BLOCKS PER SEGMENT                  @D51GDRP 01550000
.*    &AGPNBLK - SHIFT AMOUNT PAGE# TO BLOCK#                  @D51GDRP 01600000
.*    &AGSGBLK - SHIFT AMOUNT SEGMENT# TO BLOCK NUMBER         @D51GDRP 01650000
.*    &AGPABLK - NUMBER OF PAGES PER BLOCK                     @D52VDRP 01700052
.*       AIF   (NOT &SGEND OR &SGPMR).NOPRT10                  @D14BDRP 01750000
.*       PRINT NOGEN                                           @D14BDRP 01800000
.NOPRT10 ANOP                                                  @D14BDRP 01850000
.************************************************************* @D35NDRP 01900000
*                                                            * @D35NDRP 01950000
.*       IBM DISK OPERATING SYSTEM                           * @D35NDRP 02000000
*        SUPERVISOR - SGPDSECT - 5686-032-06                 * @D31UDRP 02050052
************************************************************** @D35NDRP 02100000
*                                                            * @D35NDRP 02150000
*        LICENSED MATERIALS - PROPERTY OF IBM                * @D31UDRP 02200000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"         * @D31UDRP 02250000
*        5686-032 (C) COPYRIGHT IBM CORP. 1977, 1992         * @D52VDRP 02300052
*        SEE COPYRIGHT INSTRUCTIONS                          * @D31UDRP 02350000
*                                                            * @D35NDRP 02400000
************************************************************** @D35NDRP 02450000
         TITLE 'DOS SUPERVISOR        SGPDSECT     PAGE MANAGEMENT (DSE*02500000
               CTS AND EQUATES)'                                        02550000
         SPACE 2                                                        02600000
* /* START OF SPECIFICATIONS ****************************************** 02650000
*                                                                       02700000
*01* MODULE NAME = SGPDSECT                                             02750000
*                                                                       02800000
*01* MODULE TYPE = SUPERVISOR GENERATION MACRO                          02850000
*                                                                       02900000
*01* DESCRIPTIVE NAME = DUMMY SECTIONS FOR PAGE MANAGEMENT SERVICES     02950000
*                                                                       03000000
*01* CHANGE ACTIVITY = AS FOLLOWS                                       03050000
*    CHANGE DESCRIPTIONS                                                03100000
*            NEW MACRO -- FIRST RELEASE 35                              03150000
*              E - SUPPORT                                     @D35EDRP 03200000
*              FBA - SUPPORT                                   @D35DDRP 03250000
*              S/370 - SUPPORT                                 @D35CDKH 03300000
*                                                              @D35CDAA 03350000
*              MULTIPLE PAGE I/O, VIO-SUPPORT                  @D14BDRP 03400000
*              16 MB REAL STORAGE FOR /370                     @D14ADVB 03450000
*              VAE SUPPORT                                     @DA33314 03500000
*              4 K PAGESIZE                                    @D14KDVB 03550000
*              31-BIT REAL AND XA I/O SUPPORT                  @D51GDTP 03600000
*              31-BIT ENABLING                                 @D52VDVB 03625052
*              TURBO-DISPATCHER SUPPORT                        @D61RDRP 03637561
*                                                                       03650000
* A000000-999999                                               @D35EDRP 03700000
**** END OF SPECIFICATIONS *******************************************/ 03750000
&AGPNSFT SETA  12                 SHIFT ADDRESS TO PNR         @D14KDVB 03800000
&AGEPMSK SETA  X'7FFFF0'          PAGE MASK  X'7FFFF000'       @D14KDVB 03900000
&AGBSIZB SETA  32768              BLOCK SIZE = 32 K            @D51GDRP 03950000
&AGPNBLK SETA  3                  SHIFT AMOUNT PAGE# TO BLOCK# @D51GDRP 04000000
&AGPABLK SETA  8                  PAGES / ALLOCATION UNIT      @D52VDVB 04025052
&AGSGBLK SETA  5                  SHIFT AM. SEGMENT# TO BLOCK# @D51GDRP 04750000
&AGBKPSG SETA  32                 32 BLOCKS PER SEGM. IN XA    @D51GDRP 04800000
&AGPTSPT SETA  10                 SHIFT SEGMENT# TO PTE-OFFSET @D51GDTP 04850000
.* &AGPTSPT = LOG ((SEG SIZE/PAGE SIZE) * LEN PTE)             @D51GDTP 04900000
&AGPGPSG SETA  256                NUMBER OF PAGES PER SEGMENT  @D51GDTP 04950000
&AGSGSIZ SETA  X'100000'          SEGMENT SIZE = 1M            @D51GDTP 05000000
.*   THE FOLLOWING CALCULATIONS ARE DUE TO LIMIT. IN VSE ASSEMBLER      05050000
&AGSGMSK SETA  X'FFFFFF'*X'80'+X'40' SEGMENT MASK X'7FFFFFC0'  @D51GDRP 05100000
&AGPGMSK SETA  X'07FFFF'*X'1000'  PTE  MASK  X'7FFFF000'       @D51GDRP 05150000
.*                                                                      05200000
&AGINVPT SETA  X'64'              INVALIDATION PATTERN PTE     @D52VDRP 05250052
&AGIBIT  SETA  4                  IBIT                         @D51GDTP 05300000
&AGOFMSK SETA  X'3FF'             PAGE TABLE OFFSET MASK X'3FF'@D51GDTP 05350000
*-------------------------------------------------------------*@D149DVB 05450000
*                                                                       05500000
*        DUMMY CONTROL SECTIONS AND EQUATES USED IN PAGE MANAGEMENT     05550000
*                                                                       05600000
*-------------------------------------------------------------*@D149DVB 05650000
         SPACE 1                                               @D14KDVB 05700000
&PMPTSH  SETA  2                  PTE LENGTH IS 4 BYTE FOR XA  @D51GDRP 05750000
&PMSGSH  SETA  4                  16*64K IS 1M                 @D51GDRP 05800000
****     EQUATES FOR IMMEDIATE OPERANDS    ****                         06050000
         SPACE 1                                                        06100000
PNSHIFT  EQU   &AGPNSFT           SHIFT AMOUNT PG ADDR <-> PG# @D14KDVB 06150000
PNBLSHFT EQU   &AGPNBLK           SHIFT AMOUNT PAGE# TO BLOCK# @D51GDRP 06200000
SGBLSHFT EQU   &AGSGBLK           SHIFT AMOUNT SEGM# TO BLOCK# @D51GDRP 06250000
PTSHIFT  EQU   PNSHIFT-&PMPTSH    SHIFT RIGHT OF PG ADDR TO GT         *06300000
                                  OFFSET IN PT                 @D51GDRP 06350000
PNPFTESH EQU   5                  SHIFT AMOUNT PF-# <-> OFFSET         *06480052
                                  IN PFT                       @D52VDRP 06510052
PFSHIFT  EQU   PNSHIFT-PNPFTESH   SHIFT AMOUNT PG ADDR <-> OFF-        *06540052
                                  SET IN PFT                   @D52VDRP 06570052
SNSHIFT  EQU   16+&PMSGSH         SHIFT AMOUNT SG ADDR <-> SG# @D51GDRP 06600000
SNSTESH  EQU   2                  SHIFT AMOUNT SGM# <-> STE OF @D14NDFG 06750000
PF4SHIFT EQU   4                  SHIFT AMOUNT PF/PG-ADDR  TO CORRECT  *06883352
                                  POSITION FOR PTE             @D52VDRP 06916652
PNRPTOSH EQU   &PMPTSH            SHIFT AMOUNT PNR <-> PTO     @D51GDRP 06950000
PTASPTSH EQU   &AGPTSPT           SHIFT AMOUNT SG# <-> PTE-OFF.@D14KDVB 07000000
NPAGPSEG EQU   &AGPGPSG           NUMBER OF PAGES PER SEGMENT  @D14KDVB 07050000
NPAGPBLK EQU   &AGPABLK           NUMBER OF PAGES PER ALLOCATION       *07100052
                                  ... UNIT ON DISK             @D51GDRP 07150000
STSHIFT  EQU   14+&PMSGSH         SHIFT AMOUNT ADDR <-> ST OFS @D51GDRP 07250000
****     DSECT FOR PAGE-FRAME-TABLE-ENTRY        ****                   07400000
         SPACE 1                                                        07450000
         MAPPFTE ,                                             @D14NDFG 07500000
         SPACE 3                                               @D35CDKH 07550000
         MAPSTE                                                @D14NDFG 07600000
         SPACE 2                                                        07650000
         MAPPTE                                                @D14ADVB 07700000
         SPACE 2                                                        07750000
         MAPPTASE                                              @D14NDVB 07800000
         SPACE 2                                               @D14NDVB 07850000
*                                                                       07950000
         MAPVIO                                                @D14BDRP 08000000
         SPACE 2                                                        08050000
*                                                                       08100000
*****    DSECT FOR DPDTAB  , USED TOO BY IPL AND STAND-ALONE-DUMP ****  08150000
         SPACE 1                                                        08200000
DPDADR   MAPDPD                                                         08250000
         SPACE 2                                                        08300000
*                                                                       08350000
****     DSECT FOR PARAM-LIST ENTRY OF TFIX/TFREE    ****               08400000
         SPACE 1                                                        08450000
TFIXLSTE DSECT                                                          08500000
TFIXENT  DS    0XL8               CURRENT ENTRY IN PARAM LIST  @D52VDVB 08550052
TFIXBEG  DS    F                  BEGIN ADDR                   @D52VDVB 08600052
TFIXEND  DS    F                  END ADDR                     @D52VDVB 08650052
TFIXNENT DS    F                  NEXT ENTRY IN PARAM-LIST     @D52VDVB 08700052
         SPACE 2                                                        08750000
*                                                                       08800000
****     DSECT FOR PARAM-LIST ENTRY OF PFIX/PFREE   ****                08850000
         SPACE 1                                                        08900000
PFIXLSTE DSECT                                                          08950000
PFIXENT  DS    0XL8                                                     09000000
PFIXBEG  DS    XL4                BEGIN ADDR OF AREA                    09050000
XEOL     EQU   X'80'              END OF LIST INDICATION       @D52VDVB 09075052
PFIXLNG  DS    XL4                LENGTH-1 OF AREA                      09100000
         SPACE 2                                                        09150000
*                                                                       09200000
****     DSECT FOR PARAM-LIST ENTRY OF PFIX FOR CHECKPOINT/RESTART **   09250000
         SPACE 1                                                        09300000
RESTLSTE DSECT                                                          09350000
RESTENT  DS    0XL10                                           @D35CDKH 09650000
RESTPAGE DS    XL4                ADDR OF PAGE TO BE PFIXED    @D35CDKH 09700000
RESTPF   DS    XL4                PF-ADDR WHERE PG HAS TO BE FX@D35CDKH 09750000
RESTCNT  DS    XL2                VALUE OF PFIXC OF PAGE                09850000
         SPACE 2                                                        09900000
*                                                                       09950000
****     DSECT FOR PARAM-LIST ENTRY OF RELPAG,FCEPGOUT AND PAGEIN ***   10000000
         SPACE 1                                                        10050000
POPTLSTE DSECT                                                          10100000
POPTENT  DS    0XL8                                                     10150000
POPTBEG  DS    XL4                BEGIN ADDR OF AREA                    10200000
POPTLNG  DS    XL4                LENGTH-1 OF AREA                      10250000
         SPACE 2                                                        10300000
*                                                                       10305052
****     DSECT FOR THE AREA LAYOUT OF THE SETPFA MACRO ***     @D52GDWL 10310052
         SPACE 1                                               @D52GDWL 10315052
PFAAREA  DSECT                                                 @D52GDWL 10320052
PFAENT   DS    0XL16                                           @KX41342 10322552
PFAADR   DS    A                  ADDR OF PAGE FAULT           @D52GDWL 10325052
PFASTKN  DS    0XL8               SPACE TOKEN                  @KX41342 10328052
PFASCB1  DS    H                  1ST PART OF SCB ADDR         @KX41342 10331052
PFATIB   DS    XL4                ADDR OF PHO TIB              @D52GDWL 10334052
PFASCB2  DS    H                  2ND PART OF SCB ADDR         @KX41342 10337052
PFARES   DS    A                  RESERVED                     @D52GDWL 10340052
         SPACE 2                                                        10345052
****     DUMMY CONTROL SECTION FOR PAGETAB ENTRY    ****                10350000
         SPACE 1                                                        10400000
PGTABADR DSECT                                                          10450000
PGTBLNG  DS    0XL12              LENGTH OF PAGETAB ENTRY      @D52VDVB 10500052
PGTBTID  DS    H                  TID OF REQUESTOR             @D52VDVB 10550052
PGTBIND  DS    X .                INDICATORS FOR POSTING                10650000
*TRABIT  EQU   X'80' .            PAGEIN COMPLETED                      10700000
PGT2ND   EQU   X'80'              2ND SCAN NEEDED                       10750000
PGTFULL  EQU   X'40' .            PAGETAB IS FULL                       10800000
INVPG    EQU   X'20' .            INVALID ADDR                          10850000
LENGNEG  EQU   X'10' .            NEGATIV LENGTH                        10900000
INVLI    EQU   X'08' .            ADDR LIST NOT COMPL. IN PART          10950000
TERMLL   EQU   X'04' .            PAGING ACT TOO HIGH                   11000000
TERMIN   EQU   X'02' .            REQU TASK TERMINATES                  11050000
PGTSCND  EQU   X'01'              LAST SCAN INDICATION         @D52VDVB 11075052
INVFUNC  EQU   X'01'              INCONSISTANT FUNCTION/OPTION @D52VDVB 11100052
PGTBMODE DS    X                  ADDRESSING MODE              @D52VDVB 11125052
PGTLISTP DS    F                  LIST POINTER                 @D52VDVB 11150052
PGTECBP  DS    F                  POINTER TO ECB               @D52VDVB 11175052
         SPACE 2                                                        11180052
PMRRELPG EQU   X'03'              FUNCTION CODE - RELPAGE      @D52VDVB 11185052
PMRFCEPT EQU   X'04'              FUNCTION CODE - FCEPAGOUT    @D52VDVB 11190052
PMRPAGIN EQU   X'05'              FUNCTION CODE - PAGEIN       @D52VDVB 11195052
PMRPSTAT EQU   X'06'              FUNCTION CODE - PAGESTAT     @D52GDVB 11196652
PMRSETPF EQU   X'07'              FUNCTION CODE - SETPFA       @D52GDVB 11198252
         SPACE 2                                                        11200000
*                                                                       11250000
****     DSECT FOR DEVICE CONTROL BLOCK FOR PDS DEVICE  ***    @D14BDRP 11300000
         SPACE 1                                                        11350000
         MAPDEVCB ,                                            @D14NDFG 11400000
         SPACE 2                                                        11450000
*-------------------------------------------------------------*@D52VDVB 11455552
*     DSECTS AND EQUATES FOR BLOCKED PAGE I/O                  @D52VDVB 11461052
*-------------------------------------------------------------*@D52VDVB 11466552
         SPACE 1                                               @D52VDVB 11472052
         SGPGOTB GEN=MAPPSL                                    @D52VDVB 11477552
         SPACE 2                                               @D52VDVB 11483052
         SGPGOTB GEN=EQU                                       @D52VDVB 11488552
         SPACE 2                                               @D52VDVB 11494052
*-------------------------------------------------------------*@D149DVB 11500000
*        EQUATES USED IN PAGE-MANAGEMENT                                11550000
*-------------------------------------------------------------*@D149DVB 11600000
         SPACE 2                                                        11650000
*                                                                       11950000
****     EQUATES FOR BITS IN TRADDR      ****                           12000000
         SPACE 1                                                        12050000
USERPF   EQU   X'80'              INDICATE USER PAGE-FAULT     @D36IDRP 12100000
         SPACE 2                                                        12150000
*                                                              @D35CDKH 12250000
****     EQUATES FOR PAGEBITS     ****                                  13150000
         SPACE 1                                                        13200000
RBIT     EQU   X'04'              REFERENCE-BIT SET BY H/W              13400000
CBIT     EQU   X'02'              CHANGE-BIT SET BY H/W                 13450000
         SPACE 2                                                        13500000
MINPSQ   EQU   30                 MINIMUM # OF PAGE-FRAMES AVAILABLE    13550061
*                                 FOR REPLACEMENT                       13600000
.*   9 AS IN OLD SUPPORT PLUS 2 FOR EACH CPU (MAX. 10) TO GET  @D61RDRP 13633361
.*   THE SAVE AREA IN STORAGE. (ROUNDED UP TO 30)                       13666661
LLDEFIX  EQU   X'40'              INDICATE TFREE REQ. BY CCWTR          13700000
         SPACE 2                                                        13750000
.* ------------------------------------------------------------         13800000
*                                                                       13850000
.*       AIF   (NOT &SGEND OR &SGPMR).NOPRT20                  @D14BDRP 14200000
.*       PRINT GEN                                             @D14BDRP 14250000
.NOPRT20 ANOP                                                  @D14BDRP 14300000
         MEND                                                           14350000
