         MACRO                                                          00001000
         MAPDEVCB &MOD=                                                 00002000
         GBLB  &IJBSGEN           SUPERVISOR GENERATION                 00003000
         GBLA  &NCLASS            NUMBER OF CLASSES            @D51IDRP 00004000
         GBLA  &NPART             NUMBER OF PARTITIONS         @D14NDVB 00005000
.*       IBM DISK OPERATING SYSTEM                            *         00006000
*        SUPERVISOR - MAPDEVCB - 5686-066-06                  *@D31UDRP 00007000
.**************************************************************         00008000
.*                                                            *@D31UDRP 00009000
         AGO   .ZZZ                                           *         00010000
*        LICENSED MATERIALS - PROPERTY OF IBM                 *@D31UDRP 00011000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"          *@D31UDRP 00012000
*        5686-032 (C) COPYRIGHT IBM CORP. 1982, 1999          *@DA44841 00013490
*        SEE COPYRIGHT INSTRUCTIONS                           *@D31UDRP 00014000
.ZZZ     ANOP                                                 *         00015000
.**************************************************************         00016000
.**** START OF SPECIFICATIONS ***************************************** 00017000
.*                                                                      00018000
.*01* MODULE-TYPE = MACRO                                               00019000
.*                                                                      00020000
.*01* DESCRIPTIVE NAME = MAPPING OF PAGING DEVICE CONTROL BLOCK         00021000
.*                                                                      00022000
.*  STATUS           =  NEW MACRO - FIRST RELEASE AF 2.1                00023000
.*                                                                      00024000
.*  FUNCTION         =  SHOW LAYOUT OF PAGING DEVICE CONTROL BLOCK      00025000
.*  NOTES            =  NONE                                            00026000
.*  DEPENDENCIES     =  NONE                                            00027000
.*  RESTRICTIONS     =  NONE                                            00028000
.*  REGISTER CONVENT.=  NONE                                            00029000
.*  PATCH LABEL      =  NONE                                            00030000
.*  MACRO TYPE       =  ASSEMBLER                                       00031000
.*  PROCESSOR        =  ASM                                             00032000
.*  MACRO SIZE       =  SEE OUTPUT                                      00033000
.*  ATTRIBUTES       =  MAPPING DSECT                                   00034000
.*  ENTRY POINT      =  DEVCB                                           00035000
.*  PURPOSE          =  SEE FUNCTION                                    00036000
.*  LINKAGE          =  SEE INPUT                                       00037000
.*  CALLER           =  SUPERVISOR, IPL AND SVA ROUTINES                00038000
.*  INPUT            =  NONE                                            00039000
.*  OUTPUT           =  DEVCB DSECT                                     00040000
.*  EXIT NORMAL      =  MACRO EXPANSION                                 00041000
.*  EXIT ERROR       =  NONE                                            00042000
.*  EXTERNAL REFEREN.=  NONE                                            00043000
.*  ROUTINES         =  NONE                                            00044000
.*  DATA AREAS       =  NONE                                            00045000
.*  CONTROL BLOCKS   =  NONE                                            00046000
.*  TABLES           =  NONE                                            00047000
.*  MACROS           =  NONE                                            00048000
.*  CHANGE ACTIVITIES  NEW MACRO, FIRST RELEASE 2.1                     00049000
.*  COPIED FROM SGPDSECT                                                00050000
.*  MULTIPLE VIRTUAL ADDRESS SPACES                            @D14NDFG 00051000
.*  31-BIT REAL AND XA I/O SUPPORT                             @D51GDTP 00052000
.*  MORE PARTITION SUPPORT                                     @D51IDRP 00053000
.*  DEVPDASA/DEVPDASB HAVE TO BE FULLWORD        @D52VDRP      @KY30015 00054000
.*  MULTI-PROCESSOR (TURBO-DISPATCHER) SUPPORT                 @D61RDRP 00055000
.*  PROVIDE ONE ERROR ENTRY PER PMR-DEVICE                     @DA44841 00055500
.*  SKIP DEFINITION WHICH LEAD TO WARNING IN IJBXDBUG          @XXXXXHT 00055700
.*       FINAL FIX WILL BE PROVIDED LATER                      @XXXXXHT 00055700
.*  RESTORE MAPDEVCB PROTOTYP: DELETE &MOD                     @XXXXXUL 00055700
.*  CHANGE MAPDEVCB CALL IN IJBXDBUG: DELETE MOD=IJBXDBUG PARM @XXXXXUL 00055700
.*                                                                      00056000
.*  MODIFIED REGISTER=  NONE                                            00057000
.*                                                                      00058000
.** END OF SPECICATIONS *********************************************** 00059000
.*                                                                      00060000
         AIF   (&IJBSGEN).NOWARN                               @D14ZDFG 00061000
*********************************************************************** 00062000
* WARNING: THE FOLLOWING MAPPING REFERS TO AN INTERNAL SUPERVISOR     * 00063000
*          CONTROL BLOCK. IT MAY CHANGE ANY TIME AND IS NOT COMMITTED * 00064000
*          AS AN INTERFACE OF ANY LEVEL.                              * 00065000
*********************************************************************** 00066000
.NOWARN  ANOP                                                  @D14ZDFG 00067000
DEVCB    DSECT                                                 @D14BDRP 00068000
DEVCBNXT DS    F                  ADDR OF NEXT DEVCB           @D14BDRP 00069000
DEVSTAT  DS    X                  DEVICE STATUS                @D14BDRP 00070000
DEVSTRT  EQU   X'80'              REQ. GIVEN TO IOCS/ IN PROC. @D14BDRP 00071000
DEVEMPTY EQU   X'40'              NO REQ. ENQUEUED ON THIS DEV.@D14BDRP 00072000
DEVPGWO  EQU   X'20'              REQ. WAITING FOR UNC. PAGEOUT@D14BDRP 00073000
DEVCBTYP DS    X                  TYPE OF DEVICE               @D14BDRP 00074000
DEVEXT#  DS    X                  NUMBER OF EXTENTS FOR DEVICE @D61RDVB 00075000
DEVCPSL  DS    X                  PSLSTATE BEFORE STARTING I/O @D52VDVB 00076000
         DS    0F                                              @D14BDRP 00077000
DEVACT   DS    F                  ADDR OF PGQE GIVEN TO IOCS   @D14BDRP 00078000
DEVDPD   DS    F                  ADDR OF 1ST DPD-ENTRY        @D14BDRP 00079000
DEVRELO  DS    F                  RELOC. FOR 1ST DPD ENTRY     @D14BDRP 00080000
DEVAPDAS DS    F                  A(FIRST PDAS BYTE FOR DEV.)  @D51GDRP 00081000
DEVPDASA DS    F                  PDAS ENTRIES TO BE SKIPPED   @KY30015 00082000
DEVPDASB DS    F                  PDAS ENTRIES TO BE SCANNED   @KY30015 00083000
DEVPCB   DS    F                  ADDR OF ACTUAL CLASS-PCB     @D14BDRP 00084000
DEVCBMSK DS    F                  DEVICE MASK FOR DISPATCHING  @D61RDVB 00085000
DEVERIN  DS    H                  NUMBER OF I/O ERRORS PAGE-IN @D52VDVB 00086000
DEVEROUT DS    H                  NUMBER OF I/O ERRORS PAGE-OUT@D52VDVB 00087000
APFPSS   DS    F                  RESERVED                     @D64ADOW 00088000
DEVCCB   DS    4F                 CCB FOR THIS DEVICE          @D14BDRP 00089000
DEVCCW   DS    0D                 CHANNELPROGRAM FOR DEVICE    @D14BDRP 00090000
PDSEXT   DS    2D                 EXTENT INFO FOR FBA/ECKD     @D14BDRP 00091000
         DS    D                  ALIGMENT FOR FBA/CKD COMMON          *00092000
                                  ... PROCESSING               @D52VDVB 00093000
DEVCBFBA DS    0D                 BEGIN OF CHANNELPROGR FOR FBA@D14BDRP 00094000
FBADEFEX DS    D                  FBA DEFINE EXTENT CCW        @D14BDRP 00095000
FBALOCAT DS    D                  FBA LOCATE CCW               @D14BDRP 00096000
FBARW    DS    D                  FBA READ/WRITE CCW           @D14BDRP 00097000
         ORG   DEVCCW                                          @D14BDRP 00098000
DEVCKD   DS    0D                 BEGIN OF CHANNELPROGR FOR CKD@D14BDRP 00099000
CKDSEEK  DS    D                  CKD SEEK CCW                 @D14BDRP 00100000
CKDSETS  DS    D                  CKD SET SECTOR CCW           @D14BDRP 00101000
CKDSRCH  DS    D                  CKD SRCHIDE CCW              @D14BDRP 00102000
CKDTIC   DS    D                  CKD TIC CCW                  @D14BDRP 00103000
CKDEOC   DS    D                  CKD TIC CCW                  @D52VDVB 00104000
CKDRW    DS    D                  CKD READ/WRITE CCW           @D14BDRP 00105000
         ORG   CKDRW                                           @D52VDVB 00106000
CKDGENRD DS    8D                 8* READ                      @D52VDVB 00107000
CKDGENWR DS    21D                7* WRT,SEARCH,TIC            @D52VDVB 00108000
         DS    D                  1* WRT                       @D52VDVB 00109000
DEVLCDAT DS    2D                 LOCATE DATA FOR ECKD/FBA     @D51EDRP 00110000
         ORG   DEVLCDAT                                        @D51EDRP 00111000
DEVLCOP  DS    X                  FBA/ECKD: OPERATION BYTE     @D51EDRP 00112000
         DS    X                  ALWAYS ZERO                  @D51EDRP 00113000
DEVBLCNT DS    H                  FBA: BLOCK COUNT             @D51EDRP 00114000
         ORG   DEVBLCNT                                        @D51EDRP 00115000
         DS    X                  ECKD: ALWAYS ZERO            @D51EDRP 00116000
DEVLCCNT DS    X                  ECKD: COUNT BYTE (ALWAYS 1)  @D51EDRP 00117000
DEVLCSAD DS    F                  ECKD: SEEKADDR (CCHH)        @D51EDRP 00118000
         ORG   DEVLCSAD                                        @D51EDRP 00119000
DEVRLDSP DS    F                  FBA:  REL. BLOCK# IN EXTENT  @D51EDRP 00120000
         ORG   DEVLCSAD+2                                      @D51EDRP 00121000
DEVSEEK  DS    2X                 CKD: SEEK ARGUMENT (BB.....) @D51EDRP 00122000
DEVSRCH  DS    XL5                ECKD/CKD: SEARCH ARG.(CCHHR) @D51EDRP 00123000
         ORG   DEVSRCH                                         @D51EDRP 00124000
DEVSRCC  DS    XL2                ECKD/CKD: CC VALUE           @D51EDRP 00125000
DEVSRHH  DS    XL2                ECKD/CKD: HH VALUE           @D51EDRP 00126000
DEVSRR   DS    X                  ECKD/CKD: RECORD NUMBER      @D51EDRP 00127000
DEVLCSEC DS    X                  SECTOR NUMBER                @D51EDRP 00128000
DEVLCTLF DS    XL2                TRANSF. LENGTH FAC. (ALW. 0) @D51EDRP 00129000
         ORG   DEVLCTLF                                        @D52VDVB 00130000
         DS    7XL6               MORE SEARCH FIELDS           @D52VDVB 00131000
         DS    0F                                              @D52VDVB 00132000
DEVGIDAL DS    16A                AREA TO GENERATE IDALS               *00133000
                                  FOR BLOCKED PAGE I/O         @D52VDVB 00134000
         DS    F                  RESERVED                     @D61RDRP 00135000
         DS    F                  RESERVED                     @D61RDVB 00136000
         AIF   ('&MOD' EQ 'IJBXDBUG').SKPERBK                  @XXXXXUL 00136100
         DS    0F                 FORCE WORD BOUNDARY          @DA44841 00136300
DEVERBLK DS    XL(ERRQSNS-ERBLKADR+ERRQSNSM)'00'               @DA44841 00136600
.SKPERBK ANOP                                                  @XXXXXHT 00136800
         DS    0D                                              @D52VDVB 00137000
DEVDATA  EQU   FBARW+1            DATA ADDR IN READ/WRITE CCW  @D14BDRP 00138000
CCWDATA  EQU   1                  OFFSET DATA ADDRESS IN CCW   @D52VDVB 00139000
CCWFLGS  EQU   4                  OFFSET FLAGS IN CCW          @D52VDVB 00140000
DEVCBLNA EQU   *-DEVCB            LENGTH OF FIXED PART OF DEVCB@D52VDVB 00141000
.*   ATTENTION  CKDRW AND FBARW HAVE TO HAVE THE SAME OFFSET   @D14BDRP 00142000
DEVCHCK1 EQU   CKDRW-FBARW                                     @D52VDVB 00143000
DEVCHCK2 EQU   FBARW-CKDRW                                     @D52VDVB 00144000
*                                 LOCATE INFORMATION           @D14BDRP 00145000
* END OF FIXED PART OF DEVCB                                   @D14NDVB 00146000
         AIF   (&IJBSGEN).SVR10                                @D14NDVB 00147000
*---------------------------------------------------------------------* 00148000
* WARNING: THE VARIABLE PART OF THE DEVCB CONTAINS THE PAGE FAULT     * 00149000
*          QUEUE HEADERS. ITS LENGTH IS DEPENDENT OF THE NUMBER       * 00150000
*          GENERATED PARTITIONS IN THE SUPERVISOR.                    * 00151000
*---------------------------------------------------------------------* 00152000
         MEXIT                                                 @D14NDVB 00153000
.SVR10   ANOP                                                  @D14NDVB 00154000
PFRQBEG  DS    F                  BEG OF PAGE-FAULT QUEUE      @D14BDRP 00155000
PFRQEND  DS    F                  END OF PAGE-FAULT QUEUE      @D14BDRP 00156000
         DS    ((&NPART+&NCLASS)*2)F REMAINING QUEUE HEADERS   @D51IDRP 00157000
PORQBEG  DS    F                  BEG OF PAGE-OUT QUEUE        @D14BDRP 00158000
PORQEND  DS    F                  END OF PAGE-OUT QUEUE        @D14BDRP 00159000
DEVCBLNG EQU   *-DEVCB            ACTUAL LENGTH                @D14NDFG 00160000
         MEND                                                           00161000
