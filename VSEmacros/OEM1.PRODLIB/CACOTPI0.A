*ASM XOPTS(NOPROLOG NOEPILOG SP)                                                
         GBLC  &VERSION                                                 00001000
         DFHSYS                                                         00002000
CICSUTLA DSECT                                                          00003000
         SPACE 1                                                        00004000
UCPSECT  EQU   *                                                        00005000
         SPACE 1                                                        00006000
*********************************************************************** 00007000
****                                                               **** 00008000
****       SYSTEM ADAPTER UTILITY PARAMETER AREA DESCRIPTION       **** 00009000
****               FULL SCREEN TP SYSTEMS                          **** 00010000
****                                                               **** 00011000
****            COMPUTER ASSOCIATES INTERNATIONAL INC.             **** 00012000
****          711 STEWART AVENUE, GARDEN CITY, L.I., N.Y.          **** 00013000
****                                                               **** 00014000
****                   COPYRIGHT - 1981                            **** 00015000
****                                                               **** 00016000
*********************************************************************** 00017000
         SPACE 1                                                        00018000
UCSRVAD  DS    A ......................ADDRESS OF SERVICE MODULE        00019000
UCEPRAD  DS    A ......................ADDRESS OF EXTERNAL 'PARM' (OS)  00020000
UCSYIND  DS    XL1 ....................SYSTEM CONFIGURATION INDICATORS  00021000
UCMOSVS  EQU   B'10000000' ----------->...RUNNING ON OS/VS1 OR OS/VS2   00022000
UCMSGIN  EQU   B'01000000' ----------->...COMMAND I/P FROM CONSOLE      00023000
UCDELETE EQU   B'00100000' ----------->...LINK FINISHED DELETE PROGRAM  00024000
UCFULLSC EQU   B'00010000' ----------->...I/O IS FULL SCREEN            00025000
UCSYEOJ  EQU   B'00001000' ----------->...EOJ OCCURING                  00026000
UCEURPN  EQU   B'00000100' ----------->...EUROPEAN DATE TYPE            00027000
UCNOHLP  EQU   B'00000010' ----------->...HELP IS DISABLED     DO530001 00028000
UCNOADPT EQU   B'00000001' ----------->...SYS ADAPT NOT REG'D  DO530001 00029000
UCSYSLC  DS    XL1 ....................SYSTEM LINE COUNT FOR SYSPRINT   00030000
UCREQCD  DS    XL2 ....................SERVICE REQUEST VECTOR           00031000
UCREQVC  EQU   UCREQCD+1 ..............(TO ALLOW 'MVI' OF REQUEST)      00032000
UCVFREE  EQU   0 --------------------->...ALLOCATE FREE-STORAGE         00033000
UCVFRET  EQU   4 --------------------->...RELEASE FREE-STORAGE          00034000
UCVCGET  EQU   8 --------------------->...READ A RECORD FROM SYSIN      00035000
UCVPPUT  EQU   12 -------------------->...WRITE A LINE TO SYSPRINT      00036000
UCVLINK  EQU   16 -------------------->...LINK A UTILITY SUBPROGRAM     00037000
UCVMESA  EQU   20 -------------------->...INVOKE MAIN SYSTEM ADAPTER    00038000
UCVOPNR  EQU   24 -------------------->...OPEN A DATA-SET               00039000
UCVTRDR  EQU   28 -------------------->...ISSUE TAPE READ REQUEST       00040000
UCVTWTR  EQU   32 -------------------->...ISSUE TAPE WRITE REQUEST      00041000
UCVCLSR  EQU   36 -------------------->...CLOSE A DATA-SET              00042000
UCVSRTI  EQU   40 -----S.R.A.M.------->...INITIALIZE SRAM STORAGE       00043000
UCVSRTO  EQU   44 -----S.R.A.M.------->...CALL SORT OPEN                00044000
UCVSRTC  EQU   48 -----S.R.A.M.------->...CALL SORT CLOSE               00045000
UCVSRTF  EQU   52 -----S.R.A.M.------->...CALL SORT FILL                00046000
UCVSRTG  EQU   56 -----S.R.A.M.------->...CALL SORT GET                 00047000
UCVSRTR  EQU   60 -----S.R.A.M.------->...CALL SORT RE-OPEN             00048000
UCVLOAD  EQU   64 -------------------->...LOAD A PHASE         V0510004 00049000
UCVDEL   EQU   68 -------------------->...DELETE A PHASE       V0510004 00050000
UCVTERM  EQU   72 -------------------->...TERMINAL I/O REQUEST V0510004 00051000
UCVWTTP  EQU   76 -------------------->...TERMINAL WAIT REQUESTV0510013 00052000
UCVPCLK  EQU   80 -------------------->...CICS PC LINK REQUEST V0510013 00053000
         SPACE 1                                               V0510013 00054000
*--------------------------------------------------------------------*  00055000
* --- SERVICE RELATED FIELDS FOLLOW, INITIALIZED BY APPROPRIATE      *  00056000
*     PROCESSOR BEFORE RETURNING CONTROL TO THE UTILITY.             *  00057000
*                                                                    *  00058000
*                     REFERENCED BY..  CONTENTS......................*  00059000
UCPSCLN  DS    1F --> UCVFREE,UCVFRET  STORAGE ELEMENT LENGTH        *  00060000
UCPSCIC  EQU   UCPSCLN+1      UCVFREE  STORAGE INITIALIZATION BYTE   *  00061000
UCPSCAD  DS    1F --> UCVFREE,UCVFRET  ADDRESS OF STORAGE ELEMENT    *  00062000
UCPRDRA  DS    1F --> UCVCGET          ADDRESS OF NEXT SYSIN RECORD  *  00063000
*                                      (SET TO '0' WHEN EOF OCCURS)  *  00064000
UCPPRTA  DS    1F ----------> UCVPPUT  ADDRESS OF NEXT SYSPRINT LINE *  00065000
UCPPASA  EQU   UCPPRTA   ---> UCVPPUT  SYSPRINT ASA CTLCHAR          *  00066000
UCPOCRA  DS    1F --> UCVTRDR,UCVTWTR  CURRENT OCRPL BLOCK ADDRESS   *  00067000
UCPPNAM  DS    CL8 -> UCVLINK          UTILITY SUBPROGRAM NAME ......*  00068000
         ORG   UCPPNAM                 REDEFINE ABOVE FIELD    V0510004 00069000
UCPPADR  DS    1F                      ...ADDR OF LOADED PHASE V0510004 00070000
         DS    1F                      ...UNUSED               V0510004 00071000
         SPACE 1                                                        00072000
UCPUSER  DS    16F                     USER SCRATCH-PAD WORKAREA        00073000
UCPRDREX DS    1F                      ADDR OF RDR EXIT RTN    V0510004 00074000
UCPPRTEX DS    1F                      ADDR OF PRT EXIT RTN    V0510004 00075000
UCPFCAA  DS    1F                      ADDR OF CICS SCA        V0510013 00076000
UCPTRMD  DS    1F                      TERMINAL DEFINITION     V0510013 00077000
UPPEND   EQU   *      ---END OF UTILITY COMMUNICATIONS AREA---          00078000
UCPLEN   EQU   UPPEND-UCSRVAD   LENGTH OF COMMUNICATIONS AREA           00079000
         SPACE 1                                               V0510004 00080000
*                                                              V0510004 00081000
UTLRC08  EQU   08 ---> RTC=08 -- PHASE NOT FOUND               V0510004 00082000
UTLRC12  EQU   12 ---> RTC=12 -- STORAGE ERROR                 V0510004 00083000
UTLRC24  EQU   24 ---> RTC=24 -- INVALID REQUEST               V0510004 00084000
*                             -- SRAM ALREADY INITIALIZED      V0510004 00085000
*                             -- FILE IS NOT OPEN              V0510004 00086000
*                             -- FILE IS ALREADY OPEN          V0510004 00087000
*                             -- EOF ON READER INPUT           V0510004 00088000
UTLRC28  EQU   28 ---> RTC=28 -- SRAM NOT INITIALIZED          V0510004 00089000
*                             -- EOF ON TAPE INPUT             V0510004 00090000
UTLRC32  EQU   32 ---> RTC=32 -- EOF ON SRAM WORKFILE          V0510004 00091000
*                             -- DISK FILE OPEN FAILURE        V0510004 00092000
UTLRC36  EQU   36 ---> RTC=36 -- SRAM WORKFILE OPEN FAILURE    V0510004 00093000
UTLRC40  EQU   40 ---> RTC=40 -- TOO MANY PHASE LOADS          V0510004 00094000
UTLRC44  EQU   44 ---> RTC=44 -- PHASE NOT LOADED (UCVDEL)     V0510004 00095000
UTLRC48  EQU   48 ---> RTC=48 -- PC LINK FAILED                V0510004 00096000
UTLRC52  EQU   52 ---> RTC=52 -- PC LINK REQUEST INVALID       V0510004 00097000
UTLRC56  EQU   56 ---> RTC=56 -- ICCF READBUF TRUNCATED        V0510004 00098000
         ORG   UCPSECT                                                  00099000
         DC    XL(UCPLEN)'00'                                           00100000
         ORG                                                            00101000
ECBLIST  DS    A(*-*)                                                   00102000
         DS    A(*-*)                                                   00103000
         DS    A(*-*)                                                   00104000
ADAPTECB DS    F                                                        00105000
SERVECB  DS    F                                                        00106000
TERMECB  DS    F                                                        00107000
SUBSAVE  DS    CL128                                                    00108000
         ORG   SUBSAVE                                                  00109000
SUBNAME  DS    CL8                                                      00110000
SUBPSW   DS    CL8                                                      00111000
SAVEDR9  DS    F                                                        00112000
SAVEDRA  DS    F                                                        00113000
SAVEDRB  DS    F                                                        00114000
SAVEDRC  DS    F                                                        00115000
SAVEDRD  DS    F                                                        00116000
SAVEDRE  DS    F                                                        00117000
SAVEDRF  DS    F                                                        00118000
SAVEDR0  DS    F                                                        00119000
SAVEDR1  DS    F                                                        00120000
SAVEDR2  DS    F                                                        00121000
SAVEDR3  DS    F                                                        00122000
SAVEDR4  DS    F                                                        00123000
SAVEDR5  DS    F                                                        00124000
SAVEDR6  DS    F                                                        00125000
SAVEDR7  DS    F                                                        00126000
SAVEDR8  DS    F                                                        00127000
         PRINT OFF                                                      00128000
         DFHAFCD TYPE=DSECT                                             00129000
         COPY  DFHCSADS                                                 00129000
PCTCBAR  EQU   4                                                        00130000
         COPY  DFHPCTDS                                                 00131000
PPTCBAR  EQU   1                                                        00132000
         COPY  DFHPPTDS                                                 00133000
         COPY  DFHTCTTE                                                 00135000
         PRINT ON                                                       00134000
         COPY  DFHTIOA                                                  00136000
         PRINT OFF                                                      00137000
         COPY  DFHTDOA                                                  00138000
         DFHTCA CICSYST=YES                                             00139000
         PRINT ON                                                       00140000
PREV     EQU   4                                                        00141000
NEXT     EQU   8                                                        00142000
R0       EQU   0                                                        00143000
R1       EQU   1                                                        00144000
R2       EQU   2                                                        00145000
R3       EQU   3                                                        00146000
R4       EQU   4                                                        00147000
TCTTEAR  EQU   R4                                                       00160000
R5       EQU   5                                                        00148000
R6       EQU   6                                                        00150000
TDOABAR  EQU   R6                                                       00151000
R7       EQU   7                                                        00152000
R8       EQU   8                                                        00153000
COMMBASE EQU   R8                                                       00154000
R9       EQU   9                                                        00155000
BASE     EQU   R9                                                       00156000
R10      EQU   10                                                       00157000
TIOABAR  EQU   R10                                                      00158000
R11      EQU   11                                                       00159000
R12      EQU   12                                                       00161000
R13      EQU   13                                                       00162000
R14      EQU   14                                                       00163000
R15      EQU   15                                                       00164000
         DFHEISTG                                                               
TWASAVR8 DS    F             SAVE CONTENTS R8                                   
TWASAVR9 DS    F             SAVE CONTENTS R9                                   
TWASAVRE DS    F             SAVE CONTENTS R14                                  
TWAECB   DS    F             @ ECB LIST                                         
TWARESP  DS    F             EXEC CICS RESPONSE CODE                            
TWARESP2 DS    F             EXEC CICS RESPONSE CODE 2                          
TWATIOAP DS    F             @ OF WORK TIOA                                     
TWASCSA  DS    F             @ OF STORAGE TO FREE                               
TWAPCPI  DS    CL8           PROGRAM NAME FOR INQUIRE                           
TWAPCAC  DS    CL4           ABEND CODE                                         
TWASCRHT DS    H             TERMINAL HEIGHT (FROM ASSIGN)                      
TWASCRWD DS    H             TERMINAL WIDTH  (FROM ASSIGN)                      
TWAOPID  DS    CL3           OPERATOR ID     (FROM ASSIGN)                      
TWACOLOR DS    C             COLOR TERMINAL  (FROM ASSIGN)                      
TWAHILIT DS    C             HILITE TERMINAL (FROM ASSIGN)                      
TWASCIB  DS    C             GET STORAGE INITIMG CHARACTER                      
TWASCNB  DS    H             GET STORAGE LENGTH                                 
TWATIOA  DS    0D            TERMINAL I/O WORK AREA                             
         DS    860F                                                             
TWATIOAL EQU   *-TWATIOA     TERMINAL I/O WORK AREA LENGTH                      
         DFHEIEND                                                               
         EJECT                                                                  
         TITLE 'INITIAL ENTRY FROM CICS'                                00165000
*********************************************************************   00166000
****                                                             ****   00167000
****         C O M M O N   C I C S   I N T E R F A C E           ****   00168000
****                                                             ****   00169000
****            COMPUTER ASSOCIATES INTERNATIONAL INC.           ****   00170000
****           125 JERICHO TURNPIKE, JERICHO, L.I., N.Y.         ****   00171000
****                                                             ****   00172000
****                  COPYRIGHT -- 1981                          ****   00173000
*********************************************************************   00174000
         SPACE 1                                                        00175000
CASOADPA DFHEIENT CODEREG=(9,7),DATAREG=(13),EIBREG=(11)                        
         B     OVERID                                                   00178000
STAMP    DC    CL8'CACOTPI0'           CSECT NAME              JC530035 00179000
         DC    CL8'12/31/98'           ASSEMBLY DATE           SA56C001 00180000
         DC    CL5'00.00'              ASSEMBLY TIME           JC530035 00181000
         DC    CL7'VM=6.1 '            VERSION                 SA56C001 00182000
OVERID   DS    0H                                                       00183000
         USING CICSUTLA,COMMBASE       ADDRESSABLITY TO COMM AREA       00186000
         SR    COMMBASE,COMMBASE       MAKE SURE I KNOWN SUBTASK INACT  00190000
         EXEC  CICS HANDLE ABEND LABEL(ABENDEX)                                 
         ICM   R14,B'1111',EIBTRNID    GET TRANSACTION ID.              00196000
         LA    R6,TIDTAB               GET TRANSIDS TABLE ADDRESS.      00197000
SCNTTL   DS    0H                                                       00198000
         C     R14,0(R6)               TRANSID MATCH ?                  00199000
         BE    SCNTTE                  YES, BRANCH.                     00200000
         LA    R6,12(R6)               BUMP TRANSIDS TABLE POINTER.     00201000
         CLI   0(R6),X'FF'             END OF TABLE ?                   00202000
         BNE   SCNTTL                  NO, CONTINUE.                    00203000
         B     ABTRAN                  TRANSID MISSING, BRANCH.         00204000
SCNTTE   DS    0H                                                       00205000
         STM   R8,R9,TWASAVR8          SAVE IMPORTANT REGISTERS         00212000
         L     R4,8(,R6)               GET PROGRAMS LIST ADDRESS.       00213000
PGLOOP   DS    0H                                                       00214000
         MVC   TWAPCPI,0(R4)           MOVE PROGRAM NAME.      DO530001 00215000
         EXEC  CICS INQUIRE PROGRAM(TWAPCPI)                           X        
               RESP(TWARESP) RESP2(TWARESP2)                                    
         OC    TWARESP,TWARESP                                          00216000
         BNZ   ABLOC                                                    00217000
         LA    R4,8(R4)                BUMP PROGRAM LIST POINTER.       00219000
         CLI   0(R4),X'FF'             END OF LIST ?                    00220000
         BNE   PGLOOP                  NO, CONTINUE.                    00221000
         L     R4,8(,R6)               GET PROGRAMS LIST ADDRESS.       00222000
         MVC   TWAPCPI,0(R4)           MOVE MAIN PROGRAM NAME.          00223000
         EXEC  CICS LOAD PROGRAM(TWAPCPI) SET(R8) RESP(TWARESP)                 
         OC    TWARESP,TWARESP                                          00216000
         BNZ   ABLOC                   SHOULD NOT HAPPEN                00225000
LOADOK   EQU   *                                                        00226000
         STM   R8,R9,TWASAVR8          SAVE IMPORTANT REGISTERS         00228000
         LA    R5,ECBLIST                                               00230000
         USING ECBLIST,R5                                               00231000
         ST    R5,TWAECB               SAVE ADDR OF ECB LIST            00232000
         LA    TIOABAR,TWATIOA         GET ADDRESS OF TIOA                      
         ST    TIOABAR,TWATIOAP        SAVE POINTER                             
         EXEC  CICS RECEIVE INTO(TIOADBA) LENGTH(TIOATDL)              X        
               MAXLENGTH(TLEN)                                                  
         LTR   TIOABAR,TIOABAR                                          00234000
         BZ    *+8                                                      00235000
         BAL   R14,TD3270              SET UP INITIAL INPUT             00236000
         DFHCSAD TYPE=LOCATE,ROUTE=COMREG,REG=R14                               
         USING DFHCSADS,R14                                                     
         TM    CSADATFT,X'02'          IS IT EUROPEAN DATE FORMAT       00237000
         BNO   *+8                     NOPE                             00238000
         OI    UCSYIND,UCEURPN         SHOW IT                          00239000
         L     R12,CSACDTA             GET CURRENT TASK                         
         DROP  R14                                                              
ATTACH   EQU   *                                                        00240000
         XC    TERMECB,TERMECB                                 JC530035 00241000
         XC    SERVECB,SERVECB                                 JC530035 00242000
         XC    ADAPTECB,ADAPTECB                               JC530035 00243000
         LA    R2,TERMECB              SET UP FOR CICS KC WAIT          00244000
         LA    R3,SERVECB                                               00245000
         LA    R4,0                                                     00246000
         BCTR  R4,R0                                                    00247000
         STM   R2,R4,ECBLIST                                            00248000
         EXEC  CICS ASSIGN SCRNHT(TWASCRHT) SCRNWD(TWASCRWD)           X        
               COLOR(TWACOLOR) HILIGHT(TWAHILIT) OPID(TWAOPID)         X        
               NOHANDLE                                                         
         L     TCTTEAR,TCAFCAAA        GET FACILITY ADDRESS.            00249000
         USING DFHTCTTE,R4                                                      
         ST    TCTTEAR,UCPFCAA         SAVE.                            00250000
         MVI   UCPTRMD,X'00'           INIT FACILITY INFO FLAGS.        00251000
         MVC   UCPTRMD+2(1),TWASCRHT+1 MOVE DEF SCREEN LINES NUMBER.    00252000
         MVC   UCPTRMD+3(1),TWASCRWD+1 MOVE DEF SCREEN COLUMNS NUMBER.  00253000
NOALSZ   DS    0H                                                       00259000
         CLI   TWACOLOR,X'00'          COLOUR SUPPORTED ?               00261000
         BE    NOCOLO                  NO, BRANCH.                      00262000
         OI    UCPTRMD,X'40'           SET ON COLOUR FLAG.              00263000
NOCOLO   DS    0H                                                       00264000
         CLI   TWAHILIT,X'00'          HILIGHT SUPPORTED ?              00265000
         BE    NOHILI                  NO, BRANCH.                      00266000
         OI    UCPTRMD,X'20'           SET ON HILIGHT FLAG.             00267000
NOHILI   DS    0H                                                       00269000
         MVC   SUBNAME(4),0(R6)        SET UP SUBAREA NAME              00270000
         MVC   SUBNAME+4(4),EIBTRMID                                    00271000
         AIF   ('&SYSPARM' EQ 'OS').OSATTCH                             00272000
         L     R1,20                   CHECK FOR ADAPTOR                00274000
         SVC   33                                                       00275000
         DC    X'47000CCA'                                              00276000
         DC    X'47000CCB'                                              00277000
         DC    X'47000CCC'                                              00278000
         LTR   R1,R1                   IF ADAPTER IS UP        JC530009 00279000
         BZ    ADAPTOK                 ...CONTINUE             JC530009 00280000
         CH    R1,=H'8'                IF ADAPTER DISABLE OPEN JC530035 00281000
         BE    ADAPTOK                 ...CONTINUE             JC530035 00282000
         TM    7(R6),ADNOTRQ           SYS ADAPT NOT REQ'D?    DO530001 00283000
         BNO   NOADAPTX                ...NO,NEED IT DO MSG    DO530001 00284000
         OI    UCSYIND,UCNOADPT        INDICATE IN UTIL AREA   DO530001 00285000
         B     ATTACHA                 ..CONTINUE              DO530001 00286000
NOADAPTX DS    0H                                              DO530001 00287000
         LA    R6,NOADAPTM             ADDRESS MESSAGE         JC530035 00288000
         B     EXITMSGN                MESSAGE AND EXIT        DO530001 00289000
ADAPTOK  EQU   *                                               JC530009 00290000
         LA    R0,20                                                    00291000
         LA    R1,1                                                     00292000
         SVC   33                                                       00293000
         DC    XL4'47F00AFF'                                            00294000
         B     ATTACHA                                                  00295000
         SPACE 1                                                        00296000
         SPACE 1                                                        00297000
ATTACHA  DS    0H                      DO THE ATTACH                    00298000
         MVC   UCPPNAM(4),0(R6)        MOVE TASK ID            DO530001 00299000
         MVC   UCPPNAM+4(3),TWAOPID    MOVE OPR ID             DO530001 00300000
         L     R15,SUBPSW+4            ENTRY FOR TPADAPT                00301000
         LA    R3,SUBSAVE                                               00302000
*        ATTACH (R15),SAVE=(R3),ECB=(R2)                       JC530008 00303000
         CNOP  2,4                     ALIGNMENT               JC530008 00304000
         ST    R15,14+0+*              STORE ENTRY ADDR        JC530008 00305000
         ST    R2,10+4+*               STORE ECB ADDR          JC530008 00306000
         LR    R0,R3                   POINT TO SAVE AREA      JC530008 00307000
         BAL   R1,*+16                 BRANCH AROUND PARM LIST JC530008 00308000
         DC    A(0)                    ENTRY ADDR              JC530008 00309000
         DC    A(0)                    ECB ADDR                JC530008 00310000
         DC    A(0)                    ABEND SQVE AREA ADDR    JC530008 00311000
         SVC   38                      ATTACH THE SUBTASK      JC530008 00312000
         AGO   .AFTATTC                                                 00313000
.OSATTCH ANOP                                                           00314000
         DFHIC TYPE=STOPTIME,CLASS=RUNAWAY                              00315000
         L     R1,SUBPSW+4                                              00316000
         ATTACH EP=CASADISB,ECB=(R2)                                    00317000
         LTR   R15,R15                                                  00318000
         BNZ   ABATTCH                 ABEND SUBTASK CANNOT COME UP     00319000
         ST    R1,SUBPSW               SAVE TCB ADDRESS                 00320000
         B     SLEEP                   OK ITS GOING                     00321000
         AGO   .OS1                                                     00322000
.AFTATTC ANOP                                                           00323000
         LTR   R1,R1                   SEE IF ATTACH WAS GOOD           00324000
         BP    GOODATT                 OK ITS GOING                     00325000
         LR    R2,R1                   FOR CICS WAIT                            
         EXEC  CICS WAIT EVENT ECADDR(R2)                                       
         B     ATTACH                  TRY IT AGAIN                     00328000
         DROP  R4                                                               
GOODATT  DS    0H                                                       00329000
         TM    UCSYIND,UCNOADPT         SYS ADAPT NEEDED?      DO530001 00330000
         BO    SLEEP                    ...NO,SKIP CALL        DO530001 00331000
         LA    R0,20                                                    00332000
         SLR   R1,R1                                                    00333000
         SVC   33                                                       00334000
         DC    X'47F00AFF'                                              00335000
         B     SLEEP                                                    00336000
.OS1     ANOP                                                           00337000
EOJ      EQU   *                                                        00338000
         AIF   ('&SYSPARM' NE 'OS').DOSEOJ                              00339000
         DFHIC TYPE=STOPTIME,CLASS=RUNAWAY                              00340000
         DETACH SUBPSW                 FINISHED                         00341000
.DOSEOJ  ANOP                                                           00342000
         ICM   R2,15,TWASAVR8                                           00345000
         EXEC  CICS FREEMAIN DATA(0(R2))                                        
         EXEC  CICS RETURN                                                      
SLEEP    EQU   *                                                        00348000
         LA    R2,ECBLIST                                               00349000
         EXEC  CICS WAITCICS ECBLIST(R2) NUMEVENTS(2)                           
         AIF   ('&SYSPARM' NE 'OS').NOWCHK                              00352000
         MVC   ABHOLDER(256),0(COMMBASE)                                00353000
         TM    TERMECB,X'40'            CHECK IF IT IS POSTED           00354000
         BZ    ENTYPT                   FORGET THE ABNORMAL CHECK       00355000
         OC    TERMECB+1(3),TERMECB+1   IS IT FROM THE AB ROUTINE       00356000
         BNZ   ABENDADP                 DIE !!                          00357000
.NOWCHK  ANOP                                                           00358000
         TM    TERMECB+2,X'40'          OK ITS AN ADAPTER BLOW UP       00359000
         BO    ABENDADP                                                 00360000
         TITLE 'ENTRY POINT FROM ALL USERS'                             00361000
ENTYPT   EQU   *                                                        00362000
         L     TIOABAR,TWATIOAP        RELOAD TIOA POINTER                      
         TM    UCSYIND,UCSYEOJ         IS IT OVER                       00363000
         BO    EOJ                     YES ITS OVER                     00364000
         LH    R2,UCREQCD              GET SERVICE ROUTINE PTR          00365000
         B     *+4(R2)                 GOT TO SERVICE ROUTINE           00366000
         B     STORGET            (0)  ALLOCATE STORAGE                 00367000
         B     STORFREE           (4)  RELEASE STORAGE                  00368000
         B     TERMINAL           (8)  TERMINAL SERVICE                 00369000
         B     TERMINAL           (12) TERMINAL SERVICE                 00370000
         B     PROGRAM            (16) PROGRAM LINK SERVICE             00371000
         B     WAITRTN            (20) CICS WAIT SERVICE.               00372000
         B     PCTLLINK           (24) CICS PC LINK SERVICE             00373000
         TITLE 'CICS WAIT        '                                      00374000
WAITRTN  DS    0H                                                       00375000
         L     R2,UCPUSER              GET UTILITY USER AREA.           00376000
         EXEC  CICS WAIT EVENT ECADDR(R2)                                       
         B     RESLEEP                                                  00379000
         TITLE 'CICS PROGRAM LOAD'                                      00380000
PROGRAM  EQU   *                                                        00381000
         TM    UCSYIND,UCDELETE        IS LINK FINISHED                 00382000
         BO    PRGDELE                 YES                              00383000
PRGLOAD  EQU   *                                                        00384000
         MVC   TWAPCPI,UCPPNAM         PROGRAM NAME TO TCA              00385000
         EXEC  CICS LOAD PROGRAM(TWAPCPI) ENTRY(R1) SET(R2)            X        
               RESP(TWARESP)                                                    
         OC    TWARESP,TWARESP         LOAD SUCCESSFUL?                 00216000
         BZ    PGLOK                   YES, CONTINUE                    00225000
         LA    R15,8                   SIMULATE NOT FOUND               00387000
         B     RESLEEP                 RETURN                           00388000
PGLOK    ST    R1,UCPSCAD              SAVE PROGRAM LOAD ADDR           00389000
         ST    R2,UCPOCRA              SAVE LOAD ADDRESS                00390000
         SR    R15,R15                                                  00391000
         B     RESLEEP                                                  00392000
         TITLE 'CICS PROGRAM DELETE'                                    00393000
PRGDELE  EQU   *                                                        00394000
         MVC   TWAPCPI,UCPPNAM         SET PROGRAM NAME        JC530035 00401000
         EXEC  CICS INQUIRE PROGRAM(TWAPCPI)                           X        
               RESP(TWARESP) RESP2(TWARESP2)                                    
         OC    TWARESP2,TWARESP2                                        00216000
         BNZ   ABLOC                                                    00217000
         MVC   TWAPCPI,UCPPNAM         SET PROGRAM NAME        JC530035 00401000
         EXEC  CICS RELEASE PROGRAM(TWAPCPI)                           X        
               RESP(TWARESP) RESP2(TWARESP2)                                    
         OC    TWARESP,TWARESP                                                  
         BZ    RESLEEP                 AND EXIT                JC530035         
PRGDELRE MVC   UCPSCAD,UCPOCRA         GET LOAD ADDRESS        JC530035 00404000
         B     STORFREE                GO FREE STORAGE                  00405000
         TITLE 'CICS PROGRAM LINK'                                      00406000
PCTLLINK EQU   *                                                        00407000
         MVC   TWAPCPI,UCPPNAM         PROGRAM NAME TO TCA              00408000
         XC    SAVEDRF,SAVEDRF                                          00409000
         EXEC  CICS LINK PROGRAM(TWAPCPI) RESP(TWARESP)                         
         OC    TWARESP,TWARESP         LINK SUCCESSFUL?                         
         BZ    PGLKOK                  YES, CONTINUE                            
         LA    R15,UTLRC48             INDICATE SOME KIND OF ERROR      00411000
         ST    R15,SAVEDRF             SAVE RETURN CODE                 00412000
PGLKOK   EQU   *                       IF NO ERROR LET R15 FILTER DOWN  00413000
         EXEC  CICS WAIT TERMINAL                                               
         L     R15,SAVEDRF             RESET RETURN CODE                00415000
         B     RESLEEP                                                  00416000
         TITLE 'STORAGE REQUEST HANDLER'                                00417000
STORGET  EQU   *                                                        00418000
         LH    R1,UCPSCLN+2            REQUESTED STRG LENGTH            00419000
         STH   R1,TWASCNB              NUMBER OF BYTES TO GET           00420000
         MVC   TWASCIB,UCPSCIC         BYTE TO INITIALIZE               00421000
         EXEC  CICS GETMAIN SET(R2) LENGTH(TWASCNB) INITIMG(TWASCIB)   X        
               NOSUSPEND RESP(TWARESP)                                          
         OC    TWARESP,TWARESP         STORAGE ACQUIRED?                        
         BNZ   NOGOTS                  NO, THAT'S AN ERROR                      
         ST    R2,UCPSCAD              TELL REQUESTOR ADDRESS           00425000
         SR    R15,R15                 OK STORAGE                       00426000
         B     RESLEEP                 BACK TO USER                     00427000
NOGOTS   LA    R15,4                   SAY STORAGE GET FAILED           00428000
         XC    UCPSCAD(4),UCPSCAD      MAKE SURE NO STORAGE USED        00429000
         B     RESLEEP                                                  00430000
STORFREE EQU   *                                                        00431000
         SR    R15,R15                 OK                               00432000
         L     R1,UCPSCAD              ADDR OF STRG TO BE FREED         00433000
         LTR   R1,R1                   BEWARE OF PROBLEMS               00434000
         BZ    RESLEEP                                                  00435000
         SH    R1,=H'8'                POINT TO STRG ACCTNG AREA        00436000
         LTR   R1,R1                                                    00437000
         BZ    RESLEEP                                                  00438000
         C     R1,TWATIOAP             TRYING TO FREE TIOA?             00439000
         BE    RESLEEP                 NO-NO                            00440000
         CLM   R1,7,TWATIOAP+1           TRYING TO FREE TIOA?           00439000
         BE    RESLEEP                 NO-NO                            00440000
STOFROK  EQU   *                                                        00448000
         ICM   R2,15,UCPSCAD                                                    
         EXEC  CICS FREEMAIN DATA(0(R2))                                        
         SR    R15,R15                 OK RETCODE                       00451000
         B     RESLEEP                 BACK TO USER                     00452000
         EJECT                                                          00453000
         TITLE 'TERMINAL REQUEST HANDLER'                               00454000
TERMINAL EQU   *                       MAKE A LABEL                     00455000
         SR    R3,R3                   CLEAR R3                         00456000
         SR    R6,R6                   LIKEWISE R6                      00457000
         L     R4,UCPPRTA              TERMINAL REQUEST BUFFER          00458000
         LA    R4,0(R4)                CLEAN                            00459000
         CLI   1(R4),X'C2'             FREEKBD IS SPECIAL               00460000
         BE    WRWT                                                     00461000
         IC    R3,1(,R4)               TERMINAL REQUEST TYPE   DO530001 00462000
         SLL   R3,2                    TIMES 4 FOR OFFSET               00463000
         B     *+0(R3)                 GOT TO ROUTINE                   00464000
         B     WT                      WAIT  ONLY                       00465000
         B     RD                      READ ONLY                        00466000
         B     RDWT                    READ-WAIT                        00467000
         B     WR                      WRITE ONLY                       00468000
         B     WRWT                    WRITE-WAIT                       00469000
         B     WRRD                    WRITE READ                       00470000
         B     WRRDWT                  WRITE-READ-WAIT                  00471000
         B     ER                      ERASE                            00472000
         B     ABLOGIC                 ERASE WAIT---ILLOGICAL FUNCTION  00473000
         B     ABLOGIC                 ERASE READ---ILLOGICAL FUNCTION  00474000
         B     ABLOGIC                 ERASE READ WAIT---ILLOGICAL FUNC 00475000
         B     ERWR                    ERASE WRITE                      00476000
         B     ERWRWT                  ERASE WRITE WAIT                 00477000
         B     ERWRRD                  ERASE WRITE READ                 00478000
         B     ERWRRDWT                ERASE WRITE READ WAIT            00479000
         B     READB                   READ BUFFER                      00480000
         B     WRITEB                  WRITE BUFFER                     00481000
         EJECT                                                          00482000
WT       EQU   *                                                        00483000
         EXEC  CICS WAIT TERMINAL                                               
         B     GOPGM                                                    00485000
         SPACE 2                                                        00486000
ER       EQU   *                                                        00487000
         EXEC  CICS SEND CONTROL ERASE                                          
         B     GOPGM                                                    00489000
         SPACE 2                                                        00490000
WRITEB   EQU   *                                                        00491000
         L     R4,UCPPRTA              GET ADDRESS OF SPECIAL           00492000
         L     R4,4(R4)                GET DATA                         00493000
         LH    R6,8(R4)                SCREEN LENGTH FOR GETMAIN        00494000
         LA    R6,7(R6)                FOR MY STUFF                     00495000
         L     TIOABAR,TWATIOAP        ADDRESS STORAGE                  00498000
         STH   R6,TIOATDL              CLEAN UP 6                       00499000
         SH    R6,=H'7'                RESET LENGTH                     00500000
         MVC   TIOADBA(3),=X'114040'   SETUP SBA @ R01,COL01            00502000
         LA    TIOABAR,3(TIOABAR)      ADJUST OP REG                    00503000
         LA    R4,12(R4)               POINT TO BUFFER DATA             00504000
CTIOAA   CH    R6,=H'256'                                               00505000
         BNH   LASTMVCA                                                 00506000
         MVC   TIOADBA(256),0(R4)                                       00507000
         LA    TIOABAR,256(,TIOABAR)                                    00508000
         LA    R4,256(,R4)                                              00509000
         SH    R6,=H'256'                                               00510000
         B     CTIOAA                                                   00511000
LASTMVCA BCTR  R6,0                                                     00512000
         EX    R6,MOVESCRN             MOVE THE LAST BIT                00513000
         ALR   TIOABAR,R6              POINT TO END-1                   00514000
         LA    TIOABAR,1(TIOABAR)      POINT TO END                     00515000
         L     R4,UCPPRTA               GET SPECIAL REQ BUFFER          00516000
         MVC   TWASCSA,4(R4)            PUT STRG ADDR INTO TCA          00517000
         L     R4,4(R4)                GET READB BUFFER AGAIN           00518000
         MVI   TIOADBA,X'11'           SET SBA FOR INSCUR               00519000
         MVC   TIOADBA+1(2),10(R4)     GET CURSOR ADDRESS               00520000
*        MVC   TIOADBA+1(2),=X'C1D6'   GET CURSOR ADDRESS               00521000
         MVI   TIOADBA+3,X'13'         SET INSCUR                       00522000
         ICM   R2,15,TWASCSA                                                    
         EXEC  CICS FREEMAIN DATA(0(R2))                                        
         L     TIOABAR,TWATIOAP        ADDRESS STORAGE                  00498000
         EXEC  CICS SEND FROM(TIOADBA) LENGTH(TIOATDL) WAIT                     
         B     RD                      RETURN TO CALLER                 00525000
         SPACE 2                                                        00526000
READB    EQU   *                                                        00527000
         LH    R1,TLEN                 REQUESTED STRG LENGTH            00419000
         STH   R1,TWASCNB              NUMBER OF BYTES TO GET           00420000
         EXEC  CICS GETMAIN SET(R10) LENGTH(TWASCNB) INITIMG(X'00')    X        
               NOSUSPEND RESP(TWARESP)                                          
         OC    TWARESP,TWARESP         STORAGE ACQUIRED?                        
         BNZ   NOGOTS                  NO, THAT'S AN ERROR                      
         EXEC  CICS RECEIVE INTO(TIOADBA) LENGTH(TIOATDL) BUFFER       X        
               MAXLENGTH(TLEN)                                                  
         ST    TIOABAR,UCPRDRA         SAVE BUFFER ADDRESS              00425000
         LH    R14,TIOATDL                                              00530000
         ST    R14,UCPSCLN             SAVE LENGTH                      00531000
         B     GOPGMR2                 RETURN TO CALLER                 00533000
RD       EQU   *                                                        00534000
         XC    TIOADBA(80),TIOADBA     GET READY FOR NEW DATA                   
         EXEC  CICS RECEIVE INTO(TIOADBA) LENGTH(TIOATDL)              X        
               MAXLENGTH(TLEN)                                                  
         BAL   R14,TD3270                                               00536000
         B     GOPGMR                  RETURN TO CALLER                 00538000
         SPACE 2                                                        00539000
WR       EQU   *                                                        00540000
         BAL   R14,BUILDOP             GO DO WRITE SETUP                00541000
         CLI   TIOACLCR,X'00'          CTLCHAR PRESENT?        DO530001 00542000
         BNE   CR                      ...YES                  DO530001 00543000
         EXEC  CICS SEND FROM(TIOADBA) LENGTH(TIOATDL) WAIT                     
         B     GOPGM                   RETURN TO CALLER                 00545000
         SPACE 2                                                        00546000
CR       EQU   *                                               DO530001 00547000
         EXEC  CICS SEND FROM(TIOADBA) LENGTH(TIOATDL) WAIT            X        
               CTLCHAR(TIOACLCR)                                                
         B     GOPGM                   RETURN TO CALLER        DO530001 00549000
         SPACE 2                                               DO530001 00550000
RDWT     EQU   *                       READ-WAIT                        00551000
         XC    TIOADBA(80),TIOADBA     GET READY FOR NEW DATA                   
         EXEC  CICS RECEIVE INTO(TIOADBA) LENGTH(TIOATDL)              X        
               MAXLENGTH(TLEN)                                                  
         BAL   R14,TD3270                                               00553000
         B     GOPGMR                  RETURN TO CALLER                 00555000
         SPACE 2                                                        00556000
WRWT     EQU   *                       WRITE-WAIT                       00557000
         BAL   R14,BUILDOP             SET UP FOR WRITE                 00558000
         CLI   TIOACLCR,X'C2'                                           00559000
         BE    WTCTLC                                                   00560000
         CLI   TIOACLCR,X'00'          CTLCHAR PRESENT?        DO530001 00561000
         BNE   CR                      ...YES                  DO530001 00562000
         EXEC  CICS SEND FROM(TIOADBA) LENGTH(TIOATDL) WAIT                     
         B     GOPGM                   RETURN TO CALLER                 00564000
WTCTLC   EQU   *                                                        00565000
         EXEC  CICS SEND FROM(TIOADBA) LENGTH(TIOATDL) WAIT            X        
               CTLCHAR(TIOACLCR)                                                
         B     GOPGM                                                    00566000
         SPACE 2                                                        00567000
WRRD     EQU   *                       WRITE-READ-(IMPLIED WAIT)        00568000
WRRDWT   EQU   *                       EQUAL FUNCTION (SPECIFIED WAIT)  00569000
         BAL   R14,BUILDOP             SET UP FOR TERMINAL WRITE        00570000
         CLI   TIOACLCR,X'00'          CTLCHAR PRESENT?        DO530001 00571000
         BNE   CRRD                    ...YES                  DO530001 00572000
         EXEC  CICS SEND FROM(TIOADBA) LENGTH(TIOATDL) WAIT                     
         B     RTID                    GO DO READ              DO530001 00574000
CRRD     EQU   *                       WRITE-READ-(IMPLIED WAITDO530001 00575000
         EXEC  CICS SEND FROM(TIOADBA) LENGTH(TIOATDL) WAIT            X        
               CTLCHAR(TIOACLCR)                                                
RTID     EQU   *                                                        00577000
         XC    TIOADBA(80),TIOADBA     GET READY FOR NEW DATA                   
         EXEC  CICS RECEIVE INTO(TIOADBA) LENGTH(TIOATDL)              X        
               MAXLENGTH(TLEN)                                                  
         BAL   R14,TD3270                                               00578000
         B     GOPGMR                  RETURN TO CALLER                 00580000
         SPACE 2                                                        00581000
ERWR     EQU   *                       ERASE-WRITE                      00582000
         BAL   R14,BUILDOP             SET UP FOR TERMINAL WRITE        00583000
         EXEC  CICS SEND FROM(TIOADBA) LENGTH(TIOATDL) WAIT ERASE               
         B     GOPGM                   RETURN TO CALLER                 00585000
         SPACE 2                                                        00586000
ERWRWT   EQU   *                       ERASE-WRITE-WAIT                 00587000
         BAL   R14,BUILDOP             SET UP FOR TERMINAL WRITE        00588000
         CLI   TIOACLCR,X'00'          CTLCHAR PRESENT?        DO530001 00589000
         BNE   ERCRRD                  ...YES                  DO530001 00590000
         EXEC  CICS SEND FROM(TIOADBA) LENGTH(TIOATDL) WAIT ERASE               
         B     GOPGM                   RETURN TO CALLER                 00592000
         SPACE 2                                                        00593000
ERCRRD   EQU   *                                               DO530001 00594000
         EXEC  CICS SEND FROM(TIOADBA) LENGTH(TIOATDL) WAIT ERASE      X        
               CTLCHAR(TIOACLCR)                                                
         B     GOPGM                   RETURN TO CALLER        DO530001 00596000
         SPACE 2                                               DO530001 00597000
ERWRRD   EQU   *                       ERASE-WRITE-READ (IMPLIED WAIT)  00598000
ERWRRDWT EQU   *                       FUNCTIONAL EQUIVALENT--SPECIFIED 00599000
         BAL   R14,BUILDOP             SET UP FOR TERMINAL WRITE        00600000
         CLI   TIOACLCR,X'00'          CTLCHAR PRESENT?        DO530001 00601000
         BNE   ERCRRDWT                ...YES                  DO530001 00602000
         EXEC  CICS SEND FROM(TIOADBA) LENGTH(TIOATDL) WAIT ERASE               
         B     RDITRW                                          DO530001 00604000
ERCRRDWT EQU   *                                               DO530001 00605000
         EXEC  CICS SEND FROM(TIOADBA) LENGTH(TIOATDL) WAIT ERASE      X        
               CTLCHAR(TIOACLCR)                                                
RDITRW   EQU   *                                               DO530001 00607000
         XC    TIOADBA(80),TIOADBA     GET READY FOR NEW DATA                   
         EXEC  CICS RECEIVE INTO(TIOADBA) LENGTH(TIOATDL)              X        
               MAXLENGTH(TLEN)                                                  
         BAL   R14,TD3270                                               00609000
         B     GOPGMR                  RETURN TO CALLER                 00611000
         TITLE 'BUILD TERMINAL OUTPUT BUFFER ROUTINE'                   00612000
BUILDOP  EQU   *                       SET UP TERMINAL WRITES           00613000
         ST    R14,TWASAVRE            SAVE RETURN REG                          
         L     TIOABAR,TWATIOAP        ADDRESS STORAGE                  00617000
         SR    R6,R6                   CLEAN UP 6                       00618000
         MVC   TIOATDL(2),2(R4)        TELL CICS TERMINAL IO LENGTH     00619000
         NI    TIOATDL,X'7F'           CLEAR SAVE BIT                   00620000
         LH    R6,TIOATDL              GET LENGTH                       00621000
CTIOA    CH    R6,=H'256'                                               00623000
         BNH   LASTMVC                                                  00624000
         MVC   TIOADBA(256),4(R4)                                       00625000
         LA    TIOABAR,256(,TIOABAR)                                    00626000
         LA    R4,256(,R4)                                              00627000
         SH    R6,=H'256'                                               00628000
         B     CTIOA                                                    00629000
LASTMVC  DS    0H                                              DO530001 00630000
         BCTR  R6,0                                            DO530001 00631000
         EX    R6,MVSCREEN                                              00632000
         L     R4,UCPPRTA              GET BUFFER ADDRESS AGAIN         00633000
         L     TIOABAR,TWATIOAP        TELL TIOA AGAIN         DO530001 00634000
         MVI   TIOACLCR,X'00'          CLEAR CTL CHAR AREA     DO530001 00635000
         CLI   1(R4),X'C2'             SPECIAL CTLCHAR PRESENT?DO530001 00636000
         BE    CSET2                   ...YES                  DO530001 00637000
         CLI   0(R4),X'00'             ANY USR CTLCHAR PRESENT?DO530001 00638000
         BE    CDEL                    ...NO                   DO530001 00639000
         MVI   TIOACLCR,X'C3'          SET RESET MDT AND FREEKBDO530001 00640000
         TM    0(R4),X'80'             DO NOT RESET MDT?       DO530001 00641000
         BNO   TRYRKBD                 ...NO                   DO530001 00642000
         NI    TIOACLCR,X'FE'          SET RESET MDT OFF       DO530001 00643000
TRYRKBD  DS    0H                                              DO530001 00644000
         TM    0(R4),X'40'             DO NOT RESET KEYBOARD?  DO530001 00645000
         BNO   CDEL                    ...NO                   DO530001 00646000
         NI    TIOACLCR,X'FD'          SET RESET KEYBD OFF     DO530001 00647000
         B     CDEL                                            DO530001 00648000
CSET2    DS    0H                                              DO530001 00649000
         MVI   TIOACLCR,X'C2'                                           00650000
CDEL     TM    2(R4),X'80'             NO DELETE BIT ON?                00651000
         BO    OUT7                    YES ... LEAVE IT                 00652000
*                                                                               
*  POTENTIAL PROBLEM HERE AS STORAGE ACCOUNTING AREAS IN CICS TS                
*  ARE QUITE DIFFERENT.  I HAVE SEEN INSTANCES WHERE THE PRODUCT                
*  ACTUALLY REQUESTS A FREEMAIN OF STORAGE THAT IS IN THE MIDDLE OF             
*  THEIR OWN PROGRAM, SO WE MUST BE CAREFUL HOW THIS IS HANDLED.                
*  CICS TS 1.1 SAA IS STILL 8 BYTES OF FORMAT UNNNNNNN, WHERE                   
*  NNNNNNN REPRESENTS THE CURRENT CICS USER TASK NUMBER.                        
*                                                                               
         SH    R4,=H'8'                POINT TO ALLEGED STRG ACCTNG             
         CLI   0(R4),C'U'              MAYBE CICS TS SAA ?                      
         BNE   CDEL050                 NO...CHECK FOR OLD STYLE SAA             
         TRT   1(7,R4),NUMTAB          NUMERIC TASK ID ?                        
         BZ    CDEL100                 YES...OKAY TO FREE                       
         B     OUT7                    EXIT WITHOUT FREE                        
CDEL050  EQU   *                                                                
         TM    0(R4),X'80'             SHOULD HAVE AT LEAST 8 BIT ON            
         BNO   OUT7                    NO 8 BIT DON'T FREE IT                   
         CLC   2(2,R4),=X'0000'        IS THERE A LENGTH                        
         BE    OUT7                    L=0 DON'T FREE                           
         LR    R6,R4                   VERIFY BOTH ACCTNG AREAS EQUAL           
         AH    R6,2(R4)                POINT TO ENDING STRG ACCTNG AREA         
         CLC   0(8,R4),0(R6)           ARE THEY THE SAME?                       
         BNE   OUT7                    NO ... LEAVE IT ALONE                    
CDEL100  EQU   *                                                                
         AH    R4,=H'8'                GET BACK TO CORRECT @                    
         EXEC  CICS FREEMAIN DATA(0(R4))                                        
OUT7     L     R14,TWASAVRE            RESTORE RETURN REG               00664000
         BR    R14                     GO BACK                          00664000
         SPACE 3                                                        00665000
MVSCREEN MVC   TIOADBA(0),4(R4)        MVC   EX FOR SCREEN BUFFERS      00666000
MOVESCRN MVC   TIOADBA(0),0(R4)        MVC EX FOR SCREEN BUFFERS(HELP)  00667000
         TITLE '3270 BUFFER TO TRANS DATA'                              00668000
TD3270   EQU   *                                                        00669000
* THE PRINTING OF TERMINAL BUFFERS MAY BE ACCOMPLISHED BY SETTING UP    00670000
* A DCT ENTRY FOR 'CAPT'.  THE PA2 KEY MAY BE USED TO DUMP THE TERMINAL 00671000
* IMAGE TO THE TRANSIENT DATA SET UNDER THE NAME 'CAPT'.  IN THIS IMAGE 00672000
* THE FIRST FOUR BYTES WILL CONTAIN THE ORIGINATING TERMINAL ID, THEN   00673000
* FULL TERMINAL BUFFER.  A USER PROGRAM MAY BE WRITTEN TO PROCESS THE   00674000
* TRANSIENT DATA, OR IT MAY BE DIRECTED TO A CICS PRINTER.  IF NO DCT   00675000
* ENTRY HAS BEEN MADE, PA2 WILL HAVE NO EFFECT.                         00676000
*        CLI   TCTTEAID,X'6E'          PA2                     DO530001 00677000
*        BE    WRTDS                                           DO530001 00678000
RETR7TD  EQU   *                                                        00679000
         L     TIOABAR,TWATIOAP                                         00680000
         LA    TIOABAR,8(,TIOABAR)                                              
         ST    TIOABAR,UCPRDRA                                          00682000
         BR    R14                     NOT PA 2   GO BACK               00683000
         TITLE 'RETURN TO CALLER ROUTINE'                               00715000
GOPGMR   EQU   *                       RETURN TO CALLER FROM READ       00716000
         L     TIOABAR,TWATIOAP        ADDRESS OF INPUT BUFFER          00532000
GOPGMR2  EQU   *                       RETURN TO CALLER FROM READ       00716000
         LH    R2,EIBCPOSN              GET RELATIVE CURSOR ADDRESS     00717000
         LA    R1,X'3F'                 CONVERT                         00718000
         NR    R1,R2                    RELATIVE                        00719000
         SRL   R2,6                     CURSOR ADDRESS                  00720000
         STC   R2,TIOAWCI               TO ACTUAL                       00721000
         STC   R1,TIOAWCI+1                                             00722000
         TR    TIOAWCI(2),*+10          AND PLACE TO TIOA               00723000
         B     *+4+X'40'                CONTINUE                        00724000
         DC    X'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'                      00725000
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F'                      00726000
         DC    X'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F'                      00727000
         DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'                      00728000
         MVC   UCPPASA(1),EIBAID       GIVE AID BYTE                    00729000
GOPGM    EQU   *                       RETURN TO CALLER                 00730000
         SR    R15,R15                 OK RETCODE                       00731000
RESLEEP  EQU   *                       RETURN TO CALLER                 00732000
         ST    R15,SAVEDRF             GIVE RETCODE                     00734000
         SR    R1,R1                   CLEAN MY SERVICE ECB             00735000
         ST    R1,SERVECB              SO I CAN SLEEP IN PEACE          00736000
         LA    R1,ADAPTECB             WAKE HIM UP                      00737000
         POST  (R1)                                                     00738000
         B     SLEEP                   WAIT FOR MORE WORK               00739000
         TITLE 'ERROR MESSAGE ROUTINE'                         JC530035 00740000
EXITMSGN DS    0H                                              DO530001 00741000
         LA    COMMBASE,0                                               00345000
         ICM   R2,15,TWASAVR8                                                   
         EXEC  CICS FREEMAIN DATA(0(R2))                                        
EXITMSG  DS    0H                                              DO530001 00745000
         L     TIOABAR,TWATIOAP        LOAD TIOA ADDRESS       JC530035 00747000
         MVC   TIOATDL(2),=H'80'       LENGTH                  JC530035 00748000
         MVC   TIOADBA(80),0(R6)       MOVE MESSAGE            DO530001 00749000
         EXEC  CICS SEND FROM(TIOADBA) LENGTH(TIOATDL) WAIT ERASE               
         EXEC  CICS RETURN                                              00752000
         TITLE 'ABEND HANDLERS'                                         00753000
ABTRAN   DS    0H                                                       00754000
         LA    R1,CATR                                                  00755000
         B     ABEND                                                    00756000
ABLOC    DS    0H                                                       00757000
         LA    R1,CAPR                                                  00758000
         B     ABEND                                                    00759000
ABSTRG   DS    0H                                                       00760000
         LA    R1,CAST                                                  00761000
         B     ABEND                                                    00762000
ABLOGIC  DS    0H                                                       00763000
         LA    R1,CALG                                                  00764000
         B     ABEND                                                    00765000
ABSTXT   EQU   *                                                        00766000
         LA    R1,CAXT                                                  00767000
         AIF   ('&SYSPARM' NE 'OS').DOSAB                               00768000
         B     ABEND                                                    00769000
ABATTCH  SR    COMMBASE,COMMBASE       SUBTASK NOT UP                   00770000
         LA    R1,CASB                                                  00771000
.DOSAB   ANOP                                                           00772000
ABEND    EQU   *                                                        00773000
         MVC   TWAPCAC,0(R1)           PUT IN THE CODE                  00774000
         LTR   COMMBASE,COMMBASE       IS SUBTASK ALIVE                 00775000
         BZ    ABENDADP+6              NO DONT POST                     00776000
         MVC   UCPUSER(4),0(R1)        PUT IN FOR ABEND                 00777000
         B     ABENDCON                                                 00778000
NOTAUTH  EQU   *                                                        00779000
         CVD   R15,DBL                                                  00780000
         OI    DBL+7,X'0F'                                              00781000
         UNPK  DBL(5),DBL+5(3)                                          00782000
         MVC   TWAPCAC,DBL+1                                            00783000
         B     ABENDFR                                                  00784000
DBL      DC    D'0'                                                     00785000
ABENDEX  EQU   *                                                        00786000
         LM    R8,R9,TWASAVR8          GET IMPORTANT REGISTERS          00787000
         L     R5,TWAECB               RELOAD ADDR OF ECB LIST          00788000
         LTR   COMMBASE,COMMBASE       IS SUBTASK ALIVE                 00789000
         BZ    ABENDADP+6              NO DONT POST                     00790000
         MVC   UCPUSER(4),TWAPCAC      PUT IN FOR ABEND                 00791000
ABENDCON EQU   *                                                        00792000
         OI    UCSYIND,UCSYEOJ         TELL EOJ                         00793000
         LA    R15,999                                                  00794000
         B     RESLEEP                                                  00795000
ABENDADP EQU   *                                                        00796000
         MVC   TWAPCAC,UCPUSER                                          00797000
         CLI   TWAPCAC,X'A'            MAKE SURE THERE IS ABEND CODE    00798000
         BNL   *+10                                                     00799000
         MVC   TWAPCAC,CAAB                                             00800000
         AIF   ('&SYSPARM' NE 'OS').DOSEND                              00801000
         LTR   COMMBASE,COMMBASE       IS SUBTASK ALIVE                 00802000
         BZ    COMPABND                NO DONT DETACH                   00803000
         DFHIC TYPE=STOPTIME,CLASS=RUNAWAY                              00804000
         DETACH SUBPSW                 FINISHED                         00805000
COMPABND EQU   *                                                        00806000
.DOSEND  ANOP                                                           00807000
         LTR   COMMBASE,COMMBASE       DOES PROGRAM EXIST               00808000
         BZ    ABENDFR                 NO FORGET FREEMAIN               00809000
         LA    COMMBASE,0                                               00345000
         ICM   R2,15,TWASAVR8                                                   
         EXEC  CICS FREEMAIN DATA(0(R2))                                        
ABENDFR  EQU   *                                                        00813000
*        MVC   ABHOLDER,0(COMMBASE)                                     00814000
         EXEC  CICS ABEND ABCODE(TWAPCAC) CANCEL                                
*        ***********************************                            00816000
*        ** ALL CICS ABEND CODES ARE HERE **                            00817000
*        ***********************************                            00818000
         SPACE                                                          00819000
ABENDCDS EQU   *                                                        00820000
CATR     DC    C'CATR'                TRANSID MISSING.                  00821000
CAPR     DC    C'CAPR'                PROGRAM MISSING.                  00822000
CAST     DC    C'CAST'                STORAGE PROBLEM.                  00823000
CALG     DC    C'CALG'                TERMINAL LOGIC PROBLEM.           00824000
CAXT     DC    C'CAXT'                SETXIT ROUTINE PROBLEM.           00825000
         AIF   ('&SYSPARM' NE 'OS').DOSCD                               00826000
CASB     DC    C'CASB'                OS SUBTASK CREATION FAILURE       00827000
.DOSCD   ANOP                                                           00828000
CAAB     DC    C'CAAB'                ADAPTER BLOW UP                   00829000
         SPACE 1                                               JC530035 00830000
*        ***************************                           JC530035 00831000
*        ** ALL MESSAGES ARE HERE **                           JC530035 00832000
*        ***************************                           JC530035 00833000
         SPACE 1                                               JC530035 00834000
NOADAPTM DC    C'CADS050E CA-ADAPTER SUBSYSTEM SUPPORT IS'     JC530035 00835000
         DC    C' NOT ACTIVE                             '     JC530035 00836000
NOTWAMSG DC    C'CADS053E TRANSACTION MUST HAVE A TWASIZE'     JC530035 00837000
         DC    C' OF AT LEAST 16 BYTES                   '     JC530035 00838000
         SPACE 1                                               JC530035 00839000
*        ********************************                               00840000
*        ** ALL CICS TRANSIDS ARE HERE **                               00841000
*        ********************************                               00842000
         SPACE                                                          00843000
TIDTAB   DS    0F                                                       00844000
         DC    CL4'SNTL',H'16',H'0',A(SNTLPL)                           00845000
         DC    CL4'DYNT',H'16',H'0',A(DYNTPL)                           00846000
         DC    CL4'SCHD',H'16',H'0',A(SCHDPL)                           00847000
         DC    CL4'DRIV',H'16',H'0',A(DRIVPL)  DRIVER          DO530001 00848000
         DC    CL4'TLTP',H'16',H'0',A(TLTPPL)                           00849000
         DC    CL4'DATP',H'16',H'0',A(DATPPL)                           00850000
         DC    CL4'CAOZ',H'16',H'1',A(CAOZPL)  UNISERVICE CICS DO530001 00851000
         DC    CL4'FSD0',H'16',H'0',A(FSD0PL)  FSD             DO530001 00852000
         DC    XL4'FF',H'16',H'0',A(DUM1PL)  SPARE             DO530001 00853000
         DC    XL4'FF',H'16',H'0',A(DUM2PL)  SPARE             DO530001 00854000
         DC    XL4'FF',H'16',H'0',A(DUM3PL)  SPARE             DO530001 00855000
         DC    X'FF'                                                    00856000
ADNOTRQ  EQU   B'00000001'                                     DO530001 00857000
*                                                                       00858000
*        ********************************                               00859000
*        ** ALL CICS PROGRAMS ARE HERE **                               00860000
*        ********************************                               00861000
FSD0PL   DS    0CL8                    FSD                     DO530001 00862000
         DC    CL8'CACOFSD0'                                   DO530001 00863000
         DC    24X'FF'                 LEAVE 3 SPARE ENTRIES.  DO530001 00864000
         DC    X'FF'                                           DO530001 00865000
         SPACE                                                          00866000
DRIVPL   DS    0CL8                    DRIVER                  DO530001 00867000
         DC    CL8'SCHDCICA'                                   DO530001 00868000
         DC    24X'FF'                 LEAVE 3 SPARE ENTRIES.  DO530001 00869000
         DC    X'FF'                                           DO530001 00870000
CAOZPL   DS    0CL8                    UNISERVICE CICS         DO530001 00871000
         DC    CL8'CAOZCICS'                                   DO530001 00872000
         DC    24X'FF'                 LEAVE 3 SPARE ENTRIES.           00873000
         DC    X'FF'                                                    00874000
TLTPPL   DS    0CL8                    TLMS                    DO530001 00875000
         DC    CL8'TLTPCICS'                                   DO530001 00876000
         DC    24X'FF'                 LEAVE 3 SPARE ENTRIES.  DO530001 00877000
         DC    X'FF'                                           DO530001 00878000
DATPPL   DS    0CL8                    MANAGE DASD                      00879000
         DC    CL8'MDCICTPA'                                            00880000
         DC    24X'FF'                 LEAVE 3 SPARE ENTRIES.           00881000
         DC    X'FF'                                                    00882000
SNTLPL   DS    0CL8                    SENTINEL.                        00883000
         DC    CL8'CASEM000'                                            00884000
         DC    24X'FF'                 LEAVE 3 SPARE ENTRIES.           00885000
         DC    X'FF'                                                    00886000
DUM1PL   DS    0CL8                    DUMMY 1 .               DO530001 00887000
         DC    32X'FF'                 LEAVE 4 SPARE ENTRIES.  DO530001 00888000
         DC    X'FF'                                           DO530001 00889000
DUM2PL   DS    0CL8                    DUMMY 2 .               DO530001 00890000
         DC    32X'FF'                 LEAVE 4 SPARE ENTRIES.  DO530001 00891000
         DC    X'FF'                                           DO530001 00892000
DUM3PL   DS    0CL8                    DUMMY 3 .               DO530001 00893000
         DC    32X'FF'                 LEAVE 4 SPARE ENTRIES.  DO530001 00894000
         DC    X'FF'                                           DO530001 00895000
DYNTPL   DS    0CL8                    DYNAM.                           00896000
         DC    CL8'CASACIC0'                                            00897000
         DC    CL8'CATOCATA'                                            00898000
         DC    CL8'UTILDEF0'                                            00899000
         DC    CL8'UTILOWN0'                                            00900000
         DC    CL8'UTILTAP0'                                            00901000
         DC    CL8'UTILMSG0'                                            00902000
         DC    CL8'UTILDEL0'                                            00903000
         DC    CL8'UTILADD0'                                   JC530034 00904000
         DC    CL8'CACNDEQ0'                                            00905000
         DC    24X'FF'                 LEAVE 3 SPARE ENTRIES.           00906000
         DC    X'FF'                                                    00907000
SCHDPL   DS    0CL8                    SCHEDULER.                       00908000
         AIF   ('&SYSPARM' EQ 'OS').OSPGMS                              00909000
         DC    CL8'SCHDCICA'                                            00910000
         DC    CL8'CAJOJOBA'                                            00911000
         DC    CL8'CAJOSCHA'                                            00912000
         DC    CL8'CAJOUSR0'                                            00913000
         DC    CL8'CAJOWST0'                                            00914000
         DC    CL8'CAJORSRA'                                            00915000
         DC    CL8'CAJOMON0'                                            00916000
         DC    24X'FF'                 LEAVE 3 SPARE ENTRIES.           00917000
         DC    X'FF'                                                    00918000
         AGO   .LITERAL                                                 00919000
.OSPGMS  ANOP                                                           00920000
         DC    CL8'SCHDCICB'                                            00921000
         DC    CL8'CAJOJOBB'                                            00922000
         DC    CL8'CAJOSCHB'                                            00923000
         DC    CL8'CAJOUSR0'                                            00924000
         DC    CL8'CAJOWST0'                                            00925000
         DC    CL8'CAJORSRB'                                            00926000
         DC    CL8'CAJOMON0'                                            00927000
         DC    24X'FF'                 LEAVE 3 SPARE ENTRIES.           00928000
         DC    X'FF'                                                    00929000
.LITERAL ANOP                                                           00930000
         DS    0F                                                               
NUMTAB   DC    256X'FF'                                                 00931000
         ORG   NUMTAB+X'F0'       ADDRESS NUMERIC DIGITS                        
         DC    XL10'00'                                                         
         ORG   ,                                                                
TLEN     DC    H'3440'                                                          
         LTORG                                                          00932000
         PRINT ON,DATA                                                  00933000
PATCH1   EQU   *                                                        00934000
         DC    50S(*)                                                   00935000
         SPACE 02                                                       00936000
         ORG   PATCH1                                                   00937000
         DC    50H'-1'                                                  00938000
         ORG   ,                                               JC530035 00939000
         END                                                                    
