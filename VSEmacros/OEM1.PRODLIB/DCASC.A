         MACRO                                                          00001000
&LAB     DCASC    &STR,                                                *        
               &ESC=DUMMY,&LF=DUMMY,&CR=DUMMY,                         *        
               &NULL=DUMMY,&FF=DUMMY                                            
.********************************************************************** 00003000
.*       Define ASCII constant                                        * 00004000
.*       Copyright 1998,2003 Connectivity Systems                     * 00005000
.*                                                                    * 00006000
.*       This macro translates a character string into ASCII.         * 00007000
.*                                                                    * 00008000
.*       Usage:                                                       * 00009000
.*       ›label®  DCASC  'ebcdic character string'›,ESC=x®  ...  *    * 00010000
.*                       ›,CR=x®›,LF=x®›,FF=x®›,NULL=x®               * 00011000
.*                                                                    * 00011000
.*       Usage Notes:                                                 * 00012000
.*       * The assembled data will be a hexadecimal DC statement.     * 00013000
.*       * Quotes (') and ampersands (&) must be paired in the        * 00014000
.*         same manner as a standard DC statement.                    * 00015000
.*       * The single character specified by each of the optional     * 00019000
.*         keyword parameters will be replaced with the appropriate   * 00020000
.*         ASCII character.                                           * 00021000
.*       * A null C constant will immediately precede the hex data.   * 00022000
.*         The contents of this string will be the printable          * 00023000
.*         version of the hex string (spaced for alignment). Quotes   * 00024000
.*         and ampersands show as asterisks in this string.           * 00025000
.*                                                                    * 00026000
.*                                                                    * 00026000
.*                                                                    *         
.*               .  <  (  +  !  $  *  )  ;  ^  -  /  |  ,  %          *         
.*           20 2E 3C 28 2B 21 24 2A 29 3B 5E 2D 2F 7C 2C 25          *         
.*                                                                    *         
.*            _  >  ?  `  :  #  @  =  "  a  b  c  d  e  f  g          *         
.*           5F 3E 3F 60 3A 23 40 3D 22 61 62 63 64 65 66 67          *         
.*                                                                    *         
.*            h  i  {  j  k  l  m  n  o  p  q  r  }  ~  s  t          *         
.*           68 69 7B 6A 6B 6C 6D 6E 6F 70 71 72 7D 7E 73 74          *         
.*                                                                    *         
.*            u  v  w  x  y  z  ›  ®  A  B  C  D  E  F  G  H          *         
.*           75 76 77 78 79 7A 5B 5D 41 42 43 44 45 46 47 48          *         
.*                                                                    *         
.*            I  J  K  L  M  N  O  P  Q  R  \  S  T  U  V  W          *         
.*           49 4A 4B 4C 4D 4E 4F 50 51 52 5C 53 54 55 56 57          *         
.*                                                                    *         
.*            X  Y  Z  0  1  2  3  4  5  6  7  8  9                   *         
.*           58 59 5A 30 31 32 33 34 35 36 37 38 39                   *         
.*                                                                    * 00026000
.*       Macro Version 1.5B,  7/04/03                                 * 00027000
.*                                                                    * 00028000
.********************************************************************** 00029000
         LCLC     &ITAB,&OTAB,&OSTR,&INP,&CHAR,&AMPER,&QT                       
         LCLC     &OTAB2,&DLM,&TEST,&CSTR,&VTAB                         00031000
         LCLA     &I,&LIMIT,&J,&K                                       00032000
         GBLC     &DCAESC,&DCALF,&DCACR,&DCANUL,&DCAFF                          
         LCLC     &XESC,&XLF,&XCR,&XNUL,&XFF                                    
.*                                                                              
         AIF      ('&STR' NE 'SET').NSET                                        
.*                                                                              
.*       Set global variables                                                   
.*                                                                              
         AIF      ('&ESC' EQ 'DUMMY').XL1                                       
&DCAESC  SETC     '&ESC'                                                        
.XL1     AIF      ('&LF' EQ 'DUMMY').XL2                                        
&DCALF   SETC     '&LF'                                                         
.XL2     AIF      ('&CR' EQ 'DUMMY').XL3                                        
&DCACR   SETC     '&CR'                                                         
.XL3     AIF      ('&NULL' EQ 'DUMMY').XL4                                      
&DCANUL  SETC     '&NULL'                                                       
.XL4     AIF      ('&FF' EQ 'DUMMY').XL5                                        
&DCAFF   SETC     '&FF'                                                         
.XL5     ANOP                                                                   
         MEXIT                                                                  
.*                                                                              
.*       Init local variables with global                                       
.*                                                                              
.NSET    ANOP                                                                   
&XESC    SETC     '&DCAESC'                                                     
&XLF     SETC     '&DCALF'                                                      
&XCR     SETC     '&DCACR'                                                      
&XNUL    SETC     '&DCANUL'                                                     
&XFF     SETC     '&DCAFF'                                                      
.*                                                                              
.*       Override local variables with current spec                             
.*                                                                              
         AIF      ('&ESC' EQ 'DUMMY').NL1                                       
&XESC    SETC     '&ESC'                                                        
.NL1     AIF      ('&LF' EQ 'DUMMY').NL2                                        
&XLF     SETC     '&LF'                                                         
.NL2     AIF      ('&CR' EQ 'DUMMY').NL3                                        
&XCR     SETC     '&CR'                                                         
.NL3     AIF      ('&NULL' EQ 'DUMMY').NL4                                      
&XNUL    SETC     '&NULL'                                                       
.NL4     AIF      ('&FF' EQ 'DUMMY').NL5                                        
&XFF     SETC     '&FF'                                                         
.NL5     ANOP                                                                   
.*                                                                              
.*       Create the input and output character tables                           
.*                                                                              
&ITAB    SETC     ' .<(+!$*);^-/|,%_>?`:#@="abcdefghi{jklmnopqr}~stuvwx*00033000
               yz›®ABCDEFGHIJKLMNOPQR\STUVWXYZ0123456789'               00034000
&VTAB    SETC     ' .<(+!$*);^-/|,%_>?`:#@="abcdefghi{jklmnopqr}~stuvwx*00033000
               yz›®ABCDEFGHIJKLMNOPQR\STUVWXYZ0123456789'               00034000
&OTAB    SETC     '202E3C282B21242A293B5E2D2F7C2C255F3E3F603A23403D2261*00035000
               62636465666768697B6A6B6C6D6E6F7071727D7E73747576777879'  00036000
&OTAB2   SETC   '7A5B5D4142434445464748494A4B4C4D4E4F5051525C5354555657*00037000
               58595A30313233343536373839'                              00038000
&OTAB    SETC     '&OTAB&OTAB2'                                         00039000
.*                                                                              
.*       Create input and output values for ampersand and quote                 
.*       characters, since the assembler treats them differently                
.*                                                                              
&AMPER   SETC     '&&'                                                  00041000
&AMPER   SETC     '&AMPER'(1,1)                                         00042000
&QT      SETC     ''''                                                  00044000
&QT      SETC     '&QT'(1,1)                                            00045000
&OSTR    SETC     ''                       Hex output string                    
&CSTR    SETC     ''                       Char output string                   
&INP     SETC     ''                       Input work string            00048000
.*                                                                              
.*       Validate source string.  Create a working string where                 
.*       the double quotes and ampersands are now only singles                  
.*                                                                              
         AIF      ('&STR'(1,1) EQ '''').OK1                                     
         MNOTE    8,'Input string does not start with quote'                    
         MEXIT                                                                  
.OK1     ANOP                                                                   
         ACTR     65535                                                         
&I       SETA     2                                                             
&LIMIT   SETA     K'&STR-1                                                      
.LP1     ANOP                                                                   
         AIF      (&I GT &LIMIT).LP1E      End of source                        
         AIF      ('&STR'(&I,1) EQ '&QT').DBL                                   
         AIF      ('&STR'(&I,1) NE '&AMPER').SGL                                
.DBL     ANOP                                                                   
         AIF      (&I NE &LIMIT).OK2       Not at limit                         
         MNOTE    8,'Unpaired quote or ampersand'                               
         MEXIT                                                                  
.OK2     ANOP                                                                   
&I       SETA     &I+1                                                          
.SGL     ANOP                                                                   
&INP     SETC     '&INP'.'&STR'(&I,1)      Copy a character                     
&I       SETA     &I+1                                                          
         AGO      .LP1                                                          
.LP1E    ANOP                                                                   
&LIMIT   SETA     K'&INP                                                        
.*                                                                              
.*       If any "special" characters (eg, ESC=,LF=) as specified,               
.*       pre-pend them to the input and output tables (duplications             
.*       will then trail and won't be found.                                    
.*                                                                              
&K       SETA     K'&ITAB                  Length of input table        00064000
.*                                                                              
&ITAB    SETC     '&AMPER'.'&QT'.'&ITAB'   Add quote and asterisk               
&VTAB    SETC     '**'.'&VTAB'                                                  
&OTAB    SETC     '2627'.'&OTAB'                                                
&K       SETA     &K+2                                                          
.*                                                                              
         AIF      ('&XESC' EQ '').NESC     ESC=                                 
&ITAB    SETC     '&XESC'.'&ITAB'                                               
&VTAB    SETC     '&XESC'.'&VTAB'                                               
&OTAB    SETC     '1B'.'&OTAB'                                                  
&K       SETA     &K+1                                                          
.NESC    ANOP                                                                   
.*                                                                              
         AIF      ('&XCR' EQ '').NCR       CR=                                  
&ITAB    SETC     '&XCR'.'&ITAB'                                                
&VTAB    SETC     '&XCR'.'&VTAB'                                                
&OTAB    SETC     '0D'.'&OTAB'                                                  
&K       SETA     &K+1                                                          
.NCR     ANOP                                                                   
.*                                                                              
         AIF      ('&XLF' EQ '').NLF       LF=                                  
&ITAB    SETC     '&XLF'.'&ITAB'                                                
&VTAB    SETC     '&XLF'.'&VTAB'                                                
&OTAB    SETC     '0A'.'&OTAB'                                                  
&K       SETA     &K+1                                                          
.NLF     ANOP                                                                   
.*                                                                              
         AIF      ('&XNUL' EQ '').NNULL    NULL=                                
&ITAB    SETC     '&XNUL'.'&ITAB'                                               
&VTAB    SETC     '&XNUL'.'&VTAB'                                               
&OTAB    SETC     '00'.'&OTAB'                                                  
&K       SETA     &K+1                                                          
.NNULL   ANOP                                                                   
.*                                                                              
         AIF      ('&XFF' EQ '').NFF       FF=                                  
&ITAB    SETC     '&XFF'.'&ITAB'                                                
&VTAB    SETC     '&XFF'.'&VTAB'                                                
&OTAB    SETC     '0C'.'&OTAB'                                                  
&K       SETA     &K+1                                                          
.NFF     ANOP                                                                   
.*                                                                              
.*       Compare each character in the input stream against the                 
.*       input table values.  When found, move the appropriate                  
.*       hex pair to the output string                                          
.*                                                                              
&I       SETA     1                                                     00065000
.LP3     ANOP                                                           00066000
         AIF      (&I GT &LIMIT).LP3E                                   00067000
&CHAR    SETC     '&INP'(&I,1)                                          00068000
&J       SETA     1                                                     00076000
.LP4     ANOP                                                           00077000
         AIF      ('&CHAR' NE '&ITAB'(&J,1)).NEXTJ                      00078000
&CSTR    SETC     '&CSTR '.'&VTAB'(&J,1)                                00118000
&J       SETA     ((&J-1)*2)+1                                                  
&OSTR    SETC     '&OSTR'.'&OTAB'(&J,2)                                         
&I       SETA     &I+1                                                  00119000
         AGO      .LP3                                                  00120000
.*                                                                              
.NEXTJ   ANOP                                                           00117000
&J       SETA     &J+1                                                  00079000
         AIF      (&J LE &K).LP4                                        00080000
         MNOTE    8,'Untranslatable character: &CHAR'                   00081000
         MEXIT                                                          00082000
.LP3E    ANOP                                                                   
         DS       0C'&CSTR'                                             00122000
&LAB     DC       1X'&OSTR'                                             00123000
         MEND                                                                   
