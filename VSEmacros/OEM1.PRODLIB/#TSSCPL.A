         MACRO ,                                                        00010000
         #TSSCPL   &TYPE=DSECT,                                        X00020000
               &RTLN=1024                                               00030000
.*====================================================================  00040000
.*        TYPE INDICATES WHETHER MACRO WILL BE EXPANDED AS DSECT        00050000
.*        OR CSECT                                                      00060000
.*        RTLN IS NUMERIC LENGTH ALLOCATED FOR TSSRTN                   00070000
.*====================================================================  00080000
         AIF   ('&TYPE' EQ 'DSECT').CPLLNGO                             00090000
         AIF   ('&TYPE' EQ 'CSECT').CPLLNGO                             00100000
         MNOTE 8,'INVALID TYPE FOR #TSSCPL MACRO'                       00110000
         AGO   .CPLEND                                                  00120000
.CPLLNGO ANOP  ,                                                        00130000
         LCLA  &ARTLN                                                   00140000
         AIF   ('&RTLN' NE '').CPLLNG1                                  00150000
&ARTLN   SETA  1024               DEFAULT LENGTH                        00160000
         MNOTE 0,'RTLN=1024 DEFAULT APPLIED'                            00170000
         AGO   .CPLGO                                                   00180000
.CPLLNG1 ANOP  ,                                                        00190000
&ARTLN   SETA  &RTLN              ACTUAL LENGTH SUPPLIED                00200000
         AIF   (&ARTLN GE 256).CPLLNG2                                  00210000
.CPLLNGX ANOP  ,                                                        00220000
         MNOTE 8,'RTLN=&RTLN NOT BETWEEN 256 AND 32654, INCLUSIVE'      00230000
         AGO   .CPLEND                                                  00240000
.CPLLNG2 ANOP  ,                                                        00250000
         AIF   (&ARTLN GT 32654).CPLLNGX                                00260000
.CPLLNG3 ANOP  ,                                                        00270000
         AIF   (&ARTLN GE 1024).CPLGO                                   00280000
         MNOTE 4,'WARNING: FACLIST/RESLIST SHOULD NOT BE USED'          00290000
         MNOTE 4,'WARNING: FLDXTR/FLDUPD LENGTH MUST BE LE &RTLN'       00300000
.CPLGO   ANOP  ,                                                        00310000
&AFAC#   SETA  &ARTLN/8                                                 00320000
TSSCPL   &TYPE ,                  * #TSSCPL , AUG/13/99        V3L0M005 00330000
*********************************************************************** 00340000
*                                                                     * 00350000
* TOP SECRET SECURITY APPLICATION INTERFACE PARAMETER LIST.           * 00360000
*                                                                     * 00370000
*********************************************************************** 00380000
TSSHEAD  DS    CL8                HEADER CL8'TCPLV3L0'         V3L0M005 00390000
TSSCLASS DS    CL8                CLASS NAME                            00400000
TSSRNAME DS    CL44               RESOURCE NAME                         00410000
TSSPPGM  DS    CL8                PRIVILEGED PROGRAM NAME               00420000
TSSACC   DS    CL8                ACCESS LEVEL NAME                     00430000
TSSRC    DS    XL2                RETURN CODE, AS FOLLOWS :             00440000
TSSROK   EQU   0                  CALLER IS AUTHORIZED                  00450000
TSSRND   EQU   4                  RESOURCE IS NOT DEFINED               00460000
TSSRNA   EQU   8                  CALLER IS NOT AUTHORIZED              00470000
TSSRIPL  EQU   12                 INVALID PARAMETER LIST                00480000
TSSRENV  EQU   16                 ENVIRONMENT ERROR                     00490000
TSSRINAC EQU   20                 TSS IS NOT ACTIVE                     00500000
TSSRXSAC EQU   24                 RETURN DATA > SIZE OF RETURN AREA     00510000
TSSRSEGT EQU   28                 INCORRECT FDT SEGMENT                 00520000
TSSRFDTE EQU   32                 INCORRECT FDT FIELD NAME              00530000
TSSRUSRF EQU   36                 FIELD SPECIFIED IS NOT A USER FIELD   00540000
TSSRRGF  EQU   40                 STORAGE NOT AVAIL TO COMPLETE REQUEST 00550000
TSSSTAT  DS    XL2                STATUS CODE, AS FOLLOWS :             00560000
TSSSDEF  EQU   0                  DEFINED USER                          00570000
TSSSUND  EQU   4                  UNDEFINED USER                        00580000
TSSSNSO  EQU   8                  TERMINAL NOT SIGNED-ON                00590000
TSSSIDT  EQU   12                 INVALID DEVICE TYPE                   00600000
TSSCRC   DS    CL2                RETURN CODE, IN CHARACTER             00610000
TSSCSTAT DS    CL2                STATUS CODE, IN CHARACTER             00620000
TSSCACEE DS    A                  @ USER'S ACEE                         00630000
TSSVOL   DS    CL6                VOLSER FOR DATASET CHECKS             00640000
TSSLOG   DS    C                  LOG INDICATOR                         00650000
TSSRSVD  DS    XL2                RESERVED FOR FUTURE USE               00660000
TSSDRC   DS    X                  DETAIL REASON CODE FOR DUF            00670000
TSSLRTN  DS    AL4                FULLWORD LEN OF TSSRTN                00680000
TSSRSVD2 DS    XL12               RESERVED FOR FUTURE USE               00690000
*                                                                       00700000
***      FIXED LENGTH TSSRTN WITH TSSLRTN NULLS OR BLANKS               00710000
*                                                                       00720000
TSSRTN   DS    CL&ARTLN           RETURN DATA ALLOCATED LENGTH          00730000
TSSPLEN2 EQU   *-TSSCPL           L'TSSCPL FOR FACLIST/RESLIST          00740000
*                                              FLDUPD/FLDXTR            00750000
*=============================================================          00760000
***      SHORT TSSRTN DSECT                                             00770000
*=============================================================          00780000
         ORG   TSSRTN             REDEFINE TSSRTN FOR SHORT             00790000
TSSACIDA DS    CL8                ACID ACIDNAME                         00800000
TSSFAC   DS    CL8                FACILITY NAME                         00810000
TSSMODE  DS    CL8                CURRENT MODE                          00820000
TSSTYPE  DS    CL8                ACID TYPE                             00830000
TSSTERM  DS    CL8                TERMINAL NAME                         00840000
TSSSYS   DS    CL8                SYSTEM NAME                           00850000
TSSACIDF DS    CL32               ACID FULLNAME                         00860000
TSSDEPTA DS    CL8                DEPARTMENT ACIDNAME                   00870000
TSSDEPTF DS    CL32               DEPARTMENT FULLNAME                   00880000
TSSDIVA  DS    CL8                DIVISION ACIDNAME                     00890000
TSSDIVF  DS    CL32               DIVISION FULLNAME                     00900000
TSSZONEA DS    CL8                ZONE ACIDNAME                         00910000
TSSZONEF DS    CL32               ZONE FULLNAME                         00920000
         DS    CL56                                                     00930000
TSSPLEN  EQU   370                DEFLT L'TSSRTN FOR TSSCLASS           00940000
*                                 OTHER=THAN=FACLIST/RESLIST            00950000
*=============================================================          00960000
***      FACLIST TSSRTN REDEFINITION                                    00970000
***      TSSLRTN >= 1024 FOR FACLIST CALLS                              00980000
*=============================================================          00990000
         ORG   TSSRTN             REDEFINE TSSRTN FOR FACLIST           01000000
TSSFACX  DS    &AFAC#.CL8         FACILITY LIST ENTRIES                 01010000
*                                 FOR TSSCLASS=FACLIST/RESLIST          01020000
*                                 OTHER THAN FACLIST/RESLIST            01030000
*              TSSLRTN ( >= 1024) CONTROLS LENGTH OF DATA RTND          01040000
*=============================================================          01050000
***      RESLIST TSSRTN REDEFINITION                                    01060000
***      TSSLRTN >= 1024 FOR RESLIST CALLS                              01070000
*=============================================================          01080000
         ORG   TSSRTN             REDEFINE TSSRTN FOR RESLIST           01090000
TSSRFLDS DS    AL4                NUMBER OF RETURNED RESOURCES          01100000
TSSRDATA DS    XL(&ARTLN-4)       RETURNED RESOURCE DATA                01110000
*=============================================================          01120000
***      FLDXTR/FLDUPD DSECT                                            01130000
*=============================================================          01140000
         ORG   TSSRTN             REDEFINE TSSRTN FLDXTR/FLDUPD         01150000
TSSFLLEN DS    AL4                USED LENGTH OF TSSFLDAT               01160000
TSSFLDAT DS    XL(&ARTLN-4)       RETURNED FIELD CON`ENTS               01170000
*=============================================================          01180000
***      ENTRY DSECT FOR RESLIST                                        01190000
***      TSSLRTN >= 1024 FOR RESLIST CALLS                              01200000
*=============================================================          01210000
TSSRLENT DSECT ,                  DSECT MAPPING RESLIST RETURN ENTRY    01220000
TSSRLLEN DS    AL2                USED LENGTH OF TSSRLRES               01230000
TSSRLGEN DS    XL1                GENERIC INDICATOR (X'80' = GENERIC)   01240000
TSSRLRES DS    XL255              RETURNED RESOURCE NAME                01250000
.CPLEND  ANOP  ,                                                        01260000
         MEND  ,                                                        01270000
