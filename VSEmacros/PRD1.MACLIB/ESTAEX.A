*/*                                                            @G860P38 00001000
*********************************************************************** 00002000
*   COPYRIGHT =                                                         00003000
*                                                                       00004590
*              LICENSED MATERIALS - PROPERTY OF IBM                     00005180
*              "RESTRICTED MATERIALS OF IBM"                            00005770
*              5686-066                                                 00006360
*              (C)COPYRIGHT IBM CORP. 1996                              00006950
*                                                                       00008000
*   STATUS  = HBB3310                                                   00009000
*                                                                       00010000
*    COMMENT   WHEN RESERVED BITS / BYTES ARE ASSIGNED, THE    @ZMD0006 00011000
*              IMPLEMENTATION MUST TAKE INTO ACCOUNT           @ZMD0006 00012000
*              DOWNWARD COMPATIBILITY. THAT IS, THE CODE       @ZMD0006 00013000
*              MUST ENSURE THAT PREVIOUS FAILURES TO CLEAN     @ZMD0006 00014000
*              THE RESERVED BITS / BYTES DO NOT RESULT IN      @ZMD0006 00015000
*              USAGE OF UNINTENDED (NEW) FUNCTIONS. THE        @ZMD0006 00016000
*              ABILITY TO SET A BIT FUNCTION ON ONE            @ZMD0006 00017000
*              INVOCATION AND CONTINUE TO APPLY ON SUBSEQUENT  @ZMD0006 00018000
*              INVOCATIONS MUST ALSO BE CONSIDERED, ACCORDING  @ZMD0006 00019000
*              TO THE REQUIREMENTS OF THE NEW FUNCTION.        @ZMD0006 00020000
*                                                                       00021000
*    MACMEAN EXTENDED SPECIFY TASK ABNORMAL EXIT               @G860P38 00022000
*                                                                       00023000
*    MACCOMP SC1CM                                             @G81CP2F 00024000
*    MACSTAT HBB3310                                                    00025000
*                                                                       00026000
*    CHANGE-ACTIVITY=                                                   00027000
*     $L0=     AR    HBB3310 850901 PDXB: NEW MACRO                     00028000
*     $L1=     AR    HBB3310 860201 PDXB: AR SUPPORT                    00029000
*     $D1= DCR0059   HBB3310 861029 PDU8: CANCEL KEYWORD SUPPORT  @D1A  00030000
*     $P1= PC40262   HBB3310 861029 PDA8: FIX INVALID STAM INSTRUCTIONS 00031000
*     @D61ADTN                      :VSE/ESA COMPATIBILITY WITH MVS/ESA 00032000
*                                                                       00033000
*********************************************************************** 00034000
         MACRO                                                          00035000
&NAME    ESTAEX &EXIT,&TYPE,&PARAM=I,&XCTL=NO,&MF=I,&PURGE=,&ASYNCH=,  J00036000
               &TERM=,&RECORD=,&CANCEL=,                               J00037000
               &RELATED=,&TOKEN=,&TKNPASS=                        @D1C  00038000
.********************************************************************** 00039000
         GBLC  &SYSSPLV                                        @G860P1C 00041000
         SPLEVEL TEST    INVOKE SPLEVEL MACRO                  @G860P1C 00042000
         LCLA  &LENGTH                                                  00043000
         LCLA  &FLAGS                                                   00044000
         LCLA  &FLGS                                                    00045000
         LCLA  &ANDOFF                                                  00046000
         LCLA  &TESTF                                                   00047000
         LCLA  &TURNOFF                                                 00048000
         LCLA  &EXITL                                                   00049000
         LCLA  &FLAGS4                                       @G81CP2F   00050000
         LCLB  &TERMON                                                  00051000
         LCLB  &RECON                                                   00052000
         LCLB  &XCTLON                                             @L1A 00053000
         LCLB  &BRNCHON                                                 00054000
         LCLB  &NCNLON                  NO CANCEL.  NCNL IS USED AS A  J00055000
                                        REMINDER THAT THE DEFAULT IS   J00056000
                                        CANCEL = YES, WHICH LEAVES     J00057000
                                        ESTANCNL OFF               @D1A 00058000
         LCLB  &ASY                                                     00059000
         LCLB  &ASY1                                                    00060000
         LCLB  &HALT                                                    00061000
         LCLB  &NONE                                                    00062000
         LCLB  &PNONE                                                   00063000
         LCLB  &ASYOFF                                                  00064000
         LCLB  &PRGOFF                                                  00065000
         LCLB  &TERMOFF                                                 00066000
         LCLB  &RECOFF                                                  00067000
         LCLB  &REC1                                           @D61ADTN 00068000
         LCLB  &XCTLOFF                                            @L1A 00069000
         LCLB  &BRANOFF                                                 00070000
         LCLB  &NCNLOFF                                            @D1A 00071000
         LCLB  &TOK                                          @G81CP2F   00072000
         LCLC  &GNAME                                                   00073000
         LCLC  &LNAME                                                   00074000
         LCLC  &EXITR                                                   00075000
         LCLC  &LA                      "LA" OR "LAE" VIA SYSSTATE @L1A 00076000
.********************************************************************** 00077000
.*   CHECK FOR UNSUPPORTED KEYWORDS IN THE VSE ENVIRONMENT   @D61ADTN * 00078000
.********************************************************************** 00079000
         AIF ('&RECORD' NE 'YES').VSECON1                      @D61ADTN 00080000
&REC1    SETB 1                                                @D61ADTN 00081000
.*       MNOTE 0,'RECORD=YES IS IGNORED BY VSE/ESA.'           @D62ADUL 00082000
.VSECON1 ANOP                                                  @D61ADTN 00083000
         AIF ('&XCTL' EQ 'YES').ERRXCTL                        @D61ADTN 00084000
         AIF ('&PURGE' EQ 'QUIESCE' OR                                 X00085000
               '&PURGE' EQ 'HALT').ERRPURG                     @D61ADTN 00086000
         AIF (T'&TKNPASS NE'O').ERRTKNP                        @D61ADTN 00087000
.********************************************************************** 00088000
.*   CHECK FOR UNSUPPORTED KEYWORDS IN THE VSE ENVIRONMENT   @D61ADTN * 00089000
.********************************************************************** 00090000
         GBLC  &SYSASCE                                            @L1A 00091000
         SYSSTATE TEST                  TEST ASC ENVIRONMENT       @L1A 00092000
&LA      SETC  'LA'                     MUST USE LA INSTRUCTION    @L1A 00093000
         AIF   ('&SYSASCE' EQ 'P').NOLAE                           @L1A 00094000
&LA      SETC  'LAE'                    MUST USE LAE               @L1A 00095000
.NOLAE   ANOP                                                      @L1A 00096000
&ASY1    SETB  ('&ASYNCH' EQ 'YES')                                     00097000
&ASY     SETB  ('&ASYNCH' EQ 'YES' OR '&ASYNCH' EQ '')                  00098000
&HALT    SETB  ('&PURGE' EQ 'HALT')                                     00099000
&NONE    SETB  ('&PURGE' EQ 'NONE' OR '&PURGE' EQ '')                   00100000
&PNONE   SETB  ('&PURGE' EQ 'NONE')                                     00101000
&TOK     SETB  ('&TOKEN' NE '' OR '&TKNPASS' EQ 'YES')           @L1C   00102000
&TERMON  SETB  ('&TERM' EQ 'YES')                                       00103000
         AIF   (&REC1).VSECON2                                 @DXXXXXX 00104000
&RECON   SETB  ('&RECORD' EQ 'YES')                                     00105000
.VSECON2 ANOP                                                  @DXXXXXX 00106000
&XCTLON  SETB  ('&XCTL' EQ 'YES')                                       00107000
&NCNLON  SETB  ('&CANCEL' EQ 'NO')                                 @D1A 00108000
&FLAGS   SETA  128*&TERMON+64*&RECON+32*&XCTLON+16*&NCNLON+8*&TOK+4*&ASC00109000
               Y+2*&NONE+&HALT                                     @D1C 00110000
&FLGS   SETA  128*&TERMON+64*&RECON+32*&XCTLON+16*&NCNLON+8*&TOK+4*&ASYC00111000
               +2*&PNONE+&HALT                                     @D1C 00112000
&FLAGS4  SETA  128*&TERMON+64*&RECON+32*&XCTLON+16*&NCNLON+4*&ASY+2*&NOC00113000
               NE+&HALT                                            @D1C 00114000
&LENGTH  SETA  20                  LENGTH OF PARMLIST FOR ESTAEX   @L1C 00115000
         AIF   (('&EXIT' EQ '0') AND ('&TYPE' NE '' OR '&PARAM' NE 'I' J00116000
               OR '&CANCEL' NE ''                                      J00117000
               OR '&XCTL' NE 'NO' OR '&PURGE' NE '' OR '&ASYNCH' NE    J00118000
               '' OR '&TERM' NE '' OR '&RECORD' NE '')).CANERR    @D1C  00119000
         AIF   (('&EXIT' EQ '0') AND ('&TKNPASS' EQ 'NO')).RESUME      J00120000
                                                               @ZMC0832 00121000
         AIF   (('&EXIT' EQ '0') AND ('&TKNPASS' NE '')).CANERR         00122000
.RESUME  ANOP                                                           00123000
         AIF   ('&ASYNCH' NE '' AND '&ASYNCH' NE 'NO' AND '&ASYNCH' NE C00124000
               'YES').ERRASY                                            00125000
         AIF   ('&XCTL' NE '' AND '&XCTL' NE 'NO' AND '&XCTL' NE 'YES')C00126000
               .BADXCTL                                                 00127000
         AIF   ('&PURGE' NE '' AND '&PURGE' NE 'HALT' AND '&PURGE' NE  C00128000
               'NONE' AND '&PURGE' NE 'QUIESCE').PURERR                 00129000
         AIF   ('&TERM' NE '' AND '&TERM' NE 'YES' AND '&TERM' NE      J00130000
               'NO').TERMERR                                            00131000
         AIF   ('&RECORD' NE '' AND '&RECORD' NE 'YES' AND '&RECORD'   J00132000
               NE 'NO').RECERR                                          00133000
         AIF   ('&CANCEL' NE '' AND '&CANCEL' NE 'YES' AND '&CANCEL'   J00134000
               NE 'NO').NCNLERR                                   @D1A  00135000
         AIF   ('&TYPE' NE 'CT' AND '&TYPE'                            *00136000
               NE 'OV' AND '&TYPE' NE '').BADOPT                        00137000
         AIF   ('&MF(1)' EQ 'L').LROUT                                  00138000
.OK1     AIF   ('&MF(1)' EQ 'I').IROUT                                  00139000
         AIF   ('&MF(1)' EQ 'E').EROUTA                                 00140000
         IHBERMAC 35,,&MF(1)            INVALID MF= OPERAND             00141000
         MEXIT                                                          00142000
.CANERR  ANOP                                                           00143000
         MNOTE 0,'***   EXCESS PARAMETERS W/ CANCEL IGNORED' @G38NP38   00144000
         AGO   .RESUME                                                  00145000
.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*        00146000
.*                                                                      00147000
.*       EXECUTE ROUTE       MF=E                                       00148000
.*                                                                      00149000
.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*        00150000
.EROUTA  AIF   (N'&MF GT 2).EXCESSA                                     00151000
.EROUT   AIF   ('&MF(2)' NE '').EXEC                                    00152000
.NOPARM  IHBERMAC 24                    REQ'D. PARMS. MISSING           00153000
         MEXIT                                                          00154000
.EXEC    ANOP                                                           00155000
&ASYOFF  SETB  ('&ASYNCH' EQ 'NO')                                      00156000
&PRGOFF  SETB  ('&PURGE' NE '')                                         00157000
&TERMOFF SETB  ('&TERM' EQ 'NO')                                        00158000
&RECOFF  SETB  ('&RECORD' EQ 'NO')                                      00159000
&XCTLOFF SETB  ('&XCTL' EQ 'NO')                                        00160000
&NCNLOFF SETB  ('&CANCEL' EQ 'YES')                               @D1A  00161000
&ANDOFF  SETA  255-(128*&TERMOFF+64*&RECOFF+32*&XCTLOFF+16*&NCNLOFF+4*&X00162000
               ASYOFF+3*&PRGOFF)                                  @D1C  00163000
         AIF   ('&EXIT' EQ '0').CANCEL1                        @ZMB0628 00164000
&NAME    IHBINNRR &MF(2)               LOAD LIST ADDR IN REG 1          00165000
         AIF   ('&EXIT' EQ '').TSTPX                                    00166000
         AIF   ('&EXIT'(1,1) EQ '(').ST1                                00167000
         IHBINNRA ,&EXIT               LOAD NEW EXIT ADDR IN REG 0      00168000
         ST    0,16(1)                 STORE USER EXIT ADDRESS @G860P38 00169000
         AGO   .TSTPX                                                   00170000
.ST1     ANOP                                                           00171000
         ST    &EXIT(1),16(1)          STORE USER EXIT ADDRESS @G860P38 00172000
.TSTPX   ANOP                                                           00173000
         AIF   (&ANDOFF EQ 255).TESTOR                                  00174000
&TESTF   SETA  (&FLGS+128*&TERMOFF+64*&RECOFF+32*&XCTLOFF+16*&NCNLOFF+8X00175000
               +4*&ASYOFF+3*&PRGOFF)                              @D1C  00176000
         AIF   (&TESTF GE 255).MVI1                                     00177000
         NI    0(1),&ANDOFF            TURN OFF FLAG BITS               00178000
.TESTOR  ANOP                                                           00179000
         AIF   (&FLGS EQ 0).MVISKP1                                @L1C 00180000
         OI    0(1),&FLGS              FLAGS FOR ESTAEX            @L1C 00181000
         AGO   .MVISKP1                                                 00182000
.MVI1    MVI   0(1),&FLGS              SET FLAGS FOR ESTAEX        @L1C 00183000
.MVISKP1 ANOP                                                           00184000
.TSTTOK  ANOP                                                @G81CP2F   00185000
         AIF   ('&PARAM' EQ 'I').ETSTTCB                                00186000
         AIF   ('&PARAM'(1,1) EQ '(').ST2                               00187000
         IHBINNRR ,&PARAM                                               00188000
         ST    0,8(0,1)                MODIFY REMOTE LIST - PARM ADDR   00189000
         AIF   ('&SYSASCE' EQ 'P').ETSTTCB                         @L1A 00190000
         STAM  0,0,12(1)               SAVE ALET                   @P1C 00191000
         AGO   .ETSTTCB                                                 00192000
.ST2     ST    &PARAM(1),8(0,1)        MODIFY LIST - PARAM ADDR         00193000
         AIF   ('&SYSASCE' EQ 'P').ETSTTCB                         @L1A 00194000
         STAM  &PARAM(1),&PARAM(1),12(1) SAVE ALET                 @P1C 00195000
.ETSTTCB ANOP                                                           00196000
         AIF   ('&TOKEN' EQ '').TSTOPT                       @G81CP2F   00197000
         AIF   ('&TOKEN'(1,1) EQ '(').ST4                    @G81CP2F   00198000
         AIF   (T'&TOKEN EQ 'N').SELFDEF                     @G81CP2F   00199000
         AIF   ('&EXIT' NE '0' AND '&TYPE' NE 'OV').TSTOPT   @G81CP2F   00200000
         L     0,&TOKEN            LOAD TOKEN VALUE          @G81CP2F   00201000
         ST    0,04(0,1)           MODIFY LIST - TOKEN VALUE @G81CP2F   00202000
         AGO   .TSTOPT                                       @G81CP2F   00203000
.ST4     ANOP                                                @G81CP2F   00204000
         AIF   ('&EXIT' NE '0' AND '&TYPE' NE 'OV').TSTOPT   @G81CP2F   00205000
         ST    &TOKEN(1),04(0,1)   MODIFY LIST - TOKEN VALUE @G81CP2F   00206000
.TSTOPT  ANOP                                                           00207000
         AIF   ('&EXIT' EQ '0').CANCEL                                  00208000
         AIF   ('&EXIT' EQ '' AND '&TYPE' EQ '' AND                    *00209000
                '&PARAM' EQ 'I' AND '&PURGE' EQ '' AND                 *00210000
                '&ASYNCH' EQ '' AND '&XCTL' EQ '' AND                  *00211000
                '&TERM' EQ '' AND '&RECORD' EQ '' AND                  *00212000
                '&CANCEL' EQ '' AND                                    *00213000
                '&TOKEN' EQ '' AND '&TKNPASS' EQ '').OPTTST      @D1C   00214000
         MVI   3(1),1                SET VERSION NUMBER                 00215000
         AGO   .OPTTST                                                  00216000
.EXCESSA MNOTE 0,'***     EXCESS PARAMETERS W/MF=E IGNORED'  @G38NP38   00217000
         AGO   .EROUT                                                   00218000
.SELFDEF MNOTE 12,'***     VALUE SPECIFIED FOR TOKEN IS SELF-DEFINING'  00219000
.*                                                           @G81CP2F   00220000
         MEXIT                                               @G81CP2F   00221000
.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*        00222000
.*                                                                      00223000
.*       LIST ROUTE          MF=L                                       00224000
.*                                                                      00225000
.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*        00226000
.LROUT   AIF   (N'&MF GT 1).EXCESS                                      00227000
.LROUT1  ANOP                                                           00228000
.LROUT3  ANOP                                                           00229000
.LROUT2  ANOP                                                           00230000
         DS    0F                                                       00231000
         AIF   ('&TOKEN' NE '').IGNORE2                      @G81CP2F   00232000
&NAME    DC    AL1(&FLAGS)             FLAGS FOR ESTAEX          @L1C   00233000
         AGO   .LROUT5                                       @G81CP2F   00234000
.LROUT4  ANOP                                                @G81CP2F   00235000
&NAME    DC    AL1(&FLAGS4)            FLAGS FOR ESTAEX          @L1C   00236000
.*                                                           @G81CP2F   00237000
.LROUT5  ANOP                                                @G81CP2F   00238000
         DC    AL1(0)                  SECOND FLAG BYTE                 00239000
         DC    AL1(0)                  THIRD FLAG BYTE       @G860P1C   00240000
         DC    AL1(1)                  VERSION NUMBER        @G860P1C   00241000
         DC    A(0)                    TOKEN VALUE AREA      @G81CP2F   00242000
.LREGB   AIF   ('&PARAM' NE 'I').LREGC                                  00243000
         DC    A(0)                    PARM. LIST ADDR. NOT SPECIFIED   00244000
         DC    A(0)                    ALET FOR PARM LIST               00245000
         AGO   .LREGF                                                   00246000
.LREGC   AIF   ('&PARAM'(1,1) EQ '(').LISTERR                           00247000
         DC    A(&PARAM)               STAE EXIT PARM. LIST ADDR.       00248000
         DC    A(0)                    ALET FOR PARM LIST               00249000
.LREGF   ANOP                                                           00250000
         AIF   ('&EXIT' NE '').LREGG                         @G860P1C   00251000
         DC    AL4(0)                  EXIT ADDR NOT SPECD   @G860P1C   00252000
         AGO   .LREGH                                        @G860P1C   00253000
.LREGG   ANOP                                                @G860P1C   00254000
         AIF   ('&EXIT'(1,1) EQ '(').LISTERR                 @G860P1C   00255000
         DC    AL4(&EXIT)              FOUR BYTE EXIT ADDR   @G860P1C   00256000
.LREGH   ANOP                                                @G860P1C   00257000
         MEXIT                                                          00258000
.EXCESS  MNOTE 0,'***     EXCESS PARAMETERS W/MF=L IGNORED'  @G38NP38   00259000
         AGO   .LROUT1                                                  00260000
.IGNORE2 ANOP                                                @G81CP2F   00261000
         MNOTE 0,'***    TOKEN IGNORED W/MF=L'               @G38NP38   00262000
         AGO   .LROUT4                                       @G81CP2F   00263000
.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*        00264000
.*                                                                      00265000
.*       REGULAR ROUTE       MF=I                                       00266000
.*                                                                      00267000
.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*        00268000
.IROUT   AIF   ('&EXIT' EQ '').NOPARM                                   00269000
         AIF   ('&EXIT' NE '0').STDA                                    00270000
.CANCEL1 ANOP                                                @ZMB0628   00271000
&NAME    DS    0H                                                       00272000
.CANCEL  ANOP                                                           00273000
         AIF   ('&TOKEN' EQ '').NOTOK2                       @G81CP2F   00274000
         AIF   ('&TOKEN'(1,1) EQ '(').CAN2                   @G81CP2F   00275000
         AIF   (T'&TOKEN EQ 'N').SELFDEF                     @G81CP2F   00276000
         L     1,&TOKEN                LOAD TOKEN VALUE      @G81CP2F   00277000
         LA    0,12(0,0)               CANCEL W/TOKEN CODE       @L1C   00278000
         AGO   .PC1                                          @G81CP2F   00279000
.CAN2    LR    1,&TOKEN(1)             LOAD TOKEN VALUE      @G81CP2F   00280000
         LA    0,12(0,0)               CANCEL W/TOKEN CODE       @L1C   00281000
         AGO   .PC1                                          @G81CP2F   00282000
.NOTOK2  LA    0,8(0,0)                INDICATE CANCEL OPTION    @L1C   00283000
         AGO   .PC1                                                     00284000
.STDA    CNOP  0,4                     ESTAB. FULL WD. BOUND. ALIGN.    00285000
&LNAME   SETC  'IHB'.'&SYSNDX'                                          00286000
&NAME    &LA   1,&LNAME                                                 00287000
         B     *+&LENGTH+4             LIST ADDR IN REG1 SKIP LIST      00288000
&LNAME   EQU   *                                                        00289000
         DC    AL1(&FLAGS)             FLAGS FOR ESTAEX          @L1C   00290000
         DC    AL2(0)                  SECOND FLAG BYTE                 00291000
         DC    AL1(1)                  VERSION NUMBER        @G860P1C   00292000
         DC    A(0)                    SPACE FOR TOKEN       @G81CP2F   00293000
         AIF   ('&PARAM' EQ 'I' OR '&PARAM'(1,1) EQ '(').STDC1          00294000
         DC    A(&PARAM)               ESTAE EXIT PARM. LIST ADDR.      00295000
         DC    A(0)                    ALET FOR PARAM                   00296000
         AGO   .TCBTST                                                  00297000
.STDC1   DC    A(0)                    SPACE FOR PARM LIST ADDR         00298000
         DC    A(0)                    ALET FOR PARAM                   00299000
.TCBTST  ANOP                                                           00300000
         AIF   ('&EXIT'(1,1) EQ '(').EXREG2                  @G860P1C   00301000
         DC    AL4(&EXIT)              FOUR BYTE EXIT ADDR   @G860P1C   00302000
         AGO   .EXEND2                                       @G860P1C   00303000
.EXREG2  DC    AL4(0)                  SPACE FOR EXIT ADDR   @G860P1C   00304000
.EXEND2  ANOP                                                @G860P1C   00305000
         AIF   ('&TOKEN' EQ '').STTCB                        @G81CP2F   00306000
         AIF   ('&TOKEN'(1,1) EQ '(').TOKREG2                @G81CP2F   00307000
         AIF   (T'&TOKEN EQ 'N').SELFDEF                     @G81CP2F   00308000
         AIF   ('&TYPE' NE 'OV').STTCB                       @G81CP2F   00309000
         L     0,&TOKEN                LOAD TOKEN VALUE      @G81CP2F   00310000
         ST    0,&LNAME+4              PUT TOKEN IN LIST     @G81CP2F   00311000
         AGO   .STTCB                                        @G81CP2F   00312000
.TOKREG2 ANOP                                                @G81CP2F   00313000
         AIF   ('&TYPE' NE 'OV').STTCB                       @G81CP2F   00314000
         ST    &TOKEN(1),&LNAME+4      PUT TOKEN IN LIST     @G81CP2F   00315000
.STTCB   AIF   ('&EXIT'(1,1) NE '(').CHKPARM                            00316000
         ST    &EXIT(1),&LNAME+16      PUT EXIT ADDR IN LIST @G860P1C   00317000
.CHKPARM ANOP                                                           00318000
         AIF   ('&PARAM'(1,1) NE '(').OPTTST                            00319000
         ST    &PARAM(1),&LNAME+8      PUT PARAM ADDR. IN LIST          00320000
         AIF   ('&SYSASCE' EQ 'P').OPTTST                          @L1A 00321000
         STAM  &PARAM(1),&PARAM(1),&LNAME+12 SAVE ALET             @L1A 00322000
.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*        00323000
.*                                                                      00324000
.*       COMMON TO E ROUTE AND I ROUTE                                  00325000
.*                                                                      00326000
.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*        00327000
.OPTTST  ANOP                                                           00328000
.SKBR    AIF   (('&TYPE' EQ '') OR ('&TYPE' EQ 'CT')).CREATE            00329000
         AIF   ('&TYPE' EQ 'OV').OVRLAY                                 00330000
.BADOPT  IHBERMAC  49,,&TYPE           INVALID OPTION-STD. FORM         00331000
         MEXIT                                                          00332000
.CREATE  ANOP                                                           00333000
         LA    0,0(0,0)                CREATE & PARMLST EQ 0       @L1C 00334000
         AGO   .PC                                                      00335000
.OVRLAY  LA    0,4(0,0)                OVERLAY & PARMLST EQ 0      @L1C 00336000
.PC      ANOP                                                           00337000
         AIF   ('&XCTL' NE 'YES' AND '&XCTL' NE 'NO').BADXCTL           00338000
.PC1     ANOP                                                           00339000
         L     14,16(0,0)              GET CVT ADDRESS                  00340000
         L     14,772(14,0)            GET SFT ADDRESS                  00341000
         L     14,176(14,0)            GET LX/EX                        00342000
         PC    0(14)                   ISSUE PC                         00343000
         AGO   .TOKBACK                                      @G81CP2F   00344000
.TOKBACK ANOP                                                @G81CP2F   00345000
         AIF   ('&EXIT' EQ '0' OR '&TYPE' EQ 'OV').END       @G81CP2F   00346000
         AIF   ('&TOKEN' EQ '').END                          @G81CP2F   00347000
         AIF   ('&TOKEN'(1,1) EQ '(').TOKRET                 @G81CP2F   00348000
         ST    0,&TOKEN                RETURN TOKEN VALUE    @G81CP2F   00349000
         MEXIT                                               @G81CP2F   00350000
.TOKRET  LR    &TOKEN(1),0             RETURN TOKEN VALUE    @G81CP2F   00351000
.END     MEXIT                                                          00352000
.BADXCTL MNOTE 8,'***     XCTL=&XCTL - PARAMETER INVALID'               00353000
         MEXIT                                                          00354000
.LISTERR IHBERMAC 69                    REG NOTE W/MF=L - INVALID       00355000
         MEXIT                                                          00356000
.ERRASY  ANOP                                                           00357000
         IHBERMAC  54,,&ASYNCH                                          00358000
         MEXIT                                                          00359000
.PURERR  ANOP                                                           00360000
         IHBERMAC  54,,&PURGE                                           00361000
         MEXIT                                                          00362000
.TERMERR ANOP                                                           00363000
         IHBERMAC 54,,&TERM                                             00364000
         MEXIT                                                          00365000
.RECERR  ANOP                                                           00366000
         IHBERMAC  54,,&RECORD                                          00367000
         MEXIT                                                          00368000
.NCNLERR ANOP                                                     @D1A  00369000
         IHBERMAC  54,,&CANCEL                                    @D1A  00370000
         MEXIT                                                    @D1A  00371000
.ERRXCTL ANOP                                                  @D61ADTN 00372000
         IHBERMAC 1309,XCTL            INVALID FOR VSE/ESA     @D61ADTN 00373000
         MEXIT                                                 @D61ADTN 00374000
.ERRPURG ANOP                                                  @D61ADTN 00375000
         IHBERMAC 1310,PURGE,QUIESCE|HALT INVALID IN VSE/ESA   @D64ADUL 00376490
         MEXIT                                                 @D61ADTN 00377000
.ERRTKNP ANOP                                                  @D61ADTN 00378000
         IHBERMAC 1309,TKNPASS         INVALID FOR VSE/ESA     @D61ADTN 00379000
         MEXIT                                                 @D61ADTN 00380000
         MEND                                                           00381000
*                                                           @G860P38 */ 00382000
* %ESTAEX: MACRO KEYS(PARAM,XCTL,MF,PURGE,ASYNCH,TERM,RECORD,           00383000
*                 RETCODE,RELATED,TOKEN,TKNPASS,CANCEL);     /* @D1C */ 00384000
* ANS('?'||MACLABEL||'ESTAEXP '||MACLIST||MACKEYS||';'); /* @G860P38 */ 00385000
* %END;                                                  /* @G860P38 */ 00386000
