         MACRO                                                          00050000
&NAME    DUMODFO &ERREXT=,&ERROPT=,&RDONLY=,&SEPASMB=                   00100000
         LCLC  &X,&EE,&RZ,&NAM                                          00150000
.* IBM DISK OPERATING SYSTEM                                            00200000
* DISKETTE IOCS - DUMODFO - 5686-007-02-C440                   @U31TUJS 00250000
*                                                              @U31TUJS 00300000
*    THIS MACRO IS "RESTRICTED MATERIALS OF IBM"               @U31TUJS 00350000
*    5686-007 (C) COPYRIGHT IBM CORP. 1977,1989                @U31TUJS 00400000
*    ALL RIGHTS RESERVED                                       @U31TUJS 00450000
*    LICENSED MATERIALS - PROPERTY OF IBM                      @U31TUJS 00500000
*    SEE COPYRIGHT INSTRUCTIONS, G120-2083                     @U31TUJS 00550000
*                                                              @U31TUJS 00600000
&X       SETC  '&SYSNDX'(3,2)                                           00650000
&EE      SETC  'Z'                                                      00700000
&RZ      SETC  'Z'                                                      00750000
         AIF   (T'&ERROPT NE 'O').OM010                                 00800000
         AIF   (T'&ERREXT EQ 'O').OM030                                 00850000
         MNOTE 0,'ERROPT OMITTED. ERREXT IGNORED'                       00900000
         AGO   .OM030                                                   00950000
.OM010   AIF   ('&ERROPT' EQ 'YES').OM020                               01000000
         MNOTE 0,'ERROPT INVALID. ''YES'' ASSUMED'                      01050000
.OM020   ANOP                                                           01100000
&EE      SETC  'E'                                                      01150000
         AIF   (T'&ERREXT EQ 'O').OM030                                 01200000
&EE      SETC  'C'                                                      01250000
         AIF   ('&ERREXT' EQ 'YES').OM030                               01300000
         MNOTE 0,'ERREXT INVALID. ''YES'' ASSUMED'                      01350000
.OM030   AIF   (T'&RDONLY EQ 'O').OM040                                 01400000
&RZ      SETC  'Y'                                                      01450000
         AIF   ('&RDONLY' EQ 'YES').OM040                               01500000
         MNOTE 0,'RDONLY INVALID. ''YES'' ASSUMED'                      01550000
.OM040   AIF   (T'&SEPASMB EQ 'O' OR '&SEPASMB' EQ 'YES').OM050         01600000
         MNOTE 0,'SEPASMB INVALID. ''YES'' ASSUMED'                     01650000
.OM050   AIF   (T'&NAME EQ 'O').OM060                                   01700000
&NAM     SETC  '&NAME'                                                  01750000
         AGO   .OM070                                                   01800000
.OM060   ANOP                                                           01850000
&NAM     SETC  'IJNDO&EE.Z&RZ'                                          01900000
.OM070   AIF   (T'&SEPASMB EQ 'O').OM080                                01950000
         PUNCH '    CATALR &NAM,35.0'                          @DM16800 02000000
.OM080   ANOP                                                           02050000
IJNDUD&X DSECT                                                          02100000
IJNCCB&X DS    CL21                CCB,LOGIC MODULE ADDRESS             02150000
IJNCM1&X EQU   IJNCCB&X+2          COMMN BYTE 1                         02200000
IJNCM2&X EQU   IJNCCB&X+3          COMMN BYTE 2                         02250000
IJNSTS&X EQU   IJNCCB&X+4          CSW STATUS                           02300000
IJNCCW&X EQU   IJNCCB&X+8          CURRENT CCW ADDRESS                  02350000
IJNCSW&X EQU   IJNCCB&X+12         CSW CCW ADDRESS                      02400000
IJNOCI&X DS    CL17                OPEN/CLOSE INDICATORS                02450000
IJNCB1&X DS    CL1                 OPEN COMMN BYTE 1                    02500000
IJNCB2&X DS    CL5                 OPEN COMMN BYTE 2                    02550000
IJNIOA&X DS    CL4                 ADDRESS OF IOAREA                    02600000
IJNLRW&X DS    CL4                 ADDR OF LAST RD/WT CCW               02650000
IJNLRL&X DS    CL2                 LOWER RECORD LIMIT                   02700000
IJNEDA&X DS    CL4                 END-OF-DATA SEEK ADDRESS             02750000
IJNNOR&X DS    CL2                 NUM OF RECORDS READ                  02800000
IJNCDA&X DS    CL5                 CURRENT DISKETTE ADDRESS             02850000
IJNEOF&X DS    CL3                 EOF ROUTINE ADDRESS                  02900000
IJNSCF&X DS    CL4                 SEEK ARGUMENT CONTROL FIELD          02950000
IJNCCF&X DS    CL1                 COMMAND CHAINING FACTOR              03000000
IJNSW1&X DS    CL1                 SWITCH BYTE 1                        03050000
IJNRCF&X DS    CL2                 RECSIZE*(CMDCHN FACTOR)-1            03100000
IJNSAB&X DS    CL5                 SEEK ARGUMENT BUCKET                 03150000
IJNSW2&X DS    CL3                 SWITCH BYTE 2                        03200000
IJNLDR&X DS    CL4                 LOAD USERS I/O REG                   03250000
IJNCIA&X DS    CL4                 CURRENT IOAREA ADDRESS               03300000
IJNLRS&X DS    CL4                 LOGICAL RECORD SIZE                  03350000
IJNEIA&X DS    CL4                 END OF IO AREA ADDRESS               03400000
IJNLI1&X DS    CL8                 LOGICAL INDICATORS 1                 03450000
IJNERA&X EQU   IJNLI1&X            USERS ERROR ROUTINE ADDRESS          03500000
IJNLI2&X DS    CL4                 LOGICAL INDICATORS 2                 03550000
IJNFDC&X DS    CL8                 FEED CCW                             03600000
IJNDOP&X DS    CL8                 DEFINE OPS CCW                       03650000
IJNSKC&X DS    CL8                 SEEK CCW                             03700000
IJNTIC&X DS    CL8                 TRANSFER IN CHANNEL CCW              03750000
IJNRWD&X DS    CL8                 START OF READ/WRITE CCWS             03800000
&NAM     CSECT                                                          03850000
         AIF   (T'&NAME NE 'O').OM100                                   03900000
         AIF   ('&EE' NE 'C').OM090                                     03950000
         ENTRY IJNDOEZ&RZ,IJNDOZZ&RZ                                    04000000
IJNDOEZ&RZ EQU   *                                                      04050000
IJNDOZZ&RZ EQU   *                                                      04100000
         AGO   .OM100                                                   04150000
.OM090   AIF   ('&EE' NE 'E').OM100                                     04200000
         ENTRY IJNDOZZ&RZ                                               04250000
IJNDOZZ&RZ EQU   *                                                      04300000
.OM100   ANOP                                                           04350000
         USING IJNDUD&X,IJNDTF&X                                        04400000
         USING *,IJNBAS&X                                               04450000
         B     IJNERR&X            ERROR-NO CONTROL ROUTINE             04500000
         B     IJNERR&X            ERROR-NO RELSE ROUTINE               04550000
         B     IJNERR&X            ERROR-NO GET ROUTINE                 04600000
         B     IJNPUT&X            GO TO PUT ROUTINE                    04650000
         B     IJNCLS&X            GO TO CLOSE ROUTINE                  04700000
IJNERR&X SVC   50                  ERROR-NO TRUNCATION                  04750000
         DC    H'0'                HALFWORD OF ZERO                     04800000
         B     IJNEDC&X            ENTRY FOR NEW EXTENT DURING CLOSE    04850000
         DC    CL8'&NAM'           MODULE NAME                          04900000
         DC    X'3440'             VERSION-MODIFICATION LEVEL @D41TDKY  04950000
         AIF   (T'&RDONLY NE 'O').OM110                                 05000000
IJNPUT&X STM   IJNRG9&X,IJNRGE&X,IJNSV&X SAVE USER'S AND LINK REGS      05050000
         AGO   .OM120                                                   05100000
.OM110   ANOP                                                           05150000
IJNPUT&X STM   IJNRG8&X,IJNRGE&X,0(IJNRGD&X) SAVE REGS 8-14             05200000
         LR    IJNRG8&X,IJNRGD&X   SAVE SAVEAREA POINTER                05250000
.OM120   ANOP                                                           05300000
         BAL   IJNRG9&X,IJNPC3&X   CHECK FOR BUFFER SWITCH     @DA13198 05350000
         TM    IJNOCI&X,IJNX10&X   WORKAREA SPECIFIED                   05400000
         BZ    IJNBLK&X            NO, GO TO 'BLOCK' RECORDS            05450000
         L     IJNRGD&X,IJNCIA&X   SET UP 'TO' ADDRESS                  05500000
         L     IJNRGC&X,IJNLRS&X   LOAD RECORD LENGTH                   05550000
         BCTR  IJNRGC&X,IJNRG0&X   SET UP LENGTH TO MOVE                05600000
         LR    IJNRGB&X,IJNRG0&X   SET UP 'FROM' ADDRESS                05650000
         EX    IJNRGC&X,IJNFOA&X   MOVE RECORD TO OUTPUT AREA           05700000
IJNBLK&X LM    IJNRGB&X,IJNRGD&X,IJNCIA&X LOAD 'BLOCKING' CONSTANTS     05750000
         BXH   IJNRGB&X,IJNRGC&X,IJN2IO&X TEST FOR FULL BLOCK           05800000
         ST    IJNRGB&X,IJNCIA&X   STORE NEW RECORD ADDRESS             05850000
         AIF   (T'&RDONLY NE 'O').OM130                                 05900000
IJNLIO&X LM    IJNRG9&X,IJNRGE&X,IJNSV&X RESTORE USER AND LINK REGS     05950000
         AGO   .OM140                                                   06000000
.OM130   ANOP                                                           06050000
IJNLIO&X LM    IJNRG8&X,IJNRGE&X,0(IJNRG8&X) RESTORE USER REGS 8-14     06100000
.OM140   ANOP                                                           06150000
         EX    0,IJNLDR&X          LD I/O REG WITH ADDRS FOR RECORD     06200000
         BR    IJNRGE&X            RETURN TO MAINLINE PROGRAM           06250000
IJN2IO&X TM    IJNLI1&X,IJNX08&X   DUAL I/O AREAS                       06300000
         BO    IJNDIO&X            YES, BRANCH TO GO TO WAIT & ERR TEST 06350000
         CLC   IJNCDA&X.(4),IJNEDA&X AT END-OF-DATA                     06400000
         BL    IJNWRT&X            NO, BRANCH TO ISSUE WRITE            06450000
         BAL   IJNRGA&X,IJNEXT&X   YES, BAL TO GET NEW EXTENT           06500000
         B     IJNWRT&X            BRANCH TO ISSUE WRITE                06550000
IJNDIO&X BAL   IJNRGB&X,IJNWTE&X   BAL TO WAIT AND ERROR TEST           06600000
         CLC   IJNCDA&X.(4),IJNEDA&X AT END OF EXTENT                   06650000
         BL    IJNEXC&X            NO, GO TO EXCHANGE I/O AREA ADDRS'S  06700000
         BAL   IJNRGA&X,IJNEXT&X   YES, BAL TO GET NEW EXTENT           06750000
IJNEXC&X TM    IJNSW1&X,IJNX80&X   'FIRST ENTRY AFTER OPEN'             06800000
         BZ    IJNWRT&X            YES, BR AROUND ADDRS EXCHANGE        06850000
IJNPC1&X DS    0H                                              @DA13198 06900000
         LA    IJNRGD&X,IJNRWD&X   LA OF 1ST CCW IN CHAIN               06950000
         L     IJNRGB&X,IJNIOA&X   LA OF START OF NEXT I/O AREA         07000000
         MVC   IJNIOA&X+1(3),IJNRWD&X+1 FLIP/FLOP I/O AREA ADDRESSES    07050000
         SR    IJNRGC&X,IJNRGC&X   CLEAR REGISTER                       07100000
         IC    IJNRGC&X,IJNCCF&X   PICK UP # OF CCWS IN CHAIN           07150000
         B     IJNTP1&X            BRANCH AROUND ADDS, 1ST TIME         07200000
IJNLP1&X AH    IJNRGD&X,IJNH08&X   UPDATE ADDRESS OF CCW                07250000
         A     IJNRGB&X,IJNLRS&X   UPDATE DATA ADDRESS IN CCW           07300000
IJNTP1&X STCM  IJNRGB&X,7,1(IJNRGD&X) ST NEW DATA ADDRS IN PROPER CCW   07350000
         BCT   IJNRGC&X,IJNLP1&X   DONE SETTING UP CCW DATA ADDRESSES   07400000
         TM    IJNSW1&X,IJNX40&X   FIRST ENTRY AFTER OPEN      @DA13198 07450000
         BZ    IJNPC4&X            YES, DO NOT ISSUE WRITE YET @DA13198 07500000
IJNWRT&X SVC   0                   ISSUE WRITE                          07550000
         L     IJNRGB&X,IJNIOA&X   LOAD CURRENT DATA ADDRESS            07600000
         LA    IJNRGB&X,0(IJNRGB&X) CLEAR HI-ORDER BYTE                 07650000
         ST    IJNRGB&X,IJNCIA&X   STORE CURRENT DATA ADDRESS           07700000
         AH    IJNRGB&X,IJNRCF&X   ADD 'BLKSIZE' -1                     07750000
         ST    IJNRGB&X,IJNEIA&X   SETUP END-OF-IOAREA ADDRESS          07800000
         OI    IJNSW1&X,IJNX80&X   SET TO 'NOT 1ST ENTRY AFTER OPEN'    07850000
         TM    IJNLI1&X,IJNX08&X   DUAL I/O AREAS                       07900000
         BO    IJNTPB&X            YES, GO TO TEST PARTIAL BLOCK SW     07950000
         BAL   IJNRGB&X,IJNWTE&X   BAL TO WAIT AND ERROR TEST           08000000
IJNTPB&X TM    IJNSW1&X,IJNX20&X   'PARTIAL BLOCK' SWITCH ON            08050000
         BZ    IJNLIO&X            NO, BRANCH TO RETURN TO USER         08100000
         BR    IJNRG9&X            YES, RETURN TO CLOSE ROUTINE         08150000
IJNWTE&X TM    IJNCM1&X,IJNX80&X   WAIT BIT POSTED                      08200000
         BO    IJNUSA&X            YES, BRANCH AROUND SVC TO WAIT       08250000
         SVC   7                   WAIT                                 08300000
IJNUSA&X TM    IJNSW1&X,IJNX80&X   'FIRST ENTRY AFTER OPEN'             08350000
         BZ    IJNDON&X            YES, BR ARND UPDATING SEEK ADDRS     08400000
         MVC   IJNSAB&X.(4),IJNCDA&X SAVE CURRENT SEEK ADDRS            08450000
         SR    IJNRGC&X,IJNRGC&X   CLEAR REGISTER                       08500000
         IC    IJNRGC&X,IJNCCF&X   PICK UP COMMAND CHAINING FACTOR      08550000
         AH    IJNRGC&X,IJNCDA&X+2 ADD IN CURRENT RECORD NUMBER         08600000
         CH    IJNRGC&X,IJNSCF&X+2 RECORD NO. GT LIMIT                  08650000
         BNH   IJNOK1&X            NO, STORE IT                         08700000
         LH    IJNRGC&X,IJNLRL&X   LOAD LOWER RECORD LIMIT              08750000
         LH    IJNRGD&X,IJNCDA&X   PICK-UP CYLINDER NO.                 08800000
         AH    IJNRGD&X,IJNH01&X   ADD ONE TO CYLINDER                  08850000
         STH   IJNRGD&X,IJNCDA&X   STORE CYLINDER NUMBER                08900000
IJNOK1&X STH   IJNRGC&X,IJNCDA&X+2 STORE RECORD NUMBER                  08950000
         AIF   ('&ERROPT' NE '').OM200                                  09000000
IJNDON&X BR    IJNRGB&X            RETURN TO PROPER PT. IN L. M.        09050000
         AGO   .OM500                                                   09100000
.OM200   ANOP                                                           09150000
IJNDON&X TM    IJNCM2&X,IJNX10&X   TEST FOR DATA CHECK                  09200000
         AIF   ('&ERREXT' NE '').OM210                                  09250000
         BCR   8,IJNRGB&X          NO, RETURN TO PROPER PT IN L. M.     09300000
         AGO   .OM220                                                   09350000
.OM210   BZ    IJNUIO&X            NOT DATA CHECK, TEST UNREC I/O       09400000
         NI    IJNCM1&X,IJNXDF&X   RESET UNREC I/O ERROR BIT            09450000
         B     IJNOPT&X            DETERMINE ERROPT OPTION              09500000
IJNUIO&X TM    IJNCM1&X,IJNX20&X   TEST UNREC I/O ERROR BIT             09550000
         BCR   8,IJNRGB&X          NO, RETURN TO PROPER PT IN L. M.     09600000
.OM220   ANOP                                                           09650000
IJNOPT&X TM    IJNLI1&X,IJNX80&X   ERROPT=NAME SPECIFIED                09700000
         BCR   8,IJNRGB&X          NO, RETURN TO PROPER PT IN L. M.     09750000
         L     IJNRGE&X,IJNERA&X   LOAD ADDRS OF ERROR ROUTINE          09800000
         AIF   (T'&RDONLY NE 'O').OM300                                 09850000
         STM   IJNRG9&X,IJNRGD&X,IJNRUS&X SAVE MODULE REGS              09900000
         LM    IJNRG9&X,IJNRGD&X,IJNSV&X RESTORE USER REGS              09950000
         STM   IJNRGF&X,IJNRG1&X,IJNRTR&X SAVE TABLE, MODULE REGS       10000000
         AIF   ('&ERREXT' NE '').OM230                                  10050000
         L     IJNRG1&X,IJNRWD&X   LOAD I/O AREA ADDRESS                10100000
         AGO   .OM240                                                   10150000
.OM230   MVC   IJNRTR&X+12(4),IJNRWD&X MOVE IOAREA ADDRESS TO USER LIST 10200000
         MVI   IJNRTR&X+12,IJNX00&X INSURE 1ST BYTE OF ADDRS IS ZERO    10250000
         LA    IJNRG1&X,IJNRTR&X+8 LA OF USER LIST INTO REG1            10300000
.OM240   CNOP  0,4                                                      10350000
         BALR  IJNRGE&X,IJNRGE&X   BRANCH TO USERS ERROR ROUTINE        10400000
         AIF   ('&ERREXT' NE '').OM250                                  10450000
         LM    IJNRGF&X,IJNRG1&X,14(IJNRGE&X) RESTORE TABLE, MOD REGS   10500000
         STM   IJNRG9&X,IJNRGD&X,IJNSV&X SAVE USER REGS                 10550000
         LM    IJNRG9&X,IJNRGD&X,IJNRUS&X RESTORE MODULE REGS           10600000
         BR    IJNRGB&X            RETURN TO PROPER PT IN L. M.         10650000
IJNRTR&X DC    4F'0'               SAVE AREA                            10700000
         AGO   .OM500                                                   10750000
.OM250   B     12(IJNRGE&X)        RETURN FOR SKIP                      10800000
         B     12(IJNRGE&X)        RETURN FOR IGNORE                    10850000
         B     42(IJNRGE&X)        RETURN FOR RETRY                     10900000
.* ABOVE DISPLACEMENTS MAY VARY IF NO. OF INSTRUCTIONS BELOW VARIES     10950000
         LM    IJNRGF&X,IJNRG1&X,26(IJNRGE&X) RESTORE TABLE, MOD REGS   11000000
         STM   IJNRG9&X,IJNRGD&X,IJNSV&X SAVE USER REGS                 11050000
         LM    IJNRG9&X,IJNRGD&X,IJNRUS&X RESTORE MODULE REGS           11100000
         BR    IJNRGB&X            RETURN TO PROPER PT IN L. M.         11150000
IJNRTR&X DC    4F'0'               SAVE AREA                            11200000
         LM    IJNRGF&X,IJNRG1&X,26(IJNRGE&X) LOAD TABLE, MOD REGS      11250000
         STM   IJNRG9&X,IJNRGD&X,IJNSV&X SAVE USER REGS                 11300000
         LM    IJNRG9&X,IJNRGD&X,IJNRUS&X RESTORE MODULE REGS           11350000
         AGO   .OM400                                                   11400000
.OM300   ANOP                                                           11450000
         STM   IJNRG9&X,IJNRGD&X,44(IJNRG8&X) SAVE MODULE REGS          11500000
         LM    IJNRG8&X,IJNRGD&X,0(IJNRG8&X) RELOAD USER REGS           11550000
         STM   IJNRGF&X,IJNRG1&X,28(IJNRGD&X) SAVE TABLE, MOD REGS      11600000
         AIF   ('&ERREXT' NE '').OM310                                  11650000
         L     IJNRG1&X,IJNRWD&X   LOAD I/O AREA ADDRESS                11700000
         AGO   .OM320                                                   11750000
.OM310   MVC   40(4,IJNRGD&X),IJNRWD&X MOVE I/O AREA ADDRS TO USER LIST 11800000
         MVI   40(IJNRGD&X),IJNX00&X INSURE 1ST BYTE OF ADDRS IS ZERO   11850000
         LA    IJNRG1&X,36(IJNRGD&X) LA OF USER LIST INTO REG1          11900000
.OM320   ANOP                                                           11950000
         BALR  IJNRGE&X,IJNRGE&X   BRANCH TO USER ERROR ROUTINE         12000000
         AIF   ('&ERREXT' NE '').OM330                                  12050000
         STM   IJNRG8&X,IJNRGD&X,0(IJNRGD&X) SAVE USER REGS             12100000
         LR    IJNRG8&X,IJNRGD&X   SAVE SAVEAREA POINTER                12150000
         LM    IJNRG9&X,IJNRGD&X,44(IJNRGD&X) RELOAD                    12200000
         LM    IJNRGF&X,IJNRG1&X,28(IJNRG8&X) RESTORE TABLE, MOD REGS   12250000
         BR    IJNRGB&X            RETURN TO PROPER PT IN L. M.         12300000
         AGO   .OM500                                                   12350000
.OM330   ANOP                                                           12400000
         LA    IJNRGE&X,0(IJNRGE&X) RETURN FOR SKIP                     12450000
         LA    IJNRGE&X,4(IJNRGE&X) RETURN FOR IGNORE                   12500000
         LA    IJNRGE&X,28(IJNRGE&X) RETURN FOR RETRY                   12550000
.* ABOVE DISPLACEMENTS MAY VARY IF NO. OF INSTRUCTIONS BELOW VARIES     12600000
         STM   IJNRG8&X,IJNRGD&X,0(IJNRGD&X) SAVE USER REGS             12650000
         LR    IJNRG8&X,IJNRGD&X   SAVE SAVEAREA POINTER                12700000
         LM    IJNRG9&X,IJNRGD&X,44(IJNRGD&X) RESTORE MODULE REGS       12750000
         LM    IJNRGF&X,IJNRG1&X,28(IJNRG8&X) RESTORE TABLE, MOD REGS   12800000
         BR    IJNRGE&X            BRANCH TO PROPER ROUTINE             12850000
         B     IJNRTY&X            BRANCH TO RETRY ROUTINE              12900000
         BR    IJNRGB&X            IGNORE ERROR, RETURN TO L. M.        12950000
IJNRTY&X EQU   *                                                        13000000
.OM400   ANOP                                                           13050000
         NI    IJNCM1&X,IJNXE7&X   SET TO CNCL ON UNR I/O & D.CK.ERRS   13100000
         OI    IJNLI2&X,IJNX04&X   SET 'C6' WRITTEN BIT                 13150000
         LA    IJNRGD&X,IJNSAB&X   LA OF PROPER SEARCH ADDRESS          13200000
         STCM  IJNRGD&X,7,IJNSKC&X+1 ST ADDRS OF SEARCH ADDRS IN SEEK   13250000
         XC    IJNRWD&X.(16),IJNC6W&X EXCHANGE 1ST 2 WRITE CCWS         13300000
         XC    IJNC6W&X.(16),IJNRWD&X   WITH CCWS TO WRITE A            13350000
         XC    IJNRWD&X.(16),IJNC6W&X   'C6' CONTROL RECORD             13400000
         LH    IJNRGD&X,IJNSAB&X+2 PICK UP CURRENT SECTOR #             13450000
         MVI   IJNLI1&X+4,IJNX01&X SET DEF OPS TO SINGLE WRITE          13500000
         SR    IJNRGC&X,IJNRGC&X   CLEAR REGISTER                       13550000
         IC    IJNRGC&X,IJNCCF&X   PICK UP CMDCHN FAC. FOR LOOP CNTRL   13600000
IJNC6L&X SVC   0                   WRITE 'C6' CONTROL RECORD            13650000
         TM    IJNCM1&X,IJNX80&X   WAIT BIT POSTED                      13700000
         BO    IJNARN&X            YES, BRANCH AROUND WAIT              13750000
         SVC   7                   WAIT                                 13800000
IJNARN&X LA    IJNRGD&X,1(IJNRGD&X) ADD 1 TO SECTOR NUMBER              13850000
         STH   IJNRGD&X,IJNSAB&X+2 SETUP DISKETTE ADRS FOR NEXT WRITE   13900000
         BCT   IJNRGC&X,IJNC6L&X   THRU WRITING FULL 'BLOCK' OF C6'S    13950000
         LA    IJNRGD&X,IJNCDA&X   LA OF CURRENT SEARCH ADDRS           14000000
         STCM  IJNRGD&X,7,IJNSKC&X+1 ST ADDRS OF SEARCH ADDRS IN SEEK   14050000
         MVC   IJNLI1&X+4(1),IJNCCF&X RESET DEF OPS TO CC FACTOR        14100000
         XC    IJNRWD&X.(16),IJNC6W&X EXCHANGE CCWS TO WRITE 'C6'       14150000
         XC    IJNC6W&X.(16),IJNRWD&X   CONTROL RECORD WITH             14200000
         XC    IJNRWD&X.(16),IJNC6W&X   1ST 2 WRITE CCWS                14250000
         OI    IJNCM1&X,IJNX18&X   SET TO ACCEPT UNREC I/O & D.CK. ERRS 14300000
         CLC   IJNCDA&X.(4),IJNEDA&X ROOM TO RE-WRITE THIS 'BLOCK'      14350000
         BL    IJNREW&X            YES, BRANCH TO RE-WRITE 'BLOCK'      14400000
         BAL   IJNRGA&X,IJNEXT&X   BAL TO GET NEW EXTENT                14450000
IJNREW&X SVC   0                   RE-WRITE 'BLOCK'                     14500000
         B     IJNWTE&X            BRANCH TO WAIT FOR WRITE COMPLETION  14550000
.OM500   ANOP                                                           14600000
IJNPC3&X DS    0H                                              @DA13198 14650000
         TM    IJNSW1&X,IJNX40&X   FIRST ENTRY AFTER OPEN      @DA13198 14700000
         BO    IJNPC4&X            NO, SKIP BUFFER CHECK       @DA13198 14750000
         TM    IJNLI1&X,IJNX08&X   DOUBLE BUFERING USED        @DA13198 14800000
         BNO   IJNPC4&X            NO, DON'T SWITCH BUFFER     @DA13198 14850000
         CLC   IJNCIA&X+1(3),IJNRWD&X+1 CCW ADDR = CUR I/O ADDR@DA13198 14900000
         BNE   IJNPC1&X            NO, GO SWITCH BUFFERS       @DA13198 14950000
IJNPC4&X DS    0H                                              @DA13198 15000000
         OI    IJNSW1&X,IJNX40&X   SET NOT FIRST ENTRY SWITCH  @DA13198 15050000
         BR    IJNRG9&X                                        @DA13198 15100000
IJNEXT&X EQU   *                                                        15150000
         AIF   ('&ERROPT' EQ '').OM510                                  15200000
         AIF   ('&ERREXT' EQ '').OM510                                  15250000
         TM    IJNLI2&X,IJNX04&X   C6'S WRITTEN BIT ON                  15300000
         BZ    IJNTEF&X            NO, BRANCH TO TEST IF EOF            15350000
         LA    IJNRGC&X,IJNEMP&X   LA OF CCW CHAIN TO UPDATE ERRMAP     15400000
         ST    IJNRGC&X,IJNCCW&X   STORE IN CCB                         15450000
         NI    IJNCM1&X,IJNXE7&X   SET TO CANCEL ON UNR I/O & D.CK ERRS 15500000
         SVC   0                   READ ERRMAP SECTOR                   15550000
         TM    IJNCM1&X,IJNX80&X   WAIT BIT POSTED                      15600000
         BO    IJNDN1&X            YES, BRANCH AROUND SVC 7 TO WAIT     15650000
         SVC   7                   WAIT                                 15700000
IJNDN1&X MVI   IJNERC&X+1,IJNX40&X SET ALLOWED OPS TO WRITE IN DEF OPS  15750000
         MVI   IJNEMP&X+16,IJNX05&X SET READ CMD TO WRITE CMD           15800000
         MVI   IJNEMR&X+22,IJNXC4&X SET C6'S WRITTEN ON VOL IN ERMAP    15850000
         SVC   0                   WRITE ERRMAP SECTOR                  15900000
         TM    IJNCM1&X,IJNX80&X   WAIT BIT POSTED                      15950000
         BO    IJNDN2&X            YES, BRANCH AROUND WAIT              16000000
         SVC   7                   WAIT                                 16050000
IJNDN2&X MVI   IJNERC&X+1,IJNX80&X SET ALLOWED OPS TO READ IN DEF OPS   16100000
         MVI   IJNEMP&X+16,IJNX06&X RESET CMD CODE TO READ              16150000
         LA    IJNRGC&X,IJNDOP&X   LA OF REGULAR CCW CHAIN              16200000
         ST    IJNRGC&X,IJNCCW&X   STORE IN CCB                         16250000
         OI    IJNCM1&X,IJNX18&X   SET TO ACCEPT UNREC I/O & D.CK ERRS  16300000
.OM510   ANOP                                                           16350000
IJNTEF&X TM    IJNSW1&X,IJNX08&X   EOF SWITCH ON                        16400000
         BZ    IJNNME&X            NO, BRANCH TO TEST NO MORE EXTENTS   16450000
         AIF   (T'&RDONLY NE 'O').OM513                                 16500000
         LM    IJNRG9&X,IJNRGE&X,IJNSV&X RESTORE USER AND LINK REGS     16550000
         AGO   .OM516                                                   16600000
.OM513   LM    IJNRG8&X,IJNRGE&X,0(IJNRG8&X) RESTORE USER REGS 8-14     16650000
.OM516   MVI   IJNSW1&X,IJNX00&X   SET OFF MODULE SWITCHES              16700000
         B     IJNSC9&X            BRANCH TO SVC 9 EXIT                 16750000
IJNNME&X TM    IJNCB1&X,IJNX80&X   'NO MORE EXTENTS' BIT ON             16800000
         BO    IJNPBW&X            YES, BR TO TEST PARTIAL BLOCK SW     16850000
         LA    IJNRGC&X,IJNFDC&X   POINT TO FEED CCW                    16900000
         ST    IJNRGC&X,IJNCCW&X   SET UP CCB CCW ADDRESS               16950000
         SVC   0                   ISSUE FEED                           17000000
         TM    IJNCM1&X,IJNX80&X   WAIT BIT POSTED                      17050000
         BO    IJNDN3&X            YES, BRANCH AROUND WAIT              17100000
         SVC   7                   WAIT                                 17150000
IJNDN3&X LA    IJNRGC&X,IJNDOP&X   POINT TO NORMAL CCW CHAIN            17200000
         ST    IJNRGC&X,IJNCCW&X   SET UP CCB CCW ADDRESS               17250000
IJNPBW&X TM    IJNSW1&X,IJNX20&X   'PARTIAL BLOCK' SWITCH ON            17300000
         BZ    IJNSC2&X            NO, BRANCH TO SAVE REGS & SVC 2      17350000
IJNSC9&X SVC   9                   RETURN TO CLOSE MODULE               17400000
         AIF   (T'&RDONLY NE 'O').OM520                                 17450000
IJNSC2&X STM   IJNRG0&X,IJNRG1&X,IJNSVE&X SAVE TABLE,WORK REGS          17500000
         STM   IJNRG9&X,IJNRGD&X,IJNRUS&X SAVE MODULE REGS              17550000
         LM    IJNRG9&X,IJNRGD&X,IJNSV&X RESTORE USER REGS              17600000
         LA    IJNRG0&X,IJNSVE&X+4 LA OF DTF ADDRESS                    17650000
         LA    IJNRG1&X,IJNNEX&X   LA OF NAME OF MODULE TO CALL         17700000
         SVC   2                   FETCH NEW EXTENT                     17750000
         LM    IJNRG0&X,IJNRG1&X,IJNSVE&X RESTORE TABLE, WORK REGS      17800000
         LM    IJNRG9&X,IJNRGD&X,IJNRUS&X RESTORE MODULE REGS           17850000
         AGO   .OM530                                                   17900000
.OM520   ANOP                                                           17950000
IJNSC2&X STM   IJNRG0&X,IJNRG1&X,32(IJNRG8&X) SAVE MODULE REGS          18000000
         STM   IJNRG9&X,IJNRGD&X,44(IJNRG8&X) SAVE MODULE REGS          18050000
         MVI   40(IJNRG8&X),IJNXFF&X SET PARM LIST END INDICATOR        18100000
         LM    IJNRG8&X,IJNRGD&X,0(IJNRG8&X) RELOAD USER REGS           18150000
         LA    IJNRG0&X,36(IJNRGD&X) LA OF DTF ADDRESS                  18200000
         LA    IJNRG1&X,IJNNEX&X   LA OF NAME OF MODULE TO CALL         18250000
         SVC   2                   FETCH NEW EXTENT                     18300000
         STM   IJNRG8&X,IJNRGD&X,0(IJNRGD&X) SAVE USER REGS             18350000
         LM    IJNRG0&X,IJNRG1&X,32(IJNRGD&X) RELOAD MODULE REGS        18400000
         LR    IJNRG8&X,IJNRGD&X   SAVE SAVEAREA POINTER                18450000
         LM    IJNRG9&X,IJNRGD&X,44(IJNRG8&X) RELOAD MODULE REGS        18500000
.OM530   ANOP                                                           18550000
IJNEDC&X EQU   *                                                        18600000
         MVC   IJNSAB&X.(4),IJNCDA&X SAVE CURRENT DATA ADDRESS          18650000
         BR    IJNRGA&X            RETURN TO PROPER PT. IN L. M.        18700000
         AIF   (T'&RDONLY NE 'O').OM550                                 18750000
IJNCLS&X STM   IJNRG9&X,IJNRGE&X,IJNSV&X SAVE USER REGISTERS            18800000
         AGO   .OM560                                                   18850000
.OM550   ANOP                                                           18900000
IJNCLS&X STM   IJNRG8&X,IJNRGE&X,0(IJNRGD&X)                            18950000
         LR    IJNRG8&X,IJNRGD&X   SAVE SAVEAREA POINTER                19000000
.OM560   ANOP                                                           19050000
         BAL   IJNRG9&X,IJNPC3&X   GO CHECK FOR BUFFER SWITCH  @DA13198 19100000
         OI    IJNSW1&X,IJNX20&X   SET ON PARTIAL BLOCK SWITCH          19150000
         TM    IJNLI1&X,IJNX08&X   DUAL I/O AREAS                       19200000
         BZ    IJNDTW&X            NO, DETERMINE IF HAVE DATA TO WRITE  19250000
         BAL   IJNRGB&X,IJNWTE&X   BAL TO WAIT AND ERROR TEST           19300000
IJNDTW&X L     IJNRGB&X,IJNCIA&X   LOAD CURRENT I/O AREA ADDRESS        19350000
         AH    IJNRGB&X,IJNRCF&X   ADD IN 'BLKSIZE-1'                   19400000
         S     IJNRGB&X,IJNEIA&X   I/O AREA EMPTY                       19450000
         BZ    IJNOUT&X            YES, BRANCH TO GET OUT               19500000
         CLC   IJNCDA&X.(4),IJNEDA&X AT END OF EXTENT                   19550000
         BL    IJNNRC&X            NO, GO FIND # OF RECORDS IN BLOCK    19600000
         BAL   IJNRGA&X,IJNEXT&X   YES, BRANCH TO GET AN EXTENT         19650000
IJNNRC&X SR    IJNRGA&X,IJNRGA&X   CLEAR REG FOR DIVIDE                 19700000
         D     IJNRGA&X,IJNLRS&X   FIND # OF RECORDS IN BLOCK           19750000
         IC    IJNRGA&X,IJNCCF&X   PICK UP CMDCHN FACTOR                19800000
         CR    IJNRGA&X,IJNRGB&X   'FULL BLOCK' TO BE WRITTEN           19850000
         BE    IJNWIT&X            YES, BRANCH TO WRITE IT              19900000
         STC   IJNRGB&X,IJNLI1&X+4 SET UP DEF.OPS FOR SHORT BLOCK       19950000
         LA    IJNRGA&X,IJNRWD&X   LA OF IST WRITE CCW                  20000000
         SLL   IJNRGB&X,3          DETERMINE NEW END OF CHAIN           20050000
         AR    IJNRGA&X,IJNRGB&X     ADDRESS (NEW NOP CCW)              20100000
         NI    4(IJNRGA&X),IJNXBF&X SET OFF CHAIN FLAG IN CCW           20150000
         MVI   0(IJNRGA&X),IJNX03&X SET CMD CODE TO A NOP CCW           20200000
         ST    IJNRGA&X,IJNLRW&X   SAVE NEW END OF CHAIN ADDRS          20250000
IJNWIT&X BAL   IJNRG9&X,IJNEXC&X   BAL TO WRITE LAST BLOCK              20300000
         TM    IJNLI1&X,IJNX08&X   DUAL I/O AREAS                       20350000
         BZ    IJNSNG&X            NO, SINGLE, DON'T WAIT               20400000
         BAL   IJNRGB&X,IJNWTE&X   YES, BAL TO WAIT AND ERROR TEST      20450000
IJNSNG&X CLC   IJNCCF&X.(1),IJNLI1&X+4 WAS THIS A FULL BLOCK            20500000
         BE    IJNOUT&X            YES, BRANCH TO GET OUT               20550000
         L     IJNRGA&X,IJNLRW&X   PICK UP END OF CHAIN ADDRS           20600000
         OI    4(IJNRGA&X),IJNX40&X RE-SET ON CMD CHAIN FLAG            20650000
         MVI   0(IJNRGA&X),IJNX05&X SET CMD CODE TO A WRITE             20700000
         SR    IJNRGA&X,IJNRGA&X   CLEAR REGISTER                       20750000
         IC    IJNRGA&X,IJNLI1&X+4 PICK UP # OF RECS IN SHORT BLOCK     20800000
         MVC   IJNLI1&X+4(1),IJNCCF&X RESET DEF OPS CCW CNT             20850000
         A     IJNRGA&X,IJNSAB&X   SET UP END-OF-DATA ADDRS             20900000
         ST    IJNRGA&X,IJNCDA&X   STORE END-OF-DATA-ADDRS              20950000
IJNOUT&X OI    IJNSW1&X,IJNX08&X   SET ON END-OF-FILE SWITCH            21000000
         NI    IJNSW1&X,IJNXBF&X   SET NOT FIRST ENTRY SW OFF  @DA13198 21050000
         B     IJNEXT&X            BRANCH TO FINISH PROCESSING          21100000
IJNFOA&X MVC   0(1,IJNRGD&X),0(IJNRGB&X) MOVE WA TO IOAREA              21150000
         AIF   (T'&RDONLY NE 'O').OM570                                 21200000
IJNRUS&X DC    5F'0'               MODULE REGS 9-13 SAVE AREA           21250000
IJNSV&X  DC    6F'0'               USER REGS 9-14 SAVE AREA             21300000
IJNSVE&X DC    2F'0'               DTF AND WORK REGS SAVE AREA          21350000
.OM570   ANOP                                                           21400000
IJNNEX&X DC    CL8'$$BOPEN '       MODULE TO CALL FOR NEW EXTENT        21450000
         AIF   ('&ERROPT' EQ '').OM580                                  21500000
         AIF   ('&ERREXT' EQ '').OM580                                  21550000
IJNERC&X DC    X'01800000'         ERRMAP DEF OPS CNTRL INFO            21600000
IJNEMS&X DC    X'00000005'         ERRMAP SEEK ADDRS                    21650000
IJNEMP&X CCW   47,IJNERC&X,X'60',4                                      21700000
         CCW   7,IJNEMS&X,X'60',4                                       21750000
         CCW   6,IJNEMR&X,X'60',80                                      21800000
         CCW   3,*,0,1                                                  21850000
IJNEMR&X DC    CL80' '                                                  21900000
IJNC6W&X CCW   33,IJNC6R&X,X'60',1                             @DM05059 21950000
         CCW   3,*,0,1             NOP                                  22000000
IJNC6R&X DC    X'C6'                                                    22050000
.OM580   ANOP                                                           22100000
IJNH08&X DC    H'8'                HALFWORD CONSTANT OF 8               22150000
IJNH01&X DC    H'1'                HALFWORD CONSTANT OF 1               22200000
IJNDTF&X EQU   1                   DTF TABLE BASE REG                   22250000
IJNBAS&X EQU   15                  LOGIC MOD. BASE REG                  22300000
IJNRG0&X EQU   0                   WORK REG (0)                         22350000
IJNRG1&X EQU   1                   DTF AND PARM LIST REG (1)            22400000
IJNRG8&X EQU   8                   SAVE SAVEAREA PTR REG (8) (RDONLY)   22450000
IJNRG9&X EQU   9                   WORK REG (9)                         22500000
IJNRGA&X EQU   10                  WORK REG (10)                        22550000
IJNRGB&X EQU   11                  WORK REG (11)                        22600000
IJNRGC&X EQU   12                  WORK REG (12)                        22650000
IJNRGD&X EQU   13                  WORK REG (13)                        22700000
IJNRGE&X EQU   14                  LINKAGE REG (14)                     22750000
IJNRGF&X EQU   15                  MODULE BASE REG (15)                 22800000
IJNX00&X EQU   X'00'               NO BITS ON MASK                      22850000
IJNX01&X EQU   X'01'               BIT 7 MASK                           22900000
IJNX02&X EQU   X'02'               BIT 6 MASK                           22950000
IJNX03&X EQU   X'03'               BITS 6-7 MASK                        23000000
IJNX04&X EQU   X'04'               BIT 5 MASK                           23050000
IJNX05&X EQU   X'05'               BITS 5 AND 7 MASK                    23100000
IJNX06&X EQU   X'06'               BITS 5-6 MASK                        23150000
IJNX08&X EQU   X'08'               BIT 4 MASK                           23200000
IJNX10&X EQU   X'10'               BIT 3 MASK                           23250000
IJNX18&X EQU   X'18'               BITS 3-4 MASK                        23300000
IJNX20&X EQU   X'20'               BIT 2 MASK                           23350000
IJNX40&X EQU   X'40'               BIT 1 MASK                           23400000
IJNX80&X EQU   X'80'               BIT 0 MASK                           23450000
IJNXBF&X EQU   X'BF'               SET OFF BIT 1 MASK                   23500000
IJNXC4&X EQU   X'C4'               DEFECT FLAG, ERMAP RECORD            23550000
IJNXDF&X EQU   X'DF'               SET OFF BIT 2 MASK                   23600000
IJNXE7&X EQU   X'E7'               SET OFF BITS 3-4 MASK                23650000
IJNXEF&X EQU   X'EF'               SET OFF BIT 3 MASK                   23700000
IJNXF7&X EQU   X'F7'               SET OFF BIT 4 MASK                   23750000
IJNXFF&X EQU   X'FF'               PARM LIST END IND                    23800000
         AIF   (T'&SEPASMB NE 'O').DONE                                 23850000
&SYSECT  CSECT                                                          23900000
.DONE    MEXIT                                                          23950000
         MEND                                                           24000000
