         MACRO                                                 @DL29ZCJ 00050000
&LABEL   ILOAD &PNAM,&ORIDE,&LIST=,&SYS=NO,&DE=NO,&TXT=YES,            *00100000
               &MFG=,&RET=NO                                   @D62ADWS 00150000
.*                                                                      00200000
         GBLB  &LDB,&LDB1,&LDB2                                @D14CDBC 00250000
         LCLA  &D,&DISP,&OFFS1,&TPN,&TOR,&TLI,&TMF,&DIRL,&DIRH @D36JDVB 00300000
         LCLB  &SY,&DY,&TN,&I1,&BNLIB,&BRET                    @D14CDBC 00350000
         LCLC  &PHASE,&AREA,&IPN,&IOR,&ILI,&R1,&TARGET,&DIRID  @D14CDVB 00400000
&OFFS1   SETA  4                                               @D35XDGS 00450000
&DISP    SETA  8                                               @D35XDGS 00500000
&DIRL    SETA  38                                              @D36JDVB 00550000
&R1      SETC  '1'                                             @D37ZDVB 00600000
&DIRID   SETC  '000000'                                        @D14CDVB 00650000
.*-------------------------------------------------------------@D368DVB 00700000
.*                                                             @D368DVB 00750000
.*       IBM DISK OPERATING SYSTEM                             @D368DVB 00800000
*        SUPERVISOR - ILOAD -  5686-007-06-C44                 @D62ADWS 00850000
         AGO   .ZZZ                                            @D368DVB 00900000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"           @D31UDMZ 00950000
*        5686-007 (C) COPYRIGHT IBM CORP 1981,1988             @D31UDMZ 01000000
*        ALL RIGHTS RESERVED                                   @D31UDMZ 01050000
*        LICENSED MATERIALS-PROPERTY OF IBM                    @D368DVB 01100000
.*       SEE COPYRIGHT INSTRUCTIONS, G120-2083                 @D31UDMZ 01150000
.*                                                             @D368DVB 01200000
.ZZZ     ANOP                                                  @D368DVB 01250000
.*-------------------------------------------------------------@D368DVB 01300000
.*                                                                      01350000
.* /* START OF SPECIFICATIONS ***************************************** 01400000
.*                                                                      01450000
.*01* MODULE-TYPE = SUPERVISOR GENERATION AND USER INTERFACE MACRO      01500000
.*                                                                      01550000
.*01* DECRIPTIVE NAME = LOAD MACRO                                      01600000
.*                                                                      01650000
.*01* NOTES = CHANGE ACTIVITY                                           01700000
.*    SUPPORT OF REENTRANT PROGRAMMING                         @D35XDGS 01750000
.*    SUPPORT OF CONCATENATED LIBRARIES                        @D36JDVB 01800000
.**** END OF SPECIFICATIONS ******************************************* 01850000
         AIF  ('&LABEL' EQ '').NOLAB                           @D35XDGS 01900000
&LABEL   DC    0H'0'                                           @D35XDGS 01950000
.NOLAB   ANOP                                                  @D35XDGS 02000000
.*                                                             @D35XDGS 02050000
.*       PHASE NAME OPERAND EVALUATION ----------------------- @D35XDGS 02100000
.*                                                             @D35XDGS 02150000
         AIF   (T'&PNAM NE 'O').PNAV                           @D35XDGS 02200000
      MNOTE 8,'** PHASENAME OPERAND MISSING, GENERATION SUPPRESSED **'  02250000
         AGO   .MTXT                                           @D14CDVB 02300000
.PNAV    AIF   ('&PNAM'(1,1) EQ '(').PNDYN                     @D35XDGS 02350000
&IPN     SETC  '&PNAM'                                         @D35XDGS 02400000
         AIF   (K'&PNAM LE 8).PN0                              @D35XDGS 02450000
         MNOTE 3,'** PHASE NAME TRUNCATED TO 8 CHARACTERS **'  @D35XDGS 02500000
&IPN     SETC  '&PNAM'(1,8)                                    @D35XDGS 02550000
.PN0     ANOP                                                  @D35XDGS 02600000
&TPN     SETA  1              1= A-TYPE                        @D35XDGS 02650000
&DISP    SETA  &DISP+8                                         @D35XDGS 02700000
         AGO   .PN1                                            @D35XDGS 02750000
.PNDYN   AIF   ('&PNAM'(1,3) EQ '(S,').PNSTYP                  @D35XDGS 02800000
&IPN     SETC  '&PNAM'(2,K'&PNAM-2)                            @D35XDGS 02850000
&TPN     SETA  2              2= REGISTER                      @D35XDGS 02900000
&OFFS1   SETA  &OFFS1+4       INCREMENT INSTRUCTION OFFSET     @D35XDGS 02950000
.*                            ( LA  ASSUMED )                  @D35XDGS 03000000
         AGO   .PN1                                            @D35XDGS 03050000
.PNSTYP  ANOP                                                  @D35XDGS 03100000
&IPN     SETC  '&PNAM'(4,K'&PNAM-4)                            @D35XDGS 03150000
&TPN     SETA  4              4= S-TYPE                        @D35XDGS 03200000
&OFFS1   SETA  &OFFS1+8       INCREMENT INSTRUCTION OFFSET     @D35XDGS 03250000
.*                            ( LA , ST  ASSUMED )             @D35XDGS 03300000
.PN1     ANOP                                                  @D35XDGS 03350000
.*                                                             @D35XDGS 03400000
.*       ENTRY POINT OVERRIDE OPERAND EVALUATION ------------- @D35XDGS 03450000
.*                                                             @D35XDGS 03500000
         AIF   (T'&ORIDE EQ 'O').OR1                           @D35XDGS 03550000
.ORAV    AIF   ('&ORIDE'(1,1) EQ '(').ORDYN                    @D35XDGS 03600000
&IOR     SETC  '&ORIDE'                                        @D35XDGS 03650000
&TOR     SETA  1              1= A-TYPE                        @D35XDGS 03700000
         AGO   .OR1                                            @D35XDGS 03750000
.ORDYN   AIF   ('&ORIDE'(1,3) EQ '(S,').ORSTYP                 @D35XDGS 03800000
&IOR     SETC  '&ORIDE'(2,K'&ORIDE-2)                          @D35XDGS 03850000
&TOR     SETA  2              2= REGISTER                      @D35XDGS 03900000
         AGO   .OR1                                            @D35XDGS 03950000
.ORSTYP  ANOP                                                  @D35XDGS 04000000
&IOR     SETC  '&ORIDE'(4,K'&ORIDE-4)                          @D35XDGS 04050000
&TOR     SETA  4              4= S-TYPE                        @D35XDGS 04100000
.OR1     ANOP                                                  @D35XDGS 04150000
.*                                                             @D35XDGS 04200000
.*       LIST POINTER OPERAND EVALUATION --------------------- @D35XDGS 04250000
.*                                                             @D35XDGS 04300000
         AIF   (T'&LIST EQ 'O').LI1                            @D35XDGS 04350000
.LIAV    AIF   ('&LIST'(1,1) EQ '(').LIDYN                     @D35XDGS 04400000
&ILI     SETC  '&LIST'                                         @D35XDGS 04450000
&TLI     SETA  1              1= A-TYPE                        @D35XDGS 04500000
         AGO   .LI1                                            @D35XDGS 04550000
.LIDYN   AIF   ('&LIST'(1,3) EQ '(S,').LISTYP                  @D35XDGS 04600000
&ILI     SETC  '&LIST'(2,K'&LIST-2)                            @D35XDGS 04650000
&TLI     SETA  2              2= REGISTER                      @D35XDGS 04700000
&OFFS1   SETA  &OFFS1+4       INCREMENT INSTRUCTION OFFSET     @D35XDGS 04750000
.*                            ( LA  ASSUMED )                  @D35XDGS 04800000
         AGO   .LI1                                            @D35XDGS 04850000
.LISTYP  ANOP                                                  @D35XDGS 04900000
&ILI     SETC  '&LIST'(4,K'&LIST-4)                            @D35XDGS 04950000
&TLI     SETA  4              4= S-TYPE                        @D35XDGS 05000000
&OFFS1   SETA  &OFFS1+8       INCREMENT INSTRUCTION OFFSET     @D35XDGS 05050000
.*                            ( LA , STCM  ASSUMED )           @D35XDGS 05100000
.LI1     ANOP                                                  @D35XDGS 05150000
.*                                                             @D35XDGS 05200000
.*       TXT OPTION EVALUATION ------------------------------- @D35XDGS 05250000
.*                                                             @D35XDGS 05300000
&TN      SETB  ('&TXT' EQ 'NO')        NO TEXT                 @DL29ZCJ 05350000
         AIF   (&TN OR '&TXT' EQ 'YES').DEFDY    BR IF VALID   @DL29ZCJ 05400000
         MNOTE 3,'** TXT PARAM MUST BE NO OR YES - YES ASSUMED **'      05450000
.DEFDY   ANOP                                                  @DL29ZCJ 05500000
.*                                                             @D35XDGS 05550000
.*       DE OPTION EVALUATION -------------------------------- @D35XDGS 05600000
.*                                                             @D35XDGS 05650000
&DY      SETB  ('&DE' EQ 'YES' OR '&DE'(1,3) EQ 'VSE')         @D14CDVB 05700000
&BNLIB   SETB  ('&DE'(1,3) EQ 'VSE')                           @D14CDVB 05750000
         AIF   (&DY OR '&DE' EQ 'NO').DEFSY      BR IF VALID   @DL29ZCJ 05800000
         MNOTE 3,'** DE TERM MUST BE NO, YES OR VSE(-FORM) --- NO ASSUM*05850000
               ED **'                                          @D14CDVB 05900000
         AIF   (&LDB1 OR &LDB ).DEFSY                          @D14CDVB 05950000
&LDB1    SETB  1                                               @D14CDVB 06000000
         SPACE 1                                               @D14CDVB 06050000
*---------------------------------------------------------------------* 06100000
* ---> NOTES TO  D E  PARAMETER:                                      * 06150000
*                                                                     * 06200000
*        DE=YES|VSE(-FORM): DIRECTORY ENTRY PROVIDED                  * 06250000
*            = VSE:   THE DIR_ENTRY HAS  V S E  (- N L I B) FORMAT    * 06300000
*                                                                     * 06350000
*            0   PHASENAME   CL8                                      * 06400000
*            8   DIRVSEID    XL3'FF'     ID FOR NEW FORMAT            * 06450000
*           11   DIRN        XL1'0E'     LENGTH OF DE                 * 06500000
*           12   DIRLMBR     F           LENGTH OF TXT IN BYTES       * 06550000
*           16   DIRC        XL1         FLAGS                        * 06600000
*           17               XL3         RESERVED                     * 06650000
*           20   DIRACOPY    A           INTEGRITY INFORMATION        * 06700000
*           24   DIRALPT     A           LOADPOINT AT LINKEDT TIME    * 06750000
*           28   DIRAEPT     A           ENTRYPOINT AT LINKEDT TIME   * 06800000
*           32   DIRAPART    A           BEG PARTITION                * 06850000
*           36   DIRAVSA     A           ENTRYPOINT IN SVA            * 06900000
*                                                                     * 06950000
*            = YES:   THE DIR_ENTRY HAS  OLD DE FORMAT                * 07000000
*                                                                     * 07050000
*            0   PHASENAME   CL8                                      * 07100000
*            8   DIRCOPY     XL3'0'      INTEGRITY INFORMATION        * 07150000
*           11   DIRN        XL1         LENGTH OF DE                 * 07200000
*           12   DIRTT       H           NUMBER OF 1K LIBRARY BLOCKS  * 07250000
*           14   DIRLL       H           TXT_LENGTH IN BYTES IN LAST  * 07300000
*                                                    TXT BLOCK        * 07350000
*           16   DIRC        XL1         FLAGS                        * 07400000
*           17               XL1         RESERVED                     * 07450000
*           18   DIRPPP      AL3         LOADPOINT AT LINKEDT TIME    * 07500000
*           21   DIREEE      AL3         ENTRYPOINT AT LINKEDT TIME   * 07550000
*           24               XL3         *** NOT SUPPORTED ***        * 07600000
*           26   DIRAAA      AL3         PARTITION START ADDR         * 07650000
*           30   DIRASVA     A           ENTRYPOINT IN SVA            * 07700000
*           34               A           *** NOT SUPPORTED ***        * 07750000
*                                                                     * 07800000
*    --->THE 'DIRC' FLAGS CAN BE USED TO TEST THE STATE OF THE PHASE  * 07850000
*                X'80'        SELFRELOCATABLE                         * 07900000
*                X'40'        RELOCATABLE                             * 07950000
*                X'20'        SVA - ELIGIBLE                          * 08000000
*                             ..( A SVA_PHASE IS RELOCATABLE AND SVA  * 08050000
*                                 - ELIGIBLE )                        * 08100000
*                X'10'        PHASE LOCATED IN SVA                    * 08150000
*                X'01'        SLMAP AVAILABLE                         * 08200000
*    ---> MOREOVER, 'DIRC' FLAGS REFLECT  THE STATE OF THE IN_CORE_   * 08250000
*         DIR_ENTRY                                                   * 08300000
*                X'08'        NON_SYSLIB ENTRY                        * 08350000
*                X'04'        ENTRY NOT FOUND                         * 08400000
*                X'02'        ENTRY ACTIVE   ( FETCH/LOAD SERVICE     * 08450000
*                                   HAS ALREADY PROCESSED DIR_ENTRY ) * 08500000
*    ---> N O T E :  THE 'DIRC' FLAGS ARE BITS AND SHOULD BE TESTED   * 08550000
*                    BY 'TM' - ASSEMBLER INSTRUCTIONS                 * 08600000
*    ---> N O T E :  'DIRC' MUST BE INITIALIZED AS X'00'              * 08650000
*                    'DIRN' INDICATES THE NUMBER OF HALFWORDS AFTER   * 08700000
*                           'DIRN' TO BE PROVIDED BY THE FETCH SERVICE* 08750000
*                           E.G    X'02' <= 'DIRN' =< X'0B'           * 08800000
*                           'DIRN' = X'00' ... NO RETURN_INFO PROVIDED* 08850000
*                                                                     * 08900000
*---------------------------------------------------------------------* 08950000
         SPACE 1                                               @D14CDVB 09000000
.DEFSY   ANOP                                                  @DL29ZCJ 09050000
         AIF   (NOT &BNLIB).DEFSY1                             @D14CDVB 09100000
&DIRID   SETC  'FFFFFF'                                        @D14CDVB 09150000
&DIRL    SETA  40                                              @D14CDVB 09200000
.DEFSY1  ANOP                                                  @D14CDVB 09250000
.*                                                             @D35XDGS 09300000
.*       SYS OPTION EVALUATION ------------------------------- @D35XDGS 09350000
.*                                                             @D35XDGS 09400000
&SY      SETB  ('&SYS' EQ 'YES')       SYS LIST FIRST PARAM    @DL29ZCJ 09450000
         AIF   (&SY OR '&SYS' EQ 'NO').DEFRET    BR IF VALID   @D14CDBC 09500000
         MNOTE 3,'** SYS PARAM MUST BE NO OR YES - NO ASSUMED **'       09550000
.DEFRET  ANOP                                                  @D14CDBC 09600000
.*                                                             @D14CDBC 09650000
.*       RET (RETURNE CODE) OPTION EVALUATION  --------------- @D14CDBC 09700000
.*                                                             @D14CDBC 09750000
&BRET    SETB  ('&RET' EQ 'YES')           RET PARAMETER       @D14CDBC 09800000
         AIF   (&BRET OR '&RET' EQ 'NO').DEF9    BR IF VALID   @D14CDBC 09850000
         MNOTE 3,'** RET PARAM MUST BE NO OR YES - NO ASSUMED **'       09900000
         AIF   (&LDB2).DEF9                                    @D14CDBC 09950000
&LDB2    SETB  1                                               @D14CDBC 10000000
         SPACE 1                                               @D14CDBC 10050000
*---------------------------------------------------------------------* 10100000
* ---> NOTES TO  RET  PARAMETER:                                      * 10150000
*                                                                     * 10200000
*        DE=YES|NO      NO ASSUMED                                    * 10250000
*        RET:     =YES  THE FOLLOWING RETURNE CODES ARE GIVEN IN R15:BC 10300000
*                     0 :    OK                                      BC 10350000
*                     4 :    PHASE NOT FOUND (CANCEL EXIT X'22')     BC 10400000
*                     8 :    FETCH I/O ERROR (CANCEL EXIT X'2B')     BC 10450000
*                    12 :    INVALID LIB. STRUC. (CANCEL EXIT X'29') BC 10500000
*                    16 :    INVALID ADDRESS (CANCEL EXIT X'25')     BC 10550000
*                    20 :    SECURITY VIOLATION (CANCEL EXIT X'0B')  BC 10600000
*                    24 :    INCONSISTENT USER DIRECTORY STATE       BC 10650000
*                             (FETCH WAS NOT ABLE TO USE THE         BC 10700000
*                              PROVIDED DE                           BC 10750000
*                                       A N D                        BC 10800000
*                              FOUND AN INCONSISTENCY BETWEEN        BC 10850000
*                              USER DE AND LIBRARY DE.               BC 10900000
*                              FOR THIS CASE THE USER DE IS          BC 10950000
*                              OVERWRITTEN BY THE LIBRARY DE.        BC 11000000
*                              THE PHASE IS NOT LOADED.              BC 11050000
*                              THE CHECKT VALUES ARE :               BC 11100000
*                                LENGTH OF PHASE, LOAD POINT,        BC 11150000
*                                ENTRY POINT, PARTITION START,       BC 11200000
*                                RELOCATION STATE.  )                BC 11250000
*---------------------------------------------------------------------* 11300000
.*                                                             @D35XDGS 11350000
.DEF9    AIF   (NOT &SY AND NOT &DY AND NOT &TN                        *11400000
                            AND NOT &BRET AND &TLI EQ 0).MF1   2D14CDBC 11450000
.*                                                             @D35XDGS 11500000
.*       MFG OPERAND EVALUATION ------------------------------ @D35XDGS 11550000
.*                                                             @D35XDGS 11600000
         AIF   (T'&MFG NE 'O').MFAV                            @D35XDGS 11650000
&AREA    SETC  '*'                                             @D35XDGS 11700000
&D       SETA  4                                               @D35XDGS 11750000
         AGO   .MF1                                            @D35XDGS 11800000
.MFAV    ANOP                                                  @D35XDGS 11850000
&D       SETA  0                                               @D35XDGS 11900000
&OFFS1   SETA  &OFFS1+10                                       @D35XDGS 11950000
         AIF   ('&MFG'(1,1) EQ '(').MFDYN                      @D35XDGS 12000000
&AREA    SETC  '&MFG'                                          @D35XDGS 12050000
&TMF     SETA  1              1= A-TYPE                        @D35XDGS 12100000
         AGO   .MF1                                            @D35XDGS 12150000
.MFDYN   AIF   ('&MFG'(1,3) EQ '(S,').MFSTYP                   @D35XDGS 12200000
&AREA    SETC  '0'.'&MFG'                                      @D35XDGS 12250000
&TMF     SETA  2              2= REGISTER                      @D35XDGS 12300000
         AGO   .MF1                                            @D35XDGS 12350000
.MFSTYP  ANOP                                                  @D35XDGS 12400000
&AREA    SETC  '&MFG'(4,K'&MFG-4)                              @D35XDGS 12450000
&TMF     SETA  4              4= S-TYPE                        @D35XDGS 12500000
.MF1     ANOP                                                  @D35XDGS 12550000
.*                                                             @D35XDGS 12600000
.*       OPERAND COMPATIBILITY TEST -------------------------- @D35XDGS 12650000
.*                                                             @D35XDGS 12700000
.DEF10   AIF   (NOT &DY OR &TLI EQ 0).DEF20                    @D35XDGS 12750000
         MNOTE 3,'** DE=NO ASSUMED SINCE LIST SPECIFIED **'    @D35XDGS 12800000
&DY      SETB  0                  ASSUME NO DIRECTORY ELEMENT  @DL29ZCJ 12850000
.DEF20   AIF   (&TPN NE 2 OR &TOR NE 2).DEF2A                  @D35XDGS 12900000
         AIF   ('&PNAM(1)' EQ '&ORIDE(1)').ER4   = REGS, ERROR @DL29ZCJ 12950000
.DEF2A   AIF   (&TPN NE 2 OR &TLI NE 2).DEF2B                  @DL29ZCJ 13000000
         AIF   ('&PNAM(1)' EQ '&LIST(1)').ER4    = REGS, ERROR @DL29ZCJ 13050000
.DEF2B   AIF   (&TOR NE 2 OR &TLI NE 2).DEF2C                  @DL29ZCJ 13100000
         AIF   ('&ORIDE(1)' NE '&LIST(1)').DEF2C               @D35XDGS 13150000
.ER4     MNOTE 8,'** 2 PARAMETERS USE SAME REGISTER, GENERATION SUPPRES*13200000
               SED **'                                         @D149DVB 13250000
.MTXT    ANOP                                                  @D14CDVB 13300000
         AIF   (&LDB).MEXIT                                    @D14CDVB 13350000
&LDB     SETB  1                                               @D14CDVB 13400000
         SPACE 1                                               @D14CDVB 13450000
*---------------------------------------------------------------------* 13500000
*  NOTES TO LOAD MACRO:                                               * 13550000
*                                                                     * 13600000
*  --->  THE SYNTAX OF THE LOAD MACRO IS AS FOLLOWS:                  * 13650000
*                                                                     * 13700000
*   LABEL LOAD PHASENAME|(R1)               ... MANDATORY             * 13750000
*              <,LOADADDRESS|(R0)>          ..                        * 13800000
*              <,DE=NO|YES|VSE(-FORM)>       ..                       * 13850000
*              <,LIST=LISTADDRESS|(R2)>        ..                     * 13900000
*              <,TXT=YES|NO>                    ... OPTIONAL          * 13950000
*              <,SYS=NO|YES>                   ..                     * 14000000
*              <,MFG=ADDRESS|(S,ADDRESS)|(R3)>..                      * 14050000
*              <,RET=NO|YES>                     ..                  BC 14100000
*                                                                     * 14150000
*  --->  PHASENAME: 8 BYTES ALPHAMERIC PHASENAME                      * 14200000
*        LOADADDRESS: ADDRESS WHERE THE PHASE SHOULD BE LOCATED BY THE* 14250000
*                     LOAD SERVICE                                    * 14300000
*        DE=YES|VSE(-FORM): DIRECTORY ENTRY PROVIDED                  * 14350000
*                     'YES' DIR_ENTRY HAS  CONVENTIONAL FORMAT        * 14400000
*                     'VSE' DIR_ENTRY HAS  N L I B - FORMAT           * 14450000
*        LIST:   A LOCAL DIRECTORY LIST HAS BEEN SPECIFIED, USUALLY   * 14500000
*                     GENERATED BY THE  G E N L  MACRO                * 14550000
*        TXT:    SELFEXPLANATORY                                      * 14600000
*        SYS:    SELFEXPLANATORY                                      * 14650000
*        MFG:    ADDRESS OF A STORAGE AREA PROVIDED FOR A REENTRANT   * 14700000
*                     MACRO EXPANSION                                 * 14750000
*        RET:     =YES  THE FOLLOWING RETURNE CODES ARE GIVEN IN R15:BC 14800000
*                     0 :    OK                                      BC 14850000
*                     4 :    PHASE NOT FOUND (CANCEL EXIT X'22')     BC 14900000
*                     8 :    FETCH I/O ERROR (CANCEL EXIT X'2B')     BC 14950000
*                    12 :    INVALID LIB. STRUC. (CANCEL EXIT X'29') BC 15000000
*                    16 :    INVALID ADDRESS (CANCEL EXIT X'25')     BC 15050000
*                    20 :    SECURITY VIOLATION (CANCEL EXIT X'0B')  BC 15100000
*                    24 :    INCONSISTENT USER DIRECTORY STATE       BC 15150000
*                             (FETCH WAS NOT ABLE TO USE THE         BC 15200000
*                              PROVIDED DE                           BC 15250000
*                                       A N D                        BC 15300000
*                              FOUND AN INCONSISTENCY BETWEEN        BC 15350000
*                              USER DE AND LIBRARY DE.               BC 15400000
*                              FOR THIS CASE THE USER DE IS          BC 15450000
*                              OVERWRITTEN BY THE LIBRARY DE.        BC 15500000
*                              THE PHASE IS NOT LOADED.              BC 15550000
*                              THE CHECKT VALUES ARE :               BC 15600000
*                                LENGTH OF PHASE, LOAD POINT,        BC 15650000
*                                ENTRY POINT, PARTITION START,       BC 15700000
*                                RELOCATION STATE.  )                BC 15750000
*                                                                     * 15800000
*  --->  DE AND LIST CANNOT BE USED IN THE SAME LOAD MACRO            * 15850000
*  --->  R0, R1, R2, R3 MUST BE DIFFERENT REGISTERS. THE MACRO        * 15900000
*                SERVICE DESTROYS THE GENERAL REGISTERS 0 AND 1       * 15950000
*  --->  THE SERVICE IS CANCELLED DUE TO  I N V A L I D  A D D R E S S* 16000000
*                IF THE PARAMETER LIST, THE DIRECTORY ENTRY OR THE    * 16050000
*                LOCAL LIST IS NOT PART OF THE REQUESTOR'S STORAGE    * 16100000
*                OR IF THE PHASE DOES NOT FIT INTO THE STORAGE        * 16150000
*                                                                     * 16200000
*  --->   V S E - DIRECTORY ENTRY FORMAT                              * 16250000
*                                                                     * 16300000
*            0   PHASENAME   CL8                                      * 16350000
*            8   DIRVSEID    XL3'FF'     ID FOR NEW FORMAT            * 16400000
*           11   DIRN        XL1'0E'     LENGTH OF DE                 * 16450000
*           12   DIRLMBR     F           LENGTH OF TXT IN BYTES       * 16500000
*           16   DIRC        XL1         FLAGS                        * 16550000
*           17               XL3         RESERVED                     * 16600000
*           20   DIRACOPY    A           INTEGRITY INFORMATION        * 16650000
*           24   DIRALPT     A           LOADPOINT AT LINKEDT TIME    * 16700000
*           28   DIRAEPT     A           ENTRYPOINT AT LINKEDT TIME   * 16750000
*           32   DIRAPART    A           BEG PARTITION                * 16800000
*           36   DIRAVSA     A           ENTRYPOINT IN SVA            * 16850000
*                                                                     * 16900000
*  --->   CONVENTIONAL DIRECTORY FORMAY                               * 16950000
*                                                                     * 17000000
*            0   PHASENAME   CL8                                      * 17050000
*            8   DIRCOPY     XL3'0'      INTEGRITY INFORMATION        * 17100000
*           11   DIRN        XL1         LENGTH OF DE                 * 17150000
*           12   DIRTT       H           NUMBER OF 1K LIBRARY BLOCKS  * 17200000
*           14   DIRLL       H           TXT_LENGTH IN BYTES IN LAST  * 17250000
*                                                    TXT BLOCK        * 17300000
*           16   DIRC        XL1         FLAGS                        * 17350000
*           17               XL1         RESERVED                     * 17400000
*           18   DIRPPP      AL3         LOADPOINT AT LINKEDT TIME    * 17450000
*           21   DIREEE      AL3         ENTRYPOINT AT LINKEDT TIME   * 17500000
*           24               XL3         *** NOT SUPPORTED ***        * 17550000
*           26   DIRAAA      AL3         PARTITION START ADDR         * 17600000
*           30   DIRASVA     A           ENTRYPOINT IN SVA            * 17650000
*           34               A           *** NOT SUPPORTED ***        * 17700000
*                                                                     * 17750000
*    --->THE 'DIRC' FLAGS CAN BE USED TO TEST THE STATE OF THE PHASE  * 17800000
*                X'80'        SELFRELOCATABLE                         * 17850000
*                X'40'        RELOCATABLE                             * 17900000
*                X'20'        SVA - ELIGIBLE                          * 17950000
*                             ..( A SVA_PHASE IS RELOCATABLE AND SVA  * 18000000
*                                 - ELIGIBLE )                        * 18050000
*                X'10'        PHASE LOCATED IN SVA                    * 18100000
*                X'01'        SLMAP AVAILABLE                         * 18150000
*    ---> MOREOVER, 'DIRC' FLAGS REFLECT  THE STATE OF THE IN_CORE_   * 18200000
*         DIR_ENTRY                                                   * 18250000
*                X'08'        NON_SYSLIB ENTRY                        * 18300000
*                X'04'        ENTRY NOT FOUND                         * 18350000
*                X'02'        ENTRY ACTIVE   ( FETCH/LOAD SERVICE     * 18400000
*                                   HAS ALREADY PROCESSED DIR_ENTRY ) * 18450000
*    ---> N O T E :  THE 'DIRC' FLAGS ARE BITS AND SHOULD BE TESTED   * 18500000
*                    BY 'TM' - ASSEMBLER INSTRUCTIONS                 * 18550000
*    ---> N O T E :  'DIRC' MUST BE INITIALIZED AS X'00'              * 18600000
*                    'DIRN' INDICATES THE NUMBER OF HALFWORDS AFTER   * 18650000
*                           'DIRN' TO BE PROVIDED BY THE FETCH SERVICE* 18700000
*                           E.G    X'02' <= 'DIRN' =< X'0B'           * 18750000
*                           'DIRN' = X'00' ... NO RETURN_INFO PROVIDED* 18800000
*                                                                     * 18850000
*---------------------------------------------------------------------* 18900000
         SPACE 1                                               @D14CDVB 18950000
.MEXIT   MEXIT                                                 @D14CDVB 19000000
.DEF2C   AIF   (&TLI GT 0 OR &BRET OR &SY OR &DY OR &TN).OPT   @D14CDBC 19050000
.*                                                             @D35XDGS 19100000
.*       PARAMETER TRANSFER IN GR 1 AND GR 0 ----------------- @D35XDGS 19150000
.*                                                             @D35XDGS 19200000
         AIF   (&TPN NE 1).DEF31                               @D35XDGS 19250000
         LA    1,=CL8'&PNAM'            POINT TO PHASENAME     @DL29ZCJ 19300000
         AGO   .DEF4                   TO SET UP OVERRIDE      @DL29ZCJ 19350000
.DEF31   AIF   (&TPN NE 2).DEF32                               @D35XDGS 19400000
         AIF   ('&PNAM(1)' EQ '1').DEF4          BR IF REG 1   @D35XDGS 19450000
         LR    1,&PNAM(1)               PHASENAME ADDR TO REG1 @DL29ZCJ 19500000
         AGO   .DEF4                                           @D35XDGS 19550000
.DEF32   AIF   (&TPN NE 4).DEF4                                @D35XDGS 19600000
         LA    1,&PNAM(2)               PHASENAME ADDR TO REG1 @D35XDGS 19650000
         AGO   .DEF4                                           @D35XDGS 19700000
.*                                                             @D35XDGS 19750000
.*       PARAMETER LIST GENERATION                             @D35XDGS 19800000
.*        INSTRUCTION GENERATION ----------------------------- @D35XDGS 19850000
.*                                                             @D35XDGS 19900000
.OPT     AIF   (NOT &DY OR &TPN NE 1).OPT00                    @D35XDGS 19950000
&DISP    SETA  &DISP+4                                         @D35XDGS 20000000
         AIF   (&TMF NE 0).OPT00                               @D35XDGS 20050000
&DISP    SETA  &DISP+&DIRL-12                                  @D36JDVB 20100000
         AGO   .OPT01                                          @D35XDGS 20150000
.OPT00   AIF   (&DISP NE 20).OPT000                            @D35XDGS 20200000
&OFFS1   SETA  &OFFS1+6        ADD LENGTH OF XC INSTRUCTION    @D35XDGS 20250000
.OPT000  AIF   (&TPN NE 1 OR &TMF EQ 0).OPT01                  @D35XDGS 20300000
&I1      SETB  1               TRIGGER INSTRUCTION GENERATION  @D35XDGS 20350000
&OFFS1   SETA  &OFFS1+8        ADD LENGTH OF LA AND ST INTRUCTIONS      20400000
.OPT01   AIF   (&OFFS1/4*4 EQ &OFFS1).FULLW                    @D35XDGS 20450000
.HALFW   CNOP  2,4                      ALIGN TO HALFWORD BDRY @D35XDGS 20500000
         AGO   .OPT0                                           @D35XDGS 20550000
.FULLW   CNOP  0,4                      ALIGN TO FULLWORD BDRY @D35XDGS 20600000
.OPT0    AIF   (&TMF EQ 0).PN10                                @D35XDGS 20650000
&TARGET  SETC  '&AREA'.'('.'&DISP'.')'                         @D35XDGS 20700000
         AIF   (&TMF NE 2).OPT1                                @D35XDGS 20750000
&TARGET  SETC  '0'.'('.'&DISP'.','.'&MFG(1)'.')'               @D35XDGS 20800000
.OPT1    MVC   &TARGET,*+&OFFS1         MOVE PARAMETER LIST    @D35XDGS 20850000
         AIF   (&DISP NE 20).OPT3                              @D35XDGS 20900000
&TARGET  SETC  '&AREA'.'+20(&DIRL-12)'                         @D36JDVB 20950000
         AIF   (&TMF NE 2).OPT2                                @D35XDGS 21000000
&TARGET  SETC  '20'.'(&DIRL-12,'.'&MFG(1)'.')'                 @D36JDVB 21050000
.OPT2    XC    &TARGET,20+&AREA         CLEAR DIRECTORY DATA   @D35XDGS 21100000
.OPT3    AIF   (NOT &I1).OPT4                                  @D35XDGS 21150000
         LA    &R1,8+&AREA              INSERT POINTER         @D35XDGS 21200000
         ST    &R1,&AREA                 TO PHASE NAME         @D35XDGS 21250000
.OPT4    ANOP                                                  @D35XDGS 21300000
&OFFS1   SETA  0                                               @D35XDGS 21350000
.PN10    AIF   (&TPN NE 2).PN20         PNAM OPERAND --------- @D35XDGS 21400000
         STCM  &IPN,B'0111',1+&OFFS1+&AREA SET PHASE NAME ADDR @D357DJB 21450000
         AIF   (&TMF NE 0).PN20                                @D35XDGS 21500000
&OFFS1   SETA  &OFFS1-4                                        @D35XDGS 21550000
.PN20    AIF   (&TPN NE 4).PN30                                @D35XDGS 21600000
         LA    &R1,&IPN                 SET PHASE NAME         @D35XDGS 21650000
         ST    &R1,&OFFS1-&D+&AREA       ADDRESS               @D35XDGS 21700000
         AIF   (&TMF NE 0).PN30                                @D35XDGS 21750000
&OFFS1   SETA  &OFFS1-8                                        @D35XDGS 21800000
.PN30    ANOP                                                  @D35XDGS 21850000
.LI10    AIF   (&TLI NE 2).LI20         LIST OPERAND --------- @D35XDGS 21900000
         STCM  &ILI,B'0111',&OFFS1+5+&AREA  SET LIST ADDRESS   @D35XDGS 21950000
         AIF   (&TMF NE 0).LI20                                @D35XDGS 22000000
&OFFS1   SETA  &OFFS1-4                                        @D35XDGS 22050000
.LI20    AIF   (&TLI NE 4).LI30                                @D35XDGS 22100000
         LA    &R1,&ILI                 SET LIST               @D35XDGS 22150000
         STCM  &R1,B'0111',&OFFS1+5-&D+&AREA  ADDRESS          @D35XDGS 22200000
         AIF   (&TMF NE 0).LI30                                @D35XDGS 22250000
&OFFS1   SETA  &OFFS1-8                                        @D35XDGS 22300000
.LI30    ANOP                                                  @D35XDGS 22350000
         AIF   (&TMF EQ 0).ACONST                              @D35XDGS 22400000
         LA    1,&AREA                  POINT TO PARM LIST     @D35XDGS 22450000
         B     *+4+&DISP                                       @D35XDGS 22500000
.*                                                             @D35XDGS 22550000
.*       ADDRESS CONSTANT GENERATION ------------------------- @D35XDGS 22600000
.*                                                             @D35XDGS 22650000
.ACONST  ANOP                                                  @D35XDGS 22700000
&PHASE   SETC  '&IPN'                                          @D35XDGS 22750000
         AIF   (&TPN EQ 1 AND NOT &I1).AC11                    @D35XDGS 22800000
&IPN     SETC  '0'                                             @D35XDGS 22850000
         AGO   .AC1                                            @D35XDGS 22900000
.AC11    AIF   (&DISP LE 8).AC1                                @D35XDGS 22950000
&IPN     SETC  '8+&AREA'                                       @D35XDGS 23000000
.AC1     AIF   (&TLI EQ 1).AC2                                 @D35XDGS 23050000
&ILI     SETC  '0'                                             @D35XDGS 23100000
.AC2     AIF   (&TMF NE 0).AC3                                 @D35XDGS 23150000
         BAL   1,*+4+&DISP              POINT TO PARM LIST     @D35XDGS 23200000
.AC3     ANOP                                                  @D35XDGS 23250000
         DC    A(&IPN)                  PHASE NAME ADDRESS     @D35XDGS 23300000
         DC    B'&BRET.0000&SY&DY&TN'   OPTIONS                @D14CDBC 23350000
         DC    AL3(&ILI)                LIST ADDRESS           @D35XDGS 23400000
         AIF   (&DISP EQ 8).DEF4                               @D35XDGS 23450000
         DC    CL8'&PHASE'              PHASE NAME             @D35XDGS 23500000
         AIF   (&DISP EQ 16).DEF4                              @D35XDGS 23550000
         DC    XL3'&DIRID'                                     @D14CDVB 23600000
&DIRH    SETA  (&DIRL-12)/2                                    @D36JDVB 23650000
         DC    AL1(&DIRH)               N                      @D36JDVB 23700000
         AIF   (&DISP EQ 20).DEF4                              @D35XDGS 23750000
         DC    XL(&DIRL-12)'00'         DIRECTORY DATA         @D36JDVB 23800000
.*                                                             @D35XDGS 23850000
.*       GR 0 PREPARATION AND SVC GENERATION ----------------- @D35XDGS 23900000
.*                                                             @D35XDGS 23950000
.DEF4    AIF   (&TOR GT 0).DEF41       BR IF OVERRIDE PARAM    @D35XDGS 24000000
         SLR   0,0                      NO OVERRIDE            @D36JDVB 24050000
.DEF41   AIF   (&TOR NE 1).DEF42                               @D35XDGS 24100000
         L     0,=A(&ORIDE)             OVERRIDE ADDR TO REG 0 @D35XDGS 24150000
.DEF42   AIF   (&TOR NE 2).DEF43                               @D35XDGS 24200000
         AIF   ('&IOR' EQ '0').DEFX                            @D35XDGS 24250000
         LR    0,&IOR                   OVERRIDE ADDR TO REG 0 @D35XDGS 24300000
.DEF43   AIF   (&TOR NE 4).DEFX                                @D35XDGS 24350000
         LA    0,&IOR                   OVERRIDE ADDR TO REG 0 @D35XDGS 24400000
.DEFX    SVC   4 .                      EXECUTE LOAD           @DL29ZCJ 24450000
         MEND                                                  @D35XDGS 24500000
