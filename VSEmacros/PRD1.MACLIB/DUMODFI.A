         MACRO                                                          00050000
&NAME    DUMODFI &ERREXT=,&ERROPT=,&RDONLY=,&SEPASMB=                   00100000
         LCLC  &X,&EE,&RZ,&NAM                                          00150000
.* IBM DISK OPERATING SYSTEM                                            00200000
* DISKETTE IOCS - DUMODFI - 5686-007-02-C440                   @U31TUJS 00250000
*                                                              @U31TUJS 00300000
*    THIS MACRO IS "RESTRICTED MATERIALS OF IBM"               @U31TUJS 00350000
*    5686-007 (C) COPYRIGHT IBM CORP. 1977,1989                @U31TUJS 00400000
*    ALL RIGHTS RESERVED                                       @U31TUJS 00450000
*    LICENSED MATERIALS - PROPERTY OF IBM                      @U31TUJS 00500000
*    SEE COPYRIGHT INSTRUCTIONS, G120-2083                     @U31TUJS 00550000
*                                                              @U31TUJS 00600000
&X       SETC  '&SYSNDX'(3,2)                                           00650000
&EE      SETC  'Z'                                                      00700000
&RZ      SETC  'Z'                                                      00750000
         AIF   (T'&ERROPT NE 'O').IM010                                 00800000
         AIF   (T'&ERREXT EQ 'O').IM030                                 00850000
         MNOTE 0,'ERROPT OMITTED. ERREXT IGNORED'                       00900000
         AGO   .IM030                                                   00950000
.IM010   AIF   ('&ERROPT' EQ 'YES').IM020                               01000000
         MNOTE 0,'ERROPT INVALID. ''YES'' ASSUMED'                      01050000
.IM020   ANOP                                                           01100000
&EE      SETC  'E'                                                      01150000
         AIF   (T'&ERREXT EQ 'O').IM030                                 01200000
&EE      SETC  'C'                                                      01250000
         AIF   ('&ERREXT' EQ 'YES').IM030                               01300000
         MNOTE 0,'ERREXT INVALID. ''YES'' ASSUMED'                      01350000
.IM030   AIF   (T'&RDONLY EQ 'O').IM040                                 01400000
&RZ      SETC  'Y'                                                      01450000
         AIF   ('&RDONLY' EQ 'YES').IM040                               01500000
         MNOTE 0,'RDONLY INVALID. ''YES'' ASSUMED'                      01550000
.IM040   AIF   (T'&SEPASMB EQ 'O' OR '&SEPASMB' EQ 'YES').IM050         01600000
         MNOTE 0,'SEPASMB INVALID. ''YES'' ASSUMED'                     01650000
.IM050   AIF   (T'&NAME EQ 'O').IM060                                   01700000
&NAM     SETC  '&NAME'                                                  01750000
         AGO   .IM070                                                   01800000
.IM060   ANOP                                                           01850000
&NAM     SETC  'IJNDI&EE.Z&RZ'                                          01900000
.IM070   AIF   (T'&SEPASMB EQ 'O').IM080                                01950000
         PUNCH '    CATALR &NAM,29.0'                          @DL29ZCN 02000000
.IM080   ANOP                                                           02050000
IJNDUD&X DSECT                                                          02100000
IJNCCB&X DS    CL21                CCB,LOGIC MODULE ADDRESS             02150000
IJNCM1&X EQU   IJNCCB&X+2          COMMN BYTE 1                         02200000
IJNCM2&X EQU   IJNCCB&X+3          COMMN BYTE 2                         02250000
IJNSTS&X EQU   IJNCCB&X+4          CSW STATUS                           02300000
IJNCCW&X EQU   IJNCCB&X+8          CURRENT CCW ADDRESS                  02350000
IJNCSW&X EQU   IJNCCB&X+12         CSW CCW ADDRESS                      02400000
IJNOCI&X DS    CL17                OPEN/CLOSE INDICATORS                02450000
IJNCB1&X DS    CL1                 OPEN COMMN BYTE 1                    02500000
IJNCB2&X DS    CL5                 OPEN COMMN BYTE 2                    02550000
IJNIOA&X DS    CL4                 ADDRESS OF IOAREA                    02600000
IJNLRW&X DS    CL4                 ADDR OF LAST RD/WT CCW               02650000
IJNLRL&X DS    CL2                 LOWER RECORD LIMIT                   02700000
IJNEDA&X DS    CL4                 END-OF-DATA SEEK ADDRESS             02750000
IJNNOR&X DS    CL2                 NUM OF RECORDS READ                  02800000
IJNCDA&X DS    CL4                 CURRENT DISKETTE ADDRESS             02850000
IJNEOF&X DS    CL4                 EOF ROUTINE ADDRESS                  02900000
IJNSCF&X DS    CL4                 SEEK ARGUMENT CONTROL FIELD          02950000
IJNCCF&X DS    CL1                 COMMAND CHAINING FACTOR              03000000
IJNSW1&X DS    CL1                 SWITCH BYTE 1                        03050000
IJNRCF&X DS    CL2                 RECSIZE*(CMDCHN FACTOR)-1            03100000
IJNSAB&X DS    CL5                 SEEK ARGUMENT BUCKET                 03150000
IJNSW2&X DS    CL3                 SWITCH BYTE 2                        03200000
IJNLDR&X DS    CL4                 LOAD USERS I/O REG                   03250000
IJNCIA&X DS    CL4                 CURRENT IOAREA ADDRESS               03300000
IJNLRS&X DS    CL4                 LOGICAL RECORD SIZE                  03350000
IJNEIA&X DS    CL4                 END OF IO AREA ADDRESS               03400000
IJNLI1&X DS    CL8                 LOGICAL INDICATORS 1                 03450000
IJNERA&X EQU   IJNLI1&X            USERS ERROR ROUTINE ADDRESS          03500000
IJNLI2&X DS    CL4                 LOGICAL INDICATORS 2                 03550000
IJNFDC&X DS    CL16                FEED CCW                             03600000
IJNSKC&X DS    CL8                 SEEK CCW                             03650000
IJNTIC&X DS    CL8                 TRANSFER IN CHANNEL CCW              03700000
IJNRWD&X DS    CL8                 START OF READ/WRITE CCWS             03750000
&NAM     CSECT                                                          03800000
         AIF   (T'&NAME NE 'O').IM100                                   03850000
         AIF   ('&EE' NE 'C').IM090                                     03900000
         ENTRY IJNDIEZ&RZ,IJNDIZZ&RZ                                    03950000
IJNDIEZ&RZ EQU   *                                                      04000000
IJNDIZZ&RZ EQU   *                                                      04050000
         AGO   .IM100                                                   04100000
.IM090   AIF   ('&EE' NE 'E').IM100                                     04150000
         ENTRY IJNDIZZ&RZ                                               04200000
IJNDIZZ&RZ EQU   *                                                      04250000
.IM100   ANOP                                                           04300000
         USING IJNDUD&X,IJNDTF&X                                        04350000
         USING *,IJNBAS&X                                               04400000
         B     IJNERR&X            ERROR-NO CONTROL ROUTINE             04450000
         B     IJNERR&X            ERROR-NO RELSE ROUTINE               04500000
         B     IJNGET&X            GO TO GET ROUTINE                    04550000
         B     IJNERR&X            ERROR-NO PUT ROUTINE                 04600000
         B     IJNERR&X            ERROR-NO CLOSE ROUTINE               04650000
IJNERR&X SVC   50                  ERROR-NO TRUNCATION                  04700000
         DC    CL8'&NAM'           MODULE NAME                          04750000
         DC    X'3440'             VERSION-MODIFICATION LEVEL @D41TDKY  04800000
         AIF   (T'&RDONLY NE 'O').IM110                                 04850000
IJNGET&X STM   IJNRGA&X,IJNRGE&X,IJNSV&X SAVE USER'S AND LINK REGS      04900000
         AGO   .IM120                                                   04950000
.IM110   ANOP                                                           05000000
IJNGET&X STM   IJNRG9&X,IJNRGE&X,0(IJNRGD&X) SAVE REGS 9-14             05050000
         LR    IJNRG9&X,IJNRGD&X   SAVE SAVE AREA POINTER               05100000
.IM120   ANOP                                                           05150000
         TM    IJNSW1&X,IJNX80&X   'FIRST ENTRY' AFTER OPEN             05200000
         BO    IJNDBL&X            NO, GO TO DEBLOCK ROUTINE            05250000
         OI    IJNSW1&X,IJNX80&X   SET 'NOT 1ST ENTRY AFTER OPEN' ON    05300000
IJNNEW&X CLC   IJNCDA&X.(4),IJNEDA&X AT END OF DATA                     05350000
         BNL   IJNXTN&X             YES, HANDLE IT                      05400000
         ICM   IJNRGB&X,15,IJNEDA&X PICK UP END-OF-DATA ADDRS           05450000
         L     IJNRGC&X,IJNCDA&X   LOAD CURRENT DISKETTE ADDRS          05500000
         SR    IJNRGB&X,IJNRGC&X    DETERMINE DIFFERENCE                05550000
         SR    IJNRGC&X,IJNRGC&X   CLEAR REG TO ZERO                    05600000
         IC    IJNRGC&X,IJNCCF&X    PICK UP CMDCHN FACTOR               05650000
         STC   IJNRGC&X,IJNNOR&X+1 INDICATE 'FULL BLOCK'                05700000
         CR    IJNRGB&X,IJNRGC&X    FULL BLOCK TO BE READ               05750000
         BNL   IJNRDD&X             YES, READ IT                        05800000
         BAL   IJNRGA&X,IJNSBR&X    NO, SHORTEN CCW CHAIN               05850000
         STC   IJNRGC&X,IJNNOR&X+1 INDICATE 'SHORT BLOCK'               05900000
         B     IJNRDD&X             BRANCH TO READ                      05950000
IJNDBL&X LM    IJNRGB&X,IJNRGD&X,IJNCIA&X LOAD DEBLOCKING ADDRS'S       06000000
         BXH   IJNRGB&X,IJNRGC&X,IJNTST&X TEST FOR END OF BLOCK         06050000
         ST    IJNRGB&X,IJNCIA&X   STORE NEW RECORD ADDRESS             06100000
IJNGTX&X TM    IJNOCI&X,IJNX10&X   WORK AREA SPECIFIED                  06150000
         BZ    IJNLIO&X            NO, GO TO LOAD I/O REG               06200000
         LR    IJNRGD&X,IJNRG0&X   SET UP 'TO' ADDRESS                  06250000
         BCTR  IJNRGC&X,IJNRG0&X   SET UP LENGTH TO MOVE TO WORKAREA    06300000
         EX    IJNRGC&X,IJNFWA&X   MOVE RECORD TO WORK AREA             06350000
         AIF   (T'&RDONLY NE 'O').IM130                                 06400000
IJNLIO&X LM    IJNRGA&X,IJNRGE&X,IJNSV&X RESTORE USER'S REGS            06450000
         AGO   .IM140                                                   06500000
.IM130   ANOP                                                           06550000
IJNLIO&X LM    IJNRG9&X,IJNRGE&X,0(IJNRG9&X) RESTORE USER'S REGS        06600000
.IM140   ANOP                                                           06650000
         EX    0,IJNLDR&X          LOAD I/O REG WITH RECORD ADDRESS     06700000
         BR    IJNRGE&X            RETURN TO MAINLINE PGM               06750000
IJNTST&X TM    IJNSW1&X,IJNX08&X   END OF EXTENT SWITCH ON              06800000
         BZ    IJN2IO&X            NO, TEST FOR 2 I/O AREAS             06850000
         NI    IJNSW1&X,IJNXF7&X   TURN OFF SWITCH                      06900000
         B     IJNXTN&X            BRANCH TO CALL OPEN                  06950000
IJN2IO&X TM    IJNLI1&X,IJNX08&X   DUAL I/O AREAS                       07000000
         BO    IJNWT1&X            YES, WAIT FOR CHANNEL END            07050000
IJNRDD&X SVC   0                   READ DISK                            07100000
IJNWT1&X TM    IJNCM1&X,IJNX80&X   WAIT BIT POSTED                      07150000
         BO    IJNDON&X            YES, TEST FOR EOD AND ERRORS         07200000
         SVC   7                   WAIT                                 07250000
IJNDON&X TM    IJNCM1&X,IJNX40&X   END-OF-FILE (/* OR/&)                07300000
         BO    IJNXTN&X            YES, BRANCH TO HANDLE IT             07350000
         TM    IJNCM2&X,IJNX02&X   POWER END-OF-FILE                    07400000
         BZ    IJNTER&X            NO, TEST FOR ERRORS                  07450000
         L     IJNRGD&X,IJNCSW&X   LOAD CCB CSW CCW ADDRS               07500000
         LA    IJNRGD&X,0(IJNRGD&X) CLEAR HI-ORDER BYTE                 07550000
         LA    IJNRGB&X,IJNRWD&X   LA OF START OF READ CCWS             07600000
         SR    IJNRGD&X,IJNRGB&X   DETERMINE LENGTH OF CHAIN READ       07650000
         BZ    IJNXTN&X            NO RECORDS, BR TO HANDLE POWER EOF   07700000
         SRL   IJNRGD&X,3          DETERMINE NO. OF RECORDS IN I/O AREA 07750000
         MH    IJNRGD&X,IJNLRS&X+2 DETERMINE SIZE OF 'BLOCK'            07800000
         BCTR  IJNRGD&X,0          DECREMENT BY ONE                     07850000
         L     IJNRGB&X,IJNRWD&X   LOAD CURRENT IOAREA ADDRESS          07900000
         LA    IJNRGB&X,0(IJNRGB&X) CLEAR HI-ORDER BYTE                 07950000
         ST    IJNRGB&X,IJNCIA&X   STORE NEW CURRENT IOAREA ADDRS       08000000
         AR    IJNRGD&X,IJNRGB&X   SETUP END-OF-BLOCK PTR               08050000
         ST    IJNRGD&X,IJNEIA&X   STORE END-OF-BLOCK PTR               08100000
         L     IJNRGC&X,IJNLRS&X   LOAD RECORD SIZE                     08150000
         OI    IJNSW1&X,IJNX08&X   SET ON EOE SWITCH                    08200000
         B     IJNGTX&X            BRANCH TO GIVE USER RECORDS          08250000
IJNTER&X TM    IJNCM2&X,IJNX80&X   SPECIAL RECORD TRANSFERED            08300000
         AIF   ('&ERROPT' EQ '').IM200                                  08350000
         BZ    IJNTDC&X            NO, TEST DATA CHECK                  08400000
         AGO   .IM210                                                   08450000
.IM200   BZ    IJNAD1&X            NO, BRANCH TO CONTINUE               08500000
.IM210   ANOP                                                           08550000
         LH    IJNRGC&X,IJNNOR&X   LD NUM OF RECORDS READ CNT           08600000
         BCTR  IJNRGC&X,0          DECREMENT BY ONE                     08650000
         STH   IJNRGC&X,IJNNOR&X   STORE NUM OF RECORDS READ CNT        08700000
         CLC   IJNCSW&X+1(3),IJNLRW&X+1 LAST RECORD IN CHAIN AN SRT     08750000
         BH    IJNAD1&X            YES, BRANCH TO CONTINUE              08800000
         L     IJNRGB&X,IJNLRW&X   LOAD ADDRS OF LAST RD CCW            08850000
         SH    IJNRGB&X,IJNH08&X   PT TO NEW END OF CCW CHAIN  @DM05060 08900000
         NI    4(IJNRGB&X),IJNXBF&X SET OFF CC FLAG IN LAST CCW IN CHN  08950000
         ST    IJNRGB&X,IJNLRW&X   STORE NEW END OF CCW CHAIN           09000000
         L     IJNRGD&X,IJNTIC&X   LOAD CCW ADDRES IN TIC               09050000
         LA    IJNRGD&X,0(IJNRGD&X) CLEAR HI-ORDER BYTE                 09100000
         L     IJNRGC&X,IJNCSW&X   LOAD CCB CSW CCW ADDRS               09150000
         LA    IJNRGC&X,0(IJNRGC&X) CLEAR HI-ORDER BYTE                 09200000
         LR    IJNRGB&X,IJNRGC&X   SAVE CCB CSW CCW ADDRS               09250000
         SH    IJNRGC&X,IJNH08&X   COMPUTE NEW TIC ADDRS                09300000
         ST    IJNRGC&X,IJNTIC&X   STORE NEW TIC ADDRESS                09350000
         MVI   IJNTIC&X,IJNX08&X   RESTORE TIC CMD CODE                 09400000
         SR    IJNRGB&X,IJNRGD&X   DETERMINE LENGTH OF CHAIN READ       09450000
         SRL   IJNRGB&X,3          DETERMINE NO. OF RECORDS READ        09500000
         AH    IJNRGB&X,IJNCDA&X+2 ADD IN CURRENT RECORD NUMBER         09550000
         STH   IJNRGB&X,IJNCDA&X+2 UPDATE REC NO. IN SEEK ADDRESS       09600000
         B     IJNRDD&X            BRANCH TO RE-ISSUE READ              09650000
         AIF   ('&ERROPT' EQ '').IM395                                  09700000
IJNTDC&X TM    IJNCM2&X,IJNX10&X   TEST FOR DATA CHECK                  09750000
         AIF   ('&ERREXT' NE '').IM300                                  09800000
         BZ    IJNAD1&X            CONTINUE PROCESSING IF NO ERROR      09850000
         AGO   .IM310                                                   09900000
.IM300   BZ    IJNUIO&X            NOT DATA CHECK, TEST UNREC I/O       09950000
         NI    IJNCM1&X,IJNXDF&X   RESET UNREC I/O ERR BIT              10000000
         B     IJNOPT&X            DETERMINE ERROPT OPTION              10050000
IJNUIO&X TM    IJNCM1&X,IJNX20&X   TEST UNREC I/O ERROR BIT             10100000
         BZ    IJNAD1&X            CONTINUE PROCESSING IF NO ERROR      10150000
.IM310   ANOP                                                           10200000
IJNOPT&X TM    IJNLI1&X,IJNX80&X   ERROPT=NAME SPECIFIED                10250000
         BZ    IJNTSK&X            NO, TEST SKIP OPTION                 10300000
         L     IJNRGE&X,IJNERA&X   LA OF ERROR ROUTINE                  10350000
         AIF   (T'&RDONLY NE 'O').IM350                                 10400000
         LM    IJNRGA&X,IJNRGD&X,IJNSV&X  RESTORE USER REGS             10450000
         STM   IJNRGF&X,IJNRG1&X,IJNRTR&X SAVE TABLE,MODULE REGS        10500000
         AIF   ('&ERREXT' NE '').IM320                                  10550000
         L     IJNRG1&X,IJNRWD&X   LOAD I/O AREA ADDRESS                10600000
         AGO   .IM330                                                   10650000
.IM320   MVC   IJNRTR&X+12(4),IJNRWD&X  MOVE I/O AREA ADDR TO USER LIST 10700000
         MVI   IJNRTR&X+12,IJNX00&X  INSURE 1ST BYTE OF ADDR IS ZERO    10750000
         LA    IJNRG1&X,IJNRTR&X+8 LA OF LIST INTO REG1                 10800000
.IM330   ANOP                                                           10850000
         CNOP  0,4                                                      10900000
         BALR  IJNRGE&X,IJNRGE&X   BRANCH TO USERS ERROR ROUTINE        10950000
         AIF   ('&ERREXT' EQ '').IM340                                  11000000
         B     38(IJNRGE&X)        RETURN FOR SKIP                      11050000
         B     50(IJNRGE&X)        RETURN FOR IGNORE                    11100000
.* ABOVE DISPLACEMENTS MAY VARY IF NO. OF INSTRUCTIONS BELOW VARIES     11150000
         LM    IJNRGF&X,IJNRG1&X,22(IJNRGE&X) LOAD MODULE REGS          11200000
         STM   IJNRGA&X,IJNRGD&X,IJNSV&X SAVE USER REGS                 11250000
         SVC   0                   RETRY READ                           11300000
         B     IJNWT1&X            BRANCH TO WAIT AND ERROR TEST        11350000
IJNRTR&X DC    4F'0'               SAVE AREA AND PARM LIST              11400000
         LM    IJNRGF&X,IJNRG1&X,22(IJNRGE&X) LOAD MODULE REGS          11450000
         STM   IJNRGA&X,IJNRGD&X,IJNSV&X SAVE USER REGS                 11500000
         B     IJNSKP&X            GO TO SET ON SKIP SWITCH             11550000
         LM    IJNRGF&X,IJNRG1&X,22(IJNRGE&X) LOAD MODULE REGS          11600000
         STM   IJNRGA&X,IJNRGD&X,IJNSV&X SAVE USER REGS                 11650000
         B     IJNAD1&X            CONTINUE PROCESSING                  11700000
         AGO   .IM390                                                   11750000
.IM340   LM    IJNRGF&X,IJNRG1&X,IJNRTR&X LOAD MODULE REGS  @DA28843    11800000
         STM   IJNRGA&X,IJNRGD&X,IJNSV&X SAVE USER REGS                 11850000
         OI    IJNSW1&X,IJNX10&X   SET ON SKIP SWITCH                   11900000
         B     IJNAD1&X            BRANCH TO CONTINUE PROCESSING        11950000
IJNRTR&X DC    4F'0'               SAVE AREA AND PARM LIST              12000000
         AGO   .IM390                                                   12050000
.IM350   ANOP                                                           12100000
         STM   IJNRGA&X,IJNRGD&X,40(IJNRG9&X) SAVE MODULE REGS          12150000
         STM   IJNRGF&X,IJNRG1&X,24(IJNRG9&X)  SAVE MODULE REGS         12200000
         LM    IJNRG9&X,IJNRGD&X,0(IJNRG9&X) RESTORE USER REGS          12250000
         AIF   ('&ERREXT' NE '').IM360                                  12300000
         L     IJNRG1&X,IJNRWD&X   LOAD I/O AREA ADDRESS                12350000
         AGO   .IM370                                                   12400000
.IM360   ANOP                                                           12450000
         MVC   36(4,IJNRGD&X),IJNRWD&X MOVE I/O ADDRS TO USER LIST      12500000
         MVI   36(IJNRGD&X),IJNX00&X INSURE 1ST BYTE OF ADDR IS ZERO    12550000
         LA    IJNRG1&X,32(IJNRGD&X)    LOAD REG1 WITH LIST ADDR        12600000
.IM370   ANOP                                                           12650000
         BALR  IJNRGE&X,IJNRGE&X   BRANCH TO USERS ERROR ROUTINE        12700000
         AIF   ('&ERREXT' EQ '').IM380                                  12750000
         LA    IJNRGE&X,4(IJNRGE&X) RETURN FOR SKIP                     12800000
         LA    IJNRGE&X,6(IJNRGE&X) RETURN FOR IGNORE                   12850000
         LA    IJNRGE&X,28(IJNRGE&X) RETURN FOR RETRY                   12900000
.* ABOVE DISPLACEMENTS MAY VARY IF NO. OF INSTRUCTIONS BELOW VARIES     12950000
         LM    IJNRGF&X,IJNRG1&X,24(IJNRGD&X) RESTORE MODULE REGS       13000000
         STM   IJNRG9&X,IJNRGD&X,0(IJNRGD&X) SAVE USER REGS             13050000
         LR    IJNRG9&X,IJNRGD&X   SAVE SAVE AREA POINTER               13100000
         LM    IJNRGA&X,IJNRGD&X,40(IJNRGD&X) RESTORE MODULE REGS       13150000
         BR    IJNRGE&X            GO TO PROPER ROUTINE                 13200000
         SVC   0                   RETRY READ                           13250000
         B     IJNWT1&X            BRANCH TO WAIT AND ERROR TEST        13300000
         B     IJNAD1&X            CONTINUE PROCESSING                  13350000
         OI    IJNSW1&X,IJNX10&X   SET ON SKIP SWITCH                   13400000
         B     IJNAD1&X            BRANCH TO IGNORE ROUTINE             13450000
         AGO   .IM390                                                   13500000
.IM380   ANOP                                                           13550000
         STM   IJNRG9&X,IJNRGD&X,0(IJNRGD&X) SAVE USER REGS             13600000
         LM    IJNRGF&X,IJNRG1&X,24(IJNRGD&X) RESTORE MODULE REGISTERS  13650000
         LR    IJNRG9&X,IJNRGD&X   SAVE SAVE AREA POINTER               13700000
         LM    IJNRGA&X,IJNRGD&X,40(IJNRGD&X) RESTORE MODULE REGISTERS  13750000
         B     IJNSKP&X            BRANCH TO SET SKIP SWITCH            13800000
.IM390   ANOP                                                           13850000
IJNTSK&X TM    IJNLI1&X,IJNX20&X   ERROPT =SKIP                         13900000
         BZ    IJNAD1&X            NO, ERROPT=IGNORE. BR TO IGNORE      13950000
IJNSKP&X OI    IJNSW1&X,IJNX10&X   SET SKIP SWITCH ON                   14000000
.IM395   ANOP                                                           14050000
IJNAD1&X CLC   IJNCCF&X.(1),IJNNOR&X+1 'SHORT BLOCK'                    14100000
         BE    IJNSBP&X            NO, GO TO SET UP BLOCK POINTERS      14150000
         LA    IJNRGB&X,IJNRWD&X   LA OF 1ST READ CCW                   14200000
         STCM  IJNRGB&X,7,IJNTIC&X+1 RESET UP BRANCH ADDRS IN TIC       14250000
         SR    IJNRGC&X,IJNRGC&X   CLEAR REGISTER                       14300000
         IC    IJNRGC&X,IJNCCF&X   PICK UP NO. OF CCWS  IN CHAIN        14350000
IJNCCS&X BCT   IJNRGC&X,IJNLP1&X   ALL CC BITS IN CCWS SET              14400000
         B     IJNDN1&X            YES, CONTINUE                        14450000
IJNLP1&X OI    4(IJNRGB&X),IJNX40&X TURN ON CC FLAG IN CCW              14500000
         LA    IJNRGB&X,8(IJNRGB&X) POINT TO NEXT CCW IN CHAIN          14550000
         B     IJNCCS&X            CONTINUE CC BIT SETTING LOOP         14600000
IJNDN1&X ST    IJNRGB&X,IJNLRW&X   RE-SETUP LAST CCW IN CHAIN POINTER   14650000
IJNSBP&X L     IJNRGB&X,IJNRWD&X   GET CURRENT I/O AREA ADDRS           14700000
         LA    IJNRGB&X,0(IJNRGB&X) CLEAR HI-ORDER BYTE                 14750000
         ST    IJNRGB&X,IJNCIA&X   SET UP ADDRS OF INPUT AREA           14800000
         CLC   IJNCCF&X.(1),IJNNOR&X+1 'FULL BLOCK'                     14850000
         BNE   IJNAD2&X            NO, GO HANDLE SHORT BLOCK            14900000
         AH    IJNRGB&X,IJNRCF&X   ADD 'BLOCK SIZE' -1                  14950000
         B     IJNAD3&X            BRANCH TO CONTINUE                   15000000
IJNAD2&X LH    IJNRGC&X,IJNNOR&X   LOAD NUM OF RECORDS READ             15050000
         MH    IJNRGC&X,IJNLRS&X+2 DETERMINE 'BLOCK'SIZE                15100000
         BCTR  IJNRGC&X,0          DECREMENT BY ONE                     15150000
         AR    IJNRGB&X,IJNRGC&X   DETERMINE 'END-OF-BLOCK' PTR         15200000
IJNAD3&X ST    IJNRGB&X,IJNEIA&X   STORE END OF I/O AREA ADDRS          15250000
         SR    IJNRGB&X,IJNRGB&X   CLEAR REGISTER                       15300000
         IC    IJNRGB&X,IJNCCF&X   PICK UP COMMAND CHAINING FACTOR      15350000
         AH    IJNRGB&X,IJNSAB&X+2 ADD IN CURRENT RECORD NUMBER         15400000
         CH    IJNRGB&X,IJNSCF&X+2 RECORD NO. GT LIMIT                  15450000
         BNH   IJNAD4&X            NO, STORE IT                         15500000
         LH    IJNRGB&X,IJNLRL&X   LOAD LOWER RECORD LIMIT              15550000
         LH    IJNRGC&X,IJNCDA&X   PICK-UP CYLINDER NO.                 15600000
         AH    IJNRGC&X,IJNH01&X   ADD ONE TO CYLINDER                  15650000
         STH   IJNRGC&X,IJNCDA&X   STORE CYLINDER NUMBER                15700000
IJNAD4&X STH   IJNRGB&X,IJNCDA&X+2 STORE RECORD NUMBER                  15750000
         STH   IJNRGB&X,IJNSAB&X+2 SAVE CURRENT RECORD NUM              15800000
         CLC   IJNCDA&X.(4),IJNEDA&X AT END OF DATA                     15850000
         BL    IJNAD5&X            NO, CONTINUE                         15900000
         CLI   IJNNOR&X+1,IJNX00&X I/O AREA EMPTY                       15950000
         BE    IJNXTN&X            YES, GET NEW EXTENT                  16000000
         OI    IJNSW1&X,IJNX08&X   SET ON EOE BIT                       16050000
         TM    IJNSW1&X,IJNX10&X   SKIP SWITCH ON              @DA09953 16100000
         BZ    IJNAD8&X            NO, LOAD BLOCKING CONSTANTS @DA09953 16150000
         NI    IJNSW1&X,IJNXEF&X   RESET SKIP SWITCH           @DA09953 16200000
         B     IJNTST&X            GET NEXT BLOCK IN           @DA09953 16250000
IJNAD8&X LM    IJNRGB&X,IJNRGD&X,IJNCIA&X LOAD BLOCKING CONST  @DA09953 16300000
         B     IJNGTX&X            BRANCH TO GIVE USER LAST RECORDS     16350000
IJNAD5&X CLI   IJNNOR&X+1,IJNX00&X I/O AREA EMPTY                       16400000
         BNE   IJNAD6&X            NO, CONTINUE                         16450000
         OI    IJNSW1&X,IJNX10&X   SET ON SKIP SWITCH                   16500000
IJNAD6&X ICM   IJNRGB&X,15,IJNEDA&X PICK-UP END OF DATA ADDRS           16550000
         L     IJNRGC&X,IJNCDA&X   LOAD CURRENT DISKETTE ADDRS          16600000
         SR    IJNRGB&X,IJNRGC&X   DETERMINE DIFFERENCE                 16650000
         SR    IJNRGC&X,IJNRGC&X   SET REGISTER TO ZERO                 16700000
         IC    IJNRGC&X,IJNCCF&X   PICK UP CMDCHN FACTOR                16750000
         CR    IJNRGB&X,IJNRGC&X   FULL BLOCK TO BE READ                16800000
         BNL   IJNAD7&X            YES, CONTINUE                        16850000
         BAL   IJNRGA&X,IJNSBR&X   NO, BAL TO SHORTEN CCW CHAIN         16900000
IJNAD7&X STC   IJNRGC&X,IJNNOR&X+1 SET UP NUM OF RECORDS READ CNT       16950000
         TM    IJNLI1&X,IJNX08&X   DUAL I/O AREAS                       17000000
         BZ    IJNTSS&X            NO, GO TO TEST SKIP SWITCH           17050000
         LA    IJNRGD&X,IJNRWD&X   LA OF 1ST CCW IN CHAIN               17100000
         L     IJNRGB&X,IJNIOA&X   LA OF START OF NEXT IO AREA          17150000
         MVC   IJNIOA&X+1(3),IJNRWD&X+1 FLIP/FLOP IO AREA ADDRESSES     17200000
         ICM   IJNRGB&X,8,IJNRWD&X PICK UP RD CMD CODE IN REG           17250000
         IC    IJNRGC&X,IJNCCF&X   PICK UP # OF CCWS IN CHAIN           17300000
         B     IJNTP1&X            BRANCH AROUND ADDS, 1ST TIME         17350000
IJNLP2&X AH    IJNRGD&X,IJNH08&X   UPDATE ADDRESS OF CCW                17400000
         A     IJNRGB&X,IJNLRS&X   UPDATE DATA ADDRESS IN CCW           17450000
IJNTP1&X ST    IJNRGB&X,0(IJNRGD&X) STORE NEW DATA ADDRS IN PROPER CCW  17500000
         BCT   IJNRGC&X,IJNLP2&X   DONE WITH DATA ADDRS IN CCW SETUP    17550000
         SVC   0                   YES, ISSUE READ                      17600000
IJNTSS&X TM    IJNSW1&X,IJNX10&X   SKIP SWITCH ON                       17650000
         BZ    IJNLBC&X            NO, GO LOAD 'BLOCKING' CONSTANTS     17700000
         NI    IJNSW1&X,IJNXEF&X   RESET SKIP SWITCH                    17750000
         B     IJNTST&X            GET NEXT 'BLOCK' IN                  17800000
IJNLBC&X LM    IJNRGB&X,IJNRGD&X,IJNCIA&X LOAD 'BLOCKING' CONSTANTS     17850000
         B     IJNGTX&X            BRANCH TO GIVE USER RECORDS          17900000
IJNSBR&X LR    IJNRGC&X,IJNRGB&X   SAVE CCW COUNT FOR LATER USE         17950000
         BCTR  IJNRGB&X,0          DECREMENT CCW COUNT BY ONE           18000000
         SLL   IJNRGB&X,3          MULTIPLY BY 8                        18050000
         LA    IJNRGD&X,IJNRWD&X   POINT TO FIRST READ CCW              18100000
         AR    IJNRGD&X,IJNRGB&X   POINT TO LAST CCW IN CHAIN           18150000
         NI    4(IJNRGD&X),IJNXBF&X SET OFF CC FLAG IN LAST CCW IN CHN  18200000
         ST    IJNRGD&X,IJNLRW&X   SET UP LAST CCW IN CHAIN POINTER     18250000
         BR    IJNRGA&X            RETURN TO PROPER POINT IN L. M.      18300000
IJNXTN&X TM    IJNCM2&X,IJNX02&X   'POWER' END OF FILE                  18350000
         BO    IJNEXT&X            YES, GO GET EXTENT                   18400000
         TM    IJNCM1&X,IJNX40&X    END-OF-FILE (/* OR/&)               18450000
         BO    IJNEXT&X            YES, SYSRDR OR IPT, DONT FEED        18500000
         TM    IJNCB1&X,IJNX80&X   'NO MORE EXTENTS' BIT ON             18550000
         BZ    IJNFED&X            NO, GO ISSUE FEED                    18600000
         TM    IJNLI2&X,IJNX40&X   'NO FEED' BIT ON                     18650000
         BO    IJNAFD&X            YES, BRANCH AROUND FEED              18700000
IJNFED&X LA    IJNRGB&X,IJNFDC&X   POINT TO FEED CCW                    18750000
         ST    IJNRGB&X,IJNCCW&X   SET UP CCB CCW ADDRS                 18800000
         SVC   0                   ISSUE FEED                           18850000
         TM    IJNCM1&X,IJNX80&X   WAIT BIT POSTED                      18900000
         BO    IJNAWT&X            YES, BRANCH AROUND WAIT              18950000
         SVC   7                   WAIT FOR FEED COMPLETION             19000000
IJNAWT&X LA    IJNRGB&X,IJNSKC&X   POINT TO NORMAL CCW CHAIN            19050000
         ST    IJNRGB&X,IJNCCW&X   SET UP CCB CCW ADDRS                 19100000
IJNAFD&X TM    IJNCB1&X,IJNX80&X   'NO MORE EXTENTS' BIT ON             19150000
         BZ    IJNEXT&X            NO, BRANCH AROUND SETTING U. E.      19200000
         OI    IJNSTS&X,IJNX01&X   SET ON UNIT EXCEPTION FOR OPEN       19250000
         AIF   (T'&RDONLY NE 'O').IM400                                 19300000
IJNEXT&X STM   IJNRG0&X,IJNRG1&X,IJNSVE&X SAVE DTF AND WORK ADDRS       19350000
         LM    IJNRGA&X,IJNRGD&X,IJNSV&X RESTORE USER REGS              19400000
         LA    IJNRG0&X,IJNSVE&X+4 LA OF DTF TABLE ADDRESS              19450000
         LA    IJNRG1&X,IJNNEX&X LA OF NAME OF MODULE TO CALL           19500000
         SVC   2                   FETCH NEW EXTENT                     19550000
         LM    IJNRG0&X,IJNRG1&X,IJNSVE&X RESTORE DTF AND WORK REGS     19600000
         AGO   .IM420                                                   19650000
.IM400   ANOP                                                           19700000
IJNEXT&X STM   IJNRG0&X,IJNRG1&X,28(IJNRG9&X) SAVE MODULE REGISTERS     19750000
         STM   IJNRGA&X,IJNRGD&X,40(IJNRG9&X) SAVE MODULE REGISTERS     19800000
         LM    IJNRG9&X,IJNRGD&X,0(IJNRG9&X) RELOAD USERS REGISTERS     19850000
         LA    IJNRG0&X,32(IJNRGD&X) LA OF DTF TABLE ADDRESS            19900000
         MVI   36(IJNRGD&X),IJNXFF&X SET PARM LIST END INDICATOR        19950000
         LA    IJNRG1&X,IJNNEX&X   LA OF NAME OF MODULE TO CALL         20000000
         SVC   2                   FETCH NEW EXTENT                     20050000
         STM   IJNRG9&X,IJNRGD&X,0(IJNRGD&X) SAVE USERS REGISTERS       20100000
         LM    IJNRG0&X,IJNRG1&X,28(IJNRGD&X) RELOAD MODULE REGS        20150000
         LR    IJNRG9&X,IJNRGD&X   SAVE SAVE AREA POINTER               20200000
         LM    IJNRGA&X,IJNRGD&X,40(IJNRGD&X) RELOAD MODULE REGS        20250000
.IM420   ANOP                                                           20300000
         B     IJNNEW&X            BRANCH TO PROCESS NEW EXTENT         20350000
IJNFWA&X MVC   0(1,IJNRGD&X),0(IJNRGB&X)  INS TO FILL WORKAREA          20400000
IJNH08&X DC    H'8'                HALFWORD CONSTANT OF 8               20450000
IJNH01&X DC    H'1'                HALFWORD CONSTANT OF 1               20500000
         AIF   (T'&RDONLY NE 'O').IM550                                 20550000
IJNSV&X  DC    5F'0'               USER REGS 10-14 SAVE AREA            20600000
IJNSVE&X DC    2F'0'               DTF AND WORK REGS SAVE AREA          20650000
.IM550   ANOP                                                           20700000
IJNNEX&X DC    CL8'$$BOPEN '       MODULE TO CALL FOR NEW EXTENT        20750000
IJNDTF&X EQU   1                   DTF TABLE BASE REG                   20800000
IJNBAS&X EQU   15                  LOGIC MOD. BASE REG                  20850000
IJNRG0&X EQU   0                   WORK REG (0)                         20900000
IJNRG1&X EQU   1                   DTF AND PARM LIST REG (1)            20950000
IJNRG9&X EQU   9                   SAVE SAVEAREA PTR REG (9) (RDONLY)   21000000
IJNRGA&X EQU   10                  WORK REG (10)                        21050000
IJNRGB&X EQU   11                  WORK REG (11)                        21100000
IJNRGC&X EQU   12                  WORK REG (12)                        21150000
IJNRGD&X EQU   13                  WORK REG (13)                        21200000
IJNRGE&X EQU   14                  LINKAGE REG (14)                     21250000
IJNRGF&X EQU   15                  MODULE BASE REG (15)                 21300000
IJNX00&X EQU   X'00'               NO BITS ON MASK                      21350000
IJNX01&X EQU   X'01'               BIT 7 MASK                           21400000
IJNX02&X EQU   X'02'               BIT 6 MASK                           21450000
IJNX04&X EQU   X'04'               BIT 5 MASK                           21500000
IJNX08&X EQU   X'08'               BIT 4 MASK                           21550000
IJNX10&X EQU   X'10'               BIT 3 MASK                           21600000
IJNX20&X EQU   X'20'               BIT 2 MASK                           21650000
IJNX40&X EQU   X'40'               BIT 1 MASK                           21700000
IJNX80&X EQU   X'80'               BIT 0 MASK                           21750000
IJNXBF&X EQU   X'BF'               SET OFF BIT 1 MASK                   21800000
IJNXDF&X EQU   X'DF'               SET OFF BIT 2 MASK                   21850000
IJNXEF&X EQU   X'EF'               SET OFF BIT 3 MASK                   21900000
IJNXF7&X EQU   X'F7'               SET OFF BIT 4 MASK                   21950000
IJNXFF&X EQU   X'FF'               PARM LIST END IND                    22000000
         AIF   (T'&SEPASMB NE 'O').IM570                                22050000
&SYSECT  CSECT                                                          22100000
.IM570   ANOP                                                           22150000
         MEXIT                                                          22200000
         MEND                                                           22250000
