* N O T E   WHENEVER A NEW KEYWORD IS IMPLEMENTED, PLEASE MAKE          00050000
*           CERTAIN THAT THE VALIDATION MACRO WHICH IS IKQACB1 @D35ZE86 00100000
*           IS ALTERED ACCORDINGLY.                                     00150000
*                                                                       00200000
         MACRO                                                          00250000
         IKQACBG ,                INT  ACB  RPL  NIB  EXLST            *00300000
               &AM=,               X                                   *00350000
               &BSTRNO=,                X                      @D21ADWS*00400000
               &BUFND=,                 X                              *00450000
               &BUFNI=,                 X                              *00500000
               &BUFSP=,                 X                              *00550000
               &CATALOG=,               X                              *00600000
               &CRA=,                   X                              *00650000
               &CLOSDSP=,               X                      @D36WE86*00700000
               &DDNAME=,                X                              *00750000
               &DSNAME=,                X                      @D13CDWS*00800000
               &EXLST=,                 X                              *00850000
               &MACRF=,                 X                              *00900000
               &MAREA=,                 X                              *00950000
               &MLEN=,                  X                              *01000000
               &NAME=,                  X                              *01050000
               &PASSWD=,                X                              *01100000
               &RMODE31=,               X                      @D22VDUS*01150000
               &SHRPOOL=,               X                      @D22MDUS*01200000
               &STRNO=,                 X                              *01250000
               &USERPTR=                X                               01300000
.*                                                                      01350000
.*       LOCAL DEFINITIONS                                              01400000
.*                                                                      01450000
         LCLA  &PARM,&PARMC             SUBPARAMETER COUNTER   @D13FDWS 01500000
         LCLA  &ERRCNT                  SUBPARAMETER ERROR COUNTER      01550000
         LCLB  &KEY,&ADR,&CNV,&SEQ,&DIR MACRF(1) BITS                   01600000
         LCLB  &NUB,&UBF,&IN,&OUT       MACRF(1) BITS                   01650000
         LCLB  &SKP,&RST,&AIX,&NRM,&NRS MACRF(2) BITS                   01700000
         LCLB  &NFY,&VFY                MACRF(2) BITS          @D13EDWS 01750000
         LCLB  &DDN,&DSN,&RM31,&NCM,&CMP   MACRF(3) BITS       @D61GDWM 01800061
         LCLB  &ICI,&NCI                MACRF(2) BITS  OS-ONLY @D21ADJP 01850000
         LCLB  &NSR,&LSR,&GSR,&NFX      MACRF(2) BITS  OS-ONLY          01900000
         LCLB  &NDF,&DFR,&NIS,&SIS,&CFX MACRF(2) BITS  OS-ONLY          01950000
         LCLB  &SCRA,&UCRA              CRA      BITS                   02000000
         LCLB  &CATLG                   CATALOG  BITS                   02050000
         LCLB  &KEEP,&DELETE,&DATE      CLOSDSP BITS           @D36WE86 02100000
         LCLB  &K1,&D1                  2ND CLOSDSP BITS       @D13FDWS 02150000
         LCLC  &IDDNAM                  INTERNAL VARIABLE      @D33VDVG 02200000
         LCLC  &CODE                    SUBPARAMETER CODE VALUE         02250000
         ACTR  5000                                                     02300000
.*                                                                      02350000
.* IBM DISK OPERATING SYSTEM                                            02400000
* VSAM - IKQACBG - 5686-037(C66)  -  VERSION 2 RELEASE 1.0              02450000
.*                                                                      02500000
.* /* START OF SPECIFICATIONS ****************************************  02550000
.*                                                                      02600000
.*01* MODULE-NAME = IKQACBG  (DOS/VS VSAM-E)                            02650000
.*                                                                      02700000
.*01* DESCRIPTIVE-NAME = ACCESS METHODS CONTROL BLOCK GENERATION        02750000
.*        FOR VSAM                                                      02800000
.*                                                                      02850000
.*01* COPYRIGHT =                                                       02900000
.*       LICENSED MATERIALS - PROPERTY OF IBM                @D21RDGW   02950000
.*       THIS MACRO IS "RESTRICTED MATERIALS OF IBM"         @D21RDGW   03000000
.*       5686-037 (C) COPYRIGHT IBM CORP. 1979, 1990         @D21RDGW   03050000
.*       SEE COPYRIGHT  INSTRUCTIONS                         @D21RDGW   03100000
.*                                                                      03150000
.*01* STATUS = VERSION 2, RELEASE 1.0                        @D21RDGW   03200000
.*                                                                      03250000
.*01* FUNCTION = BUILDS ACB FOR VSAM                                    03300000
.*                                                                      03350000
.*01* MODULE-TYPE = MACRO                                               03400000
.*                                                                      03450000
.*02*   PROCESSOR = /370 ASSEMBLER                                      03500000
.*                                                                      03550000
.*02*   MODULE-SIZE = SIZE OF GENERATED ACB IS 68 BYTES                 03600000
.*                                                                      03650000
.*01* INPUT = SEE PROTOTYPE STATEMENT                                   03700000
.*                                                                      03750000
.*01* OUTPUT = ACB GENERATED                                            03800000
.*                                                                      03850000
.*01* EXIT-NORMAL = NA                                                  03900000
.*                                                                      03950000
.*01* EXIT-ERROR = MNOTES:                                              04000000
.*       12,*** IKQ003 INVALID VALUE, &C, SPECIFIED FOR &B KEYWORD      04050000
.*       12,*** IKQ008 INCOMPATIBLE SUBLIST ITEMS, &C AND &D, FOR       04100000
.*              &B KEYWORD                                              04150000
.*       12,*** IKQ019  CONFLICTING KEYWORDS,&A AND &C                  04200000
.*        4,*** IKQ013 &B = &C INVALID OPTION - IGNORED                 04250000
.*        4,*** IKQ016  &B CONFLICTING OR DUPL IN MACRF - IGNORED       04300000
.*        4,*** IKQ020  &B = &C INVALID SUBLIST ITEM FOR &D - IGNORED   04350000
.*                                                                      04400000
.*        ERROR MESSAGES:                                               04450000
.*        ** ERR ** DUPLICATE KEYWORD IN MACRO CALL. LAST VALUE IS USED 04500000
.*        ** WNG ** UNDEFINED KEYWORD PARAM. DEFAULT TO POSITIONAL      04550000
.*                                                                      04600000
.*01* MACROS = IKQERMAC                                                 04650000
.*                                                                      04700000
.*01* CHANGE-ACTIVITY =                                                 04750000
.*       SPACE MANAGEMENT ENHANCEMENTS                         @D36WE86 04800000
.*       ACB EXTENSION FOR FUTURE USE                          @D21RDGW 04850000
.*       DATA SET NAME SHARING SUPPORT                         @D21ADWS 04900000
.*       MULTIPLE LSR SUPPORT                                  @D22MDUS 04950000
.*       31-BIT ADDRESSING                                     @D22VDUS 05000000
.*                                                                      05050000
.***** END OF SPECIFICATIONS *****************************************/ 05100000
.*                                                                      05150000
         AIF ('&AM' EQ '').VSMA010      VSAM EXPANSION                  05200000
         AIF ('&AM' EQ 'VSAM').VSMA010  VSAM EXPANSION                  05250000
         IKQERMAC 3,AM,&AM              INVALID ACCESS METHOD           05300000
.*                                                                      05350000
.*       DIAGNOSE ALL KEYWORDS SPECIFICATIONS FOR CORRECTNESS           05400000
.*                                                                      05450000
.VSMA010 ANOP                                                           05500000
         AIF   ('&CATALOG' NE 'YES').VSMA050                            05550000
&CATLG   SETB  1                                                        05600000
         AGO   .VSMA060                                                 05650000
.VSMA050 AIF   ('&CATALOG' EQ '' OR '&CATALOG' EQ 'NO').VSMA060         05700000
         IKQERMAC 13,CATALOG,&CATALOG   INVALID OPTION                  05750000
.VSMA060 AIF   ('&CLOSDSP' EQ '').VSMA065 DEFAULT IF NOT SPEC  @D36WE86 05800000
&KEEP    SETB  ('&CLOSDSP' EQ 'KEEP' OR '&CLOSDSP' EQ '(KEEP)' OR '&CLOX05850000
               SDSP' EQ '(KEEP,DELETE)' OR '&CLOSDSP' EQ '(KEEP,KEEP)')X05900000
                                                               @D13FDWS 05950000
&DELETE  SETB  ('&CLOSDSP' EQ 'DELETE' OR '&CLOSDSP' EQ '(DELETE)' OR  X06000000
               '&CLOSDSP' EQ '(DELETE,KEEP)' OR '&CLOSDSP' EQ '(DELETE,X06050000
               DELETE)')                                       @D13FDWS 06100000
&DATE    SETB  ('&CLOSDSP' EQ 'DATE' OR '&CLOSDSP' EQ '(DATE)' OR '&CLOX06150000
               SDSP' EQ '(DATE,DELETE)' OR '&CLOSDSP' EQ '(DATE,KEEP)')X06200000
                                                               @D13FDWS 06250000
&K1      SETB  ('&CLOSDSP' EQ '(,KEEP)' OR '&CLOSDSP' EQ '(KEEP,KEEP)' X06300000
               OR '&CLOSDSP' EQ '(DELETE,KEEP)' OR '&CLOSDSP' EQ '(DATEX06350000
               ,KEEP)')                                        @D13FDWS 06400000
&D1      SETB  ('&CLOSDSP' EQ '(,DELETE)' OR '&CLOSDSP' EQ '(KEEP,DELETX06450000
               E)' OR '&CLOSDSP' EQ '(DELETE,DELETE)' OR '&CLOSDSP' EQ X06500000
               '(DATE,DELETE)')                                @D13FDWS 06550000
         AIF  (&KEEP OR &DELETE OR &DATE OR &K1 OR &D1).VSMA063        X06600000
                           OPTION IS VALID                     @D13FDWS 06650000
         IKQERMAC 13,CLOSDSP,&CLOSDSP  INVALID OPTION          @D36WE86 06700000
.VSMA063 AIF  (&KEEP OR &DELETE OR &DATE).VSMA067 ONE SPECIFIED@D13FDWS 06750000
.VSMA065 ANOP                                                  @D36WE86 06800000
&KEEP    SETB  1                        SET DEFAULT VALUE      @D36WE86 06850000
.VSMA067 AIF   ('&CRA' EQ '').VSMA070                                   06900000
&SCRA    SETB  ('&CRA' EQ 'SCRA')                                       06950000
&UCRA    SETB  ('&CRA' EQ 'UCRA')                                       07000000
         AIF   (&SCRA OR &UCRA).VSMA070                                 07050000
         IKQERMAC 13,CRA,&CRA                                           07100000
.VSMA070 ANOP                                                           07150000
&IDDNAM  SETC  '        '               INITIAL DDNAME                  07200000
         AIF   ('&DDNAME' EQ '').VSMA080                                07250000
&IDDNAM  SETC  '&DDNAME'                SET UP DDNAME                   07300000
         AGO   .VSMA090                                                 07350000
.VSMA080 AIF   ('&NAME' EQ '').VSMA090                                  07400000
&IDDNAM  SETC  '&NAME'                  USE LABEL AS DDNAME             07450000
.VSMA090 ANOP                                                  @D33VDVG 07500000
.VSMA100 AIF   (&PARM GE N'&MACRF).VSMA110 NO MORE PARAMETERS           07550000
&PARM    SETA  &PARM+1                  POINT TO NEXT SUBPAR.           07600000
&CODE    SETC  '&MACRF(&PARM)'          PICK UP ITS VAULE               07650000
.*                                                                      07700000
.*       SET THE APPROPRIATE BIT ON                                     07750000
.*                                                                      07800000
&KEY     SETB  (&KEY OR '&CODE' EQ 'KEY')                               07850000
&ADR     SETB  (&ADR OR '&CODE' EQ 'ADR' OR '&CODE' EQ 'ADD')           07900000
&CNV     SETB  (&CNV OR '&CODE' EQ 'CNV' OR '&CODE' EQ 'BLK')           07950000
&SEQ     SETB  (&SEQ OR '&CODE' EQ 'SEQ')                               08000000
&DIR     SETB  (&DIR OR '&CODE' EQ 'DIR')                               08050000
&IN      SETB  (&IN  OR '&CODE' EQ 'IN')                                08100000
&OUT     SETB  (&OUT OR '&CODE' EQ 'OUT')                               08150000
&UBF     SETB  (&UBF OR '&CODE' EQ 'UBF')                               08200000
&NUB     SETB  (&NUB OR '&CODE' EQ 'NUB')                               08250000
&SKP     SETB  (&SKP OR '&CODE' EQ 'SKP')                               08300000
&RST     SETB  (&RST OR '&CODE' EQ 'RST')                               08350000
&NRS     SETB  (&NRS OR '&CODE' EQ 'NRS')                               08400000
&AIX     SETB  (&AIX OR '&CODE' EQ 'AIX')                               08450000
&NRM     SETB  (&NRM OR '&CODE' EQ 'NRM')                               08500000
&DDN     SETB  (&DDN OR '&CODE' EQ 'DDN')                               08550000
&DSN     SETB  (&DSN OR '&CODE' EQ 'DSN')                               08600000
&NCM     SETB  (&NCM OR '&CODE' EQ 'NCM')                               08616661
&CMP     SETB  (&CMP OR '&CODE' EQ 'CMP')                               08633261
&ICI     SETB  (&ICI OR '&CODE' EQ 'ICI')                               08650000
&NCI     SETB  (&NCI OR '&CODE' EQ 'NCI')                               08700000
&NSR     SETB  (&NSR OR '&CODE' EQ 'NSR')                               08750000
&LSR     SETB  (&LSR OR '&CODE' EQ 'LSR')                               08800000
&GSR     SETB  (&GSR OR '&CODE' EQ 'GSR')                               08850000
&DFR     SETB  (&DFR OR '&CODE' EQ 'DFR')                               08900000
&NDF     SETB  (&NDF OR '&CODE' EQ 'NDF')                               08950000
&NIS     SETB  (&NIS OR '&CODE' EQ 'NIS')                               09000000
&SIS     SETB  (&SIS OR '&CODE' EQ 'SIS')                               09050000
&NFX     SETB  (&NFX OR '&CODE' EQ 'NFX')                               09100000
&CFX     SETB  (&CFX OR '&CODE' EQ 'CFX')                               09150000
&NFY     SETB  (&NFY OR '&CODE' EQ 'NFY')                      @D13EDWS 09200000
&VFY     SETB  (&VFY OR '&CODE' EQ 'VFY')                      @D13EDWS 09250000
.*                                                                      09300000
.*       VERIFY THAT A NEW BIT WAS SET ON                               09350000
.*                                                                      09400000
&PARMC   SETA  &KEY+&ADR+&CNV+&SEQ+&DIR+&IN+&OUT+&UBF+&NUB+&SKP+&RST+&AX09450000
               IX+&NRM+&DDN+&DSN+&ICI+&NCI+&NSR+&LSR+&GSR+&DFR+&NDF+&NIX09500000
               S+&SIS+&NFX+&CFX+&NRS+&ERRCNT+&NFY+&VFY+&NCM+&CMP        09550061
         AIF   (&PARMC EQ &PARM).VSMA100                       @D13FDWS 09600000
&ERRCNT  SETA  &ERRCNT+1                IF NOT, INCREMENT               09650000
         IKQERMAC 16,&CODE              CONFLICT IN MACRF               09700000
         AGO   .VSMA100                 CONTINUE CHECKING               09750000
.*                                                                      09800000
.*                                                                      09850000
.*                                                                      09900000
.VSMA110 AIF   (NOT &NUB OR NOT &UBF).VSMA112 NOT BOTH         @D33VDVG 09950000
         IKQERMAC 16,NUB                ILL. OR DUPL IN MACRF           10000000
.VSMA112 AIF   (NOT &LSR OR NOT &UBF).VSMA114 NOT BOTH         @D33VDVG 10050000
         IKQERMAC 8,MACRF,LSR,UBF       INCOMPATIBLE ITEMS     @D33VDVG 10100000
.VSMA114 AIF   (NOT &NSR OR NOT &DFR).VSMA116 NOT BOTH         @D33VDVG 10150000
&DFR     SETB  0                                               @D33VDVG 10200000
         IKQERMAC 16,DFR                ILL. OR DUPL IN MACRF  @D33VDVG 10250000
.VSMA116 AIF   (NOT &LSR OR NOT &RST).VSMA120 NOT BOTH         @D33VDVG 10300000
         IKQERMAC 8,MACRF,LSR,RST       INCOMPATIBLE ITEMS     @D33VDVG 10350000
.VSMA120 AIF   (NOT &AIX OR NOT &NRM).VSMA125 NOT BOTH                  10400000
&AIX     SETB  0                        RESET TO NRM                    10450000
         IKQERMAC 16,AIX                ILL. OR DUPL IN MACRF           10500000
.VSMA125 AIF   (NOT &RST OR NOT &NRS).VSMA130 NOT BOTH                  10550000
&RST     SETB  0                        RESET TO NRM                    10600000
         IKQERMAC 16,NRS                ILL. OR DUPL IN MACRF           10650000
.VSMA130 AIF   (NOT &IN OR NOT &RST).VSMA131 NOT BOTH          @D33VDVG 10700000
         IKQERMAC 16,RST                INVALID DOS OPTION              10750000
.VSMA131 AIF   (('&RMODE31' EQ 'CB') OR ('&RMODE31' EQ 'NONE')         *10800000
               OR ('&RMODE31' EQ '')).VSMA132                  @D22VDUS 10850000
         AIF   (('&RMODE31' EQ 'ALL')OR ('&RMODE31' EQ 'BUFF')).VSMA133 10900000
         IKQERMAC 13,RMODE31,&RMODE31   ISSUE WARNING MNOTE    @D22VDUS 10950000
.VSMA132 ANOP                                                  @D22VDUS 11000000
&RM31    SETB  0                        SET OFF RMODE31 BIT    @D22VDUS 11050000
         AGO   .VSMA135                 GO AROUND              @D22VDUS 11100000
.VSMA133 ANOP                                                  @D22VDUS 11150000
&RM31    SETB  1                        SET ON  RMODE31 BIT    @D22VDUS 11200000
.VSMA135 AIF   (NOT &LSR OR NOT &NSR).VSMA137 NOT BOTH         @D22MDUS 11250000
&LSR     SETB  0                        RESET TO NSR           @D33VDVG 11300000
         IKQERMAC 16,LSR                ILL. OR DUPL IN MACRF  @D33VDVG 11350000
.VSMA137 AIF   (NOT &DFR OR NOT &NDF).VSMA139 NOT BOTH         @D13EDWS 11400000
&DFR     SETB  0                        RESET TO NDF           @D33VDVG 11450000
         IKQERMAC 16,DFR                ILL. OR DUPL IN MACRF  @D33VDVG 11500000
.VSMA139 AIF   (NOT &VFY OR NOT &NFY).VSMA140 NOT BOTH         @D13EDWS 11550000
&VFY     SETB  0                        RESET TO NFY           @D13EDWS 11600000
         IKQERMAC 16,VFY                ILL. OR DUPL IN MACRF  @D13EDWS 11650000
.VSMA140 AIF   (NOT &ICI).VSMA150       CHECK FOR OS-OPTION             11700000
         IKQERMAC 20,MACRF,ICI,DOS/VS   INVALID DOS OPTION              11750000
.VSMA150 AIF   (NOT &NCI).VSMA200       CHECK FOR OS-OPTION    @D21ADJP 11800000
         IKQERMAC 20,MACRF,NCI,DOS/VS   INVALID DOS OPTION              11850000
.VSMA200 AIF   (NOT &GSR).VSMA230       CHECK FOR OS-OPTION    @D33VDVG 11900000
         IKQERMAC 20,MACRF,GSR,DOS/VS   INVALID DOS OPTION              11950000
.VSMA230 AIF   (NOT &NIS).VSMA240       CHECK FOR OS-OPTION             12000000
         IKQERMAC 20,MACRF,NIS,DOS/VS   INVALID DOS OPTION              12050000
.VSMA240 AIF   (NOT &SIS).VSMA250       CHECK FOR OS-OPTION             12100000
         IKQERMAC 20,MACRF,SIS,DOS/VS   INVALID DOS OPTION              12150000
.VSMA250 AIF   (NOT &NFX).VSMA260       CHECK FOR OS-OPTION             12200000
         IKQERMAC 20,MACRF,NFX,DOS/VS   INVALID DOS OPTION              12250000
.VSMA260 AIF   (NOT &CFX).VSMA270       CHECK FOR OS-OPTION    @D21ADJP 12300000
         IKQERMAC 20,MACRF,CFX,DOS/VS   INVALID DOS OPTION              12350000
.VSMA270 AIF   (NOT &DDN OR NOT &DSN).VSMA300 NOT BOTH         @D21ADJP 12400000
         IKQERMAC 8,MACRF,DDN,DSN       INCOMPATIBLE ITEMS     @D21ADJP 12450000
.VSMA300 ANOP                           SET ALL DEFAULTS                12500000
&KEY     SETB  (NOT &ADR AND NOT &CNV OR &KEY)  KEY IF NOT CONTROL INT  12550000
&IN      SETB  (NOT &OUT AND NOT &RST OR &IN) INP IF NOT OUTP           12600000
&OUT     SETB  (NOT &IN AND &RST OR &OUT)     OUT IF RST                12650000
&SEQ     SETB  (NOT &DIR AND NOT &SKP OR &SEQ)  SEQ IF NOT DIRECT       12700000
&NRM     SETB  (NOT &AIX OR &NRM)       NRM IF NOT AIX                  12750000
.*                                                                      12800000
.*       SET UP THE CONTROL BLOCK AS SPECIFIED                          12850000
.*                                                                      12900000
.VSMB010 ANOP                                                           12950000
&NAME    DS    0F                                                       13000000
         DC    X'A0' .                  ACBID                           13050000
         DC    X'40' .                  ACB SUBTYPE FIELD      @D21ADGW 13100000
         DC    AL2(76) .                ACB LENGTH             @D21RDGW 13150000
         DC    A(0) .                   ACBAMBL POINTER                 13200000
         DC    A(0)  .                  VSAM REC.MGT. MODULE ADDRESS    13250000
         DC    X'00' .                  TEST & SET BYTE                 13300000
         DC    B'&SCRA&UCRA.000000' .   CRA FLAG-BYTE                   13350000
         AIF   ('&BUFND' EQ '').BUFNO   BR IF &BUFND NOT SPEC  @D33VDVG 13400000
         DC    AL2(&BUFND) .            NUMBER OF DATA BUFFERS @D33VDVG 13450000
         AGO   .BUFCON                                         @D33VDVG 13500000
.BUFNO   ANOP                                                  @D33VDVG 13550000
         DC    AL2(0) .                 NUMBER OF DATA BUFFERS @D33VDVG 13600000
.BUFCON  ANOP                                                  @D33VDVG 13650000
         AIF   ('&BUFNI' EQ '').NINO    BR IF &BUFNI NOT SPEC  @D33VDVG 13700000
         DC    AL2(&BUFNI) .            NUMBER OF INDEX BUFFERS@D33VDVG 13750000
         AGO   .NICON                                          @D33VDVG 13800000
.NINO    ANOP                                                  @D33VDVG 13850000
         DC    AL2(0) .                 NUMBER OF INDEX BUFFERS@D33VDVG 13900000
.NICON   ANOP                                                  @D33VDVG 13950000
         DC    B'&KEY&ADR&CNV&SEQ&DIR&IN&OUT&UBF' . MACRF(1) BYTE       14000000
         DC    B'&LSR&DFR&SKP&RST&AIX&VFY.00' . MACRF(2) BYTE  @D13EDWS 14050000
         DC    AL1(40)  .               AM/0 DOS DTF ID                 14100000
         DC    B'0000&CATLG.000' .      OPEN / CLOSE FLAGS              14150000
         AIF   ('&STRNO' EQ '').STRNO   BR IF &STRNO NOT SPEC  @D33VDVG 14200000
         DC    AL1(&STRNO) .            NUMBER OF STRINGS      @D33VDVG 14250000
         AGO   .STRCON                                         @D33VDVG 14300000
.STRNO   ANOP                                                  @D33VDVG 14350000
         DC    AL1(0) .                 NUMBER OF STRINGS      @D33VDVG 14400000
.STRCON  ANOP                                                  @D33VDVG 14450000
         DC    AL1(0)  .                ERROR FLAGS                     14500000
         AIF   ('&BUFSP' EQ '').SPNO    BR IF &BUFSP NOT SPEC  @D33VDVG 14550000
         DC    A(&BUFSP) .              BUFFER SPACE           @D33VDVG 14600000
         AGO   .SPCON                                          @D33VDVG 14650000
.SPNO    ANOP                                                  @D33VDVG 14700000
         DC    A(0) .                   BUFFER SPACE           @D33VDVG 14750000
.SPCON   ANOP                                                  @D33VDVG 14800000
         DC    CL8'&IDDNAM' .           DDNAME                          14850000
         AIF   ('&PASSWD' EQ '').PASNO  BR IF &PASSWD NOT SPEC @D33VDVG 14900000
         DC    A(&PASSWD) .             POINTER TO PASSWORD    @D33VDVG 14950000
         AGO   .PASCON                                         @D33VDVG 15000000
.PASNO   ANOP                                                  @D33VDVG 15050000
         DC    A(0) .                   POINTER TO PASSWORD    @D33VDVG 15100000
.PASCON  ANOP                                                  @D33VDVG 15150000
         AIF   ('&USERPTR' EQ '').USNO  BR IF &USERPTR NOT SPEC@D33VDVG 15200000
         DC    A(&USERPTR) .            PTR TO USER WORK AREA  @D33VDVG 15250000
         AGO   .USCON                                          @D33VDVG 15300000
.USNO    ANOP                                                  @D33VDVG 15350000
         DC    A(0) .                   PTR TO USER WORK AREA  @D33VDVG 15400000
.USCON   ANOP                                                  @D33VDVG 15450000
         DC    A(0)  .                  BUFFER ADDRESS                  15500000
         AIF   ('&EXLST' EQ '').EXNO    BR IF &EXLST NOT SPEC  @D33VDVG 15550000
         DC    A(&EXLST) .              EXIT LIST POINTER      @D33VDVG 15600000
         AGO   .EXCON                                          @D33VDVG 15650000
.EXNO    ANOP                                                  @D33VDVG 15700000
         DC    A(0) .                   EXIT LIST POINTER      @D33VDVG 15750000
.EXCON   ANOP                                                  @D33VDVG 15800000
         DC    A(0) .                   PTR TO BAM PARM LIST   @D36WE86 15850000
         AIF   ('&BSTRNO' EQ '').STRNONO BR IF NO BSTRNO       @D21ADWS 15900000
         DC    AL1(&BSTRNO) .           DSN STRING NUMBER      @D21ADWS 15950000
         AGO   .STRNCON                                        @D21ADWS 16000000
.STRNONO ANOP                                                  @D21ADWS 16050000
         DC    AL1(0) .                 DSN STRING NUMBER      @D21ADWS 16100000
.STRNCON ANOP                                                  @D21ADWS 16150000
         DC    B'00&K1&D1&KEEP&DELETE&DATE.0' . MORE O/C FLAGS @D13EDWS 16200000
         AIF   ('&MLEN' EQ '').MLNO     BR IF &MLEN NOT SPEC   @D33VDVG 16250000
         DC    AL2(&MLEN) .             MESSAGE AREA LENGTH    @D33VDVG 16300000
         AGO   .MLCON                                          @D33VDVG 16350000
.MLNO    ANOP                                                  @D33VDVG 16400000
         DC    AL2(0) .                 MESSAGE AREA LENGTH    @D33VDVG 16450000
.MLCON   ANOP                                                  @D33VDVG 16500000
         AIF   ('&MAREA' EQ '').MANO    BR IF &MAREA NOT SPEC  @D33VDVG 16550000
         DC    A(&MAREA) .              MESSAGE AREA           @D33VDVG 16600000
         AGO   .MACON                                          @D33VDVG 16650000
.MANO    ANOP                                                  @D33VDVG 16700000
         DC    A(0) .                                          @D33VDVG 16750000
.MACON   ANOP                                                  @D33VDVG 16800000
         AIF   ('&DSNAME' EQ '').DSNO   BR IF &DSNAME NOT SPEC @D13CDWS 16850000
         DC    A(&DSNAME) .             PTR TO 44 CHAR NAME    @D13CDWS 16900000
         AGO   .DSCON                                          @D13CDWS 16950000
.DSNO    ANOP                                                  @D13CDWS 17000000
         DC    A(0) .                   PTR TO 44 CHAR NAME    @D36WE86 17050000
.DSCON   ANOP                                                  @D13CDWS 17100000
         DC    B'&DSN.000&CMP.00&RM31.' .   MACRF(3) BYTE      @D61GDWM 17150061
         DC    B'00000000' .            MACRF(4) BYTE(NOT USED)@D22VDUS 17200000
.SHRPOOL ANOP                                                  @D22MDUS 17250000
         AIF   (NOT &LSR).SHRNO          BR IF NOT LSR         @D22MDUS 17300000
         AIF   ('&SHRPOOL' EQ '').SHRNO  BR IF SHRPOOL NOT SPEC@D22MDUS 17350000
         AIF   ('&SHRPOOL' LE '15' AND '&SHRPOOL' GE '0').SHRP @D22MDUS 17400000
.*                                      IF CORRECT VALUE GO    @D22MDUS 17450000
         IKQERMAC 3,SHRPOOL,&SHRPOOL    ELSE ISSUE ERROR MSG   @D22MDUS 17500000
.SHRP    ANOP                                                  @D22MDUS 17550000
         DC    AL1(&SHRPOOL)            DECLARE SHRPOOL VALUE  @D22MDUS 17600000
         AGO   .SHRCON                  SKIP AROUND            @D22MDUS 17650000
.SHRNO   DC    AL1(0)                   SHRPOOL DEFAULT VALUE  @D22MDUS 17700000
.SHRCON  DS    XL1 .                    RESERVED 1 BYTE        @D22MDUS 17750000
         DS    F .                      RESERVED WORD          @D21ADJP 17800000
         MEND                                                           17850000
