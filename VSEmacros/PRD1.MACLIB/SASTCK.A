         MACRO                                                          00050000
         SASTCK  &MTXT,&MNO=,&MRPL=,&ORPL=,                            X00100000
               &PNAME=IJWCUST,&AUTO=NO,&EOD=NO,&DEBUG=NO      @D61DDHB  00150061
         GBLA   &FRST        FIRST CALL OF THIS MACRO                   00200000
         GBLA   &NOMORE      BEYOND LAST CALL OF THIS MACRO             00250000
.* A000000-999999                                             @D31NMHB  00300000
.******************************************************                 00350000
.*                                                    *                 00400000
.*   LICENSED MATERIALS - PROPERTY OF IBM             *                 00450000
.*   THIS MACRO IS "RESTRICTED MATERIALS OF IBM"      *                 00500000
.*   5686-066 (C) COPYRIGHT IBM CORP. 1990,1995       *       @D61DDHB  00550061
.*   SEE COPYRIGHT INSTRUCTIONS                       *                 00600000
.*                                                    *                 00650000
.******************************************************                 00700000
.*                                                                      00750000
.*   MACRO TO BUILD THE QUEUE WITH ALL ANSWERS (SA CONSOLE INPUT)       00800000
.*   AND A TABLE WITH MODIFIED MESSAGES AND MESSAGE REPLIES FOR         00850000
.*   STAND ALONE VSE PROCESSING                                         00900000
.*                                                                      00950000
.*   MNO MUST BE 5 CHARS            ...  MESSAGE NUMBER                 01000000
.*   MTXT MAY NOT EXCEED 80 CHARS   ...  MESSAGE/ANSWER TEXT STRING     01050000
.*   MRPL MAY NOT EXCEED 8 CHARS    ...  MODIFIED DEFINED REPLY STRING  01100000
.*   ORPL MAY NOT EXCEED 8 CHARS    ...  ORIGINAL REPLY STRING          01150000
.*   PNAME MAY NOT EXCEED 8 CHARS   ...  NAME OF SA PHASE IN LIBRARY    01200000
.*   AUTO                           ...  AUTO INSTALLATION    @D61DDHB  01225061
.*   EOD                            ...  INDICATOR FOR LAST MACRO CALL  01250000
.*                                                                      01300000
.*   IF MNO IS SPECIFIED, A MESSAGE IS DEFINED, ELSE AN ANSWER QUEUED   01350000
.*                                                                      01400000
.*   ALWAYS PUT PARAMETERS MTXT, MRPL AND ORPL IN QUOTES                01450000
.*   NEVER PUT PARAMETER PNAME AND MNO IN QUOTES                        01500000
.*   PARAMETER EOD: ALL OTHER VALUES BUT "NO" WILL FORCE                01550000
.*                  END INDICATION OF MACRO CALL SEQUENCE               01600000
.*                                                                      01650000
         AIF   ('&NOMORE' EQ '1').B65               EOD ALREADY OCCURED 01700000
         AIF   ('&FRST' EQ '1').NEXT                SUBSEQUENT CALL     01750000
.*   FIRST CALL OF THIS MACRO                                           01800000
&FRST    SETA  1                                    FIRST CALL OCCURED  01850000
&NOMORE  SETA  1                                    ASSUME FATAL ERROR  01900000
         AIF   ('&PNAME' EQ '').B80                 EMPTY PHASE NAME    01950000
         AIF   (K'&PNAME GT 8).B80                  BAD PHASE NAME      02000000
         AIF   ('&PNAME'(1,1) EQ '''').B50          QUOTED STRING       02050000
&NOMORE  SETA  0                                    RESET ERROR INDIC.  02100000
         AIF   ('&PNAME' NE 'IJWCUST').EXPNAM                           02150000
         MNOTE '>>> MNOTE <<< DEFAULT PHASE NAME USED: IJWCUST'         02200000
.EXPNAM  ANOP                                                           02250000
SAQ      TITLE 'SA MESSAGE AND REPLY AUTOMATION'                        02300000
*         ISEQ  73,80                                                   02350000
         SPACE                                                          02400000
* ** START OF SPECIFICATIONS ****************************************** 02450000
*                                                                       02500000
* ** MODULE-NAMES = IJWANSWR IJWMESTB                                   02550000
*                                                                       02600000
* ** DESCRIPTIVE-NAME = VSE STAND ALONE MESSAGE AND REPLY AUTOMATION    02650000
*                                                                       02700000
*   *****************************************************               02750000
*   *                                                   *               02800000
*   *   LICENSED MATERIALS - PROPERTY OF IBM            *               02850000
*   *   THIS MODULE IS "RESTRICTED MATERIALS OF IBM"    *               02900000
*   *   5686-066 (C) COPYRIGHT IBM CORP. 1990,1995      *      @D61DDHB 02950061
*   *   SEE COPYRIGHT INSTRUCTIONS                      *               03000000
*   *                                                   *               03050000
*   *****************************************************               03100000
*                                                                       03150000
* ** FUNCTION = BUILD A QUEUE FOR ANSWERS AND A TABLE FOR MESSAGE TEXTS 03200000
*                                                                       03250000
* ** END OF SPECIFICATIONS *******************************************  03300000
         PUNCH ' PHASE &PNAME,*,SVA'                          @DXXXXCS  03350061
IJWANSWR CSECT                                                          03400000
         DC    CL7'IJWCUST'                                   @D61DDHB  03415061
         AIF   ('&AUTO' EQ 'YES').AUTOIN                      @D61DDHB  03430061
         DC    CL1'0'   INDICATE POST-ESA-130 USER TABLE      @D61DDHB  03445061
.***     DC    CL1' '   HAS BEEN BLANK IN OLDER RELEASES      @D61DDHB  03460061
         AGO   .NOINST  NO IBM INSTALLATION TABLE             @D61DDHB  03475061
.AUTOIN  ANOP                                                 @D61DDHB  03490061
         DC    CL1'I'   INDICATE AUTO INSTALLATION TABLE      @D61DDHB  03505061
.NOINST  ANOP           RETURN TO CALLER                      @D61DDHB  03520061
         BR    2        FOR COMPATIBILITY WITH OLD SA FETCH   @D61DDHB  03535061
         AIF   ('&DEBUG' EQ 'YES').FLDEBG                     @D61DDHB  03545861
         DC    XL1'0'                                         @D61DDHB  03556661
         AGO   .DEBGCT   GO TO DEBUG CONTINUE                 @D61DDHB  03567461
.FLDEBG  DC    XL1'80'   SET DEBUG FLAG ON                    @D61DDHB  03578261
.DEBGCT  DC    XL1'0'                                         @D61DDHB  03589061
         DC    A(IJWANSWR)                                              03600000
         DC    A(IJWANSBG)                                              03650000
         DC    A(IJWANSED)                                              03700000
         DC    V(IJWMESBG)                                              03750000
         DC    V(IJWMESED)                                              03800000
         DC    C' IJWANSWR '                                            03850000
         DC    C'5686-032 (C) COPYRIGHT IBM CORP. 1990 '       @D31NMAS 03900000
IJWANSBG DS    0F                                                       03950000
IJWMESTB CSECT                                                          04000000
         ENTRY IJWMESBG                                                 04050000
         ENTRY IJWMESED                                                 04100000
         DC    C' IJWMESTB '                                            04150000
         DC    C'5686-032 (C) COPYRIGHT IBM CORP. 1990 '       @D31NMAS 04200000
IJWMESBG DS    0F                                                       04250000
         AIF   (T'&MTXT NE 'O').NEXT               FIRST CALL ONLY      04300000
         AIF   ('&EOD' NE 'NO').LAST               LAST CALL INDICATED  04350000
         MEXIT                                                          04400000
.NEXT    ANOP                                                           04450000
         AIF   (T'&MTXT EQ 'O').B25                NO TEXT SPECIFIED    04500000
         AIF   ('&MTXT'(1,1) NE '''').B45          NO STRING QUOTES     04550000
         AIF   (K'&MTXT GT 82).B90                 MORE THAN 80 CHARS   04600000
         AIF   (T'&MNO EQ 'O').B00                 NO MESSAGE NUMBER    04650000
         AIF   (K'&MNO NE 5).B75                   NOT EXACTELY 5 CHARS 04700000
         AIF   ('&MNO'(1,1) EQ '''').B60           STRING QUOTES        04750000
.* *****************************************************                04800000
.*   MACRO TO GENERATE CODE FOR MESSAGE MODIFICATIONS                   04850000
.* *****************************************************                04900000
IJWMESTB CSECT                                                          04950000
         AIF   ('&MTXT' EQ '''''').B95             MESSAGE TEXT EMPTY   05000000
         AIF   (T'&MRPL NE T'&ORPL).B55            BOTH MANDATORY/NOT   05050000
         AIF   (K'&ORPL GT 10).B70                 MAXIMAL 8 CHARS      05100000
         AIF   (K'&MRPL GT 10).B85                 MAXIMAL 8 CHARS      05150000
         DC    A(MM1L&SYSNDX)                                           05200000
         AIF   ('&MRPL' EQ '''''').A05             REPLY IS "ENTER"     05250000
         AIF   (T'&MRPL EQ 'O').A05                NO REPLY SPECIFIED   05300000
         DC    A(MM2L&SYSNDX)                                           05350000
         AGO   .A10                                LENGTH OF NEW REPLY  05400000
.A05     ANOP                                      REPLY LENGTH IS 0    05450000
         DC    A(0)                                                     05500000
.A10     ANOP                                      ORIGINAL REPLY IS    05550000
         AIF   ('&ORPL' EQ '''''').A15                  "ENTER"         05600000
         AIF   (T'&ORPL EQ 'O').A15                     OMITTED         05650000
         DC    A(MM3L&SYSNDX)                                           05700000
         AGO   .A20                                LENGTH OF ORG. REPLY 05750000
.A15     ANOP                                      ORG REPLY LENGTH=0   05800000
         DC    A(0)                                                     05850000
.A20     ANOP                                      MESS.NO. + ALIGNMENT 05900000
         DC    CL5'&MNO'                                                05950000
         DC    CL3' '                                                   06000000
MM1B&SYSNDX  EQU  *                                                     06050000
         DC    C&MTXT                                                   06100000
MM1L&SYSNDX  EQU  *-MM1B&SYSNDX                                         06150000
         AIF   (K'&MTXT EQ 82).A23                 MESS. TEXT 80 CHARS  06200000
         DC    CL(80-MM1L&SYSNDX)' '                                    06250000
.A23     ANOP                                                           06300000
MM2B&SYSNDX  EQU  *                                                     06350000
         AIF   ('&MRPL' EQ '''''').A25             REPLY IS "ENTER"     06400000
         AIF   (T'&MRPL  EQ 'O').A25               REPLY OMITTED        06450000
         DC    C&MRPL                                                   06500000
MM2L&SYSNDX  EQU  *-MM2B&SYSNDX                                         06550000
         AIF   (K'&MRPL EQ 10).A30                 PADDING BLANKS ?     06600000
         DC    CL(8-MM2L&SYSNDX)' '                                     06650000
         AGO  .A30                                                      06700000
.A25     ANOP                                      NO REPLY SPECIFIED   06750000
         DC    CL8' '                                                   06800000
.A30     ANOP                                                           06850000
         AIF   ('&ORPL' EQ '''''').A35             NO ORG. REPLY TEXT   06900000
         AIF   (T'&ORPL EQ 'O').A35                                     06950000
MM3B&SYSNDX  EQU  *                                                     07000000
         DC    C&ORPL                                                   07050000
MM3L&SYSNDX  EQU  *-MM3B&SYSNDX                                         07100000
         AIF   (K'&ORPL EQ 10).A40                 PADDING BLANKS ?     07150000
         DC    CL(8-MM3L&SYSNDX)' '                                     07200000
         AGO   .A40                                                     07250000
.A35     ANOP                                      NO REPLY TEST        07300000
         DC    CL8' '                                                   07350000
.A40     ANOP                                                           07400000
         AIF   ('&EOD' NE 'NO').LAST               LAST CALL INDICATED  07450000
         MEXIT                                                          07500000
.* *****************************************************                07550000
.*   MACRO TO BUILD THE QUEUE WITH ALL STACKED ANSWERS *                07600000
.* *****************************************************                07650000
.B00     ANOP                                                           07700000
IJWANSWR CSECT                                                          07750000
         AIF   ('&MTXT' EQ '''''').B20            ANSWER IS "ENTER"     07800000
         DC    A(FCPL&SYSNDX)                                           07850000
FCPB&SYSNDX  EQU  *                                                     07900000
         DC    C&MTXT                                                   07950000
FCPL&SYSNDX  EQU  *-FCPB&SYSNDX                                         08000000
         AIF   (K'&MTXT EQ 82).B10                PADDING BLANKS ?      08050000
         DC    CL(80-FCPL&SYSNDX)' '                                    08100000
.B10     ANOP                                                           08150000
         AIF   ('&EOD' NE 'NO').LAST              LAST CALL INDICATED   08200000
         MEXIT                                                          08250000
.B20     ANOP                                     "ENTER": LENGTH IS 0  08300000
         DC    A(0)                                                     08350000
         DC    CL80' '                                                  08400000
         AIF   ('&EOD' NE 'NO').LAST              LAST CALL INDICATED   08450000
         MEXIT                                                          08500000
.B25     ANOP                                                           08550000
         AIF   ('&EOD' EQ 'NO').B95               CLOSE TABLE CREATE    08600000
.LAST    ANOP                                                           08650000
&NOMORE  SETA  1                                  SUPPRESS NEXT CALLS   08700000
********************************************************************    08750000
*  FOLLOWING THE MANDATORY LAST ELEMENT OF THE ANSWER LIST              08800000
********************************************************************    08850000
IJWANSWR CSECT                                                          08900000
         DC    A(2)                                                     08950000
         DC    CL2'/*'                                                  09000000
         DC    CL59' '                                                  09050000
         DC    CL19' THIS IS ANSWER END'                                09100000
IJWANSED EQU   *                                                        09150000
********************************************************************    09200000
*  FOLLOWING THE MANDATORY LAST ELEMENT OF THE MESSAGE LIST             09250000
********************************************************************    09300000
IJWMESTB CSECT                                                          09350000
         DC    A(80)                                                    09400000
         DC    A(8)                                                     09450000
         DC    A(8)                                                     09500000
         DC    CL5'/*   '                                               09550000
         DC    CL3' '                                                   09600000
         DC    CL80' '                                                  09650000
         DC    CL8'THIS IS '                                            09700000
         DC    CL8'TABLEEND'                                            09750000
IJWMESED EQU   *                                                        09800000
********************************************************************    09850000
         MEXIT                                                          09900000
.B45     ANOP                                                           09950000
         MNOTE 8,'>>> MNOTE <<< TEXT MUST BE ENCLOSED IN QUOTES'        10000000
         MEXIT                                                          10050000
.B50     ANOP                                                           10100000
         MNOTE 8,'>>> MNOTE <<< PNAME MUST NOT BE ENCLOSED IN QUOTES'   10150000
         MEXIT                                                          10200000
.B55     ANOP                                                           10250000
         MNOTE 8,'>>> MNOTE <<< MRPL REQUIRES ORPL AND VICE VERSA'      10300000
         MEXIT                                                          10350000
.B60     ANOP                                                           10400000
         MNOTE 8,'>>> MNOTE <<< MNO MUST NOT BE ENCLOSED IN QUOTES'     10450000
         MEXIT                                                          10500000
.B65     ANOP                                                           10550000
         MNOTE 8,'>>> MNOTE <<< NO EXPANSION, EOD PREVIOUSLY SPECIFIED' 10600000
         MEXIT                                                          10650000
.B70     ANOP                                                           10700000
         MNOTE 8,'>>> MNOTE <<< PARAMETER ORPL LONGER THAN 8 CHARS'     10750000
         MEXIT                                                          10800000
.B75     ANOP                                                           10850000
         MNOTE 8,'>>> MNOTE <<< PARAMETER MNO IS NOT 5 CHARS LONG'      10900000
         MEXIT                                                          10950000
.B80     ANOP                                                           11000000
         MNOTE 8,'>>> MNOTE <<< PARAMETER PNAME MUST BE 1 TO 8 CHARS'   11050000
         MEXIT                                                          11100000
.B85     ANOP                                                           11150000
         MNOTE 8,'>>> MNOTE <<< PARAMETER MRPL LONGER THAN 8 CHARS'     11200000
         MEXIT                                                          11250000
.B90     ANOP                                                           11300000
         MNOTE 8,'>>> MNOTE <<< PARAMTER MTXT LONGER THAN 80 CHARS'     11350000
         MEXIT                                                          11400000
.B95     ANOP                                                           11450000
         MNOTE 8,'>>> MNOTE <<< ANSWER / MESSAGE TEXT REQUIRED'         11500000
         MEXIT                                                          11550000
         MEND                                                           11600000
