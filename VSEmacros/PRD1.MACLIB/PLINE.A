         MACRO                                                          00050000
         PLINE &ADDR=,&TRNSP=YES,&CODE=EBCDIC,&PSWRD=,&TIMEOUT=NO,     X00100000
               &SWITCH=NO,                                     @D33RDTPX00150000
               &MODSET=AA,                                             X00200000
               &INTRPT=NO,                                     @DA30816X00250000
               &DUMMY=DUMMY        END OF PROTOTYPE STATEMENT  @D03PIPW 00300000
.*                                                                      00350000
.*       CHANGE-ACTIVITY = AS FOLLOWING                                 00400000
.*             CAF FOR INITIAL DEVELOPMENT IS                           00450000
.*       INCORRECT MODE SETTING                                @DA23510 00500000
.*       INTERRUPT MODE SET TO 2701 IF NOT NEEDED              @DA30816 00550000
.*                                                                      00600000
         GBLA  &GBL1               POWER + BSC TABLE LENGTH             00650000
         GBLA  &GBL2               NUMBER OF BSC LINES                  00700000
         GBLA  &GBL3               NUMBER OF BSC REMOTES                00750000
         GBLA  &GBL4               MACRO SEQUENCE CHECKING              00800000
         GBLA  &GBL5               POWER+BSC+SNA TBL LNGTH     @D32DUSN 00850000
         GBLA  &GBL6               NUMBER OF SNA REMOTES       @D32DUSN 00900000
         GBLA  &GBL7               FOR REMOTE ID CHECKING      @D32DUSN 00950000
         GBLA  &GBL8               FIRST SNA REMOTE ID         @D32DUSN 01000000
         GBLA  &NLUS               NR OF SNA LOG UNITS         @D32DUSN 01050000
         GBLB  &EQU                EQUATES GENERATED           @D35BIML 01100000
.*                                                                      01150000
         LCLA  &BL1                LINE ADDRESS                         01200000
         LCLA  &BL2                TIMEOUT VALUE                        01250000
         LCLA  &TBL                LINE TABLE ENTRY LENGTH     @D03PIPW 01300000
.*                                                                      01350000
         LCLB  &TIMV               TIMEOUT NO DEFAULT          @D35BIML 01400000
.*                                                                      01450000
         LCLC  &BLC                LINE PASSWORD                        01500000
         LCLC  &P                  PREFIX                      @D35BIML 01550000
.*                                                             @D35BIML 01600000
.*             FEATURE BITS                                    @D35BIML 01650000
.*                                                             @D35BIML 01700000
         LCLC  &BASCII,&BTRANS,&BSWITC                         @D03PIPW 01750000
         LCLC  &BCBIFB,&BCTRMB,&BCIRRM                         @DA30816 01800000
.*                                                                      01850000
         AGO   .ZZZ                                                     01900000
*        LICENSED MATERIALS - PROPERTY OF IBM                           01950000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                    02000000
*        5686-CF7 (C) COPYRIGHT IBM CORP. 1979, 2004                    02050001
*        SEE COPYRIGHT INSTRUCTIONS                                     02100000
.ZZZ     ANOP                                                           02150000
.*                                                                      02200000
&TBL     SETA  16                  SET LINE TABLE ENTRY LENGTH @D03PIPW 02250000
.*                                                                      02300000
&BASCII  SETC '0'                  USASCII                     @D35BIML 02350000
&BTRANS  SETC '0'                  TRANSPARENCY FOR THIS LINETE@D35BIML 02400000
&BSWITC  SETC '0'                  SWITCHED LINE               @D35BIML 02450000
.*                                                             @D35BIML 02500000
&BCBIFB  SETC '0'                  BASIC INTERFACE B SELECTED  @D35BIML 02550000
&BCTRMB  SETC '0'                  TRANSMISSION MODE B SELECTED@D35BIML 02600000
&BCIRRM  SETC '0'                  INTERRUPT MODE REQUESTED    @DA30816 02650000
.*                                                                      02700000
&BLC     SETC  ' '                 SET DEFAULT LINE PASSWORD   @D35BIML 02750000
.*                                                                      02800000
*                                                                       02850000
.* IBM DISK OPERATING SYSTEM                                            02900000
         SPACE 1                                                        02950000
* VSE/POWER - PLINE - 5686-CF7-03                                       03000001
         SPACE                                                          03050000
.*                                                                      03100000
.*       ADDR SPECIFICATION CHECKING   ************                     03150000
.*                                                                      03200000
.*             THE ADDR PARAMETER SPECIFIES THE LINE ADDRESS OR         03250000
.*             CAN BE 'RJL' TO GENERATE A DSECT MAP OR CAN BE           03300000
.*             'LCB' WHEN INVOKED FROM IPW$DLC AS AN INNER MACRO        03350000
.*             TO GENERATE THE LINE TABLE ENTRY FIELDS IN THE LCB.      03400000
.*             'NCB' IS ALSO VALID WHEN INVOKED FROM IPW$DNC            03450000
.*             TO GENERATE THE LINE TABLE ENTRY FIELDS IN THE NCB.      03500000
.*                                                                      03550000
&P       SETC  'LCB'               ASSUME INVOKATION BY $DLC   @D35BIML 03600000
         AIF   ('&ADDR' EQ 'LCB').GENIT PLINE WITHIN LCB ?     @D03PIPW 03650000
&P       SETC  'NCB'               ASSUME INVOKATION BY $DLC   @D03PIPW 03700000
         AIF   ('&ADDR' EQ 'NCB').GENIT PLINE WITHIN NCB ?     @D03PIPW 03750000
         AIF   ('&ADDR' NE 'RJL').NORJL  GEN RJL-DSECT ?       @D35BIML 03800000
.*             YES, DO IT                                      @D35BIML 03850000
RJELDS   DSECT                     LINE TABLE ENTRY LAYOUT     @D35BIML 03900000
&P       SETC  'RJL'                                           @D35BIML 03950000
         AGO   .GENIT                                          @D03PIPW 04000000
.*                                                                      04050000
.NORJL   ANOP                                                  @D35BIML 04100000
&P       SETC  'L&GBL2'                                        @D35BIML 04150000
.*                                                             @D35BIML 04200000
.*             CHECK IF MACRO OUT OF SEQUENCE                           04250000
.*                                                                      04300000
         AIF   (&GBL4 EQ 0).MNOTE1                                      04350000
         AIF   (&GBL2 EQ 100).MNOTE1  MORE THAN 100 LINES ?    @D22DDHS 04400000
         AIF   (&GBL3 NE 0).MNOTE1                                      04450000
         AIF   (&GBL6 NE 0).MNOTE1                             @D32DUSN 04500000
.*                                                             @D35BIML 04550000
         AIF   (T'&ADDR NE 'N').MNOTE2                                  04600000
&BL1     SETA  &ADDR               TAKE USER SPECIFIED LINE ADDR.       04650000
         AGO   .TIMEOUT                                        @D03PIPW 04700000
.*                                                                      04750000
.MNOTE1  MNOTE  5,'MACRO OUT OF SEQUENCE - MACRO IGNORED.'              04800000
         MEXIT                                                          04850000
.MNOTE2  MNOTE 5,'INVALID ADDR SPECIFICATION - MACRO IGNORED.'          04900000
         MEXIT                                                          04950000
.*                                                                      05000000
.*       TIMEOUT SPECIFICATION CHECKING  **************                 05050000
.*                                                                      05100000
.TIMEOUT ANOP                                                  @D03PIPW 05150000
&BL2     SETA  0                   SET DEFAULT VALUE TO NO     @D35BIML 05200000
.*                                 LEASED LINE ASSUMED                  05250000
.*                                 MAY BE CHANGED TO 3 FOR SWITCHED.    05300000
         AIF   ('&TIMEOUT' EQ 'NO').TRNSP                               05350000
         AIF   (T'&TIMEOUT NE 'N' OR &TIMEOUT GT 255).MNOTE3            05400000
&BL2     SETA  &TIMEOUT                                                 05450000
&TIMV    SETB  1                    NO DEFAULT VALUE           @D35BIML 05500000
         AGO   .TRNSP                                                   05550000
.*                                                                      05600000
.MNOTE3  MNOTE 3,'INVALID TIMEOUT SPECIFICATION - TIMEOUT=NO ASSUMED'   05650000
.*                                                                      05700000
.*                                                                      05750000
.*       TRNSP SPECIFICATION CHECKING  **************                   05800000
.*                                                                      05850000
.*             VALID SPECIFICATION FOR TRNSP ARE 'YES' OR 'NO'          05900000
.*             DEFAULT IS 'YES'                                         05950000
.TRNSP   ANOP                                                           06000000
         AIF   ('&TRNSP' EQ 'NO').CODE                                  06050000
&BTRANS  SETC  'F1TRANS'           SET DEFAULT                          06100000
         AIF   ('&TRNSP' EQ 'YES').CODE                                 06150000
         MNOTE 3,'INVALID TRNSP SPECIFICATION - TRNSP=YES ASSUMED'      06200000
.*                                                                      06250000
.*       CODE SPECIFICATION CHECKING  **************                    06300000
.*                                                                      06350000
.CODE    ANOP                                                           06400000
         AIF   ('&CODE' EQ 'EBCDIC').INTR                      @DA30816 06450000
&BASCII  SETC  'F1ASCII'                                                06500000
         AIF   ('&CODE' EQ 'ASCII').INTR                       @DA30816 06550000
         MNOTE 3,'INVALID CODE SPECIFICATION - CODE=ASCII ASSUMED.'     06600000
.INTR    ANOP                                                  @DA30816 06650000
         AIF   ('&INTRPT' EQ 'NO').MODSET                      @DA30816 06700000
         AIF   ('&INTRPT' EQ 'YES').INTRUP                     @DA30816 06750000
         MNOTE 3,'INVALID INTRPT SPECIFICATION - NO ASSUMED.'  @DA30816 06800000
         AGO   .MODSET                                         @DA30816 06850000
.INTRUP  ANOP                                                  @DA30816 06900000
&BCIRRM  SETC  'DUAIRRM'                                       @DA30816 06950000
.*                                                                      07000000
.*       MODSET SPECIFICATION CHECKING  **************                  07050000
.*                                                                      07100000
.MODSET  ANOP                                                           07150000
         AIF   ('&MODSET' EQ 'AA').PASS                                 07200000
&BCTRMB  SETC  'DUATRMB'                                       @D35BIML 07250000
         AIF   ('&MODSET' EQ 'AB').PASS                                 07300000
&BCTRMB  SETC  '0'                                             @D35BIML 07350000
&BCBIFB  SETC  'DUABIFB'                                                07400000
         AIF   ('&MODSET' EQ 'BA').PASS                                 07450000
&BCTRMB  SETC  'DUATRMB'                                       @D35BIML 07500000
         AIF   ('&MODSET' EQ 'BB').PASS                        @D35BIML 07550000
         MNOTE 3,'INVALID MODSET SPECIFICATION - MODSET=AA ASSUMED'     07600000
&BCBIFB  SETC  '0'                                             @D35BIML 07650000
&BCTRMB  SETC  '0'                                             @D35BIML 07700000
&BCIRRM  SETC  'DUAIRRM'                                       @DA30816 07750000
.*                                                                      07800000
.*       PASSWORD SPECIFICATION CHECKING   ************                 07850000
.*                                                                      07900000
.PASS    ANOP                                                           07950000
         AIF   (T'&PSWRD EQ 'O').SWITCH                        @D03PIPW 08000000
         AIF   (K'&PSWRD LT 9).PASSOK                                   08050000
         MNOTE 3,'PASSWORD TOO LONG - ONLY 8 CHARACTERS PROCESSED'      08100000
.PASSOK  ANOP                                                           08150000
&BLC     SETC  '&PSWRD'                                                 08200000
.*                                                                      08250000
.*       SWITCH SPECIFICATION CHECKING   ************                   08300000
.*                                                                      08350000
.SWITCH  ANOP                                                  @D03PIPW 08400000
         AIF   ('&SWITCH' EQ 'NO').SWITCH5                     @D33RDTP 08450000
         AIF   ('&SWITCH' NE 'YES').SWNO                       @DA10493 08500000
&BSWITC  SETC  'F1SWITC'           SET SWITCH LINE             @D33RDTP 08550000
         AGO   .SWITCH5                                        @DA10493 08600000
.SWNO    ANOP                                                  @DA10493 08650000
         MNOTE 3,'INVALID SWITCH SPECIFICATION - SWITCH=NO ASSUMED'     08700000
.*                                                                      08750000
.*       DEFAULT VALUE FOR TIME-OUT DELETED DUE TO APAR        @DA20666 08800000
.*                                                                      08850000
.SWITCH5 ANOP                                                  @D33RDTP 08900000
.*                                                                      08950000
.********************************************************************** 09000000
.*             EXPANSION OF LINE-TABLE ENTRY                          * 09050000
.********************************************************************** 09100000
.*                                                                      09150000
.GENIT   ANOP                                                  @D03PIPW 09200000
         AIF   ('&P' EQ 'LCB' OR '&P' EQ 'RJL').NOTIT          @D35BIML 09250000
         AIF   ('&P' EQ 'NCB').NOTIT                           @D03PIPW 09300000
         TITLE ' VSE/POWER          RJE,BSC   PLINE  DEFINITION  '      09350000
.NOTIT   ANOP                                                  @D35BIML 09400000
.*                                                                      09450000
&P.LPU   DC    AL2(&BL1)          PHYSICAL UNIT ADDRESS        @D35BIML 09500000
&P.TLIM  DC    AL2(&BL2.*20)      TIME OUT LIMIT(SECONDS)      @D35BIML 09550000
*                                 LINE FEATURES                @D35BIML 09600000
&P.FEA1  DC    AL1(&BASCII+&BTRANS+&BSWITC)  DUAL MODE         @D03PIPW 09650000
&P.LDM   DC    AL1(&BCBIFB+&BCTRMB+&BCIRRM)                    @DA30816 09700000
&P.L1    EQU   *-&P.LPU           LENGTH WITHOUT PASSWORD      @D35BIML 09750000
         AIF   ('&ADDR' EQ 'LCB').EXIT9                        @D35BIML 09800000
&P.LPW   DC    CL8'&BLC'          LINE PASSWORD                @D35BIML 09850000
         DC    XL2'0000'          UNUSED                       @D35BIML 09900000
         SPACE                                                          09950000
&P.L     EQU   *-&P.LPU           LENGTH OF LINE TAB ENTRY     @D35BIML 10000000
&P.NXT   EQU   *                  NEXT LINE TAB ENTRY          @D35BIML 10050000
         SPACE                                                          10100000
.*                                                             @D35BIML 10150000
         AIF   ('&ADDR' EQ 'RJL').NOEQU                        @D35BIML 10200000
         AIF   ('&ADDR' EQ 'NCB').NOEQU                        @D03PIPW 10250000
.*                                                             @D35BIML 10300000
&GBL1    SETA  &GBL1+&TBL         UPDATE LENGTH GEN TABLE      @D03PIPW 10350000
&GBL2    SETA  &GBL2+1            UPDATE NUMBER OF LINES                10400000
         ORG   IPW$POW2           ORG BACK TO POWER MACRO               10450000
         DC    AL2(&GBL1)         UPDATE TABLE LENGTH                   10500000
         ORG   IPW$POW3           ORG BACK TO POWER MACRO               10550000
         DC    AL1(&GBL2)         UPDATE NUMBER OF LINES                10600000
         ORG   IPW$POW8           ORG BACK TO POWER MACRO      @D32DUSN 10650000
&GBL5    SETA  &GBL1                                           @D32DUSN 10700000
         DC    AL2(&GBL5)         UPDATE TOTAL TABLE LENGTH    @D35BIML 10750000
         ORG   ,                  CONTINUE                              10800000
         SPACE 2                                                        10850000
.*                                                             @D35BIML 10900000
.EXIT9   ANOP                                                  @D35BIML 10950000
.*                                                                      11000000
.********************************************************************** 11050000
.*             GENERATION OF EQUATES                                  * 11100000
.********************************************************************** 11150000
.*                                                                      11200000
         AIF   (&EQU).NOEQU        BYPASS IT IF ALREADY DONE   @D35BIML 11250000
&EQU     SETB  1                   SET APPROPRIATE SWITCH      @D35BIML 11300000
         SPACE 3                                               @D35BIML 11350000
*                                                              @D35BIML 11400000
*        FEATURE - BITS                                        @D35BIML 11450000
*                                                              @D35BIML 11500000
F1ASCII  EQU   X'80'               USASCII                     @D35BIML 11550000
F1TRANS  EQU   X'40'               TRANSPARENCY FOR THIS LINE  @D35BIML 11600000
F1SWITC  EQU   X'20'               SWITCHED LINE               @D35BIML 11650000
         SPACE 2                                               @D35BIML 11700000
*              DUALDM - DECLARATION                            @D35BIML 11750000
         SPACE 1                                               @D35BIML 11800000
DUABIFB  EQU   X'20'              BASIC INTERFACE B SELECTED   @D35BIML 11850000
DUATRMB  EQU   X'08'              TRANSMISSION MODE B SELECTED @D35BIML 11900000
DUAIRRM  EQU   X'04'              INTRRUPT MODE REQUESTED      @D35BIML 11950000
         SPACE 1                                               @D35BIML 12000000
.NOEQU   ANOP                                                  @D35BIML 12050000
         AIF   ('&P' EQ 'LCB' OR '&P' EQ 'RJL').NOSPAC         @D35BIML 12100000
         AIF   ('&P' EQ 'NCB').NOSPAC                          @D03PIPW 12150000
         SPACE 4                                                        12200000
*********************************************************************** 12250000
         SPACE 3                                                        12300000
.NOSPAC  ANOP                                                  @D35BIML 12350000
         MEND                                                  @D35BIML 12400000
