*%GOTO SYSSTPLX ; /* PLX COMMENT COVERING ASSEMBLER CODE                00050052
         MACRO                                                          02750052
&LABEL   SYSSTATE &FCN,&ASCENV=                                         02800052
         GBLC  &SYSASCE                                                 02900052
         LCLC  &DEFAULT                                                 02950052
         AGO   .ZZZ                                                     02951952
******************************************************                  02953852
*                                                    *                  02955752
*   LICENSED MATERIALS - PROPERTY OF IBM             *                  02957652
*   THIS MACRO IS "RESTRICTED MATERIALS OF IBM"      *                  02959552
*   5686-032 (C) COPYRIGHT IBM CORP. 1993            *                  02961452
*   SEE COPYRIGHT INSTRUCTIONS                       *                  02963352
*                                                    *                  02965252
******************************************************                  02967152
.ZZZ     ANOP                                                           02969052
.******** START OF SPECIFICATIONS ************************************* 02970952
.*                                                                    * 02972852
.*01* MACRO-NAME = SYSSTATE                                           * 02974752
.*                                                                    * 02976652
.*01* DESCRIPTIVE-NAME = SYSTEM STATE MACRO                           * 02978552
.*                                                                    * 02980452
.*01* FUNCTION = USED TO SET THE VALUE OF THE GLOBAL SET SYMBOL       * 02982352
.*               "SYSASCE" TO A SPECIFIED VALUE OR A DEFAULT VALUE.   * 02984252
.*               THE PURPOSE IS TO REQUEST CODE GENERATION FOR        * 02986152
.*               THE SPECIFIED ASC ENVIRONMENT.                       * 02988052
.*                                                                    * 02989952
.*01* NOTES =    TO CHANGE THE DEFAULT VALUE, UPDATE THE STATEMENT    * 02991852
.*                    "&DEFAULT SETC  'P'"                            * 02993752
.*               WHICH IS SHORTLY AFTER THE MACRO PROTOTYPE STATEMENT.* 02995652
.*                                                                    * 02997552
.*02* PROCESSOR = ASSEMBLER/PL/(A)S                                   * 02999452
.*                                                                    * 03001352
.*01* MACRO PARAMETERS =                                              * 03003252
.*        FUNCTION - POSITIONAL, OPTIONAL                             * 03005152
.*                   TEST     - SET SYSASCE TO THE DEFAULT VALUE IF A * 03007052
.*                              VALUE HAS NOT ALREADY BEEN ASSIGNED   * 03008952
.*                              TO SYSASCE.                           * 03010852
.*                   ASCENV   - SET SYSASCE TO THE DEFAULT VALUE. (IN * 03012752
.*                              PLS, THIS IS NOT A POSITIONAL         * 03014652
.*                              PARAMETER. INSTEAD, ASCENV CAN BE     * 03016552
.*                              CODED AS A KEYWORD PARAMETER WITH OR  * 03018452
.*                              WITHOUT A VALUE.) THIS IS THE DEFAULT.* 03020352
.*        ASCENV   - KEYWORD, OPTIONAL                                * 03022252
.*                   SPECIFIES THE EXPLICIT VALUE TO WHICH SYSASCE IS * 03024152
.*                   TO BE SET.                                       * 03026052
.*                        'P' SUPPORT PRIMARY ASC MODE INVOKERS       * 03027952
.*                       'AR' SUPPORT ACCESS REGISTER ASC MODE        * 03029852
.*                            INVOKERS                                * 03031752
.*                      'ANY' SUPPORT EITHER PRIMARY OR AR ASC MODE   * 03033652
.*                            INVOKERS                                * 03035552
.*        ASM      - KEYWORD, VALID ONLY IN PLS, OPTIONAL             * 03037452
.*                   IN ADDITION TO SETTING THE PLS GLOBAL VARIABLE   * 03039352
.*                   SYSASCE, SET THE ASSEMBLER GLOBAL SET SYMBOL     * 03041252
.*                   SYSASCE. THE DEFAULT IS ASM(YES).                * 03043152
.*                                                                    * 03045052
.*********************************************************************  03050052
.*                                                                   *  03100052
.*       DEFAULT VALUE FOR SYSASCE IN ASSEMBLER LANGUAGE:            *  03150052
.*                                                                   *  03200052
&DEFAULT SETC 'P'                      FOR THE VALUES WHICH &DEFAULT *  03250052
.*                                     CAN BE SET TO, SEE THE        *  03300052
.*                                     DESCRIPTION OF THE PARAMETER  *  03350052
.*                                     "ASCENV" IN THIS MACRO'S      *  03400052
.*                                     PROLOGUE.                     *  03450052
.*                                                                   *  03500052
.*********************************************************************  03550052
.*                                                                      03600052
.*                                                                      03650052
         AIF   ('&LABEL' EQ '').NOLABEL                                 03700052
&LABEL   DS    0X                                                       03750052
.*                                                                      03800052
.NOLABEL ANOP                                                           03850052
         AIF   ('&FCN' EQ 'TEST').NODATE NO OUTPUT IF TEST              03900052
.*                                                                      03950052
.NODATE  ANOP                                                           04000052
         AIF   (N'&SYSLIST GT 1).ERR1  CHECK FOR TOO MANY POSITIONALS   04050052
         AIF   ('&FCN' NE '' AND '&ASCENV' NE '').ERR4 TOO MANY PARMS   04100052
.*                                                                      04150052
         AIF   ('&FCN' EQ '').CKENV                                     04200052
.*       PROCESS THE POSITIONAL PARAMETER (&FCN)                        04250052
         AIF   ('&FCN' NE 'TEST').ASCENV GO CHECK FOR 'ASCENV'          04300052
.*       &FCN IS 'TEST'                                                 04350052
         AIF   ('&SYSASCE' EQ '').SETDFLT SYSASCE IS NULL, SET DEFAULT  04400052
         AGO   .EXIT                   SYSASCE HAS A VALUE, GO EXIT     04450052
.*                                                                      04500052
.ASCENV  ANOP                                                           04550052
         AIF   ('&FCN' EQ 'ASCENV').SETDFLT 'ASCENV' WAS SPECIFIED, SET 04600052
.*                                     DEFAULT                          04650052
         AGO   .ERR2                   INVALID, NOT ASCENV OR TEST      04700052
.*                                                                      04750052
.*       CHECK WHETHER A VALID VALUE WAS SPECIFIED FOR 'ASCENV='        04800052
.CKENV ANOP                                                             04850052
         AIF   ('&ASCENV' EQ 'P').SETSPX ASSIGN VALID VALUE             04900052
         AIF   ('&ASCENV' EQ 'AR').SETSPX ASSIGN VALID VALUE            04950052
         AIF   ('&ASCENV' EQ 'ANY').SETSPX ASSIGN VALID VALUE           05000052
         AIF   ('&ASCENV' NE '').ERR3  INVALID VALUE FOR ASCENV         05050052
.*       IF TEST AND ASCENV ARE BOTH OMITTED, SET THE DEFAULT           05100052
.SETDFLT ANOP                                                           05150052
&SYSASCE SETC  '&DEFAULT'                                               05200052
         AGO   .EXIT                                                    05250052
.SETSPX  ANOP                                                           05300052
&SYSASCE SETC '&ASCENV'                                                 05350052
.EXIT    ANOP                                                           05400052
         AIF   ('&FCN' EQ 'TEST').EXITNOP IF TEST, GENERATE NO OUTPUT   05450052
         MNOTE '*        THE VALUE OF SYSSTATE IS NOW SET TO ASCENV=&SY*05500052
               SASCE'                                                   05550052
.EXITNOP ANOP                                                           05600052
         MEXIT                                                          05650052
.*                                                                      05700052
.ERR1    ANOP                                                           05750052
     MNOTE 12,'*        MORE THAN ONE POSITIONAL PARAMETER SPECIFIED.'  05800052
         MEXIT                                                          05850052
.ERR2    ANOP                                                           05900052
         MNOTE 12,'*        POSITIONAL PARAMETER MUST BE ''ASCENV'' OR *05950052
               ''TEST''.'                                               06000052
         MEXIT                                                          06050052
.ERR3    MNOTE 12,'*        THE ASCENV PARAMETER MUST BE ''P'', ''AR'' *06100052
               OR ''ANY''.'                                             06150052
         MEXIT                                                          06200052
.ERR4    MNOTE 12,'*        POSITIONAL PARAMETER MAY NOT BE SPECIFIED W*06250052
               ITH ASCENV PARAMETER.'                                   06300052
         MEND  ,                                                     */ 06350052
*        END OF PL/X COMMENT                                            06375052
*        %SYSSTPLX : ;                                                  06400052
*        %SYSSTATE: MACRO KEYS(ASCENV,ASM);                             06425052
*    /****************************************************************/ 06450052
*    /*                                                              */ 06500052
*    /*  START PL/S VERSION                                          */ 06550052
*    /*                                                              */ 06600052
*    /****************************************************************/ 06650052
*                                                                       06700052
*    DCL EHARC FIXED EXT;           /* DEFINE GLOBAL RETURN CODE     */ 06750052
*    DCL EHAMAPS CHAR EXT;          /* DEFINE EPILOG MAPS            */ 06800052
*    DCL SYSASCE CHAR EXT;          /* DEFINE GLOBAL ASC ENVIRONMENT */ 06850052
*    DCL CMT CHAR INT;              /* TO PUT OUT COMMENTS           */ 06900052
*    DCL IENV CHAR INT;             /* LOCAL COPY OF KEY ASCENV      */ 06950052
*    DCL IASM CHAR INT;             /* LOCAL COPY OF KEY ASM         */ 07000052
*    DCL DEFAULT CHAR INT;          /* DEFAULT VALUE FOR SYSASCE     */ 07050052
*    DCL INOTEXT CHAR INT;          /* USED TO BUILD MESSAGES        */ 07100052
*    EHARC = 0;                     /* INITIALIZE EXTERNAL RETURN    */ 07150052
*    /*                                                              */ 07200052
*    /*  DEFAULT VALUE FOR SYSASCE IN PLS:                           */ 07250052
*    /*                                                              */ 07300052
*    DEFAULT='P';                                                       07350052
*    /*                                                              */ 07400052
*    INOTEXT='. NO TEXT IS GENERATED.';                                 07450052
*                                                                       07500052
*    IF MACLABEL ¬= '' THEN         /* LABEL ON MACRO CALL?          */ 07550052
*      ANS(MACLABEL) COL(MACLMAR) NORESCAN; /* YES, PUT OUT LABEL    */ 07600052
*                                                                       07650052
*    IF MACLIST(1) ¬= 'TEST' THEN   /* IF 'TEST', PUT OUT NO COMMENT */ 07700052
*      DO;                                                              07750052
*        CMT = '? ' || MACLABEL || ' ' || MACNAME || ' ' || MACLIST ||  07800052
*          ' ' || MACKEYS;                                              07850052
*        ANS(COMMENT(CMT)) COL(MACLMAR) NORESCAN;                       07900052
*      END;                                                             07950052
*                                                                       08000052
*    /****************************************************************/ 08050052
*    /*                                                              */ 08100052
*    /*  PROCESS THE MACLIST                                         */ 08150052
*    /*                                                              */ 08200052
*    /****************************************************************/ 08250052
*    IF MACLIST ¬= '' THEN                                              08300052
*      DO;                                                              08350052
*        IF NUMBER(MACLIST) > 1 THEN /* TOO MANY POSITIONAL PARMS.?  */ 08400052
*          DO;                      /* YES, WRITE MESSAGE            */ 08450052
*            ANS MSG('AHA0073 TOO MANY POSITIONAL PARAMETERS ARE ' ||   08500052
*              'SPECIFIED. ONE IS PERMITTED.') NORESCAN;                08550052
*            CMT='TOO MANY POSITIONAL PARAMETERS ARE ' ||               08600052
*              'SPECIFIED. ONE IS PERMITTED.';                          08650052
*            ANS(COMMENT(CMT)) COL(10) NORESCAN SKIP(1);                08700052
*            EHARC = 12;            /* INDICATE SERIOUS ERROR        */ 08750052
*            RETURN CODE(EHARC);                                        08800052
*          END;                                                         08850052
*                                                                       08900052
*        IF MACLIST(1) ¬= 'TEST' THEN                                   08950052
*          DO;                      /* INVALID POSITIONAL PARAMETER  */ 09000052
*            ANS MSG('AHA0043 MACLIST(1) MUST BE ' ||                   09050052
*              QUOTE('TEST') || INOTEXT) NORESCAN;                      09100052
*            CMT='MACLIST(1) MUST BE ' || QUOTE('TEST') || INOTEXT;     09150052
*            ANS(COMMENT(CMT)) COL(10) NORESCAN SKIP(1);                09200052
*            EHARC = 12;            /* INDICATE SERIOUS ERROR        */ 09250052
*            RETURN CODE(EHARC);                                        09300052
*          END;                                                         09350052
*                                                                       09400052
*        ELSE                       /* MACLIST(1) = 'TEST'           */ 09450052
*          IF ASCENV ¬= '' THEN     /* ASCENV WITH TEST IS INVALID   */ 09500052
*            DO;                                                        09550052
*              ANS MSG('AHA0023 ''ASCENV'' IS INVALID WITH ''TEST'''    09600052
*                || INOTEXT) NORESCAN;                                  09650052
*              CMT='''ASCENV'' IS INVALID WITH ''TEST''' || INOTEXT;    09700052
*              ANS(COMMENT(CMT)) COL(10) NORESCAN SKIP(1);              09750052
*              EHARC = 12;                                              09800052
*            END;                                                       09850052
*          ELSE                                                         09900052
*            DO;                    /* ASCENV = '' (VALID)           */ 09950052
*              IF SYSASCE = '' THEN /* IF DEFINED, DO NOT CHANGE     */ 10000052
*                SYSASCE = DEFAULT; /* IF NULL, SET DEFAULT          */ 10050052
*              IF ASM ¬= '' | ASCENV ¬= '' THEN /* IF OTHER PARAMETERS  10100052
*                                  ARE SPECIFIED, PUT OUT WARNING    */ 10150052
*                DO;                                                    10200052
*                  ANS MSG('AHA0051 ''ASM'' WITH ''TEST'' IS IGNORED.') 10250052
*                  NORESCAN;                                            10300052
*                  CMT='''ASM'' WITH ''TEST'' IS IGNORED.';             10350052
*                  ANS(COMMENT(CMT)) COL(10) NORESCAN SKIP(1);          10400052
*                  EHARC = 4;                                           10450052
*                END;                                                   10500052
*              RETURN CODE(EHARC);                                      10550052
*            END;                   /* ASCENV = ''                   */ 10600052
*      END;                         /* MACLIST ¬= ''                 */ 10650052
*                                                                       10700052
*    /****************************************************************/ 10750052
*    /*                                                              */ 10800052
*    /*  VALIDITY CHECK THE ASM KEYWORD                              */ 10850052
*    /*                                                              */ 10900052
*    /****************************************************************/ 10950052
*                                                                       11000052
*    IF ASM = '' THEN                                                   11050052
*      IASM = 'YES';                /* SET DEFAULT IF OMITTED        */ 11100052
*    ELSE                           /* ASM WAS SPECIFIED             */ 11150052
*                                                                       11200052
*      DO;                          /* ANALYZE THE ASM KEYWORD       */ 11250052
*                                                                       11300052
*        IF NUMBER(ASM) > 1 THEN    /* TOO MANY SUBOPERANDS          */ 11350052
*          DO;                                                          11400052
*            ANS MSG('AHA0073 ''ASM'' PARAMETER HAS TOO MANY ' ||       11450052
*              'SUBPARAMETERS' || INOTEXT) NORESCAN;                    11500052
*            CMT='''ASM'' PARAMETER HAS TOO MANY ' ||                   11550052
*              'SUBPARAMETERS' || INOTEXT;                              11600052
*            ANS(COMMENT(CMT)) COL(10) NORESCAN SKIP(1);                11650052
*            EHARC = 12;            /* INDICATE SERIOUS ERROR        */ 11700052
*          END;                                                         11750052
*                                                                       11800052
*        IF ASM(1) = 'YES' THEN                                         11850052
*          IASM = 'YES';            /* SET YES IF SPECIFIED          */ 11900052
*        ELSE                                                           11950052
*          IF ASM(1) = 'NO' THEN                                        12000052
*            IASM = 'NO';           /* SET NO IF SPECIFIED           */ 12050052
*          ELSE                                                         12100052
*            DO;                    /* PROCESS INVALID VALUE         */ 12150052
*              ANS MSG('AHA0043 ''ASM'' MUST BE ''YES'' OR ''NO''' ||   12200052
*                INOTEXT) NORESCAN;                                     12250052
*              CMT='''ASM'' MUST BE ''YES'' OR ''NO''' || INOTEXT;      12300052
*              ANS(COMMENT(CMT)) COL(10) NORESCAN SKIP(1);              12350052
*              EHARC = 12;          /* INDICATE SERIOUS ERROR        */ 12400052
*            END;                                                       12450052
*      END;                                                             12500052
*    /****************************************************************/ 12550052
*    /*                                                              */ 12600052
*    /*  VALIDITY CHECK THE ASCENV KEYWORD                           */ 12650052
*    /*                                                              */ 12700052
*    /****************************************************************/ 12750052
*                                                                       12800052
*    IF ASCENV ¬= '' THEN                                               12850052
*      DO;                          /* ANALYZE THE ASCENV KEYWORD    */ 12900052
*                                                                       12950052
*        IF NUMBER(ASCENV) > 1 THEN /* TOO MANY SUBOPERANDS          */ 13000052
*          DO;                                                          13050052
*            ANS MSG('AHA0073 ''ASCENV'' PARAMETER HAS TOO MANY ' ||    13100052
*              'SUBPARAMETERS' || INOTEXT) NORESCAN;                    13150052
*            CMT='''ASCENV'' PARAMETER HAS TOO MANY ' ||                13200052
*              'SUBPARAMETERS' || INOTEXT;                              13250052
*            ANS(COMMENT(CMT)) COL(10) NORESCAN SKIP(1);                13300052
*            EHARC = 12;            /* INDICATE SERIOUS ERROR        */ 13350052
*          END;                                                         13400052
*                                                                       13450052
*        IF ASCENV(1) ¬= 'P' & ASCENV(1) ¬= 'AR'& ASCENV(1)¬='ANY'      13500052
*          & ASCENV ¬= 'ASCENV' THEN                                    13550052
*          DO;                      /* PROCESS INVALID VALUE         */ 13600052
*            ANS MSG('AHA0043 ''ASCENV'' MUST BE ''P'' , ''AR'', '      13650052
*            ||'''ANY'', OR HAVE NO VALUE' || INOTEXT) NORESCAN;        13700052
*            CMT='''ASCENV'' MUST BE ''P'' , ''AR'', '                  13750052
*            ||'''ANY'', OR HAVE NO VALUE' || INOTEXT;                  13800052
*            ANS(COMMENT(CMT)) COL(10) NORESCAN SKIP(1);                13850052
*            EHARC = 12;            /* INDICATE SERIOUS ERROR        */ 13900052
*          END;                                                         13950052
*      END;                         /* ASCENV ¬= ''                  */ 14000052
*                                                                       14050052
*    /****************************************************************/ 14100052
*    /*                                                              */ 14150052
*    /*  PROCESS THE KEYWORD PARAMETERS                              */ 14200052
*    /*                                                              */ 14250052
*    /****************************************************************/ 14300052
*    IF EHARC < 12 THEN             /* CHECK RETURN CODE             */ 14350052
*      DO;                                                              14400052
*        IF ASCENV = '' | ASCENV = 'ASCENV' THEN /* USE DEFAULT      */ 14450052
*          SYSASCE = DEFAULT;       /* SET DEFAULT SYSASCE           */ 14500052
*                                                                       14550052
*        ELSE                       /* SYSSTATE WAS PROVIDED         */ 14600052
*          SYSASCE=ASCENV(1);       /* SET ASC ENVIRONMENT           */ 14650052
*                                                                       14700052
*        CMT='THE VALUE OF SYSSTATE IS NOW SET TO ASCENV('              14750052
*                                   || SYSASCE || ').';                 14800052
*        ANS (COMMENT(CMT)) COL(MACLMAR+4) NORESCAN;                    14850052
*        IF SYSASCE¬='P' &          /* FOR AR OR ANY                 */ 14900052
*           INDEX(EHAMAPS,'AR@')=0 THEN /* MAPS NOT SPECIFIED YET    */ 14950052
*          EHAMAPS=EHAMAPS||'AR@';  /* GET AR-MODE DECLARES          */ 15000052
*                                                                       15050052
*        IF IASM = 'YES' THEN       /* ASM VERSION IS REQUESTED      */ 15100052
*          IF ASCENV = '' | ASCENV = 'ASCENV' THEN /* IN                15150052
*                                                  CASE THE PLS AND     15200052
*                                         ASSEMBLER DEFAULTS ARE        15250052
*                                         DIFFERENT, PASS THE PLS       15300052
*                                         DEFAULT                    */ 15350052
*            ANS('GEN ( SYSSTATE ASCENV=' || DEFAULT || ');')           15400052
*              COL(10) NORESCAN;    /* CREATE GENERATED VERSION      */ 15450052
*          ELSE                                                         15500052
*            ANS('GEN ( SYSSTATE ASCENV=' || ASCENV(1) || ');')         15550052
*              COL(10) NORESCAN;    /* CREATE GENERATED VERSION      */ 15600052
*                                                                       15650052
*      END;                         /* KEYWORD PARMS AND NO SERIOUS     15700052
*                                      ERRORS                        */ 15750052
*                                                                       15800052
*    RETURN CODE(EHARC);                                                15850052
*    %END SYSSTATE;                                                     15900052
