.*------------------------------------------------------------*         EZA00010
.*                                            March 1995  ROS *         EZA00020
.* Macro name:                                                *         EZA00030
.*      EZASMIF                                               *         EZA00040
.*                                                            *         EZA00050
.* COPYRIGHT:                                                 *         EZA00060
.*   LICENSED MATERIALS - PROPERTY OF IBM                     *         EZA00070
.*   THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"      *         EZA00080
.*   5655-HAL (C) COPYRIGHT IBM CORP. 1995.                   *         EZA00090
.*   ALL RIGHTS RESERVED.                                     *         EZA00100
.*   US GOVERNMENT USERS RESTRICTED RIGHTS -                  *         EZA00110
.*   USE, DUPLICATION OR DISCLOSURE RESTRICTED BY GSA ADP     *         EZA00120
.*   SCHEDULE CONTRACT WITH IBM CORP.                         *         EZA00130
.*   SEE IBM COPYRIGHT INSTRUCTIONS.                          *         EZA00140
.*                                                            *         EZA00150
.* Function:                                                  *         EZA00160
.*      Processing of FLAGS field for EZASMI macro            *         EZA00170
.*                                                            *         EZA00180
.* MACRO ARGUMENTS                                            *         EZA00190
.*      &FLAGS - Value of FLAGS parameter                     *         EZA00200
.*      &FUNCT - Function specified                           *         EZA00210
.*      &NUMBER - Sequence number to assign to constant       *         EZA00220
.*                developed in this macro                     *         EZA00230
.*      &INDEX  - Value of &SYSNDX passed from EZASMI         *         EZA00240
.*                                                            *         EZA00150
.* Change-Activity =                                          *         EZA00160
.* $P1= MV11060  TCPV3R2 950210  HERR: Add MSG_xxx to FLAGS   *         EZA00170
.*                                                            *         EZA00180
.*------------------------------------------------------------*         EZA00250
               MACRO                                                    EZA00260
         EZASMIF &FLAGS,&FUNCT,&NUMBER,&INDEX                           EZA00270
.*  GLOBAL USED FOR COMMUNICATION WITH EZASMI                           EZA00280
         GBLC  &CONST                                                   EZA00290
         LCLC  &TEMPCH                                                  EZA00300
&CONST   SETC  ''                                                       EZA00310
         AIF   (K'&FLAGS EQ 0 ).OPTF40   Flags not specified?           EZA00320
         AIF   ('&FLAGS'(1,1) NE '''').OPTF60 Passed as literal ?       EZA00330
.*  Processing for RECV, RECVFROM, and RECVMSG                          EZA00340
         AIF   ('&FUNCT'(1,4) NE 'RECV').OPTF05                         EZA00350
         AIF   ('&FLAGS'(2,3) EQ 'OOB').OPTF10  Is it OOB?              EZA00360
         AIF   ('&FLAGS'(2,7) EQ 'MSG_OOB').OPTF10  Is it OOB?     @P1A EZA00360
         AIF   ('&FLAGS'(2,4) EQ 'PEEK').OPTF20 Is it PEEK?             EZA00370
         AIF   ('&FLAGS'(2,8) EQ 'MSG_PEEK').OPTF20  Is it PEEK?   @P1A EZA00360
         MNOTE 8,'EZASM008 Invalid literal specified for FLAGS'         EZA00380
         AGO   .OPTF40                                                  EZA00390
.OPTF05  ANOP                                                           EZA00400
.*  Processing for SEND, SENDTO and SENDMSG                             EZA00410
         AIF   ('&FLAGS'(2,6) EQ 'SO_OOB').OPTF10  Is it OOB?      @P1C EZA00420
         AIF   ('&FLAGS'(2,7) EQ 'MSG_OOB').OPTF10  Is it OOB?     @P1A EZA00360
         AIF   ('&FLAGS'(2,12) EQ 'SO_DONTROUTE').OPTF30 DONTROUTE?@P1C EZA00430
         AIF   ('&FLAGS'(2,13) EQ 'MSG_DONTROUTE').OPTF30 DNTROUTE?@P1C EZA00430
         MNOTE 8,'EZASM008 Invalid literal specified for FLAGS'         EZA00440
         AGO   .OPTF40                                                  EZA00450
.*  OOB Specified                                                       EZA00460
.OPTF10  ANOP                                                           EZA00470
&CONST   SETC  'F''01'''                                                EZA00480
         AGO   .OPTF50                                                  EZA00490
.*  PEEK Specified                                                      EZA00500
.OPTF20  ANOP                                                           EZA00510
&CONST   SETC  'F''02'''                                                EZA00520
         AGO   .OPTF50                                                  EZA00530
.*  DONTROUTE Specified                                                 EZA00540
.OPTF30  ANOP                                                           EZA00550
&CONST   SETC  'F''04'''                                                EZA00560
         AGO   .OPTF50                                                  EZA00570
.*  No flag or invalid value Specified                                  EZA00580
.OPTF40  ANOP                                                           EZA00590
&CONST   SETC  'F''00'''                                                EZA00600
.OPTF50  ANOP                                                           EZA00610
&TEMPCH  SETC  'E$F'.'&NUMBER'.'&INDEX'                                 EZA00620
         LA    0,&TEMPCH              Get flag value                    EZA00630
         MEXIT                                                          EZA00640
.*  Numeric value or register Specified                                 EZA00650
.OPTF60  ANOP                                                           EZA00660
         EZASMIS &FLAGS,F,32768,&NUMBER,&INDEX                          EZA00670
         MEND                                                           EZA00680
