* /* START OF SPECIFICATIONS ****                                       00050000
*                                                                       00100000
*01*  MODULE-NAME = LOGCHAR                                             00150000
*                                                                       00200000
*01*  DESCRIPTIVE-NAME = LOGON CHARACTERISTICS TABLE ELEMENT MACRO      00250000
*                                                                       00300000
*                                                                       00350000
*01*  STATUS = ACF/VTAM VERSION 3 RELEASE 4                             00400000
*                                                                       00450000
*01*  COPYRIGHT:  LICENSED MATERIALS - PROPERTY OF IBM                  00453500
*                                                                       00457000
*                 THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"   00460500
*                                                                       00464000
*                 5685-085 (C) COPYRIGHT IBM CORP. 1984, 1991.          00467500
*                 ALL RIGHTS RESERVED.                                  00471000
*                                                                       00474500
*                 U.S. GOVERNMENT USERS RESTRICTED RIGHTS -             00478000
*                 USE, DUPLICATION OR DISCLOSURE RESTRICTED BY          00481500
*                 GSA ADP SCHEDULE CONTRACT WITH IBM CORP.              00485000
*                                                                       00488500
*                 SEE COPYRIGHT INSTRUCTIONS.                           00492000
*                                                                       00495500
*01*  FUNCTION = CREATES INDIVIDUAL ENTRIES IN A LOGON CHARACTERISTICS  00800000
*     TABLE. IT IS USED WITH INTAB AND ENDINTAB MACROS.                 00850000
*                                                                       00900000
*01*  NOTES = THE FOLLOWING MNOTES ARE ISSUED                           00950000
*        8,'CARDS OUT OF SEQUENCE'                                      01000000
*        8,'NAME ''&NAME'' IS LONGER THAN EIGHT CHARACTERS'             01050000
*        8,'''&ERRCHAR IS INVALID CHARACTER IN NAME ''&NAME'''          01100000
*        8,'LOGCHAR SHOULD HAVE NO POSITIONAL OPERANDS'                 01150000
*        8,'APPLID IS A REQUIRED KEYWORD'                               01200000
*        8,'MISMATCHED QUOTES IN SEQNCE'                                01250000
*        8,'MISMATCHED AMPERSANDS IN SEQNCE'                            01300000
*        8,'TOTAL SEQNCE LENGTH OF ''&IN1'' EXCEEDS 225'                01350000
*        8,'APPLID SHOULD BE A SUBLIST WITH TWO ARGUMENTS'              01400000
*        8,'''&APPLID(1)'' IS AN INVALID APPLID VALUE'                  01450000
*        8,'NAME IN APPLID CANNOT BE BLANK'                             01500000
*        8,'''&ERRCHAR'' IS INVALID CHARACTER IN APPLID NAME'           01550000
*        8,'APPLID NAME IS LONGER THAN EIGHT CHARACTERS'                01600000
*        8,'MISMATCHED QUOTES IN TSEQNCE'                               01650000
*        8,'MISMATCHED AMPERSANDS IN TSEQNCE'                           01700000
*        8,'TOTAL TSEQNCE LENGTH OF ''&IN1'' EXCEEDS 225'               01750000
*                                                                       01800000
*02*    CHARACTER-CODE-DEPENDENCIES = EBCDIC                            01850000
*                                                                       01900000
*02*    DEPENDENCIES = NONE                                             01950000
*                                                                       02000000
*02*    RESTRICTIONS = VTAM MAPPING MACRO                               02050000
*                                                                       02100000
*02*    REGISTER-CONVENTIONS = NONE, LIST FORM                          02150000
*                                                                       02200000
*02*    PATCH-LABEL = NONE                                              02250000
*                                                                       02300000
*01*  MODULE-TYPE = MACRO                                               02350000
*                                                                       02400000
*02*    PROCESSOR = ASSEMBLER                                           02450000
*                                                                       02500000
*02*    MACRO-SIZE = 210 STATEMENTS                                     02550000
*                                                                       02600000
*02*    ATTRIBUTES = LIST                                               02650000
*                                                                       02700000
*01*  ENTRY-POINT = NA                                                  02750000
*                                                                       02800000
*01*  EXIT-NORMAL = NA                                                  02850000
*                                                                       02900000
*01*  EXIT-ERROR = NA                                                   02950000
*                                                                       03000000
*01*  EXTERNAL-REFERENCES = AS-FOLLOWS                                  03050000
*                                                                       03100000
*02*    ROUTINES = NONE                                                 03150000
*                                                                       03200000
*02*    DATA-SETS = NONE                                                03250000
*                                                                       03300000
*02*    DATA-AREA = CMZELOG IS A GLOBAL WHICH PASSES SEQUENCING         03350000
*       INFORMATION BETWEEN INTAB, LOGCHAR AND ENDINTAB MACROS.         03400000
*                                                                       03450000
*                                                                       03500000
*01*  TABLES = NONE                                                     03550000
*                                                                       03600000
*01*  INNER-MACROS = NONE                                               03650000
*                                                                       03700000
*01*  SYSTEM-MACROS = NONE                                              03750000
*                                                                       03800000
*01*  CHANGE-ACTIVITY = AS-FOLLOWS                                      03850000
*                       NEW MACRO - FIRST RELEASE   31  DOS/VS          03900000
*                                                   03  OS/VS1          03950000
*                                                   03  OS/VS2          04000000
*                       09/29/78 FOR SNA4 - OZ35348            OZ35348  04037500
*                       04/20/83 FOR HARP SUPPORT              @R489301 04075000
*                       04/19/88 ADD COPYRIGHT INFORMATION     @R489301 04112500
*                                                                       04150000
*   $MAC(LOGCHAR),COMP(USER),PROD(VTAM): LOGON CHARACTERISTICS          04155500
*                                        TABLE ELEMENT MACRO            04161000
*                                                                       04166500
*   FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS               04172000
*   ---- -------- ------- ------ ------  ----------------------------   04177500
*   $BB= ACC@@AMS HVT3401 891025 615520: PL/AS CONVERSION               04180200
*   $01= OY29292  VM330   900214 042852: DOLLAR SIGN CHARACTER WAS NOT  04183000
*                                        ALLOWED AS A VALID CHARACTER.  04188500
*   $KF= JN@@@CLD NQNAME1 910514 591683: ENHANCED NQN SUPPORT           04191200
*                                                                       04194000
**** END OF SPECIFICATIONS ***/                                         04200000
*                                                                       04400000
         MACRO                                                          04450000
&NAME    LOGCHAR &APPLID=,&SEQNCE=                                      04500000
.*A-000000-999999                                              @DL03HLQ 04550000
.* GLOBAL CMZELOG IS USED TO CONTROL THE ORDER OF THE MACROS            04600000
.* LOCAL IN1      IS A GENERAL STRING INDEX                             04650000
.* LOCAL COUNT    IS  THE NUMBER OF CHARACTERS IN SEQNCE STRING         04700000
.* LOCAL ERRBIT   IS A GENERAL ERROR BIT. IS 1 IF ANY ERRORS ARE        04750000
.*                DETECTED BY THE MACRO                                 04800000
.* LOCAL ROUTBIT  IS 1 FOR ROUTINES, AND 0 FOR APPILICIDS. AS           04850000
.*                SPECIFIED IN THE APPLID PARAMETER                     04900000
.* LOCAL TRANBIT  IS 0 SINCE TSEQNCE WAS DELETED - DCR K1769            04950000
.* LOCAL UVARBIT  IS 1 FOR USER APPLICATION NAME VARIABLE SPECIFIED     05000000
.*                                                            @R489301  05050000
.* LOCAL ERRCHAR  IS USED AS A GENERAL ERROR MESSAGE STRING             05100000
.* LOCAL QUOTE    IS SET TO NULL OR TO ONE QUOTE DEPENDING ON           05150000
.*                WHETHER SEQNCE IS A QUOTED STRING                     05200000
.* LOCAL ONEQUOT  IS USED AS A SINGLE QUOTE CONSTANT                    05250000
.* LOCAL ONEAMPR  IS SET TO A CONSTANT OF ONE AMPERSAND                 05300000
.* LOCAL OPARM    IS USED AS A LEFT PARENTHESIS CONSTANT                05350000
.* LOCAL CPARM    IS USED AS A RIGHT PARENTHESIS CONSTANT               05400000
.* LOCAL K1       IS SET TO THE CHARACTER STRING 'BL1'                  05450000
.* LOCAL K2       IS SET TO THE CHARACTER STRING '0'                    05500000
.* LOCAL K3       IS SET TO THE CHARACTER STRING '0000'       @R489301  05550000
.* LOCAL K4       IS SET TO THE CHARACTER STRING 'CL'                   05600000
.* LOCAL K5       IS SET TO THE CHARACTER STRING 'CL8' OR 'VL3'         05650000
         GBLB  &CMZELOG(2)                                              05700000
         LCLA  &IN1,&COUNT                                              05750000
         LCLA  &IN2,&NETLEN,&EXTLEN                                @KFA 05775000
         LCLB  &ERRBIT,&ROUTBIT,&TRANBIT,&UVARBIT             @R489301  05800000
         LCLB  &EXTBIT                                             @KFA 05825000
         LCLC  &ERRCHAR,&QUOTE,&ONEQUOT,&ONEAMPR,&OPAREN,&CPAREN        05850000
         LCLC  &K1,&K2,&K3,&K4,&K5                                      05900000
         LCLC  &NETID,&APPLNM                                      @KFA 05912500
&EXTLEN  SETA  10                                                  @KFA 05925000
&EXTBIT  SETB  0                                                   @KFA 05937500
&ONEQUOT SETC  ''''                                                     05950000
&ONEAMPR SETC  '&&'                                                     06000000
&OPAREN  SETC  '('                                                      06050000
&CPAREN  SETC  ')'                                                      06100000
.* IF THE LOGCHAR MACRO IS INCORRECTLY SEQUENCED, MNOTE AN ERROR        06150000
.ONW00   AIF   (&CMZELOG(1) NE 1 ).ERR0010                              06200000
.ONW10   ANOP                                                           06250000
.* SET THE LOGCHAR WAS ISSUED FLAG                                      06300000
&CMZELOG(2) SETB 1                                                      06350000
.* IF THERE IS NO NAME, SKIP THE NAME PROCESSING                        06400000
         AIF   ('&NAME' EQ '' ).ONW30                                   06450000
.ONW20   ANOP                                                           06500000
.* SCAN THE NAME FOR VALID EBCDIC CHARACTERS, AND CHECK FOR NAME        06550000
.* LENGTH GREATER THAN EIGHT. PRINT APPROPRIATE MNOTES IF FOUND.        06600000
&IN1     SETA  &IN1+1                                                   06650000
         AIF   (&IN1 GT K'&NAME).ONW30                                  06700000
         AIF   (&IN1 GT 8 ).ERR0020                                     06750000
         AIF   ('&NAME'(&IN1,1) EQ '$' ).ONW20                     @01A 06775000
         AIF   ('&NAME'(&IN1,1) EQ '#' ).ONW20                          06800000
         AIF   ('&NAME'(&IN1,1) EQ '@' ).ONW20                          06850000
         AIF   ('&NAME'(&IN1,1) LT 'A' ).ERR0030                        06900000
         AIF   ('&NAME'(&IN1,1) LE 'I' ).ONW20                          06950000
         AIF   ('&NAME'(&IN1,1) LT 'J' ).ERR0030                        07000000
         AIF   ('&NAME'(&IN1,1) LE 'R' ).ONW20                          07050000
         AIF   ('&NAME'(&IN1,1) LT 'S' ).ERR0030                        07100000
         AIF   ('&NAME'(&IN1,1) LE 'Z' ).ONW20                          07150000
         AIF   (&IN1 EQ 1 ).ERR0030                                     07200000
         AIF   ('&NAME'(&IN1,1) LT '0' ).ERR0030                        07250000
         AIF   ('&NAME'(&IN1,1) GT '9' ).ERR0030                        07300000
         AGO   .ONW20                                                   07350000
.* CHECK IF ANY POSITIONAL OPERANDS WERE SUPPLIED. IF SO ERROR.         07400000
.ONW30   AIF   (N'&SYSLIST NE 0 ).ERR0040                               07450000
.* IF THERE IS NO SEQNCE, SKIP TO APPLID SECTION.                       07500000
.ONW40   AIF   ('&SEQNCE' EQ '' ).ONW140                                07550000
&IN1     SETA  0                       RESET STRING COUNTER             07600000
.ONW50   ANOP                          EVEN/ODD QUOTE COUNT             07650000
&QUOTE    SETC  ''''                    ASSUME EVEN NUMBER OF QUOTES    07700000
&IN1     SETA  &IN1+1                  POINT TO NEXT CHARACTER TO TEST  07750000
.* IF THIS IS THE END OF THE STRING, THE STRING IS TOTALLY AN EVEN      07800000
.* NUMBER OF CONSECUTIVE QUOTES.                                        07850000
         AIF   (&IN1 GT K'&SEQNCE ).ONW55                               07900000
.* IF THIS CHARACTER IS NOT A QUOTE, THERE ARE AN EVEN NUMBER OF        07950000
.* LEADING QUOTES, COUNT MULTIPLE AMPERSANDS AND QUOTES.                08000000
         AIF   ('&SEQNCE'(&IN1,1) NE '&ONEQUOT').ONW100                 08050000
.* THE TOTAL NUMBER OF LEADING QUOTES IS NOW ODD                        08100000
&QUOTE   SETC  ''                      SET FOR ODD NUMBER OF QUOTES     08150000
&IN1     SETA  &IN1+1                  POINT TO NEXT CHARACTER          08200000
.* IF THIS CHARACTER IS NOT A QUOTE, THE LEADING TOTAL IS ODD, START    08250000
.* COUNTING MULTIPLE QUOTES AND AMPERSANDS.                             08300000
         AIF   ('&SEQNCE'(&IN1,1) NE '&ONEQUOT').ONW70                  08350000
.* THE COUNT IS STILL EVEN CONTINUE THE LOOP. WE ARE COUNTING EACH      08400000
.* DOUBLE QUOTE AS ONE QUOTE, AND EACH DOUBLE AMPERSAND AS ONE.         08450000
&COUNT   SETA  &COUNT+1                INCREMENT THE CHARACTER COUNT    08500000
         AGO   .ONW50                  CONTINUE COUNTING LEADING QUOTES 08550000
.ONW55   ANOP                                                           08600000
&COUNT   SETA  &COUNT-1         ADJUST COUNT FOR EVEN # DOUBLE QUOTES   08650000
         AGO   .ONW120                                                  08700000
.* AT THIS POINT WE HAVE FOUND AN ODD NUMBER OF LEADING QUOTES. WE      08750000
.* MUST NOW CHECK THAT EACH QUOTE AND EACH AMPERSAND IS DOUBLED,        08800000
.* AND THAT THE STRING ENDS WITH ONE UNMATCHED QUOTE.                   08850000
.ONW60   ANOP                          CONTINE LOOP                     08900000
&COUNT   SETA  &COUNT+1                INCREMENT CHARACTER COUNT        08950000
&IN1     SETA  &IN1+1                  POINT TO NEXT CHARACTER          09000000
.* EVEN NUMBER OF AMPERSANDS, ODD NUMBER OF QUOTES                      09050000
.* IF THE NEXT CHARACTER IS AN AMPERSAND, CHECK FOR DOUBLE.             09100000
.ONW70   AIF   ('&SEQNCE'(&IN1,1) EQ '&ONEAMPR'(1,1)).ONW80             09150000
.*IF THIS CHARACTER IS NOT A QUOTE, CONTINUE THE LOOP                   09200000
         AIF   ('&SEQNCE'(&IN1,1) NE '&ONEQUOT').ONW60                  09250000
.* IF THIS QUOTE WAS THE LAST QUOTE, SUCCESS, SO LEAVE                  09300000
         AIF   (&IN1 EQ K'&SEQNCE ).ONW120                              09350000
.* CHECK FOR DOUBLED QUOTE                                              09400000
&IN1     SETA  &IN1+1                  POINT TO NEXT CHARACTER          09450000
.* IF THE QUOTE IS NOT DOUBLED, THERE IS AN ERROR.                      09500000
         AIF   ('&SEQNCE'(&IN1,1) NE '&ONEQUOT').ERR0060                09550000
.* BUMP COUNTS AND CONTINUE THE LOOP                                    09600000
         AGO   .ONW60                                                   09650000
.* AT THIS POINT THERE SHOULD BE A SECOND AMPERSAND. IF NOT. EROOR.     09700000
.ONW80   ANOP                                                           09750000
&IN1     SETA  &IN1+1                  POINT TO NEXT CHARACTER          09800000
         AIF   ('&SEQNCE'(&IN1,1) NE '&ONEAMPR'(1,1)).ERR0070           09850000
.* STILL GOOD. BUMP COUNTS AND CONTINUE.                                09900000
         AGO   .ONW60                                                   09950000
.* THIS SECTION DEALS WITH AN EVEN NUMBER OF LEADING QUOTES. EACH       10000000
.* AMPERSAND AND EACH QUOTE SHOULD BE DOUBLED.                          10050000
.ONW90   ANOP                                                           10100000
&COUNT   SETA  &COUNT+1                INCREMENT CHARACTER COUNT        10150000
&IN1     SETA  &IN1+1                  INCREMENT STRING POINTER         10200000
.* IF THE STRING LENGTH IS EXCEEDED, SUCCESS, SO LEAVE                  10250000
         AIF   (&IN1 GT K'&SEQNCE ).ONW120                              10300000
.* IF THE FIRST CHARACTER IS NOT AN AMPERSAND, CHECK FOR QUOTE          10350000
.ONW100  AIF   ('&SEQNCE'(&IN1,1) NE '&ONEAMPR'(1,1)).ONW110            10400000
.* IF THE NEXT CHARACTER IS NOT AN AMPERSAND, THERE IS AN ERROR         10450000
&IN1     SETA  &IN1+1                  INCREMENT STRING POINTER         10500000
         AIF   ('&SEQNCE'(&IN1,1) NE '&ONEAMPR'(1,1)).ERR0070           10550000
.* ALL IS FINE CONTINUE LOOP                                            10600000
         AGO   .ONW90                                                   10650000
.* IF THIS CHARACTER IS NOT A QUOTE, CONTINUE LOOP                      10700000
.ONW110  AIF   ('&SEQNCE'(&IN1,1) NE '&ONEQUOT').ONW90                  10750000
.* IF THE NEXT CHARACTER IS NOT ALSO A QUOTE THERE IS AN ERROR          10800000
&IN1     SETA  &IN1+1                  INCREMENT STRING POINTER         10850000
         AIF   ('&SEQNCE'(&IN1,1) NE '&ONEQUOT').ERR0060                10900000
.* OTHERWISE ALL IS FINE CONTINUE THE LOOP                              10950000
         AGO   .ONW90                                                   11000000
.* WE SHALL NOW CHECK THAT THE STRING LENGTH FOR SEQNCE IS LESS THAN    11050000
.* 225 CHARACTERS                                                       11100000
.ONW120  ANOP                                                           11150000
&IN1     SETA  K'&SEQNCE               SET TEMPORARY VALUE              11200000
.* IF THE SEQNCE STRING IS QUOTED, SUBTRACT TWO FROM THE TOTAL LENGTH.  11250000
         AIF   ('&QUOTE' NE '' ).ONW130                                 11300000
&IN1     SETA  &IN1-2                  SUBTRACT TWO                     11350000
.ONW130  AIF   (&IN1 GT 225 ).ERR0080                                   11400000
.* IF APPLID WAS NOT SPECIFIED, MNOTE ERROR                             11450000
.ONW140  AIF   ('APPLID' EQ '' ).ERR0050                                11500000
.* IF APPLID DOES NOT HAVE TWO SUBPARAMETERS, MNOTE AN ERROR            11550000
.ONW150  AIF   (N'&APPLID NE 2 ).ERR0090                                11600000
.* IF THE FIRST PARAMETER IS NOT 'ROUTINE' OR 'APPLICID'                11650000
.* OR 'USERVAR', THEN ERROR                                    @R489301 11700000
         AIF   ('&APPLID(1)' EQ 'APPLICID' ).ONW160                     11750000
         AIF   ('&APPLID(1)' NE 'USERVAR'  ).ONW155            @R489301 11800000
&UVARBIT SETB  1                       SET USER NAME VARIABLE  @R489301 11850000
         AGO   .ONW160                                         @R489301 11900000
.ONW155  AIF   ('&APPLID(1)' NE 'ROUTINE' ).ERR0100                     11950000
&ROUTBIT SETB  1                       SET ROUTINE INDICATOR            12000000
.* NEXT CHECK THE NAME FOR A VALID APPL NAME                            12050000
.ONW160  AIF   ('&APPLID(2)' EQ '' ).ERR0110                            12100000
&IN1     SETA  0                       SET INDEX TO ZERO                12150000
&IN2     SETA  0                       SET INDEX TO ZERO                12151900
&NETLEN  SETA  0                       INITIALIZE NETID LENGTH     @KFA 12153800
.ONW165  ANOP                                                      @KFA 12155700
&IN1     SETA  &IN1+1                  INCREMENT INDEX             @KFA 12157600
         AIF   (&IN1 GT K'&APPLID(2) ).ONW285                      @KFA 12159500
         AIF   (&IN1 EQ 9 ).ONW165A                                @KFA 12161400
         AIF   ('&APPLID(2)'(&IN1,1) EQ '$' ).ONW165               @KFA 12163300
         AIF   ('&APPLID(2)'(&IN1,1) EQ '#' ).ONW165               @KFA 12165200
         AIF   ('&APPLID(2)'(&IN1,1) EQ '@' ).ONW165               @KFA 12167100
         AIF   ('&APPLID(2)'(&IN1,1) EQ '.' ).ONW165A              @KFA 12169000
         AIF   ('&APPLID(2)'(&IN1,1) LT 'A' ).ERR0120              @KFA 12170900
         AIF   ('&APPLID(2)'(&IN1,1) LE 'I' ).ONW165               @KFA 12172800
         AIF   ('&APPLID(2)'(&IN1,1) LT 'J' ).ERR0120              @KFA 12174700
         AIF   ('&APPLID(2)'(&IN1,1) LE 'R' ).ONW165               @KFA 12176600
         AIF   ('&APPLID(2)'(&IN1,1) LT 'S' ).ERR0120              @KFA 12178500
         AIF   ('&APPLID(2)'(&IN1,1) LE 'Z' ).ONW165               @KFA 12180400
         AIF   (&IN1 EQ 1 ).ERR0120                                @KFA 12182300
         AIF   ('&APPLID(2)'(&IN1,1) LT '0' ).ERR0120              @KFA 12184200
         AIF   ('&APPLID(2)'(&IN1,1) GT '9' ).ERR0120              @KFA 12186100
         AGO   .ONW165                                             @KFA 12188000
.ONW165A AIF   ('&APPLID(2)'(&IN1,1) NE '.' ).ERR0130              @KFA 12189900
         AIF   (&IN1 EQ 1 ).ERR0120                                @KFA 12191800
         AIF   (&IN1 EQ K'&APPLID(2) ).ERR0120                     @KFA 12193700
&NETLEN  SETA  &IN1-1                                              @KFA 12195600
&NETID   SETC  '&APPLID(2)'(1,&NETLEN)                             @KFA 12197500
.ONW170  ANOP                                                           12200000
&IN2     SETA  &IN2+1                  INCREMENT INDEX             @KFC 12250000
         AIF   ((&IN1+&IN2) GT K'&APPLID(2) ).ONW280               @KFC 12300000
         AIF   (&IN2 GT 8 ).ERR0130                                @KFC 12350000
         AIF   ('&APPLID(2)'(&IN1+&IN2,1) EQ '$' ).ONW170     @01A @KFC 12375000
         AIF   ('&APPLID(2)'(&IN1+&IN2,1) EQ '#' ).ONW170          @KFC 12400000
         AIF   ('&APPLID(2)'(&IN1+&IN2,1) EQ '@' ).ONW170          @KFC 12450000
         AIF   ('&APPLID(2)'(&IN1+&IN2,1) LT 'A' ).ERR0120         @KFC 12500000
         AIF   ('&APPLID(2)'(&IN1+&IN2,1) LE 'I' ).ONW170          @KFC 12550000
         AIF   ('&APPLID(2)'(&IN1+&IN2,1) LT 'J' ).ERR0120         @KFC 12600000
         AIF   ('&APPLID(2)'(&IN1+&IN2,1) LE 'R' ).ONW170          @KFC 12650000
         AIF   ('&APPLID(2)'(&IN1+&IN2,1) LT 'S' ).ERR0120         @KFC 12700000
         AIF   ('&APPLID(2)'(&IN1+&IN2,1) LE 'Z' ).ONW170          @KFC 12750000
         AIF   (&IN2 EQ 1 ).ERR0120                                @KFC 12800000
         AIF   ('&APPLID(2)'(&IN1+&IN2,1) LT '0' ).ERR0120         @KFC 12850000
         AIF   ('&APPLID(2)'(&IN1+&IN2,1) GT '9' ).ERR0120         @KFC 12900000
         AGO   .ONW170                                                  12950000
.ONW280  ANOP                                                      @KFA 12958300
&EXTBIT  SETB  1                                                   @KFA 12966600
&APPLNM  SETC  '&APPLID(2)'(&IN1+1,&IN2-1)                         @KFA 12974900
.ONW285  AIF   (&IN2 NE 0 ).ONW290                                 @KFA 12983200
&APPLNM  SETC  '&APPLID(2)'(1,&IN1-1)                              @KFA 12991500
.* SINCE THERE IS NO TSEQNCE, SKIP TO TEST ERROR SECTION. (DCR K1769)   13000000
.ONW290  AIF   (&ERRBIT EQ 1 ).ONW320                                   13050000
.* IF THERE IS NO NAME, SKIP NAME CARD                                  13100000
         AIF   ('&NAME' EQ '' ).ONW300                                  13150000
&NAME    EQU   *                                                        13200000
.ONW300  ANOP                                                           13250000
.* SET GENERATION CHARACTER CONSTANTS                                   13300000
&K1      SETC  'BL1'                                                    13350000
&K2      SETC  '0'                                                      13400000
&K3      SETC  '000'                                     @R489301 @KFC  13450000
&K4      SETC  'CL'                                                     13500000
&K5      SETC  'CL8'                                                    13550000
.* GENERATE THE REQUIRED DC'S                                           13600000
         DC    &K1&ONEQUOT&K2&ROUTBIT&TRANBIT&UVARBIT&EXTBIT&K3&ONEQUOT 13650000
.*                                        OPTION FLAGS    @R489301 @KFC 13700000
.* IF ROUTINE SPECIFIED, GENERATE VCON                                  13750000
         AIF   (&ROUTBIT EQ 1).ONW305                          DCR1547  13800000
.* GENERATE APPLICATION NAME                                            13850000
         DC    &K5&ONEQUOT&APPLNM&ONEQUOT             APPLID NAME  @KFC 13900000
         AGO   .ONW307                                                  13950000
.* GENERATE EXTERNAL ADDRESS CONSTANT (VCON)                            14000000
.ONW305  ANOP                                                           14050000
&K5      SETC  'VL4'                                           DCR1547  14100000
         DC    &K5&OPAREN&APPLID(2)&CPAREN             ROUTINE ADDRESS  14150000
         DC    XL4'00'                                 RESERVED         14200000
.ONW307  ANOP                                                           14250000
         DC    AL1(&COUNT)                       SEQNCE CHARACTER COUNT 14300000
.* IF THE SEQNCE COUNT IS ZERO THERE IS NO SEQNCE                       14350000
         AIF   (&COUNT EQ 0 ).ONW310                                    14400000
         DC    &K4&COUNT&QUOTE&SEQNCE&QUOTE     SEQNCE CHARACTER STRING 14450000
.ONW310  ANOP                                                           14500000
         AIF   (&EXTBIT EQ 0 ).ONW320                              @KFA 14510000
         DC    AL1(&EXTLEN)                                        @KFA 14520000
         DC    AL1(&NETLEN)                                        @KFA 14530000
         DC    &K5&ONEQUOT&NETID&ONEQUOT              NETWORK ID   @KFA 14540000
.ONW320  ANOP                                                           14550000
         MEXIT                                                          14600000
.ERR0010 MNOTE 8,'CARDS OUT OF SEQUENCE'                                14650000
&ERRBIT  SETB  1                                                        14700000
         AGO   .ONW10                                                   14750000
.ERR0020 MNOTE 8,'NAME ''&NAME'' IS LONGER THAN EIGHT CHARACTERS'       14800000
&ERRBIT  SETB  1                                                        14850000
         AGO   .ONW30                                                   14900000
.ERR0030 ANOP                                                           14950000
&ERRCHAR SETC  '&NAME'(&IN1,1)                                          15000000
         MNOTE 8,'''&ERRCHAR'' IS INVALID CHARACTER IN NAME ''&NAME'''  15050000
&ERRBIT  SETB  1                                                        15100000
         AGO   .ONW20                                                   15150000
.ERR0040 MNOTE 8,'LOGCHAR SHOULD HAVE NO POSITIONAL OPERANDS'           15200000
&ERRBIT  SETB  1                                                        15250000
         AGO   .ONW40                                                   15300000
.ERR0050 MNOTE 8,'APPLID IS A REQUIRED KEYWORD'                         15350000
&ERRBIT  SETB  1                                                        15400000
         AGO   .ONW290                                                  15450000
.ERR0060 MNOTE 8,'MISMATCHED QUOTES IN SEQNCE'                          15500000
&ERRBIT  SETB  1                                                        15550000
         AGO   .ONW140                                                  15600000
.ERR0070 MNOTE 8,'MISMATCHED AMPERSANDS IN SEQNCE'                      15650000
&ERRBIT  SETB  1                                                        15700000
         AGO   .ONW140                                                  15750000
.ERR0080 MNOTE 8,'TOTAL SEQNCE LENGTH OF ''&IN1'' EXCEEDS 225'          15800000
&ERRBIT  SETB  1                                                        15850000
         AGO   .ONW140                                                  15900000
.ERR0090 MNOTE 8,'APPLID SHOULD BE A SUBLIST WITH TWO ARGUMENTS'        15950000
&ERRBIT  SETB  1                                                        16000000
         AGO   .ONW290                                                  16050000
.ERR0100 MNOTE 8,'''&APPLID(1)'' IS AN INVALID APPLID VALUE'            16100000
&ERRBIT  SETB  1                                                        16150000
         AGO   .ONW160                                                  16200000
.ERR0110 MNOTE 8,'NAME IN APPLID CANNOT BE BLANK'                       16250000
&ERRBIT  SETB  1                                                        16300000
         AGO   .ONW290                                                  16350000
.ERR0120 ANOP                                                           16400000
&ERRCHAR SETC  '&APPLID(2)'(&IN1+&IN2,1)                           @KFC 16450000
         MNOTE 8,'''&ERRCHAR'' IS INVALID CHARACTER IN APPLID NAME'     16500000
&ERRBIT  SETB  1                                                        16550000
         AIF (&IN2 EQ 0).ONW165                                    @KFA 16575000
         AGO   .ONW170                                                  16600000
.ERR0130 MNOTE 8,'APPLID NAME HAS AN INVALID LENGTH'               @KFC 16650000
&ERRBIT  SETB  1                                                        16700000
         AGO   .ONW290                                                  16750000
         MEND                                                           16800000
