         MACRO                                                          00050000
&LABEL   MODDTL &ADDR=,&NAME=,&CONTROL=,&LOCKOPT=,&KEEP=,&OWNER=,      *00100000
               &CHANGE=,&SCOPE=,&VOLID=                        @D36SDJO 00150000
         LCLA   &OFF1             OFFSET IN DTL TO FLAG1                00200000
         LCLA   &OFF2             OFFSET IN DTL TO FLAG2                00250000
         LCLA   &OFF3             OFFSET IN DTL TO RESOURCE NAME        00300000
         LCLA   &OFF4             OFFSET IN DTL TO VOLUME ID   @D36SDJO 00350000
         LCLA   &LN               LENGTH OF RESOURCE NAME               00400000
         LCLA   &LN2              LENGTH OF VOLUME ID          @D36SDJO 00450000
         LCLA   &REG              WORK REGISTER                         00500000
         LCLA   &REG1             WORK REGISTER                @D36SDJO 00550000
         LCLA   &REG2             WORK REGISTER                @D36SDJO 00600000
         LCLA   &REG3             WORK REGISTER                @D36SDJO 00650000
         LCLA   &ONFLG1           CONTROL FLAG1 (SET BITS)              00700000
         LCLA   &OUTFLG1          CONTROL FLAG1 (CLEAR BITS)            00750000
         LCLA   &ONFLG2           CONTROL FLAG2 (SET BITS)              00800000
         LCLA   &OUTFLG2          CONTROL FLAG2 (CLEAR BITS)            00850000
         LCLA   &EXCFLG           EXCLUSIVE FLAG                        00900000
         LCLA   &KEEPFLG          KEEP FLAG                             00950000
         LCLA   &PARTFLG          PARTITION FLAG                        01000000
         LCLA   &SHRFLG1          LOCKOPT FLAG1                         01050000
         LCLA   &SHRFLG2          LOCKOPT FLAG2                         01100000
         LCLA   &SHRFLG4          LOCKOPT FLAG4                @D36SDJO 01150000
         LCLA   &CHGFLG           CHANGE FLAG                           01200000
         LCLA   &EXTFLG           SCOPE=EXTERNAL               @D36SDJO 01250000
         LCLA   &VOLFLG           VOLUME ID IS SPECIFIED       @D36SDJO 01300000
         LCLB   &ON1,&OUT1        WORK TO DO FLAGS                      01350000
         LCLB   &ON2,&OUT2        WORK TO DO FLAGS                      01400000
         LCLB   &WORK             WORK TO DO FLAG                       01450000
         LCLB   &TWOREGS          TWO REGISTERS REQUIRED       @D36SDJO 01500000
         LCLB   &VOLATYP          VOLID AS A-CONSTANT          @D36SDJO 01550000
         LCLB   &VOLRTYP          VOLID IN REGISTER SPECIFIC.  @D36SDJO 01600000
         LCLB   &VOLSTYP          VOLID AS S-TYP CONSTANT      @D36SDJO 01650000
         LCLB   &ADDATYP          ADDR. AS A-CONSTANT          @D36SDJO 01700000
         LCLB   &ADDRTYP          ADDR. IN REGISTER SPECIFIC.  @D36SDJO 01750000
         LCLB   &ADDSTYP          ADDR. AS S-TYP CONSTANT      @D36SDJO 01800000
         LCLB   &NAMATYP          NAME  AS A-CONSTANT          @D36SDJO 01850000
         LCLB   &NAMRTYP          NAME  IN REGISTER SPECIFIC.  @D36SDJO 01900000
         LCLB   &NAMSTYP          NAME  AS S-TYP CONSTANT      @D36SDJO 01950000
         LCLC   &ADD              ADDRESS CODED                         02000000
         LCLC   &NAM              NAME CODED                            02050000
         LCLC   &VOL              VOLUME CODED                 @D36SDJO 02100000
         LCLC   &TARGET1          TARGET FOR FLAG1 CODED                02150000
         LCLC   &TARGET2          TARGET FOR FLAG2 CODED                02200000
         LCLC   &TARGET3          TARGET FOR NAME CODED                 02250000
         LCLC   &TARGET4          TARGET FOR VOLUME CODED      @D36SDJO 02300000
.*                                                                      02350000
         AGO   .ZZZ                                                     02400000
*********************************************************************** 02450000
*                                                                     * 02500000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                  * 02550000
*        5686-007 (C) COPYRIGHT IBM CORP. 1979,1988                   * 02600000
*        ALL RIGHTS RESERVED                                          * 02650000
*        LICENSED MATERIALS - PROGRAM PROPERTY OF IBM                 * 02700000
.*       SEE COPYRIGHT INSTRUCTIONS, G120-2083                        * 02750000
*                                                                     * 02800000
*********************************************************************** 02850000
.ZZZ ANOP                                                               02900000
.*---------------- --------------------------------------------------   02950000
.*                                                                      03000000
.* /* START OF SPECIFICATIONS ***************************************** 03050000
.*                                                                      03100000
.*01* MODULE-TYPE = SUPERVISOR - USER INTERFACE MACRO                   03150000
.*                                                                      03200000
.*01* DESCRIPTIVE NAME = MODIFY LOCK CONTROL BLOCK (DTL)                03250000
.*                                                                      03300000
.*01* NOTES = CHANGE ACTIVITY                                           03350000
.*    NEW CODE FOR RELEASE 35D TO SUPPORT RESOURCE MANAGEMENT           03400000
.*    CHANGES FOR REL 1.2 TO SUPPORT DASD SHARING              @D36SDJO 03450000
.* A000000-999999                                              @D356DDK 03500000
.**** END OF SPECIFICATIONS ******************************************* 03550000
.*                                                                      03600000
.*                                                                      03650000
.*   PURPOSE:                                                           03700000
.*                                                                      03750000
.*            THIS MACRO MODIFIES A DTL CONTROL BLOCK IN AN USER        03800000
.*            SPECIFIED STORAGE AREA.                                   03850000
.*            PARAMETER ADDR IS MANDATORY.                              03900000
.*                                                                      03950000
.*                                                                      04000000
.*   REGISTER USAGE:                                                    04050000
.*                                                                      04100000
.*            REGISTERS 0 AND 15 ARE AFFECTED.                          04150000
.*            (IF REGISTER 2 IS USED, IT IS SAVED AND RESTORED.)        04200000
.*                                                                      04250000
.*                                                                      04300000
.********************************************************************** 04350000
.*                                                                      04400000
.*                                                                      04450000
.*                                                                      04500000
.*** SETTING OF LOCAL VARIABLES ***                                     04550000
.*                                                                      04600000
.*                                                                      04650000
&OFF1    SETA   2                 SET OFFSET TO FLAG1                   04700000
&OFF2    SETA   3                 SET OFFSET TO FLAG2                   04750000
&OFF3    SETA   4                 SET OFFSET TO RESOURCE NAME           04800000
&OFF4    SETA   16                SET OFFSET TO VOLUME ID               04850000
&LN      SETA   12                SET LENGTH OF RESOURCE NAME           04900000
&LN2     SETA   6                 SET LENGTH OF VOLID          @D36SDJO 04950000
&OUTFLG1 SETA   X'FF'             SET CLEAR MASK FOR FLAG1              05000000
&OUTFLG2 SETA   X'FF'             SET CLEAR MASK FOR FLAG2              05050000
&EXCFLG  SETA   X'10'       FLG1  SET EXCLUSIVE FLAG                    05100000
&SHRFLG1 SETA   X'01'       FLG1  SET LOCKOPT=1 FLAG                    05150000
&SHRFLG2 SETA   X'02'       FLG1  SET LOCKOPT=2 FLAG                    05200000
&SHRFLG4 SETA   X'04'       FLG1  SET LOCKOPT=4 FLAG           @D36SDJO 05250000
&KEEPFLG SETA   X'80'       FLG2  SET KEEP FLAG                         05300000
&PARTFLG SETA   X'40'       FLG2  SET PARTITION FLAG                    05350000
&CHGFLG  SETA   X'20'       FLG2  SET CHANGE FLAG                       05400000
&EXTFLG  SETA   X'10'       FLG2  SET EXTERNAL FLAG            @D36SDJO 05450000
&VOLFLG  SETA   X'08'       FLG2  SET VOLUME FLAG              @D36SDJO 05500000
&REG1    SETA   15                                             @D36SDJO 05550000
&REG2    SETA   2                                              @D36SDJO 05600000
&REG3    SETA   0                                              @D36SDJO 05650000
.*                                                                      05700000
.*                                                                      05750000
         AIF   ('&LABEL' EQ '').NOLAB GO IF NO LABEL                    05800000
&LABEL   DC    0H'0'                                                    05850000
.NOLAB   ANOP                                                           05900000
.*                                                                      05950000
.* -------------- CHECK ADDR  PARAMETER ------- --------------          06000000
.*                                                                      06050000
         AIF   (T'&ADDR NE 'O').CHK1                                    06100000
         MNOTE 8,'ADDR PARAMETER MISSING. GENERATION TERMINATED.'       06150000
         MEXIT                                                          06200000
.CHK1    ANOP                                                  @D36SDJO 06250000
         AIF   ('&ADDR'(1,1) EQ '(').ADD1   GO IF DYNAMIC               06300000
&ADDATYP SETB  1                                               @D36SDJO 06350000
         AGO   .ADDEND                                         @D36SDJO 06400000
.ADD1    AIF   ('&ADDR'(1,3) EQ '(S,').ADD3                             06450000
&ADDRTYP SETB  1                                               @D36SDJO 06500000
         AGO   .ADDEND                                         @D36SDJO 06550000
.ADD3    ANOP                                                  @D36SDJO 06600000
&ADDSTYP SETB  1                                               @D36SDJO 06650000
.ADDEND  ANOP                                                  @D36SDJO 06700000
.*                                                                      06750000
.* ----------------- CHECK CONTROL PARAMETER -----------------          06800000
.*                                                                      06850000
         AIF   (T'&CONTROL EQ 'O').CONEND GO IF OMITTED                 06900000
         AIF   ('&CONTROL' EQ 'E').CONXCL GO IF EXCLUSIVE               06950000
         AIF   ('&CONTROL' EQ 'S').CONSHR GO IF SHARED                  07000000
  MNOTE 1,'CONTROL SPECIFICATION INCORRECT. IGNORED.'                   07050000
         AGO   .CONEND                                                  07100000
.CONXCL  ANOP                                                           07150000
&ON1     SETB  1                                                        07200000
&ONFLG1  SETA  &ONFLG1+&EXCFLG INDICATE EXCLUSIVE CONTROL               07250000
         AGO   .CONEND                                                  07300000
.CONSHR  ANOP                                                           07350000
&OUT1    SETB  1                                                        07400000
&OUTFLG1 SETA  &OUTFLG1-&EXCFLG CLEAR EXCLUSIVE CONTROL                 07450000
.CONEND  ANOP                                                           07500000
.*                                                                      07550000
.* ------------------ CHECK KEEP PARAMETER -------------------          07600000
.*                                                                      07650000
         AIF   (T'&KEEP EQ 'O').KEEPEND GO IF OMITTED                   07700000
         AIF   ('&KEEP' EQ 'YES').KEEPY GO IF KEEP EQ YES               07750000
         AIF   ('&KEEP' EQ 'NO').KEEPN GO IF KEEP EQ NO                 07800000
    MNOTE 1,'KEEP SPECIFICATION INCORRECT. IGNORED.'                    07850000
         AGO   .KEEPEND                                                 07900000
.KEEPY   ANOP                                                           07950000
&ON2     SETB  1                                                        08000000
&ONFLG2  SETA  &ONFLG2+&KEEPFLG INDICATE KEEP                           08050000
         AGO   .KEEPEND                                                 08100000
.KEEPN   ANOP                                                           08150000
&OUT2    SETB  1                                                        08200000
&OUTFLG2 SETA  &OUTFLG2-&KEEPFLG CLEAR KEEP                             08250000
.KEEPEND ANOP                                                           08300000
.*                                                                      08350000
.* ------------------ CHECK OWNER PARAMETER ------------------          08400000
.*                                                                      08450000
         AIF   (T'&OWNER EQ 'O').OWNEND GO IF OMITTED                   08500000
         AIF   ('&OWNER' EQ 'PARTITION').OWNP GO IF PARTITION           08550000
         AIF   ('&OWNER' EQ 'TASK').OWNT GO IF TASK                     08600000
  MNOTE 1,'OWNER SPECIFICATION INCORRECT. IGNORED.'                     08650000
         AGO   .OWNEND                                                  08700000
.OWNP    ANOP                                                           08750000
&ON2     SETB  1                                                        08800000
&ONFLG2  SETA  &ONFLG2+&PARTFLG INDICATE PARTITION                      08850000
         AGO   .OWNEND                                                  08900000
.OWNT    ANOP                                                           08950000
&OUT2    SETB  1                                                        09000000
&OUTFLG2 SETA  &OUTFLG2-&PARTFLG CLEAR PARTITION                        09050000
.OWNEND  ANOP                                                           09100000
.*                                                                      09150000
.* ----------------- CHECK LOCKOPT PARAMETER -----------------          09200000
.*                                                                      09250000
         AIF   (T'&LOCKOPT EQ 'O').SHREND GO IF OMITTED                 09300000
         AIF   ('&LOCKOPT' EQ '1').SHR1 GO IF OPTION EQ 1               09350000
         AIF   ('&LOCKOPT' EQ '2').SHR2 GO IF OPTION EQ 2               09400000
         AIF   ('&LOCKOPT' EQ '4').SHR4 GO IF OPTION EQ 4      @D36SDJO 09450000
  MNOTE 1,'LOCKOPT SPECIFICATION INCORRECT. IGNORED.'                   09500000
         AGO   .SHREND                                                  09550000
.SHR1    ANOP                                                           09600000
&ON1     SETB  1                                                        09650000
&ONFLG1  SETA  &ONFLG1+&SHRFLG1 INDICATE OPTION 1                       09700000
&OUT1    SETB  1                                                        09750000
&OUTFLG1 SETA  &OUTFLG1-&SHRFLG2-&SHRFLG4 CLEAR OPTION 2+4     @D36SDJO 09800000
         AGO   .SHREND                                                  09850000
.SHR2    ANOP                                                           09900000
&ON1     SETB  1                                                        09950000
&ONFLG1  SETA  &ONFLG1+&SHRFLG2 INDICATE OPTION 2                       10000000
&OUT1    SETB  1                                                        10050000
&OUTFLG1 SETA  &OUTFLG1-&SHRFLG1-&SHRFLG4   CLEAR OPTION 1+4   @D36SDJO 10100000
         AGO   .SHREND                                         @D36SDJO 10150000
.SHR4    ANOP                                                  @D36SDJO 10200000
&ON1     SETB  1                                               @D36SDJO 10250000
&ONFLG1  SETA  &ONFLG1+&SHRFLG4   INDICATE OPTION 4            @D36SDJO 10300000
&OUT1    SETB  1                                               @D36SDJO 10350000
&OUTFLG1 SETA  &OUTFLG1-&SHRFLG1-&SHRFLG2   CLEAR OPTION 1+2   @D36SDJO 10400000
.SHREND  ANOP                                                           10450000
.*                                                                      10500000
.* ------------------ CHECK SCOPE  AND VOLID PARAMETERS ------          10550000
.*                                                                      10600000
         AIF   (T'&SCOPE EQ 'O' AND T'&VOLID EQ 'O').VOLIDE    @D36SDJO 10650000
         AIF   (T'&SCOPE EQ 'O' ).VOLID5                       @D36SDJO 10700000
         AIF   (T'&VOLID EQ 'O').SCOPE3                        @D36SDJO 10750000
      MNOTE 4,'VOLID SPECIFIED. SCOPE SPECIFICATION IGNORED.'  @D36SDJO 10800000
         AGO   .VOLID5                                         @D36SDJO 10850000
.SCOPE3  ANOP                                                  @D36SDJO 10900000
         AIF   ('&SCOPE' NE 'EXT').SCOPE5                      @D36SDJO 10950000
&ON2     SETB  1                                                        11000000
&ONFLG2  SETA  &ONFLG2+&EXTFLG    INDICATE EXTERNAL LOCKING    @D36SDJO 11050000
&OUT2    SETB  1                                                        11100000
&OUTFLG2 SETA  &OUTFLG2-&VOLFLG    CLEAR VOLUME FLAG           @D36SDJO 11150000
         AGO   .VOLIDE                                         @D36SDJO 11200000
.SCOPE5  AIF   ('&SCOPE' EQ 'INT').SCOPE7                      @D36SDJO 11250000
   MNOTE 4,'SCOPE SPECIFICATION INCORRECT. SCOPE=INT ASSUMED.' @D36SDJO 11300000
.SCOPE7  ANOP                                                  @D36SDJO 11350000
&OUTFLG2 SETA  &OUTFLG2-&VOLFLG-&EXTFLG  RESET EXTERNAL INDIC. @D36SDJO 11400000
&OUT2    SETB  1                                                        11450000
         AGO   .VOLIDE                                         @D36SDJO 11500000
.VOLID5  ANOP                                                  @D36SDJO 11550000
&ON2     SETB  1                                               @D36SDJO 11600000
&ONFLG2  SETA  &ONFLG2+&VOLFLG    INDICATE VOLUME SPECIFIED    @D36SDJO 11650000
         AIF   ('&VOLID'(1,1) EQ '(').VOLID6                   @D36SDJO 11700000
&VOLATYP SETB  1                                               @D36SDJO 11750000
         AGO   .VOLIDE                                         @D36SDJO 11800000
.VOLID6  AIF   ('&VOLID'(1,3) EQ '(S,').VOLID7                 @D36SDJO 11850000
&VOLRTYP SETB  1                                               @D36SDJO 11900000
         AGO   .VOLIDE                                         @D36SDJO 11950000
.VOLID7  ANOP                                                  @D36SDJO 12000000
&VOLSTYP SETB  1                                               @D36SDJO 12050000
.VOLIDE  ANOP                                                  @D36SDJO 12100000
.*                                                                      12150000
.* ------------------ CHECK CHANGE PARAMETER -----------------          12200000
.*                                                                      12250000
         AIF   (T'&CHANGE EQ 'O').CHGEND GO IF OMITTED                  12300000
         AIF   ('&CHANGE' EQ 'ON').CHGON GO IF CHANGE EQ ON             12350000
         AIF   ('&CHANGE' EQ 'OFF').CHGOFF GO IF CHANGE EQ OFF          12400000
    MNOTE 1,'CHANGE SPECIFICATION INCORRECT. IGNORED.'                  12450000
         AGO   .CHGEND                                                  12500000
.CHGON   ANOP                                                           12550000
&ON2     SETB  1                                                        12600000
&ONFLG2  SETA  &ONFLG2+&CHGFLG INDICATE CHANGE ON                       12650000
         AGO   .CHGEND                                                  12700000
.CHGOFF  ANOP                                                           12750000
&OUT2    SETB  1                                                        12800000
&OUTFLG2 SETA  &OUTFLG2-&CHGFLG CLEAR CHANGE ON                         12850000
.CHGEND  ANOP                                                           12900000
.*                                                                      12950000
.*                                                                      13000000
&WORK    SETB  (&ON1 OR &OUT1 OR &ON2 OR &OUT2 )               @D36SDJO 13050000
&WORK    SETB  (&WORK OR T'&NAME NE 'O' OR T'&VOLID NE 'O')    @D36SDJO 13100000
         AIF   (NOT &WORK).GENEND                              @D36SDJO 13150000
.*                                                                      13200000
.* ------------------ CHECK NAME PARAMETER -------------------          13250000
.*                                                                      13300000
         AIF   (T'&NAME EQ 'O').NAMEND GO IF OMITTED                    13350000
         AIF   ('&NAME'(1,1) EQ '(').NAMDYN GO IF DYNAMIC               13400000
&NAMATYP SETB  1                                               @D36SDJO 13450000
&NAM     SETC  '0(&REG)'                                                13500000
         AGO   .NAMEND                                                  13550000
.NAMDYN  AIF   ('&NAME'(1,3) EQ '(S,').NAMSTYP                          13600000
&NAMRTYP SETB  1                                               @D36SDJO 13650000
         AGO   .NAMEND                                                  13700000
.NAMSTYP ANOP                                                           13750000
&NAMSTYP SETB 1                                                @D36SDJO 13800000
.NAMEND  ANOP                                                           13850000
.*                                                                      13900000
.* ------------------ GENERATE ASSEMBLER CODE-----------------          13950000
.*                                                                      14000000
.*                                                                      14050000
.*  CHECK IF TWO REGISTERS ARE REQUIRED                                 14100000
.*                                                                      14150000
&REG     SETA   &REG1                                          @D36SDJO 14200000
         AIF   (NOT &ADDATYP).GEN1                             @D36SDJO 14250000
         AIF   (NOT &NAMATYP AND NOT &VOLATYP).GEN1            @D36SDJO 14300000
&REG     SETA  &REG2                                           @D36SDJO 14350000
&TWOREGS SETB  1                REG2 REQUIRED FOR NAME/VOLUME  @D36SDJO 14400000
         LR    &REG3,&REG2                                     @D36SDJO 14450000
.*                                                                      14500000
.GEN1    AIF   (NOT &ADDATYP).GEN2                             @D36SDJO 14550000
         L     &REG1,=A(&ADDR)    LOAD TARGET ADDRESS          @D36SDJO 14600000
&TARGET1 SETC  '&OFF1'.'(&REG1)'                               @D36SDJO 14650000
&TARGET2 SETC  '&OFF2'.'(&REG1)'                               @D36SDJO 14700000
&TARGET3 SETC  '&OFF3'.'('.'&LN'.',&REG1)'                     @D36SDJO 14750000
&TARGET4 SETC  '&OFF4'.'('.'&LN2'.',&REG1)'                    @D36SDJO 14800000
.*                                                                      14850000
.GEN2    AIF   (NOT &ADDRTYP).GEN3                             @D36SDJO 14900000
&ADD     SETC  '&ADDR'(2,K'&ADDR-2) ISOLATE REGISTER                    14950000
&TARGET1 SETC  '&OFF1'.'('.'&ADD'.')'                                   15000000
&TARGET2 SETC  '&OFF2'.'('.'&ADD'.')'                                   15050000
&TARGET3 SETC  '&OFF3'.'('.'&LN'.','.'&ADD'.')'                         15100000
&TARGET4 SETC  '&OFF4'.'('.'&LN2'.','.'&ADD'.')'               @D36SDJO 15150000
.*                                                                      15200000
.GEN3    AIF   (NOT &ADDSTYP).GEN4                             @D36SDJO 15250000
&ADD     SETC  '&ADDR'(4,K'&ADDR-4) ISOLATE SYMBOLIC ADDR               15300000
&TARGET1 SETC  '&ADD'.'+'.'&OFF1'                                       15350000
&TARGET2 SETC  '&ADD'.'+'.'&OFF2'                                       15400000
&TARGET3 SETC  '&ADD'.'+'.'&OFF3'.'('.'&LN'.')'                         15450000
&TARGET4 SETC  '&ADD'.'+'.'&OFF4'.'('.'&LN2'.')'               @D36SDJO 15500000
.*                                                                      15550000
.GEN4    AIF   (NOT &ON1).GEN5    GO IF NOTHING TO SET IN FLAG1@D36SDJO 15600000
         OI    &TARGET1,&ONFLG1   SET REQUIRED BITS IN FLAG1   @D36SDJO 15650000
.GEN5    AIF   (NOT &OUT1).GEN6    GO IF NOTHING TO CLEAR      @D36SDJO 15700000
         NI    &TARGET1,&OUTFLG1  CLEAR REQUIRED BITS IN FLAG1 @D36SDJO 15750000
.GEN6    AIF   (NOT &ON2).GEN7    GO IF NOTHING TO SET IN FLAG2@D36SDJO 15800000
         OI    &TARGET2,&ONFLG2   SET REQUIRED BITS IN FLAG2   @D36SDJO 15850000
.GEN7    AIF   (NOT &OUT2).GEN8    GO IF NOTHING TO CLEAR      @D36SDJO 15900000
         NI    &TARGET2,&OUTFLG2  CLEAR REQUIRED BITS IN FLAG2 @D36SDJO 15950000
.GEN8    ANOP                                                  @D36SDJO 16000000
.*                                                             @D36SDJO 16050000
         AIF   (NOT &NAMATYP).GEN21                            @D36SDJO 16100000
         L     &REG,=A(&NAME)                                  @D36SDJO 16150000
&NAM     SETC  '0(&REG)'                                       @D36SDJO 16200000
         AGO   .GEN25                                          @D36SDJO 16250000
.GEN21   AIF   (NOT &NAMRTYP).GEN23                            @D36SDJO 16300000
&NAM     SETC  '&NAME'(2,K'&NAME-2)                                     16350000
&NAM     SETC  '0('.'&NAM'.')'                                          16400000
         AGO   .GEN25                                          @D36SDJO 16450000
.GEN23   AIF   (NOT &NAMSTYP).GEN25                            @D36SDJO 16500000
&NAM     SETC  '&NAME'(4,K'&NAME-4)                                     16550000
.GEN25   ANOP                                                  @D36SDJO 16600000
.*                                                                      16650000
   AIF (NOT &NAMATYP AND NOT &NAMSTYP AND NOT &NAMRTYP).GEN27  @D36SDJO 16700000
         MVC   &TARGET3,&NAM      MOVE RESOURCE NAME                    16750000
.GEN27   ANOP                                                  @D36SDJO 16800000
.*                                                             @D36SDJO 16850000
         AIF   (NOT &VOLATYP).GEN31                            @D36SDJO 16900000
         L     &REG,=A(&VOLID)                                 @D36SDJO 16950000
&VOL     SETC  '0(&REG)'                                       @D36SDJO 17000000
         AGO   .GEN35                                          @D36SDJO 17050000
.GEN31   AIF   (NOT &VOLRTYP).GEN33                            @D36SDJO 17100000
&VOL     SETC  '&VOLID'(2,K'&VOLID-2)                          @D36SDJO 17150000
&VOL     SETC  '0('.'&VOL'.')'                                 @D36SDJO 17200000
         AGO   .GEN35                                          @D36SDJO 17250000
.GEN33   AIF   (NOT &VOLSTYP).GEN35                            @D36SDJO 17300000
&VOL     SETC  '&VOLID'(4,K'&VOLID-4)                          @D36SDJO 17350000
.GEN35   ANOP                                                  @D36SDJO 17400000
   AIF (NOT &VOLATYP AND NOT &VOLSTYP AND NOT &VOLRTYP).GEN37  @D36SDJO 17450000
         MVC   &TARGET4,&VOL      MOVE VOLUME ID                        17500000
.GEN37   ANOP                                                  @D36SDJO 17550000
         AIF   (NOT &TWOREGS).GEN39                            @D36SDJO 17600000
         LR    &REG2,&REG3        RESTORE REGISTER 2           @D36SDJO 17650000
.GEN39   ANOP                                                  @D36SDJO 17700000
.GENEND  ANOP                                                  @D36SDJO 17750000
         MEND                                                           17800000
