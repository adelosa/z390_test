*%/*                                                                    00001000
         MACRO                                                          00002000
&NAME    TIME  &UNIT,&ADDR,        Time and date format                *00003000
               &ZONE=,             Time and date type                  *00004000
               &ERRET=,            Error routine address               *00005000
               &LINKAGE=,          SVC or PC                       @D2A*00006000
               &DATETYPE=,         Date format for LINKAGE=SYSTEM  @D3A*00007000
               &MF=                Macro form                      @D2A 00008000
         AGO   .SKIP                                                 */ 00009000
*/****  START OF SPECIFICATIONS  *************************************/ 00010000
*/*                                                                  */ 00011000
*/*01*  MACRO NAME = TIME                                            */ 00012000
*/*                                                                  */ 00013000
*/*01*  DESCRIPTIVE NAME = TIME Macro                                */ 00014000
*/*                                                                  */ 00015000
*/*01*  PROPRIETARY STATEMENT =                                      */ 00016000
*/*         LICENSED MATERIALS - PROPERTY OF IBM                     */ 00017000
*/*         "RESTRICTED MATERIALS OF IBM"                            */ 00018690
*/*         5686-066                                                 */ 00019380
*/*         (C)COPYRIGHT IBM CORP. 1996                              */ 00020070
*/*                                                                  */ 00021000
*/*01*  STATUS =  HBB4410                                            */ 00022000
*/*                                                                  */ 00023000
*/*01*  FUNCTION =                                                   */ 00024000
*/*       Returns the time of day and date. The format may be        */ 00025000
*/*       local or GMT time.                                         */ 00026000
*/*                                                                  */ 00027000
*/*02*    OPERATION =                                                */ 00028000
*/*         The TIME macro parses the keywords and invokes the       */ 00029000
*/*         TIME service routine (IEAVRT01) via SVC(11) or           */ 00030000
*/*         IEATTIME via PC.                                         */ 00031000
*/*                                                                  */ 00032000
*/*       SVC Invocations                                            */ 00033000
*/*         The time of day is returned in R0 for DEC, BIN, and      */ 00034000
*/*         TU. For MIC and STCK, the time of day is returned        */ 00035000
*/*         in the specified address.                                */ 00036000
*/*         The date is returned in R1 as packed decimal in the      */ 00037000
*/*         form CCYYDDDF, where:                                    */ 00038000
*/*              CC  - is the century                                */ 00039000
*/*              YY  - is the last two digits of the year            */ 00040000
*/*              DDD - is the day of the year                        */ 00041000
*/*              F   - allows for unpacking                          */ 00042000
*/*                                                                  */ 00043000
*/*       PC Invocations                                             */ 00044000
*/*         The time of day is returned to the first two words of    */ 00045000
*/*         a user specified fourword area.  The date is returned    */ 00046000
*/*         to the third word in decimal format (NOT packed decimal) */ 00047000
*/*         in the forms given below.                                */ 00048000
*/*                                                                  */ 00049000
*/*01*  INVOCATION = (BAL)                                           */ 00050000
*/*                                                                  */ 00051000
*/*        unit       Specifies the time of day format to be         */ 00052000
*/*                        returned.                                 */ 00053000
*/*                DEC   - Decimal (HHMMSSth). (Default)             */ 00054000
*/*                                                                  */ 00055000
*/*                BIN   - Unsigned 32-bit binary number in          */ 00056000
*/*                        hundredths of seconds.                    */ 00057000
*/*                                                                  */ 00058000
*/*                MIC   - Microseconds, where bit 51= 1 microsec.   */ 00059000
*/*                                                                  */ 00060000
*/*                STCK  - Unsigned 64-bit fixed-point number, where */ 00061000
*/*                        bit 51= 1 microsecond (TOD contents).     */ 00062000
*/*                                                                  */ 00063000
*/*                TU    - Unsigned 32-bit binary number in Timer    */ 00064000
*/*                        Units, where 1 TU= 26.04166 microseconds. */ 00065000
*/*                    Note: TU is NOT valid for LINKAGE=SYSTEM.     */ 00066000
*/*                                                                  */ 00067000
*/*       ,addr       Address of an 8-byte area for MIC and STCK.    */ 00068000
*/*                   For LINKAGE=SYSTEM, the address of a 16-byte   */ 00069000
*/*                   area for ALL formats.                          */ 00070000
*/*                                                                  */ 00071000
*/*          ,ZONE=      Return local, or GMT time and date in       */ 00072000
*/*                      units of DEC, BIN, MIC, or TU.              */ 00073000
*/*                      ZONE must be null when unit is STCK.        */ 00074000
*/*                                                                  */ 00075000
*/*                LT      Local time and date. (Default)            */ 00076000
*/*                                                                  */ 00077000
*/*                GMT     Greenwich mean time and date.             */ 00078000
*/*                                                                  */ 00079000
*/*          ,ERRET=     Error routine address.                      */ 00080000
*/*                      This is an obsolete keyword and is ignored. */ 00081000
*/*                                                                  */ 00082000
*/*          ,LINKAGE=   Method of linking to the service routine.   */ 00083000
*/*                SVC     SVC                                       */ 00084000
*/*                                                                  */ 00085000
*/*                SYSTEM  PC                                        */ 00086000
*/*                                                                  */ 00087000
*/*          ,DATETYPE=   Specifies the date format that the TOD     */ 00088000
*/*                       value is converted to:                     */ 00089000
*/*              YYYYDDD= (Default) Decimal digits of the            */ 00090000
*/*                       form 0YYYYDDD, where:                      */ 00091000
*/*                       YYYY is the four digits of the year        */ 00092000
*/*                       DDD is the day of the year                 */ 00093000
*/*                                                                  */ 00094000
*/*             MMDDYYYY= Decimal digits of the form MMDDYYYY,       */ 00095000
*/*                       where:                                     */ 00096000
*/*                       MM  is the month of the year               */ 00097000
*/*                       DD  is the day of the month                */ 00098000
*/*                       YYYY is the four digits of the year        */ 00099000
*/*                                                                  */ 00100000
*/*             DDMMYYYY= Decimal digits of the form DDMMYYYY,       */ 00101000
*/*                       where:                                     */ 00102000
*/*                       DD  is the day of the month                */ 00103000
*/*                       MM  is the month of the year               */ 00104000
*/*                       YYYY is the four digits of the year        */ 00105000
*/*                                                                  */ 00106000
*/*             YYYYMMDD= Decimal digits of the form YYYYMMDD,       */ 00107000
*/*                       where:                                     */ 00108000
*/*                       YYYY is the four digits of the year        */ 00109000
*/*                       MM  is the month of the year               */ 00110000
*/*                       DD  is the day of the month                */ 00111000
*/*                                                                  */ 00112000
*/*                                                                  */ 00113000
*/*          ,MF=         Macro form.  Default is Standard form.     */ 00114000
*/*                L        Generate a parameter list.               */ 00115000
*/*                                                                  */ 00116000
*/*               (E,addr)  Execute, where addr is the address of    */ 00117000
*/*                         the parameter list to be used.           */ 00118000
*/*                                                                  */ 00119000
*/*                                                                  */ 00120000
*/*01*  INVOCATION = (PLAS)                                          */ 00121000
*/*                        See TIMEP for PLAS input description.     */ 00122000
*/*                                                                  */ 00123000
*/*01*  NOTES =                                                      */ 00124000
*/*                                                                  */ 00125000
*/*02*    RESTRICTIONS = None                                        */ 00126000
*/*                                                                  */ 00127000
*/*02*    DEPENDENCIES = None                                        */ 00128000
*/*                                                                  */ 00129000
*/*03*      CHARACTER-CODE DEPENDENCIES= EBCDIC                      */ 00130000
*/*                                                                  */ 00131000
*/*01*  MODULE TYPE = BIL MACRO                                      */ 00132000
*/*                                                                  */ 00133000
*/*02*    PROCESSOR = ASSEMBLER-H  or  PLAS                          */ 00134000
*/*                                                                  */ 00135000
*/*02*    INVOCATION = See MACRO definition                          */ 00136000
*/*                                                                  */ 00137000
*/*01*  COMPONENT  =  SC1CV (Timer Supervision)                      */ 00138000
*/*                                                                  */ 00139000
*/*01*  DISTRIBUTION LIBRARY = AMACLIB                               */ 00140000
*/*                                                                  */ 00141000
*/**** END OF SPECIFICATIONS *****************************************/ 00142000
*/*                                                                     00143000
*                                                                       00144000
*    CHANGE ACTIVITY =                                                  00145000
*    $L0=        AOS/R2   721003  PZD81 :                               00146000
*    $L1=CLOCK   HBB4410  880411  PD16LD:  ETR support                  00147000
*    $D1=DCR1006 HBB4410  880819  PD16CU:  ETR support                  00148000
*    $D2=DHB1022 HBB4410  890206  PD16H5:  PC entry support             00149000
*    $P1=PHB1085 HBB4410  890206  PD16H5:  Fix flag                     00150000
*    $D3=DHB1031 HBB4410  890313  PD162M:  DATETYPE support             00151000
*    $P2=PEO3587 HBB4410  900226  PD16H5:  Remove ERRET support.        00152000
*    $P3=PEO3805 HBB4410  900226  PD16H5:  Fix mnote level.             00153000
*    $P4=PEO4152 HBB4410  900504  PD16H5:  4 digit year.                00154000
*                                                                       00155000
*    @D61ADTN: VSE/ESA COMPATIBILITY WITH MVS/ESA                       00156000
*                                                                       00157000
*********************************************************************** 00158000
.*                                                                      00159000
.SKIP    ANOP ,                                                    @L1A 00160000
*        MACDATE  06/09/95                                     @D61PDTN 00161000
.*                                                                      00162000
.********************************************************************** 00163000
.*                                                                    * 00164000
.*       Declare GLOBAL and LOCAL variables                           * 00165000
.*                                                                    * 00166000
.********************************************************************** 00167000
.*                                                                      00168000
         LCLA  &DATET         DATETYPE bitstring                   @D3A 00169000
         LCLA  &CTR           Return code flag                          00170000
         LCLB  &B(4)          UNIT flag                                 00171000
         LCLB  &D(4)          DATETYPE flag                        @D3A 00172000
         LCLB  &IHBERR        Error flag                           @L1A 00173000
         LCLB  &SYSTEM        Linkage type flag                    @D2A 00174000
         LCLC  &AR                                                 @D2A 00175000
         LCLC  &PL                                                 @D2A 00176000
         LCLC  &INAME                                              @D2A 00177000
         LCLC  &ENAME                                              @D2A 00178000
.*                                                                    * 00179000
.********************************************************************** 00180000
.*      Check number of positionals                                   * 00181000
.********************************************************************** 00182000
         AIF   (N'&SYSLIST GE 3).E1      Too many positionals      @L1A 00183000
.EXPOS   ANOP  ,                                                   @L1A 00184000
.*                                                                    * 00185000
.********************************************************************** 00186000
.*      ADDR operand                                                  * 00187000
.********************************************************************** 00188000
         AIF   (('&UNIT' EQ 'DEC' OR '&UNIT' EQ '' OR '&UNIT' EQ 'TU' O*00189000
               R '&UNIT' EQ 'BIN') AND ('&ADDR' NE '' AND '&LINKAGE'   *00190000
               NE 'SYSTEM')).E2                                    @D2C 00191000
         AIF   (('&UNIT' EQ 'MIC' OR '&UNIT' EQ 'STCK') AND            *00192000
               '&ADDR' EQ '' ).E3                                  @L1C 00193000
.EXADDR  ANOP  ,                                                   @L1A 00194000
.*                                                                      00195000
.********************************************************************** 00196000
.*       UNIT operand                                                 * 00197000
.********************************************************************** 00198000
&B(1)    SETB  ('&UNIT' EQ 'DEC' OR '&UNIT' EQ '')                      00199000
&B(2)    SETB  ('&UNIT' EQ 'BIN')                                       00200000
&B(3)    SETB  ('&UNIT' EQ 'MIC')                                       00201000
&B(4)    SETB  ('&UNIT' EQ 'STCK')                                      00202000
         AIF   ('&UNIT' EQ 'TU').EXUNIT                            @L1C 00203000
&CTR     SETA  &B(2)+2*&B(1)+3*&B(3)+4*&B(4)    Set UNIT bit       @L1C 00204000
         AIF   (&CTR EQ 0 AND '&LINKAGE' NE 'SYSTEM').E5           @D2C 00205000
         AIF   (&CTR EQ 0 AND '&LINKAGE' EQ 'SYSTEM').E17          @D2A 00206000
.EXUNIT  ANOP  ,                                                   @L1A 00207000
.*                                                                      00208000
.********************************************************************** 00209000
.*       ZONE keyword                                                 * 00210000
.********************************************************************** 00211000
         AIF   ('&ZONE' EQ '').EXZONE                              @L1C 00212000
         AIF   ('&UNIT' EQ 'STCK').E7           ZONE meaningless        00213000
         AIF   ('&ZONE' EQ 'LT').EXZONE                            @L1C 00214000
         AIF   ('&ZONE' NE 'GMT').E8            ZONE invalid            00215000
&CTR     SETA  &CTR+128                           GMT FLAG         @L1C 00216000
.EXZONE  ANOP  ,                                                   @L1A 00217000
.*                                                                      00218000
.********************************************************************** 00219000
.*       LINKAGE keyword                                              * 00220000
.********************************************************************** 00221000
         AIF   ('&LINKAGE' EQ 'SVC' OR '&LINKAGE' EQ '').EXLINK    @D2A 00222000
         AIF   ('&LINKAGE' NE 'SYSTEM').E9                         @D2A 00223000
         AGO   .PCLINK                                             @D2A 00224000
&SYSTEM  SETB  1                                                   @D2A 00225000
.EXLINK  ANOP                                                      @D2A 00226000
.********************************************************************** 00227000
.*       MF keyword                                                   * 00228000
.********************************************************************** 00229000
         AIF   ('&MF' NE '').E14                                   @D2A 00230000
.E14CONT ANOP                                                      @D2A 00231000
.*                                                                      00232000
.********************************************************************** 00233000
.*       ERRET keyword                                                * 00234000
.*           This keyword is obsolete.                             @P2A 00235000
.********************************************************************** 00236000
         AIF   ('&ERRET' NE '').E10     ERRET no longer supported @P2C  00237000
.E10CONT  ANOP                                                     @P2C 00238000
.*                                                                      00239000
.********************************************************************** 00240000
.*       DATETYPE keyword                                             * 00241000
.********************************************************************** 00242000
         AIF   ('&DATETYPE' NE '').E19                             @D3A 00243000
.E19CONT ANOP                                                      @D2A 00244000
.*                                                                      00245000
.********************************************************************** 00246000
.*       Generate code to set up SVC                                  * 00247000
.********************************************************************** 00248000
         AIF   (&IHBERR EQ 1).DONE            Exit if errors       @L1A 00249000
         AIF   (&CTR NE 0).LC                                           00250000
&NAME    SR    1,1                     ZERO 1 TO SPECIFY UNIT           00251000
         AGO   .SVC                                                     00252000
.LC      ANOP                                                           00253000
&NAME    LA    1,&CTR.(0,0)            LOAD 1 TO SPECIFY UNIT           00254000
         AIF   ('&UNIT' NE 'MIC' AND '&UNIT' NE 'STCK').SVC             00255000
         AIF   ('&ADDR'(1,1) EQ '(').REG                                00256000
         LA    0,&ADDR                                                  00257000
         AGO   .SVC                                                     00258000
.REG     ANOP                                                           00259000
         AIF   ('&ADDR' EQ '(0)').SVC                                   00260000
         LR    0,&ADDR(1)                                               00261000
.********************************************************************** 00262000
.*       Issue SVC                                                    * 00263000
.********************************************************************** 00264000
* R14 is used as work register for the SIMSVC                  @D61ADTN 00265000
.SVC     SIMSVC WR=(14)           ISSUE VSE/ESA TIME SVC       @D61ADTN 00266000
         SVC   11                      ISSUE TIME SVC          @D61ADTN 00267000
.DONE    MEXIT                                                          00268000
.*                                                                      00269000
.********************************************************************** 00270000
.*       Validate PC linkage keywords and options                     * 00271000
.********************************************************************** 00272000
.PCLINK  ANOP                                                      @D2A 00273000
         AIF   ('&ERRET' NE '').E6      ERRET not supported on PC  @D2A 00274000
.E6CONT  ANOP                                                      @D2A 00275000
         AIF   ('&UNIT' EQ 'TU').E4     TU not supported on PC     @D2A 00276000
.E4CONT  ANOP                                                      @D2A 00277000
         AIF   ('&ADDR' EQ '' AND '&MF' NE 'L').E18  Except for    @D2A 00278000
.*                                      list form, address required     00279000
.E18CONT ANOP                                                      @D2A 00280000
         AIF   (N'&DATETYPE GT 1).E20  Too many sublists           @D3A 00281000
&D(1)    SETB  ('&DATETYPE' EQ 'MMDDYYYY')                         @P4C 00282000
&D(2)    SETB  ('&DATETYPE' EQ 'DDMMYYYY')                         @P4C 00283000
&D(3)    SETB  ('&DATETYPE' EQ 'YYYYMMDD')                         @P4C 00284000
&D(4)    SETB  ('&DATETYPE' EQ 'YYYYDDD' OR '&DATETYPE' EQ '')     @P4C 00285000
         AIF   (&D(1) EQ 0 AND &D(2) EQ 0 AND &D(3) EQ 0 AND &D(4) EQ 0X00286000
               ).E21                    Invalid DATETYPE specified @D3A 00287000
&DATET   SETA  &D(1)+&D(2)*2+&D(3)*3    Create DATATYPE bitstring  @D3A 00288000
.********************************************************************** 00289000
.*       Validate MF keywords and options                             * 00290000
.********************************************************************** 00291000
.E21CONT ANOP                                                      @D2A 00292000
         AIF   ('&MF' EQ '').STD        Default is Standard form   @D2A 00293000
         AIF   ('&MF' EQ 'L').LIST      List form                  @D2A 00294000
         AIF   (N'&MF GT 2).E11         Too many sublists          @D2A 00295000
         AIF   ('&MF(1)' NE 'E').E12    Ensure E was specified     @D2A 00296000
         AIF   ('&MF(2)' EQ '').E13     Must be an address         @D2A 00297000
         AIF   (N'&MF(2) GT 2).E16      Too many addresses         @D2A 00298000
.ERRCHK  ANOP                                                      @D2A 00299000
         AIF   (&IHBERR EQ 1).DONE            Exit if errors       @D2A 00300000
.********************************************************************** 00301000
.*       Generate PC linkage to service routine                       * 00302000
.********************************************************************** 00303000
         AIF   ('&MF(2)'(1,1) EQ '(').ISAREG                       @D2A 00304000
&NAME    LAE   1,&MF(2)                PARAMETER LIST ADDRESS      @D2A 00305000
         AGO   .CONT                                               @D2A 00306000
.ISAREG  AIF   ('&MF(2)' EQ '(1)').CONT                            @D2A 00307000
&PL      SETC  '&MF(2)'(2,K'&MF(2)-2)  EXTRACT REGISTER NAME       @D2A 00308000
&NAME    LAE   1,0(0,&PL)              PARAMETER LIST ADDRESS      @D2A 00309000
.CONT    ANOP                                                      @D2A 00310000
         XC    0(28,1),0(1)            CLEAR PARAMETER LIST        @D2A 00311000
.COMMON  MVI   3(1),&CTR               TIME FLAGS INTO P LIST      @D2A 00312000
         MVI   2(1),&DATET             DATE FLAGS INTO P LIST      @D3A 00313000
         L     14,16(0,0)              CVT ADDRESS                 @D2A 00314000
         L     14,772(14,0)            SFT ADDRESS                 @D2A 00315000
         L     14,304(14,0)            LX/EX FOR SERVICE ROUTINE   @D2A 00316000
         PC    0(14)                                               @D2A 00317000
         AIF   ('&ADDR'(1,1) EQ '(').REGAD   IN A REGISTER         @D2A 00318000
         LAE   14,&ADDR                ADDRESS OF USER'S AREA      @D2A 00319000
         AGO   .MOVE                                               @D2A 00320000
.REGAD   ANOP                                                      @D2A 00321000
&AR      SETC  '&ADDR'(2,K'&ADDR-2)    EXTRACT REGISTER NAME       @D2A 00322000
         LAE   14,0(0,&AR)             ADDRESS OF USER'S AREA      @D2A 00323000
.MOVE    ANOP                                                      @D2A 00324000
         MVC   0(16,14),12(1)          MOVE OUTPUT FROM PARM LIST  @D2A 00325000
*                                      TO THE USER'S AREA               00326000
         MEXIT                                                     @D2A 00327000
.********************************************************************** 00328000
.*       Generate parameter list for Standard form of macro           * 00329000
.********************************************************************** 00330000
.STD     ANOP                                                      @D2A 00331000
         AIF   (&IHBERR EQ 1).DONE            Exit if errors       @D2A 00332000
&INAME   SETC   'IHB'.'&SYSNDX'.'D'                                @D2A 00333000
&ENAME   SETC   'IHB'.'&SYSNDX'.'W'                                @D2A 00334000
&NAME    DS    0H                                                  @D2A 00335000
         LAE   1,0(0,0)                 ZERO ACCESS REGISTER 1     @D2A 00336000
         LA    1,&INAME                 POINT TO PARAMETER LIST    @D2A 00337000
         B     &ENAME                   BRANCH AROUND LIST         @D2A 00338000
&INAME   DS    0F                                                  @D3A 00339000
         DC    FL2'0'                                              @D3C 00340000
         DC    FL1'0'                                              @D3A 00341000
         DC    FL1'0'                                              @D3A 00342000
         DC    2F'0'                                               @D2A 00343000
         DC    2F'0'                                               @D2A 00344000
         DC    2F'0'                                               @D2A 00345000
&ENAME   DS    0H                                                  @D2A 00346000
         AGO    .COMMON                                            @D2A 00347000
.********************************************************************** 00348000
.*       Generate List form parameter list                            * 00349000
.********************************************************************** 00350000
.LIST    ANOP                                                      @D2A 00351000
         AIF   ('&UNIT' NE '' OR '&ZONE' NE '' OR '&ADDR' NE '' OR '&DAX00352000
               TETYPE' NE '').E15                                  @D3C 00353000
.E15CONT ANOP                                                      @D2A 00354000
         AIF   (&IHBERR EQ 1).DONE            Exit if errors       @D2A 00355000
&NAME    DS    0F                                                  @D3A 00356000
         DS    XL28                                                @D3C 00357000
         MEXIT                                                     @D2A 00358000
.*                                                                    * 00359000
.********************************************************************** 00360000
.*                                                                    * 00361000
.*       Error Messages                                               * 00362000
.*                                                                    * 00363000
.********************************************************************** 00364000
.E1      MNOTE 12,' TOO MANY POSITIONAL PARAMETERS - &SYSLIST(3)'  @L1A 00365000
&IHBERR  SETB  1                          Indicate error           @L1A 00366000
         AGO   .EXPOS                     Continue                 @L1A 00367000
.E2      MNOTE 12,' AN ADDRESS WAS SPECIFIED WITH AN OPTION OTHER THAN X00368000
               ''STCK'' OR ''MIC'' WHEN LINKAGE=SYSTEM NOT SPECIFIED '  00369000
&IHBERR  SETB  1                          Indicate error           @L1A 00370000
         AGO   .EXADDR                    Continue                 @L1C 00371000
.E3      MNOTE 12,' AN ADDRESS IS REQUIRED WITH &SYSLIST(1)'       @L1C 00372000
&IHBERR  SETB  1                          Indicate error           @L1A 00373000
         AGO   .EXADDR                    Continue                 @L1C 00374000
.E4      MNOTE 12,' ''TU'' IS NOT VALID WITH LINKAGE=SYSTEM '      @D2A 00375000
&IHBERR  SETB  1                          Indicate error           @D2A 00376000
         AGO   .E4CONT                    Continue                 @D2A 00377000
.E5      MNOTE 12,' &UNIT IS NOT VALID. UNIT MUST BE ''DEC'' ''BIN'' ''X00378000
               TU'' ''MIC'' OR ''STCK'' '                          @L1C 00379000
&IHBERR  SETB  1                          Indicate error           @L1A 00380000
         AGO   .EXUNIT                    Continue                 @L1C 00381000
.E6      MNOTE 12,' ''ERRET'' IS NOT VALID WITH LINKAGE=SYSTEM '   @D2A 00382000
&IHBERR  SETB  1                          Indicate error           @D2A 00383000
         AGO   .E6CONT                    Continue                 @D2A 00384000
.E7      MNOTE  0,' ZONE NOT MEANINGFUL WITH STCK--ZONE IGNORED'   @P3C 00385000
         AGO   .EXZONE                    Continue                 @L1C 00386000
.E8      MNOTE 12,' ZONE PARAMETER MUST BE ''LT'' OR ''GMT'' '     @D1C 00387000
&IHBERR  SETB  1                          Indicate error           @L1A 00388000
         AGO   .EXZONE                    Continue                 @L1C 00389000
.E9      MNOTE 12,'LINKAGE KEYWORD MUST BE ''SVC'' OR ''SYSTEM'' ' @D2A 00390000
&IHBERR  SETB  1                          Indicate error           @D2A 00391000
         AGO   .EXLINK                    Continue                 @D2A 00392000
.E10     MNOTE 0,' THE ERRET KEYWORD IS NO LONGER SUPPORTED. IT IS IGNOX00393000
               RED.'                                               @P3C 00394000
         AGO   .E10CONT                   Continue                 @P2C 00395000
.E11     MNOTE 12,' TOO MANY SUBLISTS SPECIFIED FOR ''MF'' KEYWORD '    00396000
&IHBERR  SETB  1                          Indicate error           @D2A 00397000
         AGO   .ERRCHK                    Continue                 @D2A 00398000
.E12     MNOTE 12,' MF KEYWORD MUST BE ''L'' OR ''E'' '            @D2A 00399000
&IHBERR  SETB  1                          Indicate error           @D2A 00400000
         AGO   .ERRCHK                    Continue                 @D2A 00401000
.E13     MNOTE 12,' NO PARAMETER LIST ADDRESS SPECIFIED FOR ''MF'' KEYW*00402000
               ORD '                                               @D2A 00403000
&IHBERR  SETB  1                          Indicate error           @D2A 00404000
         AGO   .ERRCHK                    Continue                 @D2A 00405000
.E14     MNOTE 12,' MF KEYWORD IS ONLY VALID WITH LINKAGE=SYSTEM ' @D2A 00406000
&IHBERR  SETB  1                          Indicate error           @D2A 00407000
         AGO   .E14CONT                   Continue                 @D2A 00408000
.E15     MNOTE 12,' UNIT, ADDR, ZONE, AND DATETYPE NOT VALID WITH MF=L X00409000
               '                                                   @D3C 00410000
&IHBERR  SETB  1                          Indicate error           @D2A 00411000
         AGO   .E15CONT                   Continue                 @D2A 00412000
.E16     MNOTE 12,' FOR MF KEYWORD - TOO MANY PARAMETER ADDRESSES SPECI*00413000
                FIED '                                             @D2A 00414000
&IHBERR  SETB  1                          Indicate error           @D2A 00415000
         AGO   .ERRCHK                    Continue                 @D2A 00416000
.E17     MNOTE 12,' &UNIT IS NOT VALID. UNIT MUST BE ''DEC'' ''BIN'' ''X00417000
               MIC'' OR ''STCK'' '                                 @D2A 00418000
&IHBERR  SETB  1                          Indicate error           @D2A 00419000
         AGO   .EXUNIT                    Continue                 @D2A 00420000
.E18     MNOTE 12,' ADDRESS IS REQUIRED FOR LINKAGE=SYSTEM '       @D2A 00421000
&IHBERR  SETB  1                          Indicate error           @D2A 00422000
         AGO   .E18CONT                   Continue                 @D2A 00423000
.E19     MNOTE 12,' ''DATETYPE'' IS ONLY VALID WITH LINKAGE=SYSTEM '   X00424000
                                                                   @D3C 00425000
&IHBERR  SETB  1                          Indicate error           @D3A 00426000
         AGO   .E19CONT                   Continue                 @D3A 00427000
.E20     MNOTE 12,' TOO MANY SUBLISTS SPECIFIED FOR ''DATETYPE'' KEYWORX00428000
               D '                                                 @D3A 00429000
&IHBERR  SETB  1                          Indicate error           @D3A 00430000
         AGO   .E21CONT                   Continue                 @D3A 00431000
.E21     MNOTE 12,' DATETYPE KEYWORD MUST BE ''YYYYDDD'' ''MMDDYYYY'' 'X00432000
               'DDMMYYYY'' OR ''YYYYMMDD'' '                       @P4C 00433000
&IHBERR  SETB  1                          Indicate error           @D3A 00434000
         AGO   .E21CONT                   Continue                 @D3A 00435000
         MEND                                                           00436000
**/                                                                     00437000
*TIME:   MACRO                                                          00438000
*        KEYS(ZONE,ERRET,LINKAGE,MF,DATETYPE);                /* @P1C*/ 00439000
*        ANS('?'||MACLABEL||'TIMEP '||MACLIST||MACKEYS||';');           00440000
*%END;                                                                  00441000
