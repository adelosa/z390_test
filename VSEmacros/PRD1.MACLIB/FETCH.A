         MACRO                                                 @DL29ZCJ 00050000
&LABEL   FETCH &PNAM,&ORIDE,&LIST=,&SYS=NO,&DE=NO,&MFG=,       @D35XDGS*00100000
               &RET=NO                                         @D14CDBC 00150000
         GBLB  &FCB,&FCB1,&FCB2                                @D14CDBC 00200000
         LCLA  &D,&DISP,&OFFS1,&TPN,&TOR,&TLI,&TMF,&DIRL,&DIRH @D36JDVB 00250000
         LCLB  &SY,&DY,&I1,&BNLIB,&BRET                        @D14CDBC 00300000
         LCLC  &PHASE,&AREA,&IPN,&IOR,&ILI,&R1,&TARGET,&DIRID  @D14CDVB 00350000
&OFFS1   SETA  4                                               @D35XDGS 00400000
&DISP    SETA  8                                               @D35XDGS 00450000
&R1      SETC  '1'                                             @D37ZDVB 00500000
&DIRID   SETC  '000000'                                        @D14CDVB 00550000
&DIRL    SETA  38                                              @D36JDVB 00600000
.*-------------------------------------------------------------@D368DVB 00650000
.*                                                             @D368DVB 00700000
.*       IBM DISK OPERATING SYSTEM                             @D368DVB 00750000
*        SUPERVISOR - FETCH -  5686-007-06                     @D149DVB 00800000
         AGO   .ZZZ                                            @D368DVB 00850000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"           @D31UDMZ 00900000
*        5686-007 (C) COPYRIGHT IBM CORP 1981,1988             @D31UDMZ 00950000
*        ALL RIGHTS RESERVED                                   @D31UDMZ 01000000
*        LICENSED MATERIALS-PROPERTY OF IBM                    @D368DVB 01050000
.*       SEE COPYRIGHT INSTRUCTIONS, G120-2083                 @D31UDMZ 01100000
.*                                                             @D368DVB 01150000
.ZZZ     ANOP                                                  @D368DVB 01200000
.*-------------------------------------------------------------@D368DVB 01250000
.*                                                                      01300000
.* /* START OF SPECIFICATIONS ***************************************** 01350000
.*                                                                      01400000
.*01* MODULE-TYPE = SUPERVISOR GENERATION AND USER INTERFACE MACRO      01450000
.*                                                                      01500000
.*01* DECRIPTIVE NAME = FETCH MACRO                                     01550000
.*                                                                      01600000
.*01* NOTES = CHANGE ACTIVITY                                           01650000
.*    SUPPORT OF REENTRANT PROGRAMMING                         @D35XDGS 01700000
.*    SUPPORT OF CONCATENATED LIBRARIES                        @D36JDVB 01750000
.**** END OF SPECIFICATIONS ******************************************* 01800000
.*                                                                      01850000
         AIF  ('&LABEL' EQ '').NOLAB                           @D35XDGS 01900000
&LABEL   DC    0H'0'                                           @D35XDGS 01950000
.NOLAB   ANOP                                                  @D35XDGS 02000000
.*                                                             @D35XDGS 02050000
.*       PHASE NAME OPERAND EVALUATION ----------------------- @D35XDGS 02100000
.*                                                             @D35XDGS 02150000
         AIF   (T'&PNAM NE 'O').PNAV                           @D35XDGS 02200000
         MNOTE 8,'PHASE NAME OPERAND MISSING,GENERATION SUPPRESSED'     02250000
         AGO   .MTXT                                           @D14CDVB 02300000
.PNAV    AIF   ('&PNAM'(1,1) EQ '(').PNDYN                     @D35XDGS 02350000
&IPN     SETC  '&PNAM'                                         @D35XDGS 02400000
         AIF   (K'&PNAM LE 8).PN0                              @D35XDGS 02450000
         MNOTE 3,'** PHASE NAME TRUNCATED TO 8 CHARACTERS **'  @D35XDGS 02500000
&IPN     SETC  '&PNAM'(1,8)                                    @D35XDGS 02550000
.PN0     ANOP                                                  @D35XDGS 02600000
&TPN     SETA  1              1= A-TYPE                        @D35XDGS 02650000
&DISP    SETA  &DISP+8                                         @D35XDGS 02700000
         AGO   .PN1                                            @D35XDGS 02750000
.PNDYN   AIF   ('&PNAM'(1,3) EQ '(S,').PNSTYP                  @D35XDGS 02800000
&IPN     SETC  '&PNAM'(2,K'&PNAM-2)                            @D35XDGS 02850000
&TPN     SETA  2              2= REGISTER                      @D35XDGS 02900000
&OFFS1   SETA  &OFFS1+4       INCREMENT INSTRUCTION OFFSET     @D35XDGS 02950000
.*                            ( LA  ASSUMED )                  @D35XDGS 03000000
         AGO   .PN1                                            @D35XDGS 03050000
.PNSTYP  ANOP                                                  @D35XDGS 03100000
&IPN     SETC  '&PNAM'(4,K'&PNAM-4)                            @D35XDGS 03150000
&TPN     SETA  4              4= S-TYPE                        @D35XDGS 03200000
&OFFS1   SETA  &OFFS1+8       INCREMENT INSTRUCTION OFFSET     @D35XDGS 03250000
.*                            ( LA , ST  ASSUMED )             @D35XDGS 03300000
.PN1     ANOP                                                  @D35XDGS 03350000
.*                                                             @D35XDGS 03400000
.*       ENTRY POINT OVERRIDE OPERAND EVALUATION ------------- @D35XDGS 03450000
.*                                                             @D35XDGS 03500000
         AIF   (T'&ORIDE EQ 'O').OR1                           @D35XDGS 03550000
.ORAV    AIF   ('&ORIDE'(1,1) EQ '(').ORDYN                    @D35XDGS 03600000
&IOR     SETC  '&ORIDE'                                        @D35XDGS 03650000
&TOR     SETA  1              1= A-TYPE                        @D35XDGS 03700000
         AGO   .OR1                                            @D35XDGS 03750000
.ORDYN   AIF   ('&ORIDE'(1,3) EQ '(S,').ORSTYP                 @D35XDGS 03800000
&IOR     SETC  '&ORIDE'(2,K'&ORIDE-2)                          @D35XDGS 03850000
&TOR     SETA  2              2= REGISTER                      @D35XDGS 03900000
         AGO   .OR1                                            @D35XDGS 03950000
.ORSTYP  ANOP                                                  @D35XDGS 04000000
&IOR     SETC  '&ORIDE'(4,K'&ORIDE-4)                          @D35XDGS 04050000
&TOR     SETA  4              4= S-TYPE                        @D35XDGS 04100000
.OR1     ANOP                                                  @D35XDGS 04150000
.*                                                             @D35XDGS 04200000
.*       LIST POINTER OPERAND EVALUATION --------------------- @D35XDGS 04250000
.*                                                             @D35XDGS 04300000
         AIF   (T'&LIST EQ 'O').LI1                            @D35XDGS 04350000
.LIAV    AIF   ('&LIST'(1,1) EQ '(').LIDYN                     @D35XDGS 04400000
&ILI     SETC  '&LIST'                                         @D35XDGS 04450000
&TLI     SETA  1              1= A-TYPE                        @D35XDGS 04500000
         AGO   .LI1                                            @D35XDGS 04550000
.LIDYN   AIF   ('&LIST'(1,3) EQ '(S,').LISTYP                  @D35XDGS 04600000
&ILI     SETC  '&LIST'(2,K'&LIST-2)                            @D35XDGS 04650000
&TLI     SETA  2              2= REGISTER                      @D35XDGS 04700000
&OFFS1   SETA  &OFFS1+4       INCREMENT INSTRUCTION OFFSET     @D35XDGS 04750000
.*                            ( LA  ASSUMED )                  @D35XDGS 04800000
         AGO   .LI1                                            @D35XDGS 04850000
.LISTYP  ANOP                                                  @D35XDGS 04900000
&ILI     SETC  '&LIST'(4,K'&LIST-4)                            @D35XDGS 04950000
&TLI     SETA  4              4= S-TYPE                        @D35XDGS 05000000
&OFFS1   SETA  &OFFS1+8       INCREMENT INSTRUCTION OFFSET     @D35XDGS 05050000
.*                            ( LA , STCM  ASSUMED )           @D35XDGS 05100000
.LI1     ANOP                                                  @D35XDGS 05150000
.*                                                             @D35XDGS 05200000
.*       DE OPTION EVALUATION -------------------------------- @D35XDGS 05250000
.*                                                             @D35XDGS 05300000
&DY      SETB  ('&DE' EQ 'YES' OR '&DE'(1,3) EQ 'VSE')         @D14CDVB 05350000
&BNLIB   SETB  ('&DE'(1,3) EQ 'VSE')                           @D14CDVB 05400000
         AIF   (&DY OR '&DE' EQ 'NO').DEFSY      BR IF VALID   @DL29ZCJ 05450000
         MNOTE 3,'** DE TERM MUST BE NO, YES OR VSE(-FORM) --- NO ASSUM*05500000
               ED **'                                          @D14CDVB 05550000
         AIF   (&FCB1 OR &FCB ).DEFSY                          @D14CDVB 05600000
&FCB1    SETB  1                                               @D14CDVB 05650000
         SPACE 1                                               @D14CDVB 05700000
*---------------------------------------------------------------------* 05750000
* ---> NOTES TO  D E  PARAMETER:                                      * 05800000
*                                                                     * 05850000
*        DE=YES|VSE(-FORM): DIRECTORY ENTRY PROVIDED                  * 05900000
*            = VSE:   THE DIR_ENTRY HAS  V S E  (- N L I B) FORMAT    * 05950000
*                                                                     * 06000000
*            0   PHASENAME   CL8                                      * 06050000
*            8   DIRVSEID    XL3'FF'     ID FOR NEW FORMAT            * 06100000
*           11   DIRN        XL1'0E'     LENGTH OF DE                 * 06150000
*           12   DIRLMBR     F           LENGTH OF TXT IN BYTES       * 06200000
*           16   DIRC        XL1         FLAGS                        * 06250000
*           17               XL3         RESERVED                     * 06300000
*           20   DIRACOPY    A           INTEGRITY INFORMATION        * 06350000
*           24   DIRALPT     A           LOADPOINT AT LINKEDT TIME    * 06400000
*           28   DIRAEPT     A           ENTRYPOINT AT LINKEDT TIME   * 06450000
*           32   DIRAPART    A           BEG PARTITION                * 06500000
*           36   DIRAVSA     A           ENTRYPOINT IN SVA            * 06550000
*                                                                     * 06600000
*            = YES:   THE DIR_ENTRY HAS  OLD DE FORMAT                * 06650000
*                                                                     * 06700000
*            0   PHASENAME   CL8                                      * 06750000
*            8   DIRCOPY     XL3'0'      INTEGRITY INFORMATION        * 06800000
*           11   DIRN        XL1         LENGTH OF DE                 * 06850000
*           12   DIRTT       H           NUMBER OF 1K LIBRARY BLOCKS  * 06900000
*           14   DIRLL       H           TXT_LENGTH IN BYTES IN LAST  * 06950000
*                                                    TXT BLOCK        * 07000000
*           16   DIRC        XL1         FLAGS                        * 07050000
*           17               XL1         RESERVED                     * 07100000
*           18   DIRPPP      AL3         LOADPOINT AT LINKEDT TIME    * 07150000
*           21   DIREEE      AL3         ENTRYPOINT AT LINKEDT TIME   * 07200000
*           24               XL3         *** NOT SUPPORTED ***        * 07250000
*           26   DIRAAA      AL3         PARTITION START ADDR         * 07300000
*           30   DIRASVA     A           ENTRYPOINT IN SVA            * 07350000
*           34               A           *** NOT SUPPORTED ***        * 07400000
*                                                                     * 07450000
*    --->THE 'DIRC' FLAGS CAN BE USED TO TEST THE STATE OF THE PHASE  * 07500000
*                X'80'        SELFRELOCATABLE                         * 07550000
*                X'40'        RELOCATABLE                             * 07600000
*                X'20'        SVA - ELIGIBLE                          * 07650000
*                             ..( A SVA_PHASE IS RELOCATABLE AND SVA  * 07700000
*                                 - ELIGIBLE )                        * 07750000
*                X'10'        PHASE LOCATED IN SVA                    * 07800000
*                X'01'        SLMAP AVAILABLE                         * 07850000
*    ---> MOREOVER, 'DIRC' FLAGS REFLECT THE STATE OF THE IN_CORE_    * 07900000
*         DIR_ENTRY                                                   * 07950000
*                X'08'        NON_SYSLIB ENTRY                        * 08000000
*                X'04'        ENTRY NOT FOUND                         * 08050000
*                X'02'        ENTRY ACTIVE   ( FETCH/LOAD SERVICE     * 08100000
*                                   HAS ALREADY PROCESSED DIR_ENTRY ) * 08150000
*    ---> N O T E :  THE 'DIRC' FLAGS ARE BITS AND SHOULD BE TESTED   * 08200000
*                    BY 'TM' - ASSEMBLER INSTRUCTIONS                 * 08250000
*    ---> N O T E :  'DIRC' MUST BE INITIALIZED AS X'00'              * 08300000
*                    'DIRN' INDICATES THE NUMBER OF HALFWORDS AFTER   * 08350000
*                           'DIRN' TO BE PROVIDED BY THE FETCH SERVICE* 08400000
*                           E.G    X'02' LE 'DIRN' LE X'0B'           * 08450000
*                           'DIRN' = X'00' ... NO RETURN_INFO PROVIDED* 08500000
*                                                                     * 08550000
*---------------------------------------------------------------------* 08600000
         SPACE 1                                               @D14CDVB 08650000
.DEFSY   ANOP                                                  @DL29ZCJ 08700000
         AIF    (NOT &BNLIB).DEFSY1                            @D14CDVB 08750000
&DIRID   SETC  'FFFFFF'                                        @D14CDVB 08800000
&DIRL    SETA  40                                              @D14CDVB 08850000
.DEFSY1  ANOP                                                  @D14CDVB 08900000
.*                                                             @D35XDGS 08950000
.*       SYS OPTION EVALUATION ------------------------------- @D35XDGS 09000000
.*                                                             @D35XDGS 09050000
&SY      SETB  ('&SYS' EQ 'YES')       SYS LIST FIRST PARAM    @DL29ZCJ 09100000
         AIF   (&SY OR '&SYS' EQ 'NO').DEFRET    BR IF VALID   @D14CDBC 09150000
         MNOTE 3,'** SYS PARAM MUST BE NO OR YES - NO ASSUMED **'       09200000
.DEFRET  ANOP                                                  @D14CDBC 09250000
.*                                                             @D14CDBC 09300000
.*       RET (RETURNE CODE) OPTION EVALUATION  --------------- @D14CDBC 09350000
.*                                                             @D14CDBC 09400000
&BRET    SETB  ('&RET' EQ 'YES')           RET PARAMETER       @D14CDBC 09450000
         AIF   (&BRET OR '&RET' EQ 'NO').DEF9    BR IF VALID   @D14CDBC 09500000
         MNOTE 3,'** RET PARAM MUST BE NO OR YES - NO ASSUMED **'       09550000
         AIF   (&FCB2).DEF9                                    @D14CDVB 09600000
&FCB2    SETB  1                                               @D14CDVB 09650000
         SPACE 1                                               @D14CDVB 09700000
*---------------------------------------------------------------------* 09750000
* ---> NOTES TO  RET  PARAMETER:                                      * 09800000
*                                                                     * 09850000
*        DE=YES|NO      NO ASSUMED                                    * 09900000
*        RET:     =YES  THE FOLLOWING RETURNE CODES ARE GIVEN IN R15:BC 09950000
*                     0 :    OK                                      BC 10000000
*                     4 :    PHASE NOT FOUND (CANCEL EXIT X'22')     BC 10050000
*                     8 :    FETCH I/O ERROR (CANCEL EXIT X'2B')     BC 10100000
*                    12 :    INVALID LIB. STRUC. (CANCEL EXIT X'29') BC 10150000
*                    16 :    INVALID ADDRESS (CANCEL EXIT X'25')     BC 10200000
*                    20 :    SECURITY VIOLATION (CANCEL EXIT X'0B')  BC 10250000
*                    24 :    INCONSISTENT USER DIRECTORY STATE       BC 10300000
*                             (FETCH WAS NOT ABLE TO USE THE         BC 10350000
*                              PROVIDED DE                           BC 10400000
*                                       A N D                        BC 10450000
*                              FOUND AN INCONSISTENCY BETWEEN        BC 10500000
*                              USER DE AND LIBRARY DE.               BC 10550000
*                              FOR THIS CASE THE USER DE IS          BC 10600000
*                              OVERWRITTEN BY THE LIBRARY DE.        BC 10650000
*                              THE PHASE IS NOT LOADED               BC 10700000
*                              AND NOT EXECUTED.                     BC 10750000
*                              THE CHECKT VALUES ARE :               BC 10800000
*                                LENGTH OF PHASE, LOAD POINT,        BC 10850000
*                                ENTRY POINT, PARTITION START,       BC 10900000
*                                RELOCATION STATE.  )                BC 10950000
*---------------------------------------------------------------------* 11000000
.*                                                             @D35XDGS 11050000
.DEF9    AIF   (NOT &SY AND NOT &DY AND NOT &BRET                      *11100000
                                    AND  &TLI EQ 0).MF1        @D14CDBC 11150000
.*                                                             @D35XDGS 11200000
.*       MFG OPERAND EVALUATION ------------------------------ @D35XDGS 11250000
.*                                                             @D35XDGS 11300000
         AIF   (T'&MFG NE 'O').MFAV                            @D35XDGS 11350000
&AREA    SETC  '*'                                             @D35XDGS 11400000
&D       SETA  4                                               @D35XDGS 11450000
         AGO   .MF1                                            @D35XDGS 11500000
.MFAV    ANOP                                                  @D35XDGS 11550000
&D       SETA  0                                               @D35XDGS 11600000
&OFFS1   SETA  &OFFS1+10                                       @D35XDGS 11650000
         AIF   ('&MFG'(1,1) EQ '(').MFDYN                      @D35XDGS 11700000
&AREA    SETC  '&MFG'                                          @D35XDGS 11750000
&TMF     SETA  1              1= A-TYPE                        @D35XDGS 11800000
         AGO   .MF1                                            @D35XDGS 11850000
.MFDYN   AIF   ('&MFG'(1,3) EQ '(S,').MFSTYP                   @D35XDGS 11900000
&AREA    SETC  '0'.'&MFG'                                      @D35XDGS 11950000
&TMF     SETA  2              2= REGISTER                      @D35XDGS 12000000
         AGO   .MF1                                            @D35XDGS 12050000
.MFSTYP  ANOP                                                  @D35XDGS 12100000
&AREA    SETC  '&MFG'(4,K'&MFG-4)                              @D35XDGS 12150000
&TMF     SETA  4              4= S-TYPE                        @D35XDGS 12200000
.MF1     ANOP                                                  @D35XDGS 12250000
.*                                                             @D35XDGS 12300000
.*       OPERAND COMPATIBILITY TEST -------------------------- @D35XDGS 12350000
.*                                                             @D35XDGS 12400000
.DEF10   AIF   (NOT &DY OR &TLI EQ 0).DEF20                    @D35XDGS 12450000
         MNOTE 3,'** DE=NO ASSUMED SINCE LIST SPECIFIED **'    @D35XDGS 12500000
&DY      SETB  0                  ASSUME NO DIRECTORY ELEMENT  @DL29ZCJ 12550000
.DEF20   AIF   (&TPN NE 2 OR &TOR NE 2).DEF2A                  @D35XDGS 12600000
         AIF   ('&PNAM(1)' EQ '&ORIDE(1)').ER4   = REGS, ERROR @DL29ZCJ 12650000
.DEF2A   AIF   (&TPN NE 2 OR &TLI NE 2).DEF2B                  @DL29ZCJ 12700000
         AIF   ('&PNAM(1)' EQ '&LIST(1)').ER4    = REGS, ERROR @DL29ZCJ 12750000
.DEF2B   AIF   (&TOR NE 2 OR &TLI NE 2).DEF2C                  @DL29ZCJ 12800000
         AIF   ('&ORIDE(1)' NE '&LIST(1)').DEF2C               @D35XDGS 12850000
.ER4     MNOTE 8,'2 PARAMETERS USE SAME REGISTER,GENERATION SUPPRESSED' 12900000
.MTXT    AIF   (&FCB).MEXIT                                    @D14CDVB 12950000
&FCB     SETB  1                                               @D14CDVB 13000000
         SPACE 1                                               @D14CDVB 13050000
*---------------------------------------------------------------------* 13100000
*  NOTES TO FETCH MACRO:                                              * 13150000
*                                                                     * 13200000
*  --->  THE SYNTAX OF THE FETCH MACRO IS AS FOLLOWS:                 * 13250000
*                                                                     * 13300000
*   LABEL FETCH PHASENAME|(R1)              ... MANDATORY             * 13350000
*              <,ENTRYPOINT|(R0)>           ..                        * 13400000
*              <,DE=NO|YES|VSE(-FORM)>       ..                       * 13450000
*              <,LIST=LISTADDRESS|(R2)>        ... OPTIONAL           * 13500000
*              <,SYS=NO|YES>                   ..                     * 13550000
*              <,MFG=ADDRESS|(S,ADDRESS)|(R3)>..                      * 13600000
*              <,RET=NO|YES>                     ..                  BC 13650000
*                                                                     * 13700000
*  --->  PHASENAME: 8 BYTES ALPHAMERIC PHASENAME                      * 13750000
*        ENTRYPOINT:  ADDRESS WHERE THE PHASE SHOULD BE ENTERED AFTER * 13800000
*                     TYHE FETCH SERVICE.                             * 13850000
*        DE=YES|VSE(-FORM): DIRECTORY ENTRY PROVIDED                  * 13900000
*                     'YES' DIRECTORY ENTRY HAS  CONVENTIONAL FORMAT  * 13950000
*                     'VSE' DIRECTORY ENTRY HAS  N L I B - FORMAT     * 14000000
*        LIST:   A LOCAL DIRECTORY LIST HAS BEEN SPECIFIED, USUALLY   * 14050000
*                     GENERATED BY THE  G E N L  MACRO                * 14100000
*        SYS:    SELFEXPLANATORY                                      * 14150000
*        MFG:    ADDRESS OF A STORAGE AREA PROVIDED FOR A REENTRANT   * 14200000
*                     MACRO EXPANSION                                 * 14250000
*        RET:     =YES  THE FOLLOWING RETURNE CODES ARE GIVEN IN R15:BC 14300000
*                     0 :    OK                                      BC 14350000
*                     4 :    PHASE NOT FOUND (CANCEL EXIT X'22')     BC 14400000
*                     8 :    FETCH I/O ERROR (CANCEL EXIT X'2B')     BC 14450000
*                    12 :    INVALID LIB. STRUC. (CANCEL EXIT X'29') BC 14500000
*                    16 :    INVALID ADDRESS (CANCEL EXIT X'25')     BC 14550000
*                    20 :    SECURITY VIOLATION (CANCEL EXIT X'0B')  BC 14600000
*                    24 :    INCONSISTENT USER DIRECTORY STATE       BC 14650000
*                             (FETCH WAS NOT ABLE TO USE THE         BC 14700000
*                              PROVIDED DE                           BC 14750000
*                                       A N D                        BC 14800000
*                              FOUND AN INCONSISTENCY BETWEEN        BC 14850000
*                              USER DE AND LIBRARY DE.               BC 14900000
*                              FOR THIS CASE THE USER DE IS          BC 14950000
*                              OVERWRITTEN BY THE LIBRARY DE.        BC 15000000
*                              THE PHASE IS NOT LOADED               BC 15050000
*                              AND NOT EXECUTED.                     BC 15100000
*                              THE CHECKT VALUES ARE :               BC 15150000
*                                LENGTH OF PHASE, LOAD POINT,        BC 15200000
*                                ENTRY POINT, PARTITION START,       BC 15250000
*                                RELOCATION STATE.  )                BC 15300000
*                                                                     * 15350000
*  --->  DE AND LIST CANNOT BE USED IN THE SAME FETCH MACRO           * 15400000
*  --->  R0, R1, R2, R3 MUST BE DIFFERENT REGISTERS. THE MACRO        * 15450000
*                SERVICE DESTROYS THE GENERAL REGISTERS 0 AND 1       * 15500000
*  --->  THE SERVICE IS CANCELLED DUE TO  I N V A L I D  A D D R E S S* 15550000
*              . IF THE PARAMETER LIST, THE DIRECTORY ENTRY OR THE    * 15600000
*                LOCAL LIST IS NOT PART OF THE REQUESTOR'S STORAGE    * 15650000
*              . OR IF THE PHASE DOES NOT FIT INTO THE STORAGE        * 15700000
*                                                                     * 15750000
*  --->   V S E - DIRECTORY ENTRY FORMAT                              * 15800000
*                                                                     * 15850000
*            0   PHASENAME   CL8                                      * 15900000
*            8   DIRVSEID    XL3'FF'     ID FOR NEW FORMAT            * 15950000
*           11   DIRN        XL1'0E'     LENGTH OF DE                 * 16000000
*           12   DIRLMBR     F           LENGTH OF TXT IN BYTES       * 16050000
*           16   DIRC        XL1         FLAGS                        * 16100000
*           17               XL3         RESERVED                     * 16150000
*           20   DIRACOPY    A           INTEGRITY INFORMATION        * 16200000
*           24   DIRALPT     A           LOADPOINT AT LINKEDT TIME    * 16250000
*           28   DIRAEPT     A           ENTRYPOINT AT LINKEDT TIME   * 16300000
*           32   DIRAPART    A           BEG PARTITION                * 16350000
*           36   DIRAVSA     A           ENTRYPOINT IN SVA            * 16400000
*                                                                     * 16450000
*  --->   CONVENTIONAL DIR_ENTRY  FORMAT                              * 16500000
*                                                                     * 16550000
*            0   PHASENAME   CL8                                      * 16600000
*            8   DIRCOPY     XL3'0'      INTEGRITY INFORMATION        * 16650000
*           11   DIRN        XL1         LENGTH OF DE                 * 16700000
*           12   DIRTT       H           NUMBER OF 1K LIBRARY BLOCKS  * 16750000
*           14   DIRLL       H           TXT_LENGTH IN BYTES IN LAST  * 16800000
*                                                    TXT BLOCK        * 16850000
*           16   DIRC        XL1         FLAGS                        * 16900000
*           17               XL1         RESERVED                     * 16950000
*           18   DIRPPP      AL3         LOADPOINT AT LINKEDT TIME    * 17000000
*           21   DIREEE      AL3         ENTRYPOINT AT LINKEDT TIME   * 17050000
*           24               XL3         *** NOT SUPPORTED ***        * 17100000
*           26   DIRAAA      AL3         PARTITION START ADDR         * 17150000
*           30   DIRASVA     A           ENTRYPOINT IN SVA            * 17200000
*           34               A           *** NOT SUPPORTED ***        * 17250000
*                                                                     * 17300000
*    --->THE 'DIRC' FLAGS CAN BE USED TO TEST THE STATE OF THE PHASE  * 17350000
*                X'80'        SELFRELOCATABLE                         * 17400000
*                X'40'        RELOCATABLE                             * 17450000
*                X'20'        SVA - ELIGIBLE                          * 17500000
*                             ..( A SVA_PHASE IS RELOCATABLE AND SVA  * 17550000
*                                 - ELIGIBLE )                        * 17600000
*                X'10'        PHASE LOCATED IN SVA                    * 17650000
*                X'01'        SLMAP AVAILABLE                         * 17700000
*    ---> MOREOVER, 'DIRC' FLAGS REFLECT THE STATE OF THE IN_CORE_    * 17750000
*         DIR_ENTRY                                                   * 17800000
*                X'08'        NON_SYSLIB ENTRY                        * 17850000
*                X'04'        ENTRY NOT FOUND                         * 17900000
*                X'02'        ENTRY ACTIVE   ( FETCH/LOAD SERVICE     * 17950000
*                                   HAS ALREADY PROCESSED DIR_ENTRY ) * 18000000
*    ---> N O T E :  THE 'DIRC' FLAGS ARE BITS AND SHOULD BE TESTED   * 18050000
*                    BY 'TM' - ASSEMBLER INSTRUCTIONS                 * 18100000
*    ---> N O T E :  'DIRC' MUST BE INITIALIZED AS X'00'              * 18150000
*                    'DIRN' INDICATES THE NUMBER OF HALFWORDS AFTER   * 18200000
*                           'DIRN' TO BE PROVIDED BY THE FETCH SERVICE* 18250000
*                           E.G    X'02' LE 'DIRN' LE X'0B'           * 18300000
*                           'DIRN' = X'00' ... NO RETURN_INFO PROVIDED* 18350000
*                                                                     * 18400000
*---------------------------------------------------------------------* 18450000
         SPACE 1                                               @D14CDVB 18500000
.MEXIT   MEXIT                                                 @D14CDVB 18550000
.DEF2C   AIF   (&TLI GT 0 OR &BRET OR &SY OR &DY).OPT          @D14CDBC 18600000
.*                                                             @D35XDGS 18650000
.*       PARAMETER TRANSFER IN GR 1 AND GR 0 ----------------- @D35XDGS 18700000
.*                                                             @D35XDGS 18750000
         AIF   (&TPN NE 1).DEF31                               @D35XDGS 18800000
         LA    1,=CL8'&PNAM'            POINT TO PHASENAME     @DL29ZCJ 18850000
         AGO   .DEF4                   TO SET UP OVERRIDE      @DL29ZCJ 18900000
.DEF31   AIF   (&TPN NE 2).DEF32                               @D35XDGS 18950000
         AIF   ('&PNAM(1)' EQ '1').DEF4          BR IF REG 1   @D35XDGS 19000000
         LR    1,&PNAM(1)               PHASENAME ADDR TO REG1 @DL29ZCJ 19050000
         AGO   .DEF4                                           @D35XDGS 19100000
.DEF32   AIF   (&TPN NE 4).DEF4                                @D35XDGS 19150000
         LA    1,&PNAM(2)               PHASENAME ADDR TO REG1 @D35XDGS 19200000
         AGO   .DEF4                                           @D35XDGS 19250000
.*                                                             @D35XDGS 19300000
.*       PARAMETER LIST GENERATION                             @D35XDGS 19350000
.*        INSTRUCTION GENERATION ----------------------------- @D35XDGS 19400000
.*                                                             @D35XDGS 19450000
.OPT     AIF   (NOT &DY OR &TPN NE 1).OPT00                    @D35XDGS 19500000
&DISP    SETA  &DISP+4                                         @D35XDGS 19550000
         AIF   (&TMF NE 0).OPT00                               @D35XDGS 19600000
&DISP    SETA  &DISP+&DIRL-12                                  @D36JDVB 19650000
         AGO   .OPT01                                          @D35XDGS 19700000
.OPT00   AIF   (&DISP NE 20).OPT000                            @D35XDGS 19750000
&OFFS1   SETA  &OFFS1+6        ADD LENGTH OF XC INSTRUCTION    @D35XDGS 19800000
.OPT000  AIF   (&TPN NE 1 OR &TMF EQ 0).OPT01                  @D35XDGS 19850000
&I1      SETB  1               TRIGGER INSTRUCTION GENERATION  @D35XDGS 19900000
&OFFS1   SETA  &OFFS1+8        ADD LENGTH OF LA AND ST INTRUCTIONS      19950000
.OPT01   AIF   (&OFFS1/4*4 EQ &OFFS1).FULLW                    @D35XDGS 20000000
.HALFW   CNOP  2,4                      ALIGN TO HALFWORD BDRY @D35XDGS 20050000
         AGO   .OPT0                                           @D35XDGS 20100000
.FULLW   CNOP  0,4                      ALIGN TO FULLWORD BDRY @D35XDGS 20150000
.OPT0    AIF   (&TMF EQ 0).PN10                                @D35XDGS 20200000
&TARGET  SETC  '&AREA'.'('.'&DISP'.')'                         @D35XDGS 20250000
         AIF   (&TMF NE 2).OPT1                                @D35XDGS 20300000
&TARGET  SETC  '0'.'('.'&DISP'.','.'&MFG(1)'.')'               @D35XDGS 20350000
.OPT1    MVC   &TARGET,*+&OFFS1         MOVE PARAMETER LIST    @D35XDGS 20400000
         AIF   (&DISP NE 20).OPT3                              @D35XDGS 20450000
&TARGET  SETC  '&AREA'.'+20(&DIRL-12)'                         @D36JDVB 20500000
         AIF   (&TMF NE 2).OPT2                                @D35XDGS 20550000
&TARGET  SETC  '20'.'(&DIRL-12,'.'&MFG(1)'.')'                 @D36JDVB 20600000
.OPT2    XC    &TARGET,20+&AREA         CLEAR DIRECTORY DATA   @D35XDGS 20650000
.OPT3    AIF   (NOT &I1).OPT4                                  @D35XDGS 20700000
         LA    &R1,8+&AREA              INSERT POINTER         @D35XDGS 20750000
         ST    &R1,&AREA                 TO PHASE NAME         @D35XDGS 20800000
.OPT4    ANOP                                                  @D35XDGS 20850000
&OFFS1   SETA  0                                               @D35XDGS 20900000
.PN10    AIF   (&TPN NE 2).PN20         PNAM OPERAND --------- @D35XDGS 20950000
         STCM  &IPN,B'0111',1+&OFFS1+&AREA SET PHASE NAME ADDR @D357DJB 21000000
         AIF   (&TMF NE 0).PN20                                @D35XDGS 21050000
&OFFS1   SETA  &OFFS1-4                                        @D35XDGS 21100000
.PN20    AIF   (&TPN NE 4).PN30                                @D35XDGS 21150000
         LA    &R1,&IPN                 SET PHASE NAME   ???   @D35XDGS 21200000
         ST    &R1,&OFFS1-&D+&AREA       ADDRESS         ???   @D35XDGS 21250000
         AIF   (&TMF NE 0).PN30                                @D35XDGS 21300000
&OFFS1   SETA  &OFFS1-8                                        @D35XDGS 21350000
.PN30    ANOP                                                  @D35XDGS 21400000
.LI10    AIF   (&TLI NE 2).LI20         LIST OPERAND --------- @D35XDGS 21450000
         STCM  &ILI,B'0111',&OFFS1+5+&AREA  SET LIST ADDRESS   @D35XDGS 21500000
         AIF   (&TMF NE 0).LI20                                @D35XDGS 21550000
&OFFS1   SETA  &OFFS1-4                                        @D35XDGS 21600000
.LI20    AIF   (&TLI NE 4).LI30                                @D35XDGS 21650000
         LA    &R1,&ILI                 SET LIST               @D35XDGS 21700000
         STCM  &R1,B'0111',&OFFS1+5-&D+&AREA  ADDRESS    ?!?   @D35XDGS 21750000
         AIF   (&TMF NE 0).LI30                                @D35XDGS 21800000
&OFFS1   SETA  &OFFS1-8                                        @D35XDGS 21850000
.LI30    ANOP                                                  @D35XDGS 21900000
         AIF   (&TMF EQ 0).ACONST                              @D35XDGS 21950000
         LA    1,&AREA                  POINT TO PARM LIST     @D35XDGS 22000000
         B     *+4+&DISP                                       @D35XDGS 22050000
.*                                                             @D35XDGS 22100000
.*       ADDRESS CONSTANT GENERATION ------------------------- @D35XDGS 22150000
.*                                                             @D35XDGS 22200000
.ACONST  ANOP                                                  @D35XDGS 22250000
&PHASE   SETC  '&IPN'                                          @D35XDGS 22300000
         AIF   (&TPN EQ 1 AND NOT &I1).AC11                    @D35XDGS 22350000
&IPN     SETC  '0'                                             @D35XDGS 22400000
         AGO   .AC1                                            @D35XDGS 22450000
.AC11    AIF   (&DISP LE 8).AC1                                @D35XDGS 22500000
&IPN     SETC  '8+&AREA'                                       @D35XDGS 22550000
.AC1     AIF   (&TLI EQ 1).AC2                                 @D35XDGS 22600000
&ILI     SETC  '0'                                             @D35XDGS 22650000
.AC2     AIF   (&TMF NE 0).AC3                                 @D35XDGS 22700000
         BAL   1,*+4+&DISP              POINT TO PARM LIST     @D35XDGS 22750000
.AC3     ANOP                                                  @D35XDGS 22800000
         DC    A(&IPN)                  PHASE NAME ADDRESS     @D35XDGS 22850000
         DC    B'&BRET.0000&SY&DY.0'    OPTIONS                @D14CDBC 22900000
         DC    AL3(&ILI)                LIST ADDRESS           @D35XDGS 22950000
         AIF   (&DISP EQ 8).DEF4                               @D35XDGS 23000000
         DC    CL8'&PHASE'              PHASE NAME             @D35XDGS 23050000
         AIF   (&DISP EQ 16).DEF4                              @D35XDGS 23100000
         DC    XL3'&DIRID'                                     @D14CDVB 23150000
&DIRH    SETA  (&DIRL-12)/2                                    @D36JDVB 23200000
         DC    AL1(&DIRH)               N                      @D36JDVB 23250000
         AIF   (&DISP EQ 20).DEF4                              @D35XDGS 23300000
         DC    XL(&DIRL-12)'00'         DIRECTORY DATA         @D36JDVB 23350000
.*                                                             @D35XDGS 23400000
.*       GR 0 PREPARATION AND SVC GENERATION ----------------- @D35XDGS 23450000
.*                                                             @D35XDGS 23500000
.DEF4    AIF   (&TOR GT 0).DEF41       BR IF OVERRIDE PARAM    @D35XDGS 23550000
         SLR   0,0                      NO OVERRIDE            @D36JDVB 23600000
.DEF41   AIF   (&TOR NE 1).DEF42                               @D35XDGS 23650000
         L     0,=V(&ORIDE)             OVERRIDE ADDR TO REG 0 @D35XDGS 23700000
.DEF42   AIF   (&TOR NE 2).DEF43                               @D35XDGS 23750000
         AIF   ('&IOR' EQ '0').DEFX                            @D35XDGS 23800000
         LR    0,&IOR                   OVERRIDE ADDR TO REG 0 @D35XDGS 23850000
.DEF43   AIF   (&TOR NE 4).DEFX                                @D35XDGS 23900000
         LA    0,&IOR                   OVERRIDE ADDR TO REG 0 @D35XDGS 23950000
.DEFX    SVC   1                        EXECUTE FETCH          @DL29ZCJ 24000000
         MEND                                                  @D35XDGS 24050000
