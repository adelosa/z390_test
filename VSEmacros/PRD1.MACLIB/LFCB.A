         MACRO                                                          00050000
&L       LFCB  &P1,&P2,&P3,&P4,&FORMS=,&LPI=                            00100000
.* IBM DISK OPERATING SYSTEM                                   @DM08066 00150000
.* IPL AND BUFFER LOAD - LFCB - 5686-007-02-C550               @D52VDAK 00200052
.* COPYRIGHT = 5686-007 (C) COPYRIGHT IBM CORP. 1977,1992      @D52VDAK 00250052
.*             ALL RIGHTS RESERVED                             @U31TUJS 00300000
.*             LICENSED MATERIALS - PROPERTY OF IBM            @U31TUJS 00350000
.*             SEE COPYRIGHT INSTRUCTIONS, G120-2083           @U31TUJS 00400000
.* /* START OF SPECIFICATIONS ****                                      00450000
.*01  MODULE-TYPE = MACRO                                               00500000
.*02    PROCESSOR = ASSEMBLER                                           00550000
.*                                                                      00600000
.**** END OF SPECIFICATIONS ***/                                        00650000
.* THE LFCB MAC MAY BE USED TO LOAD THE FCB OF 3203,3211,5203  @D30DDVD 00700000
.* THE PARAMETERS OF THE LFCB MACRO ARE.                       @D30DDVD 00750000
.* P1      SYSXXX ASSIGNED TO A PRINTER WITH AN FCB.           @D30DDVD 00800000
.* P2      THE PHASENAME OF THE PHASE CONTAINING THE           @D30DDVD 00850000
.*         FCB-IMAGE LOADED INTO THE FCB.                      @D30DDVD 00900000
.* P3      NULMSG INDICATOR                                    @D30DDVD 00950000
.* P4      VARIAB TESTS FOR TOO MANY POSITIONAL PARAMETERS.    @D30DDVD 01000000
.* FORMS=  KEYWORDPARAM IDENTIFICATION OF PRINTER FORMS        @D30DDVD 01050000
.* LPI=    KEYWORD PARAM TO SPECIFY NUMBER OF LINES PER INCH.  @D30DDVD 01100000
         LCLA  &UN                                             @D30DDVD 01150000
         LCLC  &UX,&PN,&A,&I3,&FRV,&IFRV,&ILPI                 @D30DDVD 01200000
.* TEST FOR MANDETORY OPERANDS.IF ERROR,NO GENERATION          @D30DDVD 01250000
         AIF   (T'&P1 NE 'O').CHKO2                            @D30DDVD 01300000
         MNOTE 4,'OPERAND SYSXXX NOT SPECIFIED. NO GENERATION.'         01350000
         MEXIT  ,                                              @D30DDVD 01400000
.CHKO2   AIF   (T'&P2 NE 'O').CHKV1                            @D30DDVD 01450000
         MNOTE 4,'OPERAND PHASENAME NOT SPECIFIED. NO GENERATION.'      01500000
         MEXIT  ,                                              @D30DDVD 01550000
.CHKV1   AIF   (K'&P1 NE 6).OP1E    INCORRECT LENGTH           @D30DDVD 01600000
         AIF   ('&P1'(1,3) NE 'SYS').OP1E  NOT SYS SPECIFIED.  @D30DDVD 01650000
&UX      SETC  '&P1'(4,3)                                      @D30DDVD 01700000
         AIF   ('&UX' EQ 'LOG' OR '&UX' EQ 'LST').OP1OK        @D30DDVD 01750000
.* CHECK  XXX OF SYSXXX  FOR  0 LE XXX LE 254 .                @DA31417 01800000
.* FIRST CHECK  XXX FOR NUMERIC CHARACTERS.                    @D30DDVD 01850000
         AIF   ( '&UX'(1,1) LT '0' OR '&UX'(1,1) GT '2').OP1E  @D30DDVD 01900000
         AIF   ( '&UX'(2,1) LT '0' OR '&UX'(2,1) GT '9').OP1E  @D30DDVD 01950000
         AIF   ( '&UX'(3,1) LT '0' OR '&UX'(2,1) GT '9').OP1E  @D30DDVD 02000000
&UN      SETA  &UX    XXX IS A NUMBER CHARACTER STRING         @D30DDVD 02050000
         AIF   ( &UN LE 254).OP1OK                             @DA31417 02100000
.OP1E    MNOTE 4,'OPERAND &P1 INVALID. NO GENERATION.'         @D30DDVD 02150000
         MEXIT  ,                                              @D30DDVD 02200000
.OP1OK   AIF   ( K'&P2 LE 8 ).OP2T2                            @D30DDVD 02250000
         MNOTE 3,'PHASENAME TOO LONG. TRUNCATED TO 8 CHARACTERS.'       02300000
&PN      SETC  '&P2'(1,8)                                      @D30DDVD 02350000
         AGO   .TSTCH                                          @D30DDVD 02400000
.OP2T2   ANOP   ,                                              @D30DDVD 02450000
&PN      SETC  '&P2'                                           @D30DDVD 02500000
.TSTCH   ANOP   ,                                              @D30DDVD 02550000
.* CHECK IS FIRST CHARACTER OF PHASENAME ALPHA.                @D30DDVD 02600000
&A       SETC  '&PN'(1,1)                                      @D30DDVD 02650000
         AIF   ('&A' EQ '$' OR '&A' EQ '@' OR '&A' EQ '#').OP2OK        02700000
.* TEST FOR 3 NATIONAL ALPHA CHARACTERS                        @D30DDVD 02750000
         AIF   ('&A' LT 'A').OP2E  OUTSIDE OF ALPHA VALUES     @D30DDVD 02800000
         AIF   ('&A' GT 'Z').OP2E                              @D30DDVD 02850000
         AIF   ('&A' GT 'I' AND  '&A' LT 'J').OP2E             @D30DDVD 02900000
         AIF   ('&A' LE 'R'  OR  '&A' GE 'S').OP2OK            @D30DDVD 02950000
.OP2E    MNOTE 4,'OPERAND &PN INVALID. NO GENERATION.'         @D30DDVD 03000000
         MEXIT  ,                                              @D30DDVD 03050000
.OP2OK   ANOP   ,                                              @D30DDVD 03100000
&I3      SETC  '0'                                             @D30DDVD 03150000
         AIF   (T'&P3 EQ 'O').OP4                              @D30DDVD 03200000
         AIF   (K'&P3 EQ 6 AND '&P3'  EQ 'NULMSG' ).OP3OK      @D30DDVD 03250000
         MNOTE 4,'3RD PARAMETER INVALID. NULMSG ASSUMED.'      @D30DDVD 03300000
.OP3OK   ANOP   ,                                              @D30DDVD 03350000
&I3      SETC  '1'                                             @D30DDVD 03400000
.* TEST FOR 4TH POSITIONAL PARAMETER, THAT SHOULD NOT OCCUR.   @D30DDVD 03450000
.OP4     AIF   (T'&P4  EQ 'O').CHKEY                           @D30DDVD 03500000
         MNOTE 4,'4TH AND SUBSEQUENT POSITIONAL OPERANDS IGNORED.'      03550000
.CHKEY   ANOP   ,                                              @D30DDVD 03600000
&IFRV    SETC  '0'                                             @D30DDVD 03650000
&FRV     SETC  'XXXX'                                          @D30DDVD 03700000
         AIF   (T'&FORMS EQ 'O').LPICHK                        @D30DDVD 03750000
         AIF   (K'&FORMS LE 4 ).FORMOK                         @D30DDVD 03800000
         MNOTE 4,'FORMS VALUE TOO LONG. TRUNCATED TO 4 CHARACTERS.'     03850000
&FRV     SETC  '&FORMS'(1,4)                                   @D30DDVD 03900000
         AGO   .SETIFRV                                        @D30DDVD 03950000
.FORMOK  ANOP   ,                                              @D30DDVD 04000000
&FRV     SETC  '&FORMS'                                        @D30DDVD 04050000
.SETIFRV ANOP   ,                                              @D30DDVD 04100000
&IFRV    SETC  '1'                                             @D30DDVD 04150000
.LPICHK  ANOP   ,                                              @D30DDVD 04200000
&ILPI    SETC  '0'                                             @D30DDVD 04250000
         AIF   (T'&LPI EQ 'O').GEN                             @D30DDVD 04300000
         AIF   ('&LPI' EQ '6'  OR  '&LPI' EQ '8').LPIOK        @D30DDVD 04350000
         MNOTE 4,'LPI VALUE INVALID. OPERAND  IGNORED.'        @D30DDVD 04400000
         AGO   .GEN                                            @D30DDVD 04450000
.LPIOK   ANOP   ,                                              @D30DDVD 04500000
&ILPI    SETC  '&LPI'                                          @D30DDVD 04550000
.GEN     ANOP   ,                                              @D30DDVD 04600000
&L       DS    0H                                              @D30DDVD 04650000
         BAL   1,*+30                                          @D30DDVD 04700000
         DC    CL8'$$BATTF0'                                   @D30DDVD 04750000
         DC    CL8'&PN'    BUFFERLOAD NAME                     @D30DDVD 04800000
         DC    CL3'&UX'    XXX OF SYSXXX                       @D30DDVD 04850000
         DC    CL1'&IFRV'  FORMS AVAIL. FLAG,'1' FORMS AVAIL.  @D30DDVD 04900000
         DC    CL4'&FRV'   FORMS VALUE                         @D30DDVD 04950000
         DC    CL1'&ILPI'  LPI   VALUE                         @D30DDVD 05000000
         DC    CL1'&I3'    NULMSGINDICATOR '1' NULMSG YES      @D30DDVD 05050000
         LA    0,8(1)                                          @D30DDVD 05100000
         XR    14,14               SET LENGTH CODE TO 1        @D52VDAK 05107152
         BALR  14,0           GET NEXT INSTRUCTION ADDRESS AND @D52VDAK 05114252
*                             SET HIGH ORDER BIT IF AMODE 31   @D52VDAK 05121352
         LTR   14,14          RUNNING IN 24 BIT MODE ?         @D52VDAK 05128452
         BP    8(,14)         YES,OK                           @D52VDAK 05135552
         SVC   130            EXECUTION MODE VIOLATION         @D52VDAK 05142652
         SVC   2                                               @D30DDVD 05150000
         MEND                                                           05200000
