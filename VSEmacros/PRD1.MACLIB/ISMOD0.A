.* IBM  DISK OPERATING SYSTEM                                           00050000
.* ISFMS - ISMOD0 - 5686-007-02-C440                           @U31TUJS 00100000
.* COPYRIGHT = 5686-007 (C) COPYRIGHT IBM CORP. 1977,1989      @U31TUJS 00150000
.*             ALL RIGHTS RESERVED                             @U31TUJS 00200000
.*             LICENSED MATERIALS - PROPERTY OF IBM            @U31TUJS 00250000
.*            SEE COPYRIGHT INSTRUCTIONS, G120-2083            @U31TUJS 00300000
* ISFMS - ISMOD0 - 5686-007-02-C440                            @U31TUJS 00350000
.*   SW(1) INDICATES LOAD FUNCTION                                  3-4 00400000
.*   SW(2) INDICATES ADD FUNCTION PRESENT.                              00450000
.*   SW(3) INDICATES RETRIEVE                                       3-4 00500000
.*   SW(4) INDICATES ADDRTR                                         3-4 00550000
.*   SW(5) INDICATES FIXUNB                                         3-4 00600000
.*   SW(6) INDICATES FIXBLK                                         3-4 00650000
.*   SW(7) INDICATES BOTH                                           3-4 00700000
.*   SW(8) INDICATES RANDOM RETRIEVE FUNCTION PRESENT.                  00750000
.*   SW(9) INDICATES SEQUENTIAL RETRIEVE FUNCTION PRESENT.              00800000
.*   SW(10) INDICATES RANSEQ                                        3-4 00850000
.*   SW(11) INDICATES HOLD FUNCTION PRESENT                         3-9 00900000
.*   SW(12) INDICATES SEPASMB FUNCTION PRESENT                 @DL30SMP 00950000
.*   SW(15) INDICATES RPS FUNCTION PRESENT                     @DL30SMP 01000000
.*   SW(21) INDICATES INDEX IN CORE FUNCTION PRESENT.                   01050000
.*   SW(22) INDICATES PRIME DATA IN CORE ADD FUNCTION PRESENT.          01100000
.*   SW(23) INDICATES ERREXT                                        3-4 01150000
.*   SW(24) INDICATES RDONLY                                        3-4 01200000
.*   SW(25) INDICATES IOAREA2                                       3-4 01250000
.*   B(1)   INDICATES IOROUT=ADDRTR (V WITH / A WITHOUT RPS)   @DL30SMP 01300000
.*   B(2)   INDICATES IOROUT=ADD    (V WITH / I WITHOUT RPS)   @DL30SMP 01350000
.*   B(3)   INDICATES IOROUT=LOAD   (X WITH / L WITHOUT RPS)   @DL30SMP 01400000
.*   B(4)   INDICATES IOROUT=RETRVE (V WITH / R WITHOUT RPS)   @DL30SMP 01450000
         AIF   (T'&RPS EQ 'O').AA2     RPS NOT SPECIFIED       @DL30SMP 01500000
         AIF   ('&RPS' EQ 'SVA').AA1   RPS SPECIFIED           @DL30SMP 01550000
         MNOTE 0,'IMPROPER RPS OPTION SPECIFIED, ''SVA'' ASSUMED'       01600000
*                                                              @DL30SMP 01650000
.AA1     ANOP                                                  @DL30SMP 01700000
&SW(15)  SETB  1                                               @DL30SMP 01750000
.AA2     ANOP                                                  @DL30SMP 01800000
&B(1)    SETC  'AV'(1+&SW(15),1)       SET ISMOD NAME 'B'      @DL30SMP 01850000
&B(2)    SETC  'IV'(1+&SW(15),1)       FIELD VALUE TO          @DL30SMP 01900000
&B(3)    SETC  'LX'(1+&SW(15),1)       'A/I/L/R' WITHOUT RPS   @DL30SMP 01950000
&B(4)    SETC  'RV'(1+&SW(15),1)       OR 'V/V/X/V' WITH RPS   @DL30SMP 02000000
         AIF   ('&HOLD' EQ '' OR '&HOLD' EQ 'NO').AD                3-9 02050000
         AIF   ('&IOROUT' NE 'LOAD').AB                             3-9 02100000
         MNOTE 0,'''HOLD'' INVALID WITH LOAD. PARAMETER IGNORED'    3-9 02150000
         AGO   .AD                                                  3-9 02200000
.AB      AIF   ('&HOLD' EQ 'YES').AC                                3-9 02250000
         MNOTE 0,'IMPROPER ''HOLD''. ''YES'' ASSUMED'               3-9 02300000
.AC      ANOP                                                       3-9 02350000
&SW(11)  SETB  1                       HOLD FUNCTION                3-9 02400000
.AD      ANOP                                                       3-9 02450000
         AIF   ('&IOAREA2' EQ '').AD1    IOAREA2 NOT SPECIFIED @DL30SMP 02500000
         AIF   ('&IOROUT' EQ 'LOAD' OR '&TYPEFLE' EQ 'SEQNTL').AE   3-9 02550000
         MNOTE 0,'IOAREA2 INVALID PARAMETER.  IGNORED'              3-4 02600000
         AGO   .AG                                                  3-4 02650000
.AD1     ANOP                                                  @DL30SMP 02700000
         AIF   ('&IOROUT' NE 'LOAD' OR NOT &SW(15)).AG         @DL30SMP 02750000
*                                        IOROUT NOT EQUAL LOAD @DL30SMP 02800000
*                                        OR RPS NOT SPECIFIED  @DL30SMP 02850000
         MNOTE 0,'IOAREA2 = ''YES'' ASSUMED (LOAD AND RPS SPECIFIED)'   02900000
         AGO   .AF                                             @DL30SMP 02950000
.AE      ANOP                                                       3-4 03000000
         AIF   ('&IOAREA2' EQ 'YES').AF                             3-4 03050000
         MNOTE 0,'IMPROPER ''IOAREA2''.  ''YES'' ASSUMED'           3-4 03100000
.AF      ANOP                                                       3-4 03150000
&SW(25)  SETB  1                                                    3-4 03200000
.AG      ANOP                                                       3-4 03250000
         AIF   (T'&RDONLY NE 'O').AG1    RDONLY SPECIFIED      @DL30SMP 03300000
         AIF   (NOT &SW(15)).AI          RPS NOT SPECIFIED     @DL30SMP 03350000
         MNOTE 0,'RDONLY = ''YES'' ASSUMED (RPS SPECIFIED)'    @DL30SMP 03400000
         AGO   .AH                                             @DL30SMP 03450000
.AG1     ANOP                                                  @DL30SMP 03500000
         AIF   ('&RDONLY' EQ 'YES').AH                              3-4 03550000
         MNOTE 0,'IMPROPER RDONLY. ''YES'' ASSUMED.'                3-4 03600000
.AH      ANOP                                                       3-4 03650000
&SW(24)  SETB  1                                                    3-4 03700000
.AI      ANOP                                                       3-3 03750000
         AIF   (T'&ERREXT NE 'O').AI1    ERREXT SPECIFIED      @DL30SMP 03800000
         AIF   (NOT &SW(15)).AK          RPS NOT SPECIFIED     @DL30SMP 03850000
         MNOTE 0,'ERREXT = ''YES'' ASSUMED (RPS SPECIFIED)'    @DL30SMP 03900000
         AGO   .AJ                                             @DL30SMP 03950000
.AI1     ANOP                                                  @DL30SMP 04000000
         AIF   ('&ERREXT' EQ 'YES').AJ                              3-4 04050000
         MNOTE '0,IMPROPER ERREXT PARAMETER. ''YES'' ASSUMED.'      3-4 04100000
.AJ      ANOP                                                       3-4 04150000
&SW(23)  SETB  1                                                    3-4 04200000
.AK      ANOP                                                       3-4 04250000
         AIF   ('&CORDATA' EQ '').AN                                3-9 04300000
         AIF   (NOT('&IOROUT' EQ 'ADD' OR '&IOROUT' EQ 'ADDRTR')).AL    04350000
         AIF   (NOT '&CORDATA' EQ 'YES').AM                         3-9 04400000
&SW(22)  SETB  1                                                    3-4 04450000
         AGO   .AN                                                  3-9 04500000
.AL      MNOTE 0,'''CORDATA'' INVALID PARAMETER. IGNORED.'          3-9 04550000
         AGO   .AN                                                  3-9 04600000
.AM      MNOTE 0,'IMPROPER ''CORDATA''. PARAMETER IGNORED.'         3-9 04650000
.AN      ANOP                                                       3-9 04700000
         AIF   ('&CORINDX' EQ 'YES').AN1 CORINDX SPECIFIED     @DL30SMP 04750000
         AIF   ('&IOROUT' EQ 'LOAD' OR NOT &SW(15)).AO         @DL30SMP 04800000
*                                       IOROUT EQUAL LOAD OR   @DL30SMP 04850000
*                                       RPS NOT SPECIFIED      @DL30SMP 04900000
         MNOTE 0,'CORINDX = ''YES'' ASSUMED (ADD OR RETRVE OR ADDRTR,  *04950000
               AND RPS SPECIFIED)'                             @DL30SMP 05000000
.AN1     ANOP                                                  @DL30SMP 05050000
&SW(21)  SETB  1                        INDEX IN CORE ROUTINE.          05100000
         AGO   .AP                                                  3-9 05150000
.AO      AIF   ('&CORINDX' EQ '').AP    CORINDX NOT SPECIFIED  @DL30SMP 05200000
         MNOTE 0,'IMPROPER CORINDX. PARAMETER IGNORED.'             3-9 05250000
.AP      AIF   ('&IOROUT' EQ 'LOAD').AQ                             3-9 05300000
         AIF   ('&IOROUT' EQ 'ADDRTR').A4                           3-9 05350000
         AIF   (NOT &SW(15)).AP1        RPS NOT SPECIFIED      @DL30SMP 05400000
         MNOTE 0,'IOROUT = ''ADDRTR'' ASSUMED (ADD OR RETRVE AND RPS   *05450000
               SPECIFIED)'                                     @DL30SMP 05500000
         AGO   .A4                                             @DL30SMP 05550000
.AP1     ANOP                                                  @DL30SMP 05600000
         AIF   ('&IOROUT' EQ 'ADD').AT                              3-9 05650000
         AIF   ('&IOROUT' EQ 'RETRVE').AY                           3-9 05700000
         MNOTE 0,'IMPROPER IOROUT. ''RETRVE'' ASSUMED'                  05750000
         AGO   .AY                                                  3-9 05800000
.AQ      ANOP                           ENTERED HERE FOR IOROUT=LOAD.   05850000
&SW(1)   SETB  1                                                        05900000
         AIF   (NOT &SW(21)).AR                                     3-9 05950000
         MNOTE 0,'CORINDX INVALID PARAMETER. IGNORED.'                  06000000
&SW(21)  SETB  0                                                        06050000
.AR      AIF   ('&RECFORM' EQ '' OR '&RECFORM' EQ 'BOTH').AS        3-4 06100000
         MNOTE 0,'RECFORM INVALID PARAMETER. IGNORED'                   06150000
.AS      AIF   ('&TYPEFLE' EQ '').BC                                3-9 06200000
         MNOTE 0,'TYPEFLE INVALID PARAMETER. IGNORED'                   06250000
         AGO   .BC                                                  3-9 06300000
.AT      AIF   ('&RECFORM' EQ 'FIXUNB').AU   ENTER HERE IOROUT=ADD. 3-9 06350000
         AIF   ('&RECFORM' EQ 'BOTH').AV                            3-9 06400000
         AIF   ('&RECFORM' EQ 'FIXBLK').AW                          3-9 06450000
         MNOTE 0,'IMPROPER RECFORM. ''BOTH'' ASSUMED'                   06500000
         AGO   .AV                                                  3-9 06550000
.AU      ANOP                                                       3-9 06600000
&SW(5)   SETB  1                                                        06650000
         AGO   .AX                                                  3-9 06700000
.AV      ANOP                                                       3-9 06750000
&SW(7)   SETB  1                                                        06800000
&SW(5)   SETB  1                                                        06850000
.AW      ANOP                                                       3-9 06900000
&SW(6)   SETB  1                                                        06950000
.AX      ANOP                                                       3-9 07000000
&SW(2)   SETB  1                        ADD FUNCTION.                   07050000
         AIF   ('&TYPEFLE' EQ '').BC                                3-9 07100000
         MNOTE 0,'TYPEFLE INVALID PARAMETER. IGNORED'                   07150000
         AGO   .BC                                                  3-9 07200000
.AY      ANOP                           ENTER HERE FOR IOROUT=RETRVE.   07250000
&SW(3)   SETB  1                                                        07300000
         AIF   ('&RECFORM' EQ '').AZ                                3-9 07350000
         MNOTE 0,'RECFORM INVALID PARAMETER. IGNORED'                   07400000
.AZ      AIF   ('&TYPEFLE' EQ 'RANDOM').A0                          3-9 07450000
         AIF   ('&TYPEFLE' EQ 'SEQNTL').A2                          3-9 07500000
         AIF   ('&TYPEFLE' EQ 'RANSEQ').A1                          3-9 07550000
         MNOTE 0,'IMPROPER TYPEFLE. ''RANSEQ'' ASSUMED'                 07600000
         AGO   .A1                                                  3-9 07650000
.A0      ANOP                                                       3-9 07700000
&SW(8)   SETB  1                        RANDOM RETRIEVE FUNCTION.       07750000
         AGO   .BC                                                  3-9 07800000
.A1      ANOP                                                       3-9 07850000
&SW(8)   SETB  1                        RANDOM RETRIEVE FUNCTION.       07900000
&SW(10)  SETB  1                                                        07950000
         AGO   .A3                                                  3-9 08000000
.A2      AIF   (NOT &SW(21)).A3                                     3-9 08050000
         MNOTE 0,'CORINDX INVALID PARAMETER. IGNORED.'                  08100000
&SW(21)  SETB  0                                                        08150000
.A3      ANOP                                                       3-9 08200000
&SW(9)   SETB  1                        SEQUENTIAL RETRIEVE FUNCTION.   08250000
         AGO   .BC                                                  3-9 08300000
.A4      ANOP                           ENTER HERE FOR IOROUT=ADDRTR.   08350000
&SW(4)   SETB  1                                                        08400000
&SW(3)   SETB  1                                                        08450000
&SW(2)   SETB  1                        ADD FUNCTION.                   08500000
         AIF   ('&RECFORM' EQ 'BOTH').A6                            3-9 08550000
         AIF   (NOT &SW(15)).A41        RPS NOT SPECIFIED      @DL30SMP 08600000
         MNOTE 0,'RECFORM = ''BOTH'' ASSUMED (RPS SPECIFIED)'  @DL30SMP 08650000
         AGO   .A6                                             @DL30SMP 08700000
.A41     ANOP                                                  @DL30SMP 08750000
         AIF   ('&RECFORM' EQ 'FIXUNB').A5                          3-9 08800000
         AIF   ('&RECFORM' EQ 'FIXBLK').A7                          3-9 08850000
         MNOTE 0,'IMPROPER RECFORM. ''BOTH'' ASSUMED'                   08900000
         AGO   .A6                                                  3-9 08950000
.A5      ANOP                                                       3-9 09000000
&SW(5)   SETB  1                                                        09050000
         AGO   .A8                                                  3-9 09100000
.A6      ANOP                                                       3-9 09150000
&SW(7)   SETB  1                                                        09200000
&SW(5)   SETB  1                                                        09250000
.A7      ANOP                                                       3-9 09300000
&SW(6)   SETB  1                                                        09350000
.A8      ANOP                                                  @DL30SMP 09400000
         AIF   ('&TYPEFLE' EQ 'RANSEQ').BA                          3-9 09450000
         AIF   (&SW(25) OR NOT &SW(15)).A81  IOAREA2 EQUAL YES @DL30SMP 09500000
*                                       OR RPS NOT SPECIFIED   @DL30SMP 09550000
         MNOTE 0,'TYPEFLE = ''RANSEQ'' ASSUMED (RPS, BUT NOT IOAREA2   *09600000
               SPECIFIED)'                                     @DL30SMP 09650000
         AGO   .BA                                             @DL30SMP 09700000
.A81     ANOP                                                  @DL30SMP 09750000
         AIF   ('&TYPEFLE' EQ 'SEQNTL').BB                          3-9 09800000
         AIF   (NOT &SW(25) OR NOT &SW(15)).A82 IOAREA2 NE YES @DL30SMP 09850000
*                                       OR RPS NOT SPECIFIED   @DL30SMP 09900000
         MNOTE 0,'TYPEFLE = ''SEQNTL'' ASSUMED (RETRVE OR ADDRTR, AND  *09950000
               RPS AND IOAREA2 SPECIFIED)'                     @DL30SMP 10000000
         AGO   .BB                                             @DL30SMP 10050000
.A82     ANOP                                                  @DL30SMP 10100000
         AIF   ('&TYPEFLE' EQ 'RANDOM').A9                          3-9 10150000
         MNOTE 0,'IMPROPER TYPEFLE. ''RANSEQ'' ASSUMED'                 10200000
         AGO   .BA                                                  3-9 10250000
.A9      ANOP                                                       3-9 10300000
&SW(8)   SETB  1                        RANDOM RETRIEVE FUNCTION.       10350000
         AGO   .BC                                                  3-9 10400000
.BA      ANOP                                                       3-9 10450000
&SW(8)   SETB  1                        RANDOM RETRIEVE FUNCTION.       10500000
&SW(10)  SETB  1                                                        10550000
.BB      ANOP                                                       3-9 10600000
&SW(9)   SETB  1                        SEQUENTIAL RETRIEVE FUNCTION.   10650000
.* ARITHMETIC USE OF LCLB'S YIELDS SUBSCRIPT NUMBER FOR SETC LETTER 3-9 10700000
.BC      ANOP                                                       3-9 10750000
&NM(1)   SETC  'ZUBA'(1+&SW(5)+(&SW(6)*2),1)                        3-9 10800000
         AIF   (NOT &SW(15)).BC1        RPS NOT SPECIFIED      @DL30SMP 10850000
&NM(2)   SETC  'XWYV'(1+&SW(2)+(&SW(6)*2),1)                   @DL30SMP 10900000
         AGO   .BC2                                            @DL30SMP 10950000
.BC1     ANOP                                                  @DL30SMP 11000000
&NM(2)   SETC  'LIRA'(1+&SW(2)+(&SW(3)*2),1)                        3-9 11050000
.BC2     ANOP                                                  @DL30SMP 11100000
.* (SW(1)*SW(25)) TERM ZEROES EFFECT OF LOAD EXCEOPT WITH IOAREA2   3-9 11150000
&NM(3) SETC 'ZSRBG'(1+&SW(9)+(&SW(8)*2)+(&SW(25)*3)+(&SW(1)*&SW(25)),1) 11200000
&NM(4)   SETC  'ZCOB'(1+&SW(21)+(&SW(11)*2),1)                      3-9 11250000
&NM(7)   SETC  'ZO'(1+&SW(11),1)                                    3-9 11300000
&NM(5)   SETC  'ZTPGYSOF'(1+&SW(23)+(&SW(22)*2)+(&SW(24)*4),1)      3-9 11350000
&NM(6)   SETC  'ZPYO'(1+&SW(22)+(&SW(24)*2),1)                      3-9 11400000
         AIF   ('&SEPASMB' EQ 'YES').BD                             3-9 11450000
         AIF   (NOT &SW(15)).BC3        RPS NOT SPECIFIED      @DL30SMP 11500000
         MNOTE 0,'SEPASMB = ''YES'' ASSUMED (RPS SPECIFIED)'   @DL30SMP 11550000
         AGO   .BD                                             @DL30SMP 11600000
.BC3     ANOP                                                  @DL30SMP 11650000
         AIF   ('&SEPASMB' EQ '' OR '&SEPASMB' EQ 'NO').BF          3-9 11700000
         MNOTE 0,'IMPROPER SEPASMB. IGNORED'                            11750000
         AGO   .BF                                                  3-9 11800000
.BD      ANOP                                                  @DL30SMP 11850000
&SW(12)  SETB  1                       SEPASMB FUNCTION        @DL30SMP 11900000
         AIF   ('&NAME' EQ '').BE                                   3-9 11950000
         AIF   (NOT &SW(15)).BD1        RPS NOT SPECIFIED      @DL30SMP 12000000
         PUNCH ' CATALR &NAME,35.2 '                           @DA20857 12050000
         PUNCH ' PHASE  &NAME,*,NOAUTO,SVA '                   @DM08008 12100000
         AGO   .BF                      BUILD DTFIS DSECT      @DL30SMP 12150000
.BD1     PUNCH '    CATALR &NAME,35.2'                         @DA20857 12200000
         AGO   .BF                                                  3-2 12250000
.BE      AIF   (NOT &SW(15)).BE1        RPS NOT SPECIFIED      @DL30SMP 12300000
        PUNCH ' CATALR IJH&NM(1)&NM(2)&NM(3)&NM(4)&NM(5),35.2' @DA20857 12350000
        PUNCH ' PHASE  IJH&NM(1)&NM(2)&NM(3)&NM(4)&NM(5),*,NOAUTO,SVA'  12400000
         AGO   .BF                      BUILD DTFIS DSECT      @DL30SMP 12450000
.BE1    PUNCH ' CATALR IJH&NM(1)&NM(2)&NM(3)&NM(4)&NM(5),35.2' @DA20857 12500000
.BF      AIF   (&SW(1)).M7                                          3-9 12550000
****************************** TABLE ONE ****************************** 12600000
IJHCTBL1 DSECT                                                          12650000
IJHCCCB  DS    CL16                    CCB                              12700000
IJHADDR  DS    F                       ADDR.OF LOGIC MODULE         3-5 12750000
         DS    CL1                     DTFTYPE FOR OPEN/CLOSE           12800000
IJHCOPT  DC    XL1'0'                  OPTION CODES                     12850000
         DC    CL7' '                  FILE NAME                        12900000
IJHCPDDV DC    XL1'0'                  PRIME DATA DEVICE TYPE INDIC     12950000
IJHCSTBY DC    XL1'0'                  STATUS OR CONDITION CODES        13000000
IJHCHNDV DC    XL1'0'                  HI LEVEL INDEX DEVICE TYPE INDIC 13050000
IJHCPNT  DS    CL1       RELATIVE POINTER FOR DSKXNT (DIVIDED BY 4).    13100000
         DC    XL1'0'                                                   13150000
IJHCDRCY DC    XL2'0'                  HR  FIRST PRIME DATA RCD IN CYL  13200000
         DS    H                       HH LAST PRIME DATA TRK IN CYL    13250000
         DS    XL1                     HI RECORD # ON MAST.NDX/CYL.NDX  13300000
IJHCPDH  DS    XL1                     HI RECORD # ON PRIME DATA TRACK  13350000
         DS    XL1                     HI RECORD # ON OVFLO TRACK       13400000
IJHCSTH  DS    XL1                     HI RECORD # ON SHARED TRACK      13450000
IJHCTIH  DS    XL1                     HI RECORD # ON TI TRACK          13500000
IJHCRTR  DS    CL1       RETRIEVAL SWITCH (TYPE OF MACRO ISSUED).       13550000
         DS    XL7                     PRIME DATA START                 13600000
IJHCCIS  DS    XL7      MBBCCHH  CYL INDEX START                        13650000
IJHCMIS  DS    XL7      MBBCCHH  MST INDEX START                        13700000
IJHCILN  DS    CL1       INDEX LEVEL NUMBER AND RPS INDICATORS @DL30SMP 13750000
IJHCCLPA DS    XL8         MBBCCHHR  LAST PRIME DATA RCDADR             13800000
IJHCRESZ DS    H         RECORD LENGTH.                     LENGTH-0.   13850000
IJHCKYSZ DS    H         KEY LENGTH.                              -1.   13900000
IJHCBLSZ DS    H         BLOCKSIZE.                               -2.   13950000
IJHCRL10 DS    H         RECORD LENGTH+10.                        -3.   14000000
IJHCBFAC DS    H         BLOCKING FACTOR.                         -4.   14050000
         DS    H         KEYLEN+10 (INDEX ENTRY LENGTH).          -5.   14100000
IJHCABCD DS    H         PR.DATA REC. KEY+DATA                    -6.   14150000
         DS    H         KEYLEN + RECORD LENGTH +10.              -7.   14200000
IJHCCMAX DS    H         KEYLEN+BLOCKSIZE+8.                      -8.   14250000
         DS    H         KEYLEN+RECLEN+18.                        -9.   14300000
IJHCKYLC DS    H         KEY LOCATION.                            -A.   14350000
         DS    H         CONSTANT 5.                              -B.   14400000
         DS    H         CONSTANT 10.                             -C.   14450000
IJHCATB2 DS    H                       BASE FOR TABLE2                  14500000
IJHCATB3 DS    H                       BASE FOR TABLE3                  14550000
IJHCSADR DS    XL10                    COMMON SEEK/SEARCH BUCKET        14600000
IJHCBCKT DS    5H                      WORKAREA USED IN MACRO           14650000
         AIF   (NOT &SW(2)).BG                                      3-9 14700000
IJHACPRC DC    F'0'         PRIME RECORD  COUNT                         14750000
IJHACSTI DC    XL1'0'       STATUS INDICATORS                           14800000
IJHACLTA DC    XL5'0'    CCHHR-LAST TRACK INDEX NORMAL ENTRY ADDRESS.   14850000
IJHACLCA DC    XL5'0'       CCHHR   LAST CYL INDEX  ENTRY ADDR          14900000
IJHACLMA DC    XL5'0'       CCHHR   LAST MASTER INDEX ADDR              14950000
IJHACLOA DC    XL8'0'       MBBCCHHR  LAST INDEP OVFLO  RCD ADDR        15000000
IJHACOTC DC    2X'0'        NO. OF IND OVFLO  TRACKS                    15050000
IJHACOFC DC    2X'0'        NO. OF CYLNDER OVFLO  AREA FULL             15100000
IJHACORC DC    2X'0'        OVFL  RCD COUNT                             15150000
IJHACOLL DC    XL7'0'       NBBCCHH IND OVFLO LOWER LMT                 15200000
IJHACOUP DC    XL7'0'       MBBCCHH IND OVFLO UPPER LMT                 15250000
IJHCILST EQU   *                                                        15300000
IJHAHRAA DC    A(IJHACOCR)                           ADD DATA AREA-0.   15350000
         DC    A(IJHACTNA)                                        -1.   15400000
         DC    A(IJHACTOA)                                        -2.   15450000
         DC    A(IJHACRID)                                        -3.   15500000
         DC    A(IJHACFID)                                        -4.   15550000
         DC    A(IJHACTIN)                                        -5.   15600000
IJHADLNK DC    A(IJHACLNK)                                        -6.   15650000
IJHAARAD DC    F'0'      ADDR OF IOAREAL.                         -7.   15700000
IJHACUSE DC    F'0'      ADDR OF WORKL.                           -8.   15750000
IJHADKEY DC    F'0'      ADDR OF ADD KEY AREA.                    -9.   15800000
         DC    F'0'      ADDR OF IOAREAL+8.                       -A.   15850000
IJHAKLN8 DC    F'0'      ADDR OF IOAREAL+8+KEYLEN.                -B.   15900000
.BG      ANOP                                                       3-9 15950000
****************************** TABLE TWO ****************************** 16000000
IJHCTBL2 DSECT                                                          16050000
IJHCASAD DS    F         ADDR COMMON SEEK/SEARCH+3.  RET DATA AREA-0.   16100000
         DS    F         ADDR OF RANDOM  SEQ BUCKET.              -1.   16150000
         AIF   (&SW(2) AND NOT (&SW(3) OR &SW(4))).BH          @DL30SMP 16200000
.*                       ADD & NOT (RETRVE OR ADDRTR)          @DL30SMP 16250000
IJHSIOAR DS    F         ADDR OF IOAREAS.                         -2.   16300000
IJHCRARA DS    F         ADDR OF IOAREAR.                         -3.   16350000
IJHCRKEY DS    F         KEYARG ADDR.                             -4.   16400000
IJHCRWOR DS    F         WORKR ADDR.                              -5.   16450000
IJHSDB1  DS    F         SEQ. CURRENT IOAREA.                     -6.   16500000
IJHSLIOR DS    F                       L  IOREG,*-4 SEQUENTIAL          16550000
IJHSLMIT DS    4H       MBBCCHHR       LIMITS FOR SEQUENTIAL            16600000
IJHSINIT DS    4H       MBBCCHHR       INITIAL VALUES FOR SEQ.          16650000
IJHSCADR DS    4H       MBBCCHHR       CURRENT DISKADDR FOR SEQ.        16700000
IJHSCOVF DS    4H       MBBCCHHR       CURRENT OVFL ENTRY FOR SEQ.      16750000
IJHSRCNT DS    H                       SEQ. RECORD COUNTER              16800000
IJHSTICU DS    H                       HR CURRENT TRCNDX ENTRY FOR SEQ. 16850000
         DS    H                       NUMBER OF RECORDS TAGGED         16900000
         AIF   (NOT &SW(8)).BH                                      3-9 16950000
IJHRREGS DS    H                       LOAD IOREG                       17000000
IJHRIDSV DS    4H       MBBCCHHR  DISK  ADDR  FROM READ TO WRITE        17050000
IJHRADSV DS    F        RECORD POINTER WITHIN IOAREA FOR WRITE          17100000
IJHROVCN DS    F        NON 1ST OVFL RECORD COUNT                       17150000
.BH      AIF   (NOT &SW(21)).BI                                     3-9 17200000
IJHCTBLX DSECT                                                          17250000
IJHCORST DS    F                        START OF INDEX CORE.            17300000
         DS    H                        CORE AVAILABLE FOR INDEX.       17350000
         DS    C'MBBCCHHR'              NEXT CYLINDER INDEX ENTRY TO    17400000
*                                         BE READ.                      17450000
         DS    C'CCHHR'                 LAST CYLINDER INDEX ENTRY.      17500000
IJHCORBT DS    X'00'                    CORE INDEX BYTE.                17550000
IJHCORKY DS    F                        POINTER TO KEY.                 17600000
.BI      AIF   (NOT &SW(22)).BJ                                     3-9 17650000
         DC    F'0'           ALIGN ACCORDING TO DTF TABLE     @DL2W721 17700000
IJHPSIZE DS    H                        IOAREAL SIZE.                   17750000
IJHPMAX  DS    H                        MAXIMUM NUMBER OF PRIME RECORDS 17800000
*                                         IN CORE.                      17850000
IJHPDSP1 DS    F                        ADDRESS OF WRITE CCW'S (FIRST   17900000
*                                         SIDE).                        17950000
IJHPDSP2 DS    F                        ADDRESS OF READ CCW'S (SIDE).   18000000
IJHPSW   DC    X'00'                    SWITCH BYTE.                    18050000
         DC    X'00'                    NOT YET USED           @DA07584 18100000
IJHDCWRK DS    H                        # RECORDS TO BE READ   @DA07584 18150000
         AGO   .BK                                                  3-9 18200000
.BJ      DS    4D                       RESERVED.                   3-9 18250000
.BK      ANOP                                                       3-9 18300000
***************************** TABLE THREE ***************************** 18350000
IJHCTBL3 DSECT                                                          18400000
         AIF  (NOT &SW(15)).BK1        RPS NOT SPECIFIED       @DL30SMP 18450000
IJHCBLD  DS    1D                      UNUSED CCW BUILD AREA   @DL30SMP 18500000
IJHVTRS  DS    1D                      TEMPORY REG SAVE AREA   @DL30SMP 18550000
         DS    6D                      REMAINDER UNUSED CCW    @DA07584 18600000
*                                      BUILD AREA              @DL30SMP 18650000
         AGO   .BK2                                            @DL30SMP 18700000
.BK1     ANOP                                                  @DL30SMP 18750000
         CCW   X'07',IJHCSADR+1,X'40',6    SEEK                         18800000
IJHCCCW  DS    D                       BUILD AREA FOR CCW'S         3-4 18850000
IJHSKCCB DS    2D                      CCB FOR OVERSEEK TEST        3-4 18900000
IJHSKCCW DS    4D                      AREA FOR OVERSEEK CCW'S      3-4 18950000
.BK2     ANOP                                                  @DL30SMP 19000000
         AIF   (NOT &SW(2)).BL                                      3-9 19050000
         DC    8D'0'                                                    19100000
IJHACOCR DC    4H'0'                   CYL OFL CTL REC AREA             19150000
IJHACTNA DC    4H'0'                                                    19200000
IJHACTOA DC    4H'0'                   ADDR OF TI OFL REC               19250000
IJHACRID DC    4H'0'                                                    19300000
IJHACFID DC    4H'0'                                                    19350000
IJHACTIN DC    5H'0'                                                    19400000
IJHACLNK DC    5H'0'                                                    19450000
IJHACTIA DC    5H'0'                                                    19500000
IJHAGATE DC    X'00'                    GATE.                           19550000
         DC    C'CCH'                   FORCE CCH UNEQUAL.              19600000
IJHAOCOH DC    C'HR'     MAX HEAD & RECORD ON OVERFLOW.                 19650000
         DC    C'MBBCCHH'        PRIME DATA LIMITS                      19700000
IJHACLTR DC    C'R'      RECORD CHANGE LIMIT.                           19750000
IJHAICOM DC    XL6'0'    CLC 0(&KEYLEN,13),0(6) (UNBLOCKED).            19800000
IJHAISKY DC    XL6'0'    MVC 0(&KEYLEN,13),0(12) (UNBLOCKED).           19850000
.BL      ANOP                                                       3-9 19900000
         DS    F         MODULE CELL TABLE--LOGICAL UNIT NUMBER, BB.    19950000
*        END OF TABLE    END OF TABLE   END OF TABLE   END OF TABLE     20000000
         AIF   (NOT &SW(15)).BL1       RPS NOT SPECIFIED       @DL30SMP 20050000
         EJECT                                                          20100000
******************TABLE FOR DTFIS EXTENSION******************* @DL30SMP 20150000
IJHVDTF  DSECT                         DTFIS RPS EXTENSION     @DL30SMP 20200000
IJHVSEEK DS    8C                      INITIAL SEEK CCW        @DL30SMP 20250000
IJHVCCW  DS    8C                      BUILD AREA FOR CCW'S    @DL30SMP 20300000
IJHVKCCB DS    16C                     CCB FOR OVERSEEK TEST   @DL30SMP 20350000
IJHVKCCW DS    32C                     CCW FOR OVERSEEK CCW'S  @DL30SMP 20400000
         DS    80C                     REMAINDER CCW BLD AREA  @DL30SMP 20450000
IJHVARG  DS    0C                      RPS SECTOR ARGUMENTS    @DL30SMP 20500000
IJHVARG0 DS    1C                      SECTOR ARG FIELD # 0    @DL30SMP 20550000
IJHVARG1 DS    1C                      SECTOR ARG FIELD # 1    @DL30SMP 20600000
IJHVARG2 DS    1C                      SECTOR ARG FIELD # 2    @DL30SMP 20650000
IJHVARG3 DS    1C                      SECTOR ARG FIELD # 3    @DL30SMP 20700000
IJHVARG4 DS    1C                      SECTOR ARG FIELD # 4    @DL30SMP 20750000
IJHVWRK  DS    3C                      RPS WORK AREA           @DL30SMP 20800000
IJHVWRK1 DS    12C                     RPS WORK AREA # 1       @DL30SMP 20850000
IJHVAERT DS    4C                      ADDR RPS ERREXT RTN     @DL30SMP 20900000
IJHVMR14 DS    4C                      ISMOD REGISTER 14       @DL30SMP 20950000
IJHVUR13 DS    4C                      USER REGISTER 13        @DL30SMP 21000000
IJHVDCA  DS    4C                      ADDR DTF CCW BUILD AREA @DL30SMP 21050000
IJHVUMA  DS    4C                      ADDR USER ISMOD         @DL30SMP 21100000
IJHVUSA  DS    0C                      USER SAVE AREA (RDONLY) @DL30SMP 21150000
         DS    48C                     FRONT SECTION OF U S A  @DL30SMP 21200000
IJHVUR14 DS    4C                      USER REGISTER 14        @DL30SMP 21250000
         DS    20C                     REMAINDER OF USER S A   @DL30SMP 21300000
IJHVERTM DS    44C                     RPS ERREXT ROUTINE      @DL30SMP 21350000
         DS    84C                     UNUSED AREA WITHIN EXT. @DL30SMP 21400000
***************END OF TABLE FOR DTFIS EXTENSION*************** @DL30SMP 21450000
.BL1     ANOP                                                  @DL30SMP 21500000
         EJECT                                                          21550000
