         MACRO                                                          00010002
&NAME    IPW$DSD   &SECT=,&REL=,&APAR=                         @DA30492 00020002
.*                                                                      00030002
.* IBM DISK OPERATING SYSTEM                                            00040002
.* VSE/POWER - 5686-CF7-03                                     @D61ODSW 00050010
.* IPW$DSD MACRO INSTRUCTION - DEFINE STORAGE DESCRIPTOR                00060002
.* POWER 6.3 CLC CODE CHANGE                                   @D63QDSW 00061002
.* POWER 6.4 CLC CODE CHANGE                                   @D64VDAT 00061103
.* POWER 6.5 CLC CODE CHANGE                                   @D65DDAT 00061204
.* POWER 6.6 CLC CODE CHANGE                                   @D66GDAT 00061306
.* POWER 6.7 CLC CODE CHANGE                                   @D67QDAT 00061407
.* POWER 6.8 CLC CODE CHANGE 85C                               @D68QDAT 00061509
.* POWER 6.8 CLC CODE CHANGE 80C                               @D68QDAT 00061609
.* VERSION 7.1 UPGRADE                                         @D71QDAT 00061710
.*                                                                      00062002
.*                                                                      00063002
.* *****************************************************************    00064002
.* *  NOTE: IPW$DSD AND IPW$DSD1 MUST BE UPDATED AT THE SAME TIME!!*    00065002
.* *         - CLEAR MEMBER IPW$DSD CREATES THE IPW$DSD.A MACRO,   *    00066002
.* *           THAT MAY BE EDECK'ED BY CUSTOMER FOR USE WITH       *    00067002
.* *           VSE/ASSEMBLER, WHICH DOES NOT ACCEPT '&SYSDATE'     *    00068002
.* *         - CLEAR MEMBER IPW$DSD1 CREATES THE IPW$DSD MACRO     *    00069002
.* *           USED FOR TSO DRIVER ASSEMBLY, WITH '&SYSDATE'       *    00070002
.* *****************************************************************    00080002
.*                                                                      00090002
.*                                                                      00100002
.*           &NAME WILL BE USED AS THE STORAGE DESCRIPTOR LABEL         00110002
.*           &SECT WILL BE USED AS THE STORAGE DESCRIPTOR NAME          00120002
.*           &REL  WILL BE USED TO INSERT THE CORRECT LEVEL             00130002
.*           &APAR WILL BE USED TO INSERT THE UP-TO-DATE APAR NUMBER    00140002
.*                                                                      00150002
         GBLB  &$PWSA                                          @D52IDAT 00160002
         LCLC  &SD                                                      00170002
         LCLC  &USER                                                    00180002
         LCLC  &SECTION                                                 00190002
         LCLC  &RL                                             @D03PIQE 00200002
.*                                                                      00210002
         AGO   .ZZZZ               BRANCH AROUND COPYRIGHT              00220002
***************************************************************         00230002
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"          *         00240002
*        5686-CF7 (C) COPYRIGHT IBM CORP. 1979, 1996          *         00250010
*        ALL RIGHTS RESERVED                                  *         00260002
*        LICENSED MATERIALS-PROPERTY OF IBM                   *         00270002
*        SEE COPYRIGHT INSTRUCTIONS, G120-2083                *         00280002
***************************************************************         00290002
.ZZZZ    ANOP                                                           00300002
.*                                                                      00310002
.*             SET UP THE COMPILE DATE OR THE APAR NUMBER               00320002
.*                                                                      00330002
         AIF   (T'&APAR EQ 'O').SD1   BRANCH IF OMITTED        @DA30492 00340002
         AIF   ('&APAR'(1,2) NE 'DY').MN3  BRANCH IF NOT 'DY'  @DA30492 00350002
         AIF   (K'&APAR NE 7).MN3     BRANCH IF LENGTH NOT 7   @DA30492 00360002
&USER    SETC  '&APAR'                TAKE DEFINED APAR NUMBER @DA30492 00370002
         AGO   .SD1A                  AND CONTINUE             @DA30492 00380002
.*                                                                      00390002
.SD1     ANOP                                                  @DA30492 00400002
&USER    SETC  ' '                                                      00410002
.*             NEXT INST DISABLED FOR SHIPMENT                          00420002
&USER    SETC  '&SYSDATE'(1,5)     SET DATE (DD/MM)                     00430002
.*                                                                      00440002
.*             CHECK FOR A SECTION NAME DEFINED OR THE PRESENCE         00450002
.*             OF A VALID CSECT                                         00460002
.*                                                                      00470002
.SD1A    ANOP                                                  @DA30492 00480002
         AIF   (T'&SECT NE 'O').SD2   IS A SECTION NAME DEFINED         00490002
&SECTION SETC  '&SYSECT'             SET SECTION NAME                   00500002
&$PWSA   SETB  1                     SET 'IN VSE/POWER MODULE' @D52IDAT 00510002
         AGO   .SD3                                                     00520002
.*                                                                      00530002
.*             SECT WAS SPECIFIED AND IS USED AS THE NAME OF THE        00540002
.*             STORAGE DESCRIPTOR. THE DATE IS ALSO SET TO BLANK        00550002
.*             UNLESS THE SECTION NAME IS 'NUCS'.                       00560002
.*                                                                      00570002
.SD2     ANOP                                                           00580002
&SECTION SETC '&SECT'(1,8)         SET SECTION NAME            @D35BIQI 00590002
         AIF  ('&SECTION' EQ 'NUCS').SD3                       @D23DDWS 00600002
&USER    SETC ' '                  SET DATE TO BLANK           @D35BIQI 00610002
.*                                                                      00620002
.*            THE &NAME IS USED AS THE LABEL FOR THE STORAGE            00630002
.*            DESCRIPTOR IF SPECIFIED. IF NOT SPECIFIED THE             00640002
.*            CSECT NAME WILL BE USED IF PRESENT. IF NO CSECT           00650002
.*            THEN THE NAME WILL BE BLANK.                              00660002
.*                                                                      00670002
.SD3     ANOP                                                           00680002
&SD      SETC  '&NAME'             SET SECTION DESCRIPTOR NAME @D35BIQI 00690002
         AIF   ('&NAME' NE '').SD5   IF NAME NOT DEFINED       @D35BIQI 00700002
&SD      SETC  ''                  SET SECTION DESCRIPTOR NAME @D52QDSW 00710002
         AIF   ('&SYSECT' EQ '').SD5 NO CSECT(I.E.NOT EX CODE) @D52QDSW 00720002
&SD      SETC  '&SYSECT'(1,2).'SD'   STORAGE DESC NAME                  00730002
.SD5     ANOP                                                           00740002
&RL      SETC  '81C'               DEFAULT FOR BASE PRODUCT    @D71QDAT 00750010
         AIF   (T'&REL EQ 'O').SD7    IF RELEASE NOT SPECIFIED @D03PIQE 00760002
         AIF   ('&REL' EQ 'BASE').SD7 IF BASE IS SPECIFIED     @D03PIQE 00770002
         AIF   ('&REL' EQ 'PNET').SD7 IF PNET SPECIFIED        @D03PIQE 00780002
         AIF   ('&REL' EQ 'RJE').SD7  IF RJE SPECIFIED         @D03PIQE 00790002
         AGO   .MN1                  WRONG SPECIFICATION       @D22DDWS 00800002
.SD7     ANOP                                                  @D03PIQE 00810002
         SPACE 1                                                        00820002
&SD      DC    CL16'&SECTION &RL &USER'  SECTION DESCRIPTOR    @D51QDIR 00830002
         SPACE 1                                                        00840002
.*                                                                      00850002
.*             IF &SECT WAS SPECIFIED DO NOT PRINT THE NOTE             00860002
.*                                                                      00870002
         AIF   (T'&SECT NE 'O').SD8                                     00880002
.*                                                                      00890002
         SPACE 2                                                        00960002
.SD8     ANOP                                                           00970002
         MEXIT                                                          00980002
.*                                                                      00990002
.MN1     MNOTE   8,'&REL MUST BE BASE, PNET OR RJE'                     01000002
         MEXIT                                                          01010002
.*                                                                      01020002
.MN2     MNOTE   4,'&SECT MUST BE SPECIFIED FOR DSECTS'                 01030002
         MEXIT                                                 @DA30492 01040002
.*                                                                      01050002
.MN3     MNOTE   4,'APAR-NUMBER INCORRECT'                     @DA30492 01060002
         MEND                                                           01070002
