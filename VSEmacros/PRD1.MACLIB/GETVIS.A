         MACRO                                                          00010001
&NAME    GETVIS &ERROR,            INVALID POSITIONAL PARAMETER@D14CDJB*00020001
               &ADDRESS=,          SPID/POOL OR GETVIS ADDRESS @D14CDJB*00030001
               &LENGTH=,           LENGTH OF REQUESTED AREA    @D14CDJB*00040001
               &PAGE=NO,           PAGE BOUNDARY OPTION        @D14CDJB*00050001
               &PFIX=NO,           PFIX FOR SYSTEM SUBPOOL     @D14CDJB*00060001
               &POOL=NO,           POOL OPTION                 @D14CDJB*00070001
               &SVA=NO,            SYSTEM GETVIS OPTION        @D14CDJB*00080001
               &SPID=,             SUBPOOL REQUIRED            @D14CDJB*00090001
               &TSKSUBP=NO,        TASK SUBPOOL OPTION         @D14CDJB*00100001
               &SPACE=NO,          SPACE GETVIS REQUEST        @D51IDMZ*00110001
               &SPCNTRL=NO,        INDEX CONTROLLOED SUBPOOL   @D51IDMZ*00120001
               &LOC=RES,           DEFAULT LOC OPTION          @D51IDMZ*00130001
               &IPL=NO,            DEFAULT IPL OPTION          @D61NDMZ*00140001
               &STATUS=NO          DEFAULT STATUS OPTIONS      @DY44311 00150001
         LCLB  &BLPAGE,&BLPOOL,&BLSVA                                   00160001
         LCLB  &BLSSPK             SSP KEY                     @@@@@@MZ 00170003
         LCLB  &BLSPID             SUBPOOL SWITCH              @D14CDJB 00171003
         LCLB  &BLPFIX             PFIX SWITCH                 @D14CDJB 00180001
         LCLB  &BLTSKSP            TASK SUBPOOL SWITCH         @D14CDJB 00190001
         LCLB  &BLPKEY             PROTECT WITH PARTITION KEY  @D51IDMZ 00200001
         LCLB  &BLFTCH             FETCH PROTECT               @D51IDMZ 00210001
         LCLB  &BLSPCE             SPACE GETVIS REQUEST        @D51IDMZ 00220001
         LCLB  &BLLNGR             LENGTH IN REGISTER NOTATION @D51IDMZ 00230001
         LCLB  &BLLNGV             LENGTH VARIABLE SPECFIED    @D51IDMZ 00240001
         LCLB  &BLSPIR             SUBPOOL IN REGISTER NOTATION@D51IDMZ 00250001
         LCLB  &BLSPIV             SUBPOOL VARIABLE SPECFIED   @D51IDMZ 00260001
         LCLB  &BLSPCN             INDEX CONTROLLED SUBPOOL    @D51IDMZ 00270001
         LCLB  &BLBEL              LOC = BELOW SPECIFIED       @D51IDMZ 00280001
         LCLB  &BLANY              LOC = ANY SPECIFIED         @D51IDMZ 00290001
         LCLB  &BLIPL              IPL=YES SPECIFIED           @DDCR360 00300001
         LCLB  &BLSTSZ             STATUS=SIZE SPECIFIED       @DY44311 00310001
         LCLB  &BLSTAL             STATUS=ALL  SPECIFIED       @DY44311 00320001
.*       IBM DISK OPERATING SYSTEM                            *@D149DJB 00330001
*        SUPERVISOR - GETVIS - 5686-066                       *@D61NDMZ 00340001
         AGO   .ZZZ                                           *@D149DJB 00350001
*        LICENSED MATERIALS - PROPERTY OF IBM                 *@D149DJB 00360001
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"          *@D31UDMZ 00370001
*        5686-066 (C) COPYRIGHT IBM CORP. 1981, 1994          *@D61NDMZ 00380001
*        SEE COPYRIGHT INSTRUCTIONS                           *@D31UDMZ 00390001
.*                                                                      00400001
.*  CHANGE ACTIVITY :                                                   00410001
.*    AF 5.1.0 SUPPORT MORE PARTITIONS                         @D51IDMZ 00420001
.*    AF 5.2.0 31-BIT SUPPORT                                  @D52VDMZ 00430001
.*    AF 6.1.0 IPL PARAMETER                                   @D61NDMZ 00440001
.*    AF 6.1.0 STATUS PARAMETER                                @DY44311 00450001
.*      CHANGE A(B'  ') TO B'    ' SINCE FOR THE VSE ASSEMBLER A        00460001
.*      SELF-DEFINING VALUE MUST NOT BE GREATER THAN FFFFFF             00470001
.*                                                                      00480001
.ZZZ     ANOP                                                 *@D149DJB 00490001
         AIF   (T'&ERROR EQ 'O').G0                                     00500001
         MNOTE 4,'POSITIONAL PARAMETER SPECIFIED - MACRO IGNORED'       00510001
         MEXIT                                                          00520001
.G0      ANOP                                                           00530001
.**************************************************************         00540001
.*  PROCESS STATUS PARAMETER                                  *@DY44311 00550001
.**************************************************************         00560001
         AIF   ('&STATUS' EQ 'NO').GIPL                        @DY44311 00570001
         AIF   ('&STATUS' EQ 'SIZE').GSTS                      @DY44311 00580001
         AIF   ('&STATUS' EQ 'ALL').GSTA                       @DY44311 00590001
         MNOTE 8,'INVALID STATUS OPERAND SPECIFIED - MACRO IGNORED'     00600001
         MEXIT                                                 @DY44311 00610001
.GSTS    ANOP                                                           00620001
&BLSTSZ  SETB  1                  STATUS=SIZE SPECIFIED        @DY44311 00630001
         AGO   .GSTC                                           @DY44311 00640001
.GSTA    ANOP                                                           00650001
&BLSTAL  SETB  1                  STATUS=ALL  SPECIFIED        @DY44311 00660001
.GSTC    ANOP                                                           00670001
         AIF   ('&SPACE' EQ 'NO').GSTC1                        @D63BDMZ 00680001
         MNOTE 8,'SPACE MUST NOT BE SPECIFIED TOGETHER WITH STATUS - MA*00690001
               CRO IGNORED'                                             00700001
         MEXIT                                                 @DY44311 00710001
.GSTC1   ANOP                                                           00720001
         AIF   ('&SVA' EQ 'YES').GSTSV                         @DY44311 00730001
         AGO   .G47                                            @D63BDMZ 00740001
.GSTSV   ANOP                                                           00750001
&BLSVA   SETB  1                  SVA = YES SPECIFIED          @DY44311 00760001
         AGO   .G47                                            @DY44311 00770001
.**************************************************************@D51IDMZ 00780001
.*  PROCESS IPL PARAMETER                                     *@D61NDMZ 00790001
.**************************************************************@D51IDMZ 00800001
.GIPL    ANOP                                                           00810001
         AIF   ('&IPL' EQ 'NO').G01                            @D61NDMZ 00820001
         AIF   ('&IPL' EQ 'YES').G02                           @D61NDMZ 00830001
         MNOTE 8,'INVALID IPL OPERAND SPECIFIED - MACRO IGNORED'        00840001
         MEXIT                                                 @D61NDMZ 00850001
.G02     ANOP                                                           00860001
         AIF   ('&SVA' EQ 'YES').G03                           @D61NDMZ 00870001
         MNOTE 8,'SVA=YES MUST BE SPECIFIED TOGETHER WITH IPL=YES - MAC*00880001
               RO IGNORED'                                              00890001
         MEXIT                                                 @D61NDMZ 00900001
.G03     ANOP                                                           00910001
&BLIPL   SETB  1                  IPL = YES SPECIFIED          @D61NDMZ 00920001
&BLSVA   SETB  1                  SVA = YES SPECIFIED          @D61NDMZ 00930001
         AGO   .G47                                            @D61NDMZ 00940001
.G01     ANOP                                                           00950001
.**************************************************************@D51IDMZ 00960001
.*  PROCESS SPACE PARAMETER                                   *@D51IDMZ 00970001
.**************************************************************@D51IDMZ 00980001
         AIF   ('&SPACE' EQ 'NO').G1                           @D51IDMZ 00990001
&BLSPCE  SETB  1                  INDICATE SPACE GETVIS REQUEST@D51IDMZ 01000001
         AIF   ('&SPACE' EQ 'YES').SPCHEK3                     @D51IDMZ 01010001
         AIF   ('&SPACE' EQ 'FTCHPR').SPCHEK0                  @D51IDMZ 01020001
         AIF   ('&SPACE' EQ 'PARTKEY').SPCHEK1                 @D51IDMZ 01030001
         MNOTE 8,'INVALID SPACE OPERAND SPECIFIED - MACRO IGNORED'      01040001
         MEXIT                                                 @D51IDMZ 01050001
.SPCHEK0 ANOP                                                  @D51IDMZ 01060001
&BLFTCH  SETB  1                  INDICATE FETCH PROTECT REQ.  @D51IDMZ 01070001
         AGO   .SPCHEK2                                        @D51IDMZ 01080001
.SPCHEK1 ANOP                                                  @D51IDMZ 01090001
&BLPKEY  SETB  1                  INDICATE PART. KEY PROT. REQ.@D51IDMZ 01100001
.SPCHEK2 ANOP                                                  @D51IDMZ 01110001
         AIF   (T'&SPID NE 'O').SPCHEK3                        @D51IDMZ 01120001
         MNOTE 8,'SPID SPECIFICATION MISSING - MACRO IGNORED'           01130001
         MEXIT                                                 @D51IDMZ 01140001
.SPCHEK3 ANOP                                                  @D51IDMZ 01150001
         AIF   ('&PFIX' NE 'YES').SPCHEK4                      @D51IDMZ 01160001
         MNOTE 8,'PFIX=YES NOT ALLOWED WITH SPACE=YES - MACRO IGNORED'  01170001
         MEXIT                                                 @D51IDMZ 01180001
.SPCHEK4 ANOP                                                  @D51IDMZ 01190001
         AIF   ('&POOL' NE 'YES').SPCHEK5                      @D51IDMZ 01200001
         MNOTE 8,'POOL=YES NOT ALLOWED WITH SPACE=YES - MACRO IGNORED'  01210001
         MEXIT                                                 @D51IDMZ 01220001
.SPCHEK5 ANOP                                                  @D51IDMZ 01230001
         AIF   ('&SVA' EQ 'NO').SPCHEK6                        @D51IDMZ 01240003
         MNOTE 8,'SVA NOT ALLOWED WITH SPACE=YES - MACRO IGNORED'       01250003
         MEXIT                                                 @D51IDMZ 01260001
.SPCHEK6 ANOP                                                  @D51IDMZ 01270001
         AIF   ('&TSKSUBP' NE 'YES').G1                        @D51IDMZ 01280001
         MNOTE 8,'TSKSUBP=YES NOT ALLOWED WITH SPACE=YES-MACRO IGNORED' 01290001
         MEXIT                                                 @D51IDMZ 01300001
.SPCHEK7 ANOP                                                  @D51IDMZ 01310001
.G1      ANOP                                                           01320001
.**************************************************************@D51IDMZ 01330001
.*  PROCESS LOC PARAMETER                                     *@D51IDMZ 01340001
.**************************************************************@D51IDMZ 01350001
         AIF   ('&LOC' EQ 'RES').G7                            @D51IDMZ 01360001
         AIF   ('&LOC' EQ 'BELOW').LOCBEL                      @D51IDMZ 01370001
         AIF   ('&LOC' EQ 'ANY').LOCANY                        @D51IDMZ 01380001
         MNOTE 8,'INVALID LOC OPERAND SPECIFIED - MACRO IGNORED'        01390001
         MEXIT                                                 @D51IDMZ 01400001
.LOCBEL  ANOP                                                  @D51IDMZ 01410001
&BLBEL   SETB  1                  LOC = BELOW SPECIFIED        @D51IDMZ 01420001
         AGO   .G7                                             @D51IDMZ 01430001
.LOCANY  ANOP                                                  @D51IDMZ 01440001
&BLANY   SETB  1                  LOC = ANY SPECIFIED          @D51IDMZ 01450001
.G7      ANOP                                                           01460001
.**************************************************************@D149DJB 01470001
.*  PROCESS LENGTH PARAMETER                                  *@D149DJB 01480001
.**************************************************************@D149DJB 01490001
         AIF   (T'&LENGTH EQ 'O').G4 IS LENGTH PAR. SPECIFIED?          01500001
         AIF   ('&LENGTH'(1,1) NE '(').G2 YES,IS IT A REGISTER?         01510001
         AIF   (T'&LENGTH(1) NE 'N').G15                                01520001
         AIF   (&LENGTH(1) EQ 0).G4 REGISTER 0?                         01530001
.G15     ANOP                                                  @D51IDMZ 01540001
&BLLNGR  SETB  1                  INDICATE REGISTER NOTATION   @D51IDMZ 01550001
         AGO   .G4                                                      01560001
.G2      AIF   (T'&LENGTH NE 'N').G3                                    01570001
         MNOTE 8,'LENGTH PARAMETER INVALID  - MACRO IGNORED'            01580001
         MEXIT                                                          01590001
.G3      ANOP                                                           01600001
&BLLNGV  SETB  1                  INDICATE VARIABLE NOTATION   @D51IDMZ 01610001
.**************************************************************@D149DJB 01620001
.*  PROCESS PAGE PARAMETER                                    *@D149DJB 01630001
.**************************************************************@D149DJB 01640001
.G4      ANOP                                                           01650001
         AIF   ('&PAGE' EQ 'NO').G42                                    01660001
         AIF   ('&PAGE' EQ 'YES').G41                                   01670001
         MNOTE 3,'PAGE PARAMETER INCORRECT - ''NO'' ASSUMED'            01680001
         AGO   .G42                                                     01690001
.G41     ANOP                                                           01700001
&BLPAGE  SETB  1                                                        01710001
.**************************************************************@D149DJB 01720001
.*  PROCESS SPID PARAMETER                                    *@D149DJB 01730001
.**************************************************************@D149DJB 01740001
.G42     AIF   (T'&SPID EQ 'O').G429      PARAMETER SPECIFIED  @D14CDJB 01750001
         AIF   ('&SPID'(1,1) NE '(').G422      REG NOTATION    @D14CDJB 01760001
         AIF   (T'&SPID(1) NE 'N').G421      ABS REG NOTATION  @D14CDJB 01770001
         AIF   (&SPID(1) EQ 1).G424      SKIP FOR RIGHT REG    @D14CDJB 01780001
.G421    ANOP                                                           01790001
&BLSPIR  SETB  1                  INDICATE REGISTER NOTATION   @D51IDMZ 01800001
         AGO   .G424                                           @D14CDJB 01810001
.G422    AIF   (T'&SPID NE 'N').G423      VALID SYMBOL         @D14CDJB 01820001
         AIF   (&BLFTCH OR &BLPKEY).G425  SPID MUST BE VALID   @D51IDMZ 01830001
         MNOTE 3,'SPID PARAMETER INVALID - IGNORED'            @D51IDMZ 01840001
         AGO   .G429                                           @D51IDMZ 01850001
.G425    ANOP                                                  @D51IDMZ 01860001
         MNOTE 8,'REQUIRED SPID PARAMETER IS INVALID - MACRO IGNORED'   01870001
         MEXIT                                                 @D51IDMZ 01880001
.G423    ANOP                                                           01890001
&BLSPIV  SETB  1                  INDICATE VARIABLE NOTATION   @D51IDMZ 01900001
.G424    ANOP                                                  @D14CDJB 01910001
&BLSPID  SETB  1                                               @D14CDJB 01920001
         AIF   ('&POOL' EQ 'NO').G44                           @D14CDJB 01930001
         MNOTE 3,'POOL NOT VALID WITH SPID - ''NO'' FOR POOL ASSUMED'   01940001
         AGO   .G44                                            @D14CDJB 01950001
.**************************************************************@D149DJB 01960001
.*  PROCESS POOL PARAMETER                                    *@D149DJB 01970001
.**************************************************************@D149DJB 01980001
.G429    AIF   ('&POOL' EQ 'NO').G44                           @D14CDJB 01990001
         AIF   ('&POOL' EQ 'YES').G43                                   02000001
         MNOTE 3,'POOL PARAMETER INCORRECT - ''NO'' ASSUMED'            02010001
         AGO   .G44                                                     02020001
.G43     ANOP                                                           02030001
         AIF   ('&LOC' EQ 'ANY').G431                          @D52VDCS 02040001
&BLPOOL  SETB  1                                               @D52VDCS 02050001
         AGO   .G44                                            @D52VDMZ 02060001
.G431    ANOP                                                  @D52VDCS 02070001
         MNOTE 3,'POOL PARAMETER IGNORED FOR LOC=ANY'          @D52VDCS 02080001
.**************************************************************@D149DJB 02090001
.*  PROCESS SVA PARAMETER                                     *@D149DJB 02100001
.**************************************************************@D149DJB 02110001
.G44     ANOP                                                           02120001
         AIF   ('&SVA' EQ 'NO').G46                                     02130001
&BLSVA   SETB  1                                                        02131002
         AIF   ('&SVA' EQ 'YES').G45                                    02140001
         AIF   ('&SVA' EQ 'SSPKEY').G450                                02141002
         MNOTE 3,'SVA PARAMETER INCORRECT - ''NO'' ASSUMED'             02150001
         AGO   .G46                                                     02160001
.G450    ANOP                                                           02161002
&BLSSPK  SETB  1                                                        02161103
         AIF   (T'&SPID NE 'O').G46                            @@@@@@MZ 02162002
         MNOTE 8,'SPID SPECIFICATION MISSING - MACRO IGNORED'           02163002
         MEXIT                                                 @D51IDMZ 02164002
.G45     ANOP                                                           02170001
.**************************************************************@D149DJB 02190001
.*  PROCESS PFIX PARAMETER                                    *@D149DJB 02200001
.**************************************************************@D149DJB 02210001
.G46     ANOP                                                  @D14CDJB 02220001
         AIF   ('&PFIX' EQ 'NO').SPCNT                         @D14CDJB 02230001
         AIF   ('&PFIX' EQ 'YES').CHKPFIX                      @D14CDJB 02240001
         MNOTE 8,'PFIX PARAMETER INCORRECT - ''NO'' ASSUMED'   @D14CDJB 02250001
         AGO   .SPCNT                                          @D14CDJB 02260001
.CHKPFIX ANOP                                                  @D14CDJB 02270001
         AIF   (&BLSPID AND &BLSVA).PFIXOK                     @D14CDJB 02280001
         MNOTE 8,'PFIX PARAMETER ONLY VALID WITH SPID AND SVA=YES - ''N*02290001
               O'' ASSUMED'                                    @D14CDJB 02300001
         AGO   .SPCNT                                          @D14CDJB 02310001
.PFIXOK  ANOP                                                  @D14CDJB 02320001
&BLPFIX  SETB  1                                               @D14CDJB 02330001
.**************************************************************         02340001
.*  PROCESS SPCNTRL PARAMETER                                 *         02350001
.**************************************************************         02360001
.SPCNT   ANOP                                                  @D14CDJB 02370001
         AIF   ('&SPCNTRL' EQ 'NO').TSKSP                      @D51IDMZ 02380001
         AIF   ('&SPCNTRL' EQ 'YES').CHKSPCN                   @D51IDMZ 02390001
         MNOTE 4,'SPCNTRL PARAMETER INCORRECT - ''NO'' ASSUMED'         02400001
         AGO   .TSKSP                                          @D51IDMZ 02410001
.CHKSPCN ANOP                                                  @D51IDMZ 02420001
         AIF   (&BLSPID).SPCNOK                                @D51IDMZ 02430001
         MNOTE 4,'SPCNTRL PARAMETER ONLY VALID WITH SPID PARAMETER - ''*02440001
               NO'' ASSUMED'                                            02450001
         AGO   .TSKSP                                          @D51IDMZ 02460001
.SPCNOK  ANOP                                                  @D51IDMZ 02470001
&BLSPCN  SETB  1                                               @D51IDMZ 02480001
.**************************************************************@D149DJB 02490001
.*  PROCESS TSKSUBP PARAMETER                                 *@D149DJB 02500001
.**************************************************************@D149DJB 02510001
.TSKSP   AIF   ('&TSKSUBP' EQ 'NO').SR15                       @D14CDJB 02520001
         AIF   ('&TSKSUBP' EQ 'YES').CHKTSKS                   @D14CDJB 02530001
         MNOTE 8,'TSKSUBP PARAMETER INVALID - ''NO'' ASSUMED'  @D14CDJB 02540001
         AGO   .SR15                                           @D14CDJB 02550001
.CHKTSKS ANOP                                                  @D14CDJB 02560001
         AIF   (NOT (&BLSPID OR &BLPOOL OR &BLSVA)).TSKOK      @D14CDJB 02570001
         MNOTE 8,'TSKSUBP NOT VALID WITH SPID OR SVA=YES OR POOL=YES - *02580001
               ''NO'' ASSUMED'                                 @D14CDJB 02590001
         AGO   .SR15                                           @D14CDJB 02600001
.TSKOK   ANOP                                                  @D14CDJB 02610001
&BLTSKSP SETB  1                                               @D14CDJB 02620001
.**************************************************************@D149DJB 02630001
.*  PROVIDE LENGTH AND SPID PARAMETER                         *@D51IDMZ 02640001
.*  SET UP OPTION REGISTER 15 AND ISSUE SVC                   *@D149DJB 02650001
.**************************************************************@D149DJB 02660001
.SR15    ANOP                                                  @D51IDMZ 02670001
         AIF   (T'&NAME EQ 'O').PRSP                                    02680001
&NAME    DS    0H                                                       02690001
.PRSP    ANOP                                                  @D51IDMZ 02700001
         AIF   (&BLLNGR).SR150                                 @D51IDMZ 02710001
         AIF   (&BLLNGV).SR151                                 @D51IDMZ 02720001
         AGO   .SR152                                          @D51IDMZ 02730001
.SR150   LR    0,&LENGTH(1)       LOAD LENGTH INTO REGISTER 0           02740001
         AGO   .SR152                                          @D51IDMZ 02750001
.SR151   L     0,&LENGTH          LOAD LENGTH INTO REGISTER 0           02760001
.SR152   ANOP                                                           02770001
         AIF   (&BLSPIR).SR153                                 @D51IDMZ 02780001
         AIF   (&BLSPIV).SR154                                 @D51IDMZ 02790001
         AGO   .SR155                                          @D51IDMZ 02800001
.SR153   LR    1,&SPID(1)         GET SUBPOOL ID FIELD ADDR.   @D14CDJB 02810001
         AGO   .SR155                                          @D51IDMZ 02820001
.SR154   LA    1,&SPID            GET SUBPOOL ID FIELD ADDR.   @D14CDJB 02830001
.SR155   ANOP                                                           02840001
         AIF   (&BLSPID OR &BLPAGE OR &BLPOOL OR &BLSVA).G47   @D14CDJB 02850001
         AIF   (&BLPFIX OR &BLTSKSP OR &BLSPCE OR &BLBEL).G47  @D51IDMZ 02860001
         AIF   (&BLANY).G47                                    @D51IDMZ 02870001
         SR    15,15              CLEAR INDICATORS                      02880001
         AGO   .G48                                                     02890001
.G47     ANOP                                                  @D51IDMZ 02900001
         CNOP  0,4                                                      02910001
         BAL   15,*+8                                                   02920001
         DC    B'0000000000000&BLSSPK&BLSTAL&BLSTSZ&BLIPL.0&BLANY&BLBEL*02921003
               &BLSPCN&BLPKEY&BLSPCE.00&BLFTCH&BLTSKSP&BLPFIX&BLSPID&BL*02922003
               SVA&BLPOOL&BLPAGE'                                       02923003
         L     15,0(,15)                                                02960001
.G48     SVC   61                 ISSUE SVC FOR GETVIS                  02970001
.**************************************************************@D149DJB 02980001
.*  PROCESS ADDRESS PARAMETER                                 *@D149DJB 02990001
.**************************************************************@D149DJB 03000001
         AIF   (T'&ADDRESS EQ 'O').G6                                   03010001
         AIF   ('&ADDRESS'(1,1) EQ '(').G5                              03020001
         AIF   (T'&ADDRESS NE 'N').G49                                  03030001
         MNOTE 3,'ADDRESS PARAMETER INVALID - IGNORED'                  03040001
         MEXIT                                                          03050001
.G49     ST    1,&ADDRESS         STORE RETURNED ADDRESS                03060001
         AGO   .G6                                                      03070001
.G5      AIF   (T'&ADDRESS(1) NE 'N').G56                               03080001
         AIF   ('&ADDRESS(1)' EQ '1').G6 REGISTER 1 WAS SPECIFIED       03090001
         AIF   (&ADDRESS(1) NE 15).G56                                  03100001
         MNOTE 4,'RETURN CODE IN REGISTER 15 WILL BE DESTROYED'         03110001
.G56     LR    &ADDRESS(1),1      LOAD RETURNED ADDRESS FROM REGISTER 1 03120001
.G6      MEND                                                           03130001
