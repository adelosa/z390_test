*  /* START OF SPECIFICATIONS ***************************************** 00050000
*                                                                     * 00100000
*01*  MODULE-NAME = ATSET                                             * 00150000
*                                                                     * 00200000
*01*  MACDATE = 93.170                                                * 00250000
*                                                                     * 00300000
*01*  DESCRIPTIVE-NAME = AUTHORIZATION TABLE SET MACRO                * 00350000
*                                                                     * 00400000
*01*  COPYRIGHT =                                                     * 00450000
*            5655-068                                                 * 00500000
*            THIS MODULE IS "RESTRICTED MATERIALS OF IBM"             * 00550000
*            (C)COPYRIGHT IBM CORP. 1980, 1993                        * 00600000
*            LICENSED MATERIALS - PROPERTY OF IBM                     * 00650000
*                                                                     * 00700000
*01*  STATUS = HBB5510                                            @P1C* 00750000
*                                                                     * 00800000
*01*  FUNCTION = INTERFACE TO THE ATSET SERVICE                       * 00850000
*                                                                     * 00900000
*01*  NOTES = THE 'PT' AND 'SSAR' PARAMETERS DEFAULT TO 'NO'.         * 00950000
*                                                                     * 01000000
*01*  MODULE-TYPE = MACRO                                             * 01050000
*                                                                     * 01100000
*01*  PROCESSOR = ASSEMBLER,PL/S-III (BILINGUAL)              @ZM50111* 01150000
*                                                                     * 01200000
*01*  MACROS = IHBERMAC                                               * 01250000
*                                                                     * 01300000
*01*  CHANGE ACTIVITY =       NEW FOR OS/VS2 JBB1226.                 * 01350000
*                             UPDATE BOTH BAL AND PL/S MACDATE        * 01400000
*                             WHENEVER THIS MACRO IS CHANGED.         * 01450000
*                             THE S/D CODE IS @G387PXX.               * 01500000
*             VS50111 - (PL/S) FORCE RC=12 ON NULL AX(1).     @ZM50111* 01550000
*             PBB0031 - (PL/S) PTR NOTATION CHECKS INVALID.   @ZMB0031* 01600000
*             PBB0091 - REMOVE DEPENDENCY ON BITS 0-15        @ZMB0091* 01650000
*                       OF AN AX REGISTER BEING ZERO.         @ZMB0091* 01700000
*           $P1=PIG1968,HBB5510,930616,PDXB: Savearea                 * 01750000
*                                                                     * 01800000
*01*  MNOTES = IHB001 AX OPERAND REQUIRED, NOT SPECIFIED              * 01850000
*              IHB002 INVALID PT OPERAND - ___                        * 01900000
*              IHB002 INVALID SSAR OPERAND - ___                      * 01950000
*              IHB002 INVALID AX OPERAND - ___                        * 02000000
*              IHB027 EXCESSIVE POSITIONAL PARAMETERS SPECIFIED       * 02050000
*                                                                     * 02100000
*01*  PL/S MSGS = AS FOLLOWS:                                         * 02150000
*     AHA0013 AX(1) IS REQUIRED BUT IS NOT SPECIFIED.                 * 02200000
*             NO TEXT IS GENERATED.                                   * 02250000
*     AHA0013 AX IS REQUIRED BUT IS NOT                               * 02300000
*             SPECIFIED. NO TEXT IS GENERATED.                        * 02350000
*     AHA0033 AX - '_' - HAS TOO MANY ARGUMENTS.                      * 02400000
*             NO TEXT IS GENERATED.                                   * 02450000
*     AHA0033 PT - '_' - HAS TOO MANY ARGUMENTS.                      * 02500000
*             NO TEXT IS GENERATED.                                   * 02550000
*     AHA0033 SSAR - '_' - HAS TOO MANY ARGUMENTS.                    * 02600000
*             NO TEXT IS GENERATED.                                   * 02650000
*     AHA0043 PT MUST BE 'YES' OR 'NO'. NO TEXT IS GENERATED.         * 02700000
*     AHA0043 SSAR MUST BE 'YES' OR 'NO'. NO TEXT IS GENERATED.       * 02750000
*     AHA0053 THE MACLIST '_' IS NOT ALLOWED. NO TEXT IS GENERATED.   * 02800000
*                                                                     * 02850000
*A000000-999999                                                @G387PXX 02900000
***** END OF SPECIFICATIONS ******************************************* 02950000
         MACRO                                                          03000000
&LABL    ATSET &AX=,       REQUIRED AX VALUE (SEE NOTE BELOW)  (INPUT) X03050000
               &PT=,       OPTIONAL, 'YES' OR 'NO'(DEFAULT)    (INPUT) X03100000
               &SSAR=,     OPTIONAL, 'YES' OR 'NO'(DEFAULT)    (INPUT) X03150000
               &RELATED=   OPTIONAL VALUE FOR DOCUMENTATION             03200000
.* NOTE: IF THE AX PARAMETER IS EXPRESSED IN RX FORM IT IS              03250000
.*         INTERPRETED AS THE ADDRESS OF A HALFWORD AX VALUE.           03300000
.*       IF THE AX PARAMETER IS EXPRESSED IN REGISTER NOTATION          03350000
.*         THE MACRO ASSUMES THE AX VALUE IS IN THE LOW-ORDER HALF      03400000
.*         OF THE SPECIFIED REGISTER. THE HIGH-ORDER HALF OF THE        03450000
.*         REGISTER IS REQUIRED TO BE ZERO BY ATSET.                    03500000
.********************************************************************** 03550000
.*       PUT OUT BAL MACDATE COMMENT LINE REGARDLESS OF ERRORS.       * 03600000
.********************************************************************** 03650000
*              MACDATE = 80.122                                         03700000
.********************************************************************** 03750000
.*       DECLARE ARITHMETIC VARIABLES                                 * 03800000
.********************************************************************** 03850000
         LCLA  &A          GENERAL USAGE LOCAL ARITH. VAR.              03900000
         LCLA  &BOPTS      OPTIONS BYTE INIT VALUE.                     03950000
.********************************************************************** 04000000
.*       DECLARE BIT VARIABLES                                        * 04050000
.********************************************************************** 04100000
         LCLB  &AX0        SPECIAL REGISTER 0 NOTATION WAS SPECIFIED    04150000
.*                         FOR THE 'AX' PARAMETER.                      04200000
         LCLB  &B(8)       OPTIONS BITS                                 04250000
.********************************************************************** 04300000
.*       DECLARE CHARACTER VARIABLES                                  * 04350000
.********************************************************************** 04400000
         LCLC  &LT1        LOAD TYPE 1 - THIS VARIABLE WILL BE:         04450000
.*                              'R' IF AX IS IN A REG                   04500000
.*                           OR 'H' IF AX IS AN RX TYPE HALFWORD ADDR   04550000
         LCLC  &AXV        HOLDS VALUE OF AX FOR USE IN INSTRUCTIONS    04600000
         LCLC  &OPERAND    HOLDS OPERAND STRINGS FOR MNOTES             04650000
         LCLC  &OPVAL      HOLDS OPERAND VALUES FOR MNOTES              04700000
         LCLC  &P1         CHAR VARIABLE FOR 'PT' PARM                  04750000
         LCLC  &P2         CHAR VARIABLE FOR 'SSAR' PARM                04800000
.********************************************************************** 04850000
.*       INITIALIZE BIT VARIABLES                                     * 04900000
.********************************************************************** 04950000
&AX0     SETB  0           INDICATE NOT SPECIAL REG 0 INPUT PARM        05000000
&B(1)    SETB  0           SHOW PT=NO                                   05050000
&B(2)    SETB  0           SHOW SSAR=NO                                 05100000
&B(3)    SETB  0           RESERVED BIT- MUST BE ZERO                   05150000
&B(4)    SETB  0           RESERVED BIT- MUST BE ZERO                   05200000
&B(5)    SETB  0           RESERVED BIT- MUST BE ZERO                   05250000
&B(6)    SETB  0           RESERVED BIT- MUST BE ZERO                   05300000
&B(7)    SETB  0           RESERVED BIT- MUST BE ZERO                   05350000
&B(8)    SETB  0           RESERVED BIT- MUST BE ZERO                   05400000
.********************************************************************** 05450000
.*       CHECK FOR EXTRANEOUS POSITIONAL PARMS                        * 05500000
.********************************************************************** 05550000
         AIF   (N'&SYSLIST NE 0).ERR027                                 05600000
.********************************************************************** 05650000
.*       VALIDATE AX PARAMETER IF PRESENT                             * 05700000
.********************************************************************** 05750000
&OPERAND SETC  'AX'                     SET FOR POSSIBLE ERROR MSGS     05800000
&OPVAL   SETC  '&AX'                    SET FOR POSSIBLE ERROR MSGS     05850000
         AIF   (T'&AX EQ 'O').ERR001    REQ'D PARM IS MISSING           05900000
.*   DETERMINE WHETHER AX ADDRESS IS RX OR REG TYPE                     05950000
         AIF   ('&AX'(1,1) EQ '(').OK11       BRANCH IF REG TYPE        06000000
&AXV     SETC  '&AX'                    SET VALUE FOR USE IN INST.      06050000
&LT1     SETC  'H'                      SET TO 'H' FOR RX TYPE ADDR     06100000
         AGO   .OK20                    GO CHECK SSAR PARAMETER         06150000
.OK11    ANOP                                                           06200000
&LT1     SETC  'R'                      SET TO 'R' FOR REG              06250000
&AXV     SETC  '&AX'(2,K'&AX-2)         EXTRACT REGISTER NAME           06300000
         AIF   (N'&AX NE 1).ERR002      BR IF INVALID REG FORM          06350000
         AIF   ('&AXV' NE '0').OK20     BR IF NOT SPECIAL REG 0         06400000
&AX0     SETB  1                        INDICATE SPECIAL REG 0 USED     06450000
.********************************************************************** 06500000
.*       VALIDATE PT PARAMETER IF PRESENT                             * 06550000
.********************************************************************** 06600000
.OK20    ANOP                                                           06650000
         AIF   (T'&PT EQ 'O').DEF20                                     06700000
&OPERAND SETC  'PT'                                                     06750000
&OPVAL   SETC  '&PT'                                                    06800000
         AIF   ('&PT' NE 'YES' AND '&PT' NE 'NO').ERR002                06850000
         AIF   ('&PT' EQ 'YES').ERR310                         @D64XDUG 06875001
&P1      SETC  '&PT'                                                    06900000
         AGO   .OK30                                                    06950000
.DEF20   ANOP  DEFAULT TO PT=NO IF PT NOT SPECIFIED                     07000000
.*             UNLESS IT IS THE EXECUTE FORM                            07050000
&P1      SETC  'NO'                                                     07100000
.********************************************************************** 07150000
.*       VALIDATE SSAR PARAMETER IF PRESENT                           * 07200000
.********************************************************************** 07250000
.OK30    ANOP                                                           07300000
         AIF   (T'&SSAR EQ 'O').DEF30                                   07350000
&OPERAND SETC  'SSAR'                                                   07400000
&OPVAL   SETC  '&SSAR'                                                  07450000
         AIF   ('&SSAR' NE 'YES' AND '&SSAR' NE 'NO').ERR002            07500000
&P2      SETC  '&SSAR'                                                  07550000
         AGO   .OK99                                                    07600000
.DEF30   ANOP  DEFAULT TO SSAR=NO IF SSAR NOT SPECIFIED                 07650000
.*             UNLESS IT IS THE EXECUTE FORM                            07700000
&P2      SETC  'NO'                                                     07750000
.OK99    ANOP                                                           07800000
.********************************************************************** 07850000
.* MAP OPTIONS BITS INTO OPTIONS BYTE                                   07900000
.********************************************************************** 07950000
&B(1)    SETB  ('&P1' EQ 'YES')                                         08000000
&B(2)    SETB  ('&P2' EQ 'YES')                                         08050000
&BOPTS   SETA  128*&B(1)+64*&B(2)+32*&B(3)+16*&B(4)+8*&B(5)+4*&B(6)+2*&X08100000
               B(7)+&B(8)               OPTIONS BYTE INIT. VALUE        08150000
.********************************************************************** 08200000
.*       GENERATE THE CODE                                            * 08250000
.********************************************************************** 08300000
&LABL    DC    0H'0'                .ATSET XM SERVICE PC                08350000
         STM   14,12,12(13)         .SAVE CALLER'S REGS                 08400000
         LA    15,&BOPTS            .LOAD OPTIONS BYTE VALUE            08450000
         SLL   15,16                .SHIFT INTO PROPER POSITION         08500000
         AIF   ('&LT1' EQ 'R').SF40     BR IF REGISTER FORMAT           08550000
         ANOP                           RX FORM CODE                    08600000
         ICM   15,3,&AXV            .INSERT HALFWORD AX VALUE           08650000
         LR    0,15                 .TRANSFER TO PARM REG 0             08700000
         AGO   .SF90                    BR TO COMMON CODE               08750000
.*                                      REGISTER FORMS                  08800000
.SF40    SLL   &AXV,16              .ASSURE NO GARBAGE IN HIGH @ZMB0091 08850000
         SRL   &AXV,16              .ORDER BYTES OF AX REG     @ZMB0091 08900000
         AIF   (&AX0 EQ 1).SF50         BR IF SPECIAL REG 0 NOTATION    08950000
         OR    15,&AXV              .OR IN AX VALUE FROM REG            09000000
         LR    0,15                 .TRANSFER TO PARM REG 0             09050000
         AGO   .SF90                    BR TO COMMON CODE               09100000
.SF50    ANOP                           SPECIAL REG 0                   09150000
         OR    0,15                 .OR OPTIONS AND AX VALUE TOGETHER   09200000
.SF90    ANOP                           COMMON INTERFACE CODE           09250000
         SLR   1,1                  .PARM REG 1 MUST BE ZERO            09300000
         ESAR  2                    .EXTRACT SASID                      09350000
         ST    2,16(,13)            .SAVE SASID IN REG 15 SLOT          09400000
         L     2,16(0,0)            .GET ADDR OF CVT                    09450000
         L     2,772(,2)            .GET ADDR OF SYSTEM FUNCTION TABLE  09500000
         L     2,40(,2)             .GET LX/EX FOR ATSET                09550000
         PC    0(2)                 .PC TO ATSET SERVICE                09600000
         L     2,16(,13)            .EXTRACT SAVED SASID                09650000
         SSAR  2                    .RESTORE SASID                      09700000
         LM    2,12,28(13)          .RESTORE REGS 2-12                  09750000
         L     14,12(,13)           .RESTORE REG 14                     09800000
         MEXIT                                                          09850000
.********************************************************************** 09900000
.*       PROCESS ERROR CONDITIONS                                     * 09950000
.********************************************************************** 10000000
.ERR001  ANOP SEV 12 MNOTE -- IHB001 &OPERAND REQ''D - NOT SPECIFIED    10050000
         IHBERMAC 1017,&OPERAND                                         10100000
         MEXIT                                                          10150000
.ERR002  ANOP SEV 12 MNOTE -- IHB002 - INVALID &OPERAND OPERAND &OPVAL  10200000
         IHBERMAC 1001,&OPERAND,&OPVAL                                  10250000
         MEXIT                                                          10300000
.ERR310  ANOP SEV 8 MNOTE -- IHB310 - &OPERAND OPERAND &OPVAL NOT SUPP. 10312500
         IHBERMAC 1310,&OPERAND,&OPVAL                         @D64XDUG 10325001
         MEXIT                                                          10337500
.ERR027  ANOP SEV 12 MNOTE -- IHB027 - EXCESSIVE POSITIONAL PARAMETERS  10350000
         IHBERMAC 238                                                   10400000
         MEND                                                           10450000
* */                                                                    10500000
*@EJECT;                                                                10550000
*%ATSET:                                                                10600000
*        MACRO KEYS(AX,PT,SSAR,RELATED);                                10650000
*/********************************************************************/ 10700000
*/*                DEFINE EXTERNAL MACRO VARIABLES                   */ 10750000
*/********************************************************************/ 10800000
* DECLARE                                                               10850000
*   EHAREGS   CHARACTER EXT, /* CONTAINS DECLARED REGISTER NUMBERS.  */ 10900000
*   EHAMAPS   CHARACTER EXT; /* CONTAINS REQUESTS FOR MAPPINGS       */ 10950000
*/********************************************************************/ 11000000
*/*                DEFINE INTERNAL MACRO VARIABLES                   */ 11050000
*/********************************************************************/ 11100000
* DECLARE CC       FIXED;    /* STARTING COLUMN FOR PL/S COMMENTS    */ 11150000
* DECLARE IVDOCMT  CHAR;     /* COMMENT FOR DO AND END LINES.        */ 11200000
* DECLARE IVINDNT2 FIXED;    /* STARTING COLUMN FOR ALL ANSWER TEXT  */ 11250000
*                            /*   EXCEPT DO AND END STATEMENTS.      */ 11300000
* DECLARE IVLP1    FIXED;    /* LEFT  PAREN COL FOR PTR FORM AX      */ 11350000
* DECLARE IVOPTS   FIXED;    /* OPTIONS BYTE INITIALIZATION VALUE.   */ 11400000
*                            /* ADD 128 TO INDICATE 'PT'   SPECIFIED.*/ 11450000
*                            /* ADD  64 TO INDICATE 'SSAR' SPECIFIED.*/ 11500000
* DECLARE IVRC     FIXED;    /* RETURN CODE TO DETERMINE IF TEXT WILL*/ 11550000
*                            /* BE GENERATED.                        */ 11600000
* DECLARE IVRP1    FIXED;    /* RIGHT PAREN COL FOR PTR FORM AX      */ 11650000
*/********************************************************************/ 11700000
*/*                INITIALIZE INTERNAL MACRO VARIABLES                  11750000
*/********************************************************************/ 11800000
* CC         = MACRMAR-35;   /* COMMENTS COLUMN (DEFAULT= 72-35 = 37)*/ 11850000
* IVDOCMT    = ' ATSET';     /* START OF DO-END COMMENT--NO KEYS YET */ 11900000
* IVINDNT2   = MACCOL + 2;   /* SET INDENTATION MARGIN.              */ 11950000
*                            /*   END OF AN ANSWER ASSIGNMENT.       */ 12000000
* IVLP1      = INDEX(AX(1),'('); /* GET POSITION OF LEFT PAREN       */ 12050000
*                            /* NOTE: A NON-ZERO VALUE FOR IVLP1     */ 12100000
*                            /* INDICATES A POINTER FORM PARAMETER.  */ 12150000
* IVOPTS     = 0;            /* INDICATE NO OPTIONS SPECIFIED.       */ 12200000
* IVRC       = 0;            /* SET CODE TO GENERATE TEXT            */ 12250000
* IF IVLP1=1 THEN            /* IF POINTER NOTATION          @ZMB0031*/ 12300000
*   DO;                      /* POINTER NOTATION.            @ZMB0031*/ 12350000
*     IVRP1=LENGTH(AX(1));   /* GET LAST CHAR OF SUBLIST     @ZMB0031*/ 12400000
*     IF AX(1,IVRP1:IVRP1)¬=')' THEN /* IF NOT A RIGHT PAREN @ZMB0031*/ 12450000
*       IVRP1=IVLP1;         /* FORCE 'NOT-SPECIFIED' ERROR  @ZMB0031*/ 12500000
*   END;                     /* POINTER NOTATION.            @ZMB0031*/ 12550000
* ELSE                       /* NOT POINTER NOTATION.        @ZMB0031*/ 12600000
*   DO;                      /* NOT POINTER NOTATION.        @ZMB0031*/ 12650000
*     IVLP1=0;               /* NOT POINTER NOTATION.        @ZMB0031*/ 12700000
*     IVRP1=0;               /* NOT POINTER NOTATION.        @ZMB0031*/ 12750000
*   END;                     /* NOT POINTER NOTATION.        @ZMB0031*/ 12800000
*/********************************************************************/ 12850000
*/*                CHECK FOR MACLIST                                 */ 12900000
*/********************************************************************/ 12950000
* IF MACLIST ¬= ''           /* MACLIST SUPPLIED?                    */ 13000000
*   THEN DO;                 /* YES -                                */ 13050000
*     IVRC = 12;             /*       PREVENT TEXT GENERATION        */ 13100000
*     ANS MSG('AHA0053 THE MACLIST '||QUOTE(MACLIST)||                  13150000
*             ' IS NOT ALLOWED. NO TEXT IS GENERATED.');                13200000
*                            /*       INDICATE MACLIST IN DO-COMMENT.*/ 13250000
*     IVDOCMT = IVDOCMT||' '||MACLIST;                                  13300000
*   END;                     /* END - MACLIST SPECIFIED.             */ 13350000
*/********************************************************************/ 13400000
*/*                PROCESS AX KEYWORD                                */ 13450000
*/********************************************************************/ 13500000
* IF AX ¬= ''                /* AX SPECIFIED?                        */ 13550000
*   THEN DO;                 /* YES - ADD KEYWD TO COMMENT           */ 13600000
*     IVDOCMT = IVDOCMT||' AX'||AX;                                     13650000
*     IF AX(1) ¬= '' &       /* WAS ARGUMENT SPECIFIED?      @ZM50111*/ 13700000
*       ((IVLP1=0 & IVRP1=0)|/* AND NOT POINTER FORM         @ZM50111*/ 13750000
*       (IVLP1+1 <= IVRP1-1))/* AND NOT = '()' ?             @ZM50111*/ 13800000
*       THEN DO;             /* YES - AX(1) SPECIFIED                */ 13850000
*         IF NUMBER(AX) > 1 /* TOO MANY AX ARGS?                     */ 13900000
*           THEN DO;         /* YES - ISSUE AHA0033                  */ 13950000
*             ANS MSG('AHA0033 AX - '||AX||                             14000000
*                      ' - HAS TOO MANY ARGUMENTS. '||                  14050000
*                      'NO TEXT IS GENERATED.');                        14100000
*             IVRC = 12;     /* KILL TEXT GENERATION                 */ 14150000
*           END;             /* NUMBER(AX) > 1                       */ 14200000
*       END;                 /* AX(1) SPECIFIED                      */ 14250000
*       ELSE DO;             /* NO ARGUMENT-ISSUE AHA0013            */ 14300000
*         ANS MSG('AHA0013 AX(1) IS REQUIRED '||                        14350000
*                 'BUT IS NOT SPECIFIED. NO TEXT IS GENERATED.');       14400000
*         IVRC = 12;         /* KILL TEXT GENERATION                 */ 14450000
*       END;                 /* NO AX ARGUMENT                       */ 14500000
*   END;                     /* AX KEYWORD SPECIFIED                 */ 14550000
*   ELSE DO;                 /* AX NOT SPECIFIED                     */ 14600000
*     ANS MSG('AHA0013 AX IS REQUIRED FOR STANDARD FORM '||             14650000
*             'BUT IS NOT SPECIFIED. NO TEXT IS GENERATED.');           14700000
*     IVRC = 12;             /* KILL TEXT GENERATION                 */ 14750000
*   END;                     /* AX NOT SPECIFIED                     */ 14800000
*/********************************************************************/ 14850000
*/*                PROCESS PT KEYWORD                                */ 14900000
*/********************************************************************/ 14950000
* IF PT ¬= ''                /* PT SPECIFIED?                        */ 15000000
*   THEN DO;                 /* YES                                  */ 15050000
*                            /* ADD PT KYWD TO COMMENT               */ 15100000
*     IVDOCMT=IVDOCMT||' PT'||PT;                                       15150000
*     IF PT(1) ¬= ''         /* ARGUMENT SPECIFIED?                  */ 15200000
*       THEN DO;             /* YES                                  */ 15250000
*         IF PT(1)='YES' /* IS ARGUMENT 'YES'                        */ 15300000
*           THEN DO;         /* YES                          @D64XDUG*/ 15330001
*             ANS MSG('AHA0060 PT=YES IS NOT SUPPORTED IN VSE/ESA.');   15360000
*             IVRC=8;        /* PREVENT TEXT GENERATION      @D64XDUG*/ 15390001
*           END;             /*                              @D64XDUG*/ 15420001
*           ELSE DO;         /* ARGUMENT IS NOT 'YES'                */ 15450000
*             IF PT(1)¬='NO' /* ARGUMENT OTHER THAN 'NO'             */ 15500000
*               THEN DO;     /* YES - INVALID ARGUMENT               */ 15550000
*                 ANS MSG('AHA0043 PT MUST BE '||                       15600000
*                         '''YES'' OR ''NO''. '||                       15650000
*                         'NO TEXT IS GENERATED.');                     15700000
*                 IVRC=12;   /* PREVENT TEXT GENERATION              */ 15750000
*               END;         /* INVALID PT ARGUMENT                  */ 15800000
*           END;             /* ARGUMENT IS NOT 'YES'                */ 15850000
*           IF NUMBER(PT) > 1 /* TOO MANY PT ARGS?                   */ 15900000
*             THEN DO;                  /* YES -                     */ 15950000
*               ANS MSG('AHA0033 PT - '||PT||                           16000000
*                      ' - HAS TOO MANY ARGUMENTS. '||                  16050000
*                      'NO TEXT IS GENERATED.');                        16100000
*               IVRC = 12;   /* KILL TEXT GENERATION                 */ 16150000
*             END;           /* TOO MANY PT ARGS                     */ 16200000
*       END;                 /* ARGUMENT SPECIFIED                   */ 16250000
*       ELSE DO;             /* NO ARGUMENT                          */ 16300000
*         ANS MSG('AHA0043 PT MUST BE ''YES'' OR ''NO''. '||            16350000
*                         'NO TEXT IS GENERATED.');                     16400000
*         IVRC=12;           /* PREVENT TEXT GENERATION              */ 16450000
*       END;                 /* NO ARGUMENT                          */ 16500000
*   END;                     /* PT SPECIFIED                         */ 16550000
*/********************************************************************/ 16600000
*/*                PROCESS SSAR KEYWORD                              */ 16650000
*/********************************************************************/ 16700000
* IF SSAR ¬= ''              /* SSAR SPECIFIED?                      */ 16750000
*   THEN DO;                 /* YES                                  */ 16800000
*                            /* ADD SSAR KYWD TO COMMENT             */ 16850000
*     IVDOCMT=IVDOCMT||' SSAR'||SSAR;                                   16900000
*     IF SSAR(1) ¬= ''       /* ARGUMENT SPECIFIED?                  */ 16950000
*       THEN DO;             /* YES                                  */ 17000000
*         IF SSAR(1)='YES' /* IS ARGUMENT 'YES'                      */ 17050000
*           THEN             /* YES                                  */ 17100000
*             IVOPTS=IVOPTS+64; /* INDICATE 'SSAR' OPTION WAS SPEC'D */ 17150000
*           ELSE DO;         /* ARGUMENT IS NOT 'YES'                */ 17200000
*             IF SSAR(1)¬='NO' /* ARGUMENT OTHER THAN 'NO'           */ 17250000
*               THEN DO;     /* YES - INVALID ARGUMENT               */ 17300000
*                 ANS MSG('AHA0043 SSAR MUST BE '||                     17350000
*                         '''YES'' OR ''NO''. '||                       17400000
*                         'NO TEXT IS GENERATED.');                     17450000
*                 IVRC=12;   /* PREVENT TEXT GENERATION              */ 17500000
*               END;         /* INVALID SSAR ARGUMENT                */ 17550000
*           END;             /* ARGUMENT IS NOT 'YES'                */ 17600000
*           IF NUMBER(SSAR) > 1 /* TOO MANY SSAR ARGS?               */ 17650000
*             THEN DO;                  /* YES -                     */ 17700000
*               ANS MSG('AHA0033 SSAR - '||SSAR||                       17750000
*                      ' - HAS TOO MANY ARGUMENTS. '||                  17800000
*                      'NO TEXT IS GENERATED.');                        17850000
*               IVRC = 12;   /* KILL TEXT GENERATION                 */ 17900000
*             END;           /* TOO MANY SSAR ARGS                   */ 17950000
*       END;                 /* ARGUMENT SPECIFIED                   */ 18000000
*       ELSE DO;             /* NO ARGUMENT                          */ 18050000
*         ANS MSG('AHA0043 SSAR MUST BE ''YES'' OR ''NO''. '||          18100000
*                         'NO TEXT IS GENERATED.');                     18150000
*         IVRC=12;           /* PREVENT TEXT GENERATION              */ 18200000
*       END;                 /* NO ARGUMENT                          */ 18250000
*   END;                     /* SSAR SPECIFIED                       */ 18300000
*/********************************************************************/ 18350000
*/*                PROCESS 'RELATED' KEYWORD                         */ 18400000
*/********************************************************************/ 18450000
* IF RELATED ¬= ''           /* IF RELATED WAS SPECIFIED             */ 18500000
*   THEN                     /* ADD KEYWORD TO DO COMMENT            */ 18550000
*     IVDOCMT=IVDOCMT||IVBLANK||'RELATED'||RELATED;                     18600000
*/********************************************************************/ 18650000
*/*   INDICATE EPILOG IS TO PRODUCE STANDARD SAVEAREA MAPPING.       */ 18700000
*/********************************************************************/ 18750000
* IF INDEX(EHAMAPS,'SA@')=0   /* IF SA@ MAPPING NOT PREVIOUSLY       */ 18800000
*   THEN                      /* REQUESTED THEN INDICATE REQUEST     */ 18850000
*     EHAMAPS = EHAMAPS||'SA@,'; /* NOTE: COMMAS SEPERATE REQUESTS   */ 18900000
*/********************************************************************/ 18950000
*/*   INDICATE EPILOG IS TO PROVIDE ACCESS TO XM SERVICES.           */ 19000000
*/********************************************************************/ 19050000
* IF INDEX(EHAMAPS,'XM@')=0   /* IF XM SERVICES NOT PREVIOUSLY       */ 19100000
*   THEN                      /* REQUESTED THEN INDICATE REQUEST     */ 19150000
*     EHAMAPS = EHAMAPS||'XM@,'; /* NOTE: COMMAS SEPERATE REQUESTS   */ 19200000
*/********************************************************************/ 19250000
*/*                PUT OUT LABELS (IF ANY)                           */ 19300000
*/********************************************************************/ 19350000
* IF MACLABEL ¬= ''          /* ANY LABELS SPECIFIED?                */ 19400000
*   THEN                     /* YES - PUT THEM OUT.                  */ 19450000
*     ANS(MACLABEL) COL(MACLMAR);                                       19500000
*/********************************************************************/ 19550000
*/*     PUT OUT DO STATEMENT                                         */ 19600000
*/********************************************************************/ 19650000
* IVDOCMT=IVDOCMT||' MACDATE(80.122) '; /* ADD MACDATE TO COMMENT    */ 19700000
*                            /* GENERATE THE DO STATEMENT.           */ 19750000
* ANS('DO; '||COMMENT(IVDOCMT)) COL(MACCOL);                            19800000
*/********************************************************************/ 19850000
*/*                DETERMINE IF TEXT SHOULD BE PRODUCED              */ 19900000
*/********************************************************************/ 19950000
* IF IVRC > 4                /* ANY SEVERE ERRORS?                   */ 20000000
*   THEN                     /* YES -                                */ 20050000
*     GOTO LHA09000;         /*       EXIT - NO TEXT PRODUCED.       */ 20100000
*/********************************************************************/ 20150000
*/*                DEFINE REGISTER NAMES                             */ 20200000
*/********************************************************************/ 20250000
* IF INDEX(EHAREGS,'00F')=0  /* REGISTER 0 PREVIOUSLY DEFINED?       */ 20300000
*   THEN                     /* NO  -                                */ 20350000
*     EHAREGS=EHAREGS||'00F';/*       DEFINE IT.                     */ 20400000
* IF INDEX(EHAREGS,'01P')=0  /* REGISTER 1 PREVIOUSLY DEFINED?       */ 20450000
*   THEN                     /* NO  -                                */ 20500000
*     EHAREGS=EHAREGS||'01P';/*       DEFINE IT.                     */ 20550000
* IF INDEX(EHAREGS,'02P')=0  /* REGISTER 2 PREVIOUSLY DEFINED?       */ 20600000
*   THEN                     /* NO  -                                */ 20650000
*     EHAREGS=EHAREGS||'02P';/*       DEFINE IT.                     */ 20700000
* IF INDEX(EHAREGS,'12P')=0  /* REGISTER 12 PREVIOUSLY DEFINED?      */ 20750000
*   THEN                     /* NO  -                                */ 20800000
*     EHAREGS=EHAREGS||'12P';/*       DEFINE IT.                     */ 20850000
* IF INDEX(EHAREGS,'13P')=0  /* REGISTER 13 PREVIOUSLY DEFINED?      */ 20900000
*   THEN                     /* NO  -                                */ 20950000
*     EHAREGS=EHAREGS||'13P';/*       DEFINE IT.                     */ 21000000
* IF INDEX(EHAREGS,'14P')=0  /* REGISTER 14 PREVIOUSLY DEFINED?      */ 21050000
*   THEN                     /* NO  -                                */ 21100000
*     EHAREGS=EHAREGS||'14P';/*       DEFINE IT.                     */ 21150000
* IF INDEX(EHAREGS,'15F')=0  /* FIXED REG 15 PREVIOUSLY DEFINED?     */ 21200000
*   THEN                     /* NO  -                                */ 21250000
*     EHAREGS=EHAREGS||'15F';/*       DEFINE IT.                     */ 21300000
*/********************************************************************/ 21350000
*/*                GENERATE LINKAGE TO ATSET SERVICE                 */ 21400000
*/********************************************************************/ 21450000
* ANS('RESPECIFY')                                COL(IVINDNT2);        21500000
* ANS('(GPR00F,')                                 COL(IVINDNT2+1);      21550000
* ANS('GPR01P,')                                  COL(IVINDNT2+2);      21600000
* ANS('GPR02P,')                                  COL(IVINDNT2+2);      21650000
* ANS('GPR13P,')                                  COL(IVINDNT2+2);      21700000
* ANS('GPR14P,')                                  COL(IVINDNT2+2);      21750000
* ANS('GPR15F) RESTRICTED;')                      COL(IVINDNT2+2);      21800000
* ANS('STM(GPR14P,GPR12P,SA@G1412);')             COL(IVINDNT2);        21850000
* ANS(COMMENT(' SAVE CALLER''S REGS            ')) COL(CC);             21900000
* IF IVLP1 ¬= 1              /* IF NOT POINTER FORM ADDRESS  @ZMB0031*/ 21950000
*   THEN DO;                                                            22000000
*     ANS('GPR00F='||AX(1)||';')                  COL(IVINDNT2);        22050000
*     ANS(COMMENT(' AX VALUE TO PARM REG 0    ')) COL(CC);              22100000
*   END;                                                                22150000
*   ELSE DO;                                                            22200000
*     ANS('GPR00F='||AX(1,IVLP1+1:IVRP1-1)||';')  COL(IVINDNT2);        22250000
*     ANS(COMMENT(' AX VALUE TO PARM REG 0    ')) COL(CC);              22300000
*   END;                                                                22350000
* ANS('SLL(GPR00F,16);')                   COL(IVINDNT2); /* @ZMB0091*/ 22400000
* ANS(COMMENT(' ASSURE NO GARBAGE IS IN HIGH  ')) COL(CC);              22450000
* ANS('SRL(GPR00F,16);')                   COL(IVINDNT2); /* @ZMB0091*/ 22500000
* ANS(COMMENT(' ORDER HALF OF REGISTER ZERO.  ')) COL(CC);              22550000
* ANS('GPR15F='||CHAR(IVOPTS)||';')               COL(IVINDNT2);        22600000
* ANS(COMMENT(' SET WORK REG WITH OPTIONS BYTE')) COL(CC);              22650000
* ANS('SLL(GPR15F,16);')                          COL(IVINDNT2);        22700000
* ANS(COMMENT(' SHIFT OPTIONS BYTE INTO POS.  ')) COL(CC);              22750000
* ANS('GPR00F=(GPR00F|GPR15F);')                  COL(IVINDNT2);        22800000
* ANS(COMMENT(' OR TOGETHER INTO PARM REG 0   ')) COL(CC);              22850000
* ANS('GPR01P=0;')                                COL(IVINDNT2);        22900000
* ANS(COMMENT(' PARM REG 1 MUST BE ZERO       ')) COL(CC);              22950000
* ANS('ESAR(SA@GPR15);')                          COL(IVINDNT2);        23000000
* ANS(COMMENT('SAVE SASID                     ')) COL(CC);              23050000
* ANS('GPR02P=XM@(11);')                          COL(IVINDNT2);        23100000
* ANS(COMMENT(' GET LX/EX FOR ATSET FROM SFT  ')) COL(CC);              23150000
* ANS('GEN (DS 0H);') COL(IVINDNT2);  /* MAKE PL/AS USE SAVEAREA @P1A*/ 23200000
* ANS('PC(GPR02P);')                              COL(IVINDNT2);        23250000
* ANS(COMMENT(' PC TO ATSET SERVICE           ')) COL(CC);              23300000
* ANS('SSAR(SA@GPR15);')                          COL(IVINDNT2);        23350000
* ANS(COMMENT('RESTORE SASID                  ')) COL(CC);              23400000
* ANS('LM(GPR02P,GPR12P,SA@G0212);')              COL(IVINDNT2);        23450000
* ANS(COMMENT(' RESTORE REGS 2-12             ')) COL(CC);              23500000
* ANS('GPR14P=SA@GPR14;')                         COL(IVINDNT2);        23550000
* ANS(COMMENT(' RESTORE REG 14                ')) COL(CC);              23600000
* ANS('RESPECIFY')                                COL(IVINDNT2);        23650000
* ANS('(GPR00F,')                                 COL(IVINDNT2+1);      23700000
* ANS('GPR01P,')                                  COL(IVINDNT2+2);      23750000
* ANS('GPR02P,')                                  COL(IVINDNT2+2);      23800000
* ANS('GPR13P,')                                  COL(IVINDNT2+2);      23850000
* ANS('GPR14P,')                                  COL(IVINDNT2+2);      23900000
* ANS('GPR15F) UNRESTRICTED;')                    COL(IVINDNT2+2);      23950000
*/********************************************************************/ 24000000
*/*                CLOSE OUT DO GROUP AND RETURN                     */ 24050000
*/********************************************************************/ 24100000
*LHA09000:                   /* CLOSE OUT DO GROUP.                  */ 24150000
* ANS('END; ') COL(MACCOL);                                             24200000
* RETURN CODE(IVRC);                                                    24250000
*/********************************************************************/ 24300000
*/*                END OF ATSET MACRO                                */ 24350000
*/********************************************************************/ 24400000
* % END ATSET;                                                          24450000
