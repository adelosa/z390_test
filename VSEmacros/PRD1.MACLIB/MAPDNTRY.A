*%GOTO MPDIR ;   /*PLS COMMENT FOR ASSEMBLER PART OF THE MACRO @D36JDVB 00050000
         MACRO                                                          00100000
&NAME    MAPDNTRY &DE=,&DSECT=YES,&PNAME=                               00133361
         GBLB  &IJBSGEN                                        @D52VDMZ 00166652
         GBLC  &LABEL                                          @D61VDMZ 00169661
         GBLC  &IRNAME                                         @D61VDMZ 00172661
         GBLC  &IRCOPY                                         @D61VDMZ 00175661
         GBLC  &IRIDVSE                                        @D61VDMZ 00178661
         GBLC  &IRN                                            @D61VDMZ 00181661
         GBLC  &PN                                             @D61VDMZ 00184661
         LCLA  &DIRL                                           @D61DDMZ 00187661
         LCLA  &DIRH                                           @D61DDMZ 00190661
         LCLC  &CS                                             @D61DDMZ 00193661
         LCLC  &BL                                             @D61DDMZ 00196661
*--------------------------------------------------------------@D368DVB 00200000
.*                                                             @D368DVB 00250000
.*       IBM DISK OPERATING SYSTEM                             @D368DVB 00300000
*        SUPERVISOR - MAPDNTRY 5686-066-06                     @D61NDMZ 00343761
         AGO   .ZZZ                                            @D52VDMZ 00387552
*        LICENSED MATERIALS - PROPERTY OF IBM                  @D52VDMZ 00425052
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"           @D31UDMZ 00450000
*        5686-066 (C) COPYRIGHT IBM CORP. 1979, 1995           @D61NDMZ 00500061
*        SEE COPYRIGHT INSTRUCTIONS                            @D52VDMZ 00550052
.ZZZ     ANOP                                                  @D52VDMZ 00600052
.*                                                             @D368DVB 00700000
*--------------------------------------------------------------@D368DVB 00750000
.*                                                                      00850000
.*01* MODULE NAME = MAPDNTRY                                            00900000
.*                                                                      00950000
.*01* MODULE TYPE = DIRECTORY ENTRY MAP MACRO                           01000000
.*                                                                      01050000
.*01* DESCRIPTIVE NAME = MAP OF A CORE IMAGE DIRECTORY ENTRY            01100000
.*                                                                      01150000
.*01* CHANGE ACTIVITY = AS FOLLOWS                                      01200000
.*            NEW MACRO -- AF REL 1.2                                   01250000
.*              CONCATENATED LIBRARIES                         @D36JDVB 01300000
.*            AF 1.3 SLMAP                                     @D37CDVB 01350000
.*            AF 2.1 NEW LIBRARIAN                             @D14CDVB 01400000
.*            AF 5.2.0 31 BIT SUPPORT                          @D52VDMZ 01425052
.*            AF 6.1.0 GENERATE INLINE CODE                    @D61NDMZ 01437561
.*                                                                      01450000
.* A000000-999999                                                       01500000
*------- MAPPING OF DIRECTORY ENTRY --------------------------*         01600052
         SPACE 1                                               @D369DVB 01650000
         AIF   (T'&NAME EQ 'O').NONAME                         @D61DDMZ 01652361
&LABEL   SETC  '&NAME'                                         @D61DDMZ 01654661
         AGO   .YNAME                                          @D61DDMZ 01656961
.NONAME  ANOP                                                  @D61DDMZ 01659261
&LABEL   SETC  'DIRECTRY'                                      @D61DDMZ 01661561
.YNAME   ANOP                                                  @D61DDMZ 01663861
&PN      SETC  ''                                              @D61DDMZ 01666161
         AIF   (T'&PNAME EQ 'O').NOPHAS                        @D61DDMZ 01668461
&PN      SETC  '&PNAME'                                        @D61DDMZ 01670761
.NOPHAS  ANOP                                                  @D61DDMZ 01673061
.* CHECK DE OPERAND                                            @D61DDMZ 01675361
&DIRL    SETA  0                                               @D61DDMZ 01677661
&DIRH    SETA  0                                               @D61DDMZ 01679961
         AIF   (T'&DE  EQ 'O').EDECHK                          @D61DDMZ 01682261
&DIRL    SETA  28                                              @D61DDMZ 01684561
&DIRH    SETA  (&DIRL)/2                                       @D61DDMZ 01686861
         AIF   ('&DE' EQ 'VSE').EDECHK                         @D61DDMZ 01689161
&DIRL    SETA  26                                              @D61DDMZ 01691461
&DIRH    SETA  (&DIRL)/2                                       @D61DDMZ 01693761
         AIF   ('&DE' EQ 'YES').EDECHK                         @D61DDMZ 01696061
         MNOTE 8,'INVALID DE OPERAND SPECIFIED. MACRO IGNORED'          01698361
         MEXIT                                                 @D61DDMZ 01700661
.EDECHK  ANOP                                                  @D61DDMZ 01702961
         AIF   ('&DSECT' EQ 'YES').DSECT  GENERATE DSECT       @D61DDMZ 01705261
         AIF   ('&DSECT' NE 'NO').MNOT1   MNOTE                @D61DDMZ 01707561
         AIF   (T'&DE  NE 'O').INLINE                          @D61DDMZ 01709861
.MNOT2   ANOP                                                  @D61DDMZ 01712161
         MNOTE 8,'DE=VSE OR DE=YES IS REQUIRED FOR DSECT=NO'            01714461
         MEXIT                                                 @D61DDMZ 01716761
.MNOT1   ANOP                                                  @D61DDMZ 01719061
         MNOTE 8,'INVALID DSECT OPERAND SPECIFIED, MACRO IGNORED'       01721361
         MEXIT                                                 @D61DDMZ 01723661
.DSECT   ANOP                                                  @D61DDMZ 01725961
&CS      SETC  'S'                                             @D61DDMZ 01728261
&BL      SETC  ' '                                             @D61DDMZ 01730561
&IRNAME  SETC  'DIRNAME'                                       @D61DDMZ 01732861
&IRCOPY  SETC  'DIRCOPY'                                       @D61DDMZ 01735161
&IRIDVSE SETC  'DIRIDVSE'                                      @D61DDMZ 01737461
&IRN     SETC  'DIRN'                                          @D61DDMZ 01739761
&LABEL   DSECT .                  DIRECTORY ENTRY              @D36JDVB 01742061
         AGO   .INL5                                           @D61DDMZ 01744361
.INLINE  ANOP                                                  @D61DDMZ 01746661
&CS      SETC  'C'                                             @D61DDMZ 01748961
&BL      SETC  ''                                              @D61DDMZ 01751261
&IRNAME  SETC  ''                                              @D61DDMZ 01753561
&IRCOPY  SETC  ''                                              @D61DDMZ 01755861
&IRIDVSE SETC  ''                                              @D61DDMZ 01758161
&IRN     SETC  ''                                              @D61DDMZ 01760461
&LABEL   DS    0F                                              @D61DDMZ 01762761
.INL5    ANOP                                                  @D61DDMZ 01765061
&IRNAME  D&CS  CL8&BL.'&PN'       DIRECTORY NAME FIELD         @D61DDMZ 01767361
         AIF   (T'&DE  EQ 'O').DIRSK01                         @D52VDMZ 01771452
         AIF   ('&DE' EQ 'VSE').DIRSK05                        @D52VDMZ 01792852
         AIF   ('&DE' EQ 'YES').DIRSK03                        @D52VDMZ 01814252
.DIRSK01 ANOP                                                  @D52VDMZ 01878452
&IRCOPY  D&CS  0XL3               ADDRESS OF DIR_COPY          @D14CDVB 01893361
         AGO   .DIRSK05                                        @D52VDMZ 01908352
.DIRSK03 ANOP                                                  @D52VDMZ 01916652
&IRCOPY  D&CS  XL3&BL.'000000'    ADDRESS OF DIR_COPY          @D14CDVB 01924961
         AGO   .DIRSK10                                        @D52VDMZ 01933252
.DIRSK05 ANOP                                                  @D52VDMZ 01941552
&IRIDVSE D&CS  XL3&BL.'FFFFFF'    VSE_ID                       @D14CDVB 01961061
         AIF   ('&DSECT' EQ 'NO').DIRSK10                      @D61DDMZ 01980561
VSEID    EQU   X'FFFFFF'                                       @D14CDVB 02000000
.DIRSK10 ANOP                                                  @D52VDMZ 02025052
&IRN     D&CS  AL1&BL.(&DIRH)     NUMBER OF FOLLOWING HALFWORDS@D61NDMZ 02031961
         AIF   ('&DSECT' EQ 'YES').DIRDS                       @D61DDMZ 02038861
         D&CS  XL&DIRL.'00'                                    @D61DDMZ 02045761
         AGO   .DIRSK99                                        @D61DDMZ 02052661
.DIRDS   ANOP                                                  @D61DDMZ 02059561
         AIF   ('&DE' EQ 'VSE').DIRSK15                        @D52VDMZ 02066652
         AIF   ('&DE' EQ 'YES').DIRSK18                        @D52VDMZ 02083252
DIRLMBR  D&CS  0XL4&BL.'00000000' LENGTH OF LIBRARAY MEMBER    @D14CDVB 02095761
         AGO   .DIRSK18                                        @D52VDMZ 02108352
.DIRSK15 ANOP                                                  @D52VDMZ 02116652
DIRLMBR  D&CS  XL4&BL.'00000000'  LENGTH OF LIBRARAY MEMBER    @D14CDVB 02124961
         AGO   .DIRSK20                                        @D52VDMZ 02133252
.DIRSK18 ANOP                                                  @D52VDMZ 02141552
DIRTT    D&CS  XL2&BL.'0000'      NUMBER OF TEXT BLOCKS        @D36JDVB 02174661
DIRLL    D&CS  XL2&BL.'0000'      NO. OF TEXT BYTES, LAST BLOCK@D36JDVB 02207761
.DIRSK20 ANOP                                                  @D52VDMZ 02240952
DIRC     D&CS  XL1&BL.'00'        FLAGS (PHASE DESCRIPTION)    @D36JDVB 02281861
.*-------------------------------------------------------------@D369DVB 02322752
SELFREL  EQU   X'80'              PHASE IS SELFRELOCATABLE     @D36JDVB 02363652
RELPHASE EQU   X'40'              PHASE IS RELOCATABLE         @D36JDVB 02404552
SVAELIG  EQU   X'20'              PHASE IS SVA-ELIGIBLE        @D36JDVB 02445452
SVAPHASE EQU   X'10'              PHASE IS LOCATED IN THE SVA  @D36JDVB 02486352
PCIL     EQU   X'08'              NON-SYSLIB PHASE             @D36JDVB 02527252
NOTFND   EQU   X'04'              PHASE NOT FOUND              @D36JDVB 02568152
         AIF   (('&DE' EQ 'VSE') OR ('&DE' EQ 'YES')).DIRSK22  @D52VDMZ 02609052
DIRDELET EQU   X'04'                                           @D36JDOH 02650000
.DIRSK22 ANOP                                                  @D52VDMZ 02675052
ACTIVE   EQU   X'02'              ENTRY ACTIVE                 @D36JDVB 02700052
         AIF   (('&DE' EQ 'VSE') OR ('&DE' EQ 'YES')).DIRSK23  @D52VDMZ 02725052
SLMAPID  EQU   X'01'                                           @D37CDVB 02750000
.DIRSK23 ANOP                                                  @D52VDMZ 02783352
.*-------------------------------------------------------------@D369DVB 02816652
DIRSWIT  D&CS  XL1&BL.'00'        MORE FLAGS / STOW HALFBYTE   @D61NDMZ 02849961
         AIF   (('&DE' EQ 'VSE') OR ('&DE' EQ 'YES')).DIRSK24  @D52VDMZ 02883352
.*-------------------------------------------------------------@D369DVB 02916652
DIRJCLM  EQU   X'80'              MOVE MODE FLAG  FROM SET SDL @D36JDOH 02950000
DIRJCLS  EQU   X'40'              SVA ELIG. FLAG  FROM SET SDL @D36JDOH 03000000
.DIRSK24 ANOP                                                  @D52VDMZ 03004152
DIRRMOD  EQU   X'20'              1: RMODE=ANY, 0:RMODE=24     @D52VDMZ 03008352
DIRAM31  EQU   X'10'              1: AMODE=31 OR AMODE=ANY     @D52VDMZ 03016652
DIRAM24  EQU   X'08'              1: AMODE=24 OR AMODE=ANY     @D52VDMZ 03024952
*                                 11: AMODE=ANY, 10: AMODE=31  @D52VDMZ 03033252
*                                 01 OR 00: AMODE=24           @D52VDMZ 03041552
*--------------------------------------------------------------@D369DVB 03050000
         AIF   ('&DE' EQ 'VSE').DIRSK30                        @D52VDMZ 03075052
DIRPPP   D&CS  XL3&BL.'000000'    LOAD POINT AT LINKEDIT TIME  @D36JDVB 03108361
DIREEE   D&CS  XL3&BL.'000000'    ENTRY POINT AT LINKEDIT TIME @D36JDVB 03141661
         AIF   (NOT &IJBSGEN).DIRSK35                          @D52VDMZ 03175052
DIRRR    D&CS  XL2&BL.'0000'      *** NOT SUPPORTED ***        @D14CDVB 03203361
DIRR     D&CS  XL1&BL.'00'        *** NOT SUPPORTED ***        @D14CDVB 03231661
         AGO   .DIRSK36                                        @D52VDMZ 03260052
.DIRSK35 ANOP                                                  @D52VDMZ 03270052
         D&CS  XL3&BL.'000000'    RESERVED                     @D52VDMZ 03280061
.DIRSK36 ANOP                                                  @D52VDMZ 03290052
DIRAAA   D&CS  XL3&BL.'000000'    PARTITION START AT LINKEDIT  @D36JDVB 03335061
DIRVEE   D&CS  XL4&BL.'00000000'  ENTRY POINT OF PHASE IN SVA  @D36JDVB 03380061
         AIF   (NOT &IJBSGEN).DIRSK50                          @D52VDMZ 03425052
DIRPLIB  D&CS  XL4&BL.'00000000'  PTR TO FTTAB ENTRY OF LIBRARY        *03462561
                                  WHERE DIR. ENTRY BELONGS TO  @D36JDVB 03500000
         AGO   .DIRSK55                                        @D52VDMZ 03510052
.DIRSK50 ANOP                                                  @D52VDMZ 03520052
         D&CS  XL4'00000000'      RESERVED                     @D52VDMZ 03530061
.DIRSK55 ANOP                                                  @D52VDMZ 03540052
DIRMAPXD EQU   *                                               @D36JDVB 03550000
DIRMAPLN EQU   DIRMAPXD-DIRNAME   LENGTH OF MAP                @D36JDVB 03600000
         AIF   ('&DE' EQ 'YES').DIRSK99                        @D52VDMZ 03625052
         ORG   DIRC                                            @D14CDVB 03650000
DIRFLAGS D&CS  XL4&BL.'00000000'  FLAGS / RESERVED             @D14CDVB 03680061
         AGO   .DIRSK60                                        @D52VDMZ 03710052
.DIRSK30 ANOP                                                  @D52VDMZ 03720052
         D&CS  XL2&BL.'0000'      RESERVED                     @D52VDMZ 03730061
.DIRSK60 ANOP                                                  @D52VDMZ 03740052
DIRACOPY D&CS  A&BL.(0)           ADDRESS TO DIR_COPY          @D14CDVB 03782061
DIRALPT  D&CS  A&BL.(0)           LOAD POINT AT LINEDIT TIMET  @D14CDVB 03824061
DIRAEPT  D&CS  A&BL.(0)           ENTRY POINT AT LINKEDIT TIME @D14CDVB 03866061
DIRAPART D&CS  A&BL.(0)           PARTITION START AT LINKEDIT ...      *03908061
                                  ...TIME                      @D14CDVB 03950000
DIRASVA  D&CS  A&BL.(0)           ENTRY POINT OF PHASE IN SVA  @D14CDVB 04000061
DIRLNVSE EQU   *-DIRNAME                                       @D14CDVB 04050000
.DIRSK99 ANOP                                                  @D52VDMZ 04075052
         MEND                                                  @D36JDVB 04100000
* END OF PLS COMMENT                                         @D36JDVB*/ 04150000
*%MPDIR:;                                                  /*@D36JDVB*/ 04200000
* /*---------------------------------------------------------@D36JDVB*/ 04250000
* /*     IBM DISK OPERATING SYSTEM                           @D36JDVB*/ 04300000
* /*     SUPERVISOR - MAPDNTRY 5686-032-06                   @D52VDMZ*/ 04350052
* /*     LICENSED MATERIALS - PROPERTY OF IBM                @D52VDMZ*/ 04400052
* /*     5686-032 (C) COPYRIGHT IBM CORP 1979, 1993          @D52VDMZ*/ 04450052
* /*---------------------------------------------------------@D36JDVB*/ 04550000
*                                                          /*@D36JDOH*/ 04600000
* /**********************************************************@D36JDOH*/ 04650000
* /* U S E R  F O R M A T  O F  C I L  D I R E C T O R Y  E N T R Y  */ 04700000
* /**********************************************************@D36JDOH*/ 04750000
* DECLARE                                                  /*@D36JDOH*/ 04800000
*  1 DIRENTRY    BASED  BDY(WORD),                         /*@D21ZDVB*/ 04850000
*    2 DIRNAME CHAR(8),    /*PHASE NAME.                     @D36JDOH*/ 04900000
* /*---------------------------------------------------------@D36JDOH*/ 04950000
*    2 DIRCOPY BIT(24),    /*                                @D14CDVB*/ 05000000
*      3 DIRIDVSE BIT(24), /*                                @D14CDVB*/ 05050000
*    2 DIRN BIT(8),        /* INCORE DIRECTORIES:            @D14CDVB*/ 05100000
*                    /* # OF HALFWORDS FOLLOWING THESE BYTES @D36JDOH*/ 05150000
* /*---------------------------------------------------------@D36JDOH*/ 05200000
*    2 DIRPHINF CHAR(28),  /* PHASE INFORMATION              @D14LDGK*/ 05250000
*      3 DIRLMBR BIT(32),  /* LENGTH OF MEMBER IN NLIB       @D14CDVB*/ 05300000
*        4 DIRTT BIT(16),  /* FBA: NR. OF TEXT RECORDS       @D14CDVB   05350000
*                                  (<1024 BYTES>),           @D14CDVB   05400000
*                               CKD: NR. OF TEXT BLOCKS.     @D14CDVB*/ 05450000
*        4 DIRLL BIT(16),  /* NR. OF TEXT BYTES IN           @D36JDOH   05500000
*                                 LAST TEXT BLOCK.           @D36JDOH*/ 05550000
* /*---------------------------------------------------------@D36JDOH*/ 05600000
*      3 DIRC BIT(8),        /* PHASE TYPE SWITCH.           @D14CDVB*/ 05650000
*        4 DIRBSR BIT(1),    /* SELF RELOCATING PHASE.       @D14CDVB*/ 05700000
*        4 DIRBRL BIT(1),    /* RELOCATING PHASE.            @D14CDVB*/ 05750000
*        4 DIRBSE BIT(1),    /* SVA ELIGIBLE PHASE.          @D14CDVB*/ 05800000
*        4 DIRBSV BIT(1),    /* PHASE IN SVA.                @D14CDVB*/ 05850000
*        4 DIRBPC BIT(1),    /* PCIL FLAG FOR INCORE DIR     @D14CDVB*/ 05900000
*        4 DIRBNF BIT(1),    /* NOT FOUND FLAG   E DIR.)  .  @D14CDVB*/ 05950000
*          5 DIRDELET BIT(1),  /* DELETED ENTRY IN DIR.      @D14CDVB*/ 06000000
*        4 DIRACT BIT(1),    /* ENTRY IS ACTIVE (RE DIR.).   @D14CDVB*/ 06050000
*        4 DIRSLMP BIT(1),   /* SLMAP AVAIE                  @D37CDVB*/ 06100000
*      3   DIRSWIT BIT(8),   /* SWITCH BYT                   @D21ZDVB*/ 06150000
*        4 DIRJCLM BIT(1),   /* SET SDL: MMODE PHASE.        @D21ZDVB*/ 06200000
*        4 DIRJCLS BIT(1),   /* SET SDL: SLIGIBLE PHASE.     @D21ZDVB*/ 06250000
*        4 DIRRMOD BIT(1),   /* 1:RMODE=ANY, 0:RMODE=24      @D52VDMZ*/ 06270052
*        4 DIRAM31 BIT(1),   /* 0:AMODE=24, 1:AMODE=31 OR ANY@D52VDMZ*/ 06290052
*        4 DIRAM24 BIT(1),   /* 1:AMODE=24 OR AMODE=ANY      @D52VDMZ   06310052
*                               11:AMODE=ANY,10:AM=31,00 OR 01 AM=24 */ 06330052
*        4 DIRMARK BIT(1),   /* INTERNALLYD .                @D21ZDVB*/ 06350000
*        4 DIR3M33I BIT(1),  /* INTERNALLYD.                 @D21ZDVB*/ 06400000
*        4 DIRBMSG BIT(1),   /* INTERNALLYD.                 @D21ZDVB*/ 06450000
*      3   DIRPPP BIT(24),   /* LOAD POINT AT LNKEDT TIME.   @D21ZDVB*/ 06500000
*      3   DIREEE BIT(24),   /* ENTRY POINT AT LNKEDT TIME   @D21ZDVB*/ 06550000
*      3   *    BIT  (24),                                 /*@D21ZDVB*/ 06600000
*      3   DIRAAA BIT(24),   /* PARTITION START AT LNKEDT    @D21ZDVB*/ 06650000
*      3   DIRVEE BIT(32),   /* ENTRY POINT IN SVA           @D21ZDVB*/ 06700000
*      3   DIRPLIB BIT(32),  /* ADDRESS OF LIBRARY RESOURCE  @D21ZDVB*/ 06750000
*      3   *      BIT(16);                                 /*@D21ZDVB*/ 06800000
* /*---------------------------------------------------------@D14CDVB*/ 06850000
* DECLARE                                                  /*@D21ZDVB*/ 06900000
*  1 DIREXTN CHAR(24) DEF(DIRC) BDY(WORD),                 /*@D21ZDVB*/ 06950000
*      3  * BIT(32),                                       /*@D21ZDVB*/ 07000000
*      3  DIRACOPY PTR(31), /* ADDR OF INTGR INFO            @D21ZDVB*/ 07050000
*      3  DIRALPT PTR(31),  /* LOAD  POINT AT LINK TIME      @D21ZDVB*/ 07100000
*      3  DIRAEPT PTR(31),  /* ENTRY POINT AT LINK TIME      @D21ZDVB*/ 07150000
*      3  DIRAPART PTR(31),/* PARTITION START AT LINKTIME    @D21ZDVB*/ 07200000
*      3  DIRASVA PTR(31); /* ENTRY POINT OF PHASE IN SVA    @D21ZDVB*/ 07250000
* /*---------------------------------------------------------@D14LDGK*/ 07300000
* DECLARE                                                  /*@D14LDGK*/ 07350000
*    DIRLEN  BIN(15) BASED; /* LIST LENGTH                   @D14LDGK*/ 07400000
