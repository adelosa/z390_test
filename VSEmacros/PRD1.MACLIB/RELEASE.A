         MACRO                                                          00050000
&NAME    RELEASE   &OP1,&OP2                                            00100000
         LCLA  &I,&N                                                    00150000
         LCLC  &O(16)                                                   00200000
.*                                                                  BSW 00250000
         ACTR  1000                                                     00300000
.*                                                                  BSW 00350000
.*                                                                      00400000
         AGO   .ZZZ                                                     00450000
*********************************************************************** 00500000
*                                                                     * 00550000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                  * 00600000
*        5686-007 (C) COPYRIGHT IBM CORP. 1979,1988                   * 00650000
*        ALL RIGHTS RESERVED                                          * 00700000
*        LICENSED MATERIALS - PROGRAM PROPERTY OF IBM                 * 00750000
.*       SEE COPYRIGHT INSTRUCTIONS, G120-2083                        * 00800000
*                                                                     * 00850000
*********************************************************************** 00900000
.ZZZ ANOP                                                               00950000
.*                                                                  BSW 01000000
         AIF   (T'&OP1 NE 'O').CHECK                           @D36SDEM 01050000
         MNOTE 8,'OPERAND MISSING, GENERATION SUPPRESSED'               01100000
         MEXIT                                                 @D36SDEM 01150000
.*------ C H E C K   O P E R A N D S ---------------------------------- 01200000
.CHECK   ANOP                                                           01250000
         AIF   (N'&OP1 LE 16).L00   MORE THAN 16, ISSUE MNOTE           01300000
         MNOTE 8,'MORE THAN 16 OPERANDS IN SUBLIST, GENERATION SUPPRESSX01350000
               ED'                                             @D36SDEM 01400000
         MEXIT                                                 @D36SDEM 01450000
.L00     ANOP                                                           01500000
&N       SETA  0      &N = INDICATE AT LEAST ONE VALID ENTRY   @D369DEM 01550000
&I       SETA  1      &I = LOOP CONTROL                        @D369DEM 01600000
.L10     ANOP                                                           01650000
         AIF   (T'&OP1(&I) EQ 'O').L45          EMPTY SUBOPERAND    BSW 01700000
         AIF   (K'&OP1(&I) NE 6).L30            CHK LEN OF SUBOPRND BSW 01750000
         AIF   ('&OP1(&I)'(1,3) NE 'SYS').L30   1ST CHARS = SYS ?   BSW 01800000
&O(&I)   SETC  '&OP1(&I)'(4,3)                  YES            @D36SDEM 01850000
         AIF   ('&O(&I)' LT '000' OR '&O(&I)' GT '254').L30    @DA31417 01900000
         AIF   ('&O(&I)'(2,1) LT '0' OR '&O(&I)'(2,1) GT '9').L30       01950000
         AIF   ('&O(&I)'(3,1) GE '0' AND '&O(&I)'(3,1) LE '9').L40      02000000
.L30     MNOTE 3,'&OP1(&I) NOT A VALID PROGRAMMER UNIT NAME, SUBPARAMETC02050000
               ER IGNORED'                                              02100000
&O(&I)   SETC  ''                      NO VALID PARAMETER      @D36SDEM 02150000
         AGO   .L45                                                 BSW 02200000
.L40     ANOP                                                       BSW 02250000
&N       SETA  1                       INDIC.:AT LEAST ONE VALID PAR.   02300000
.L45     ANOP                                                       BSW 02350000
&I       SETA  &I+1                    UPDATE SUBLIST POINTER       BSW 02400000
         AIF   (&I LE 16).L10          NEXT SUBOPERAND, IF ANY      BSW 02450000
.*------ G E N E R A T E   C O D E ---------------------------------BSW 02500000
         AIF   (&N NE 0).L70           IF ALL PARM INVALID, MNOTE   BSW 02550000
         MNOTE 5,'ALL PARAMETERS INVALID, GENERATION SUPPRESSED'        02600000
         MEXIT                                                          02650000
.L70     AIF   (T'&OP2 EQ 'O').L80                                  BSW 02700000
.*       - FOR 'RELEASE (DEV1,DEV2,...),SAVE' MACRO CALL            BSW 02750000
&NAME    STM   0,1,&OP2                SAVE REGS 0 AND 1            BSW 02800000
         LA    1,=C'$$BRELSE'          POINT TO RELEASE PHASE NAME  BSW 02850000
         BAL   0,IJBZ&SYSNDX           BRANCH TO FETCH AND SKIP TBL BSW 02900000
         DC    C'&O(1)&O(2)&O(3)&O(4)&O(5)&O(6)&O(7)&O(8)&O(9)&O(10)&O(*02950000
               11)&O(12)&O(13)&O(14)&O(15)&O(16)'              @DM06694 03000000
IJBZ&SYSNDX SVC   2                    FETCH $$BRELSE               BSW 03050000
         LM    0,1,&OP2                RESTORE REGS 0 AND 1         BSW 03100000
         MEXIT                                                          03150000
.*       - FOR 'RELEASE (DEV1,DEV2,...)' MACRO CALL                 BSW 03200000
.L80     ANOP                          NO SAVE AREA REQUESTED       BSW 03250000
&NAME    LA    1,=C'$$BRELSE'          POINT TO RELEASE PHASE NAME  BSW 03300000
         BAL   0,IJBZ&SYSNDX           BRANCH TO FETCH AND SKIP TBL BSW 03350000
         DC    C'&O(1)&O(2)&O(3)&O(4)&O(5)&O(6)&O(7)&O(8)&O(9)&O(10)&O(*03400000
               11)&O(12)&O(13)&O(14)&O(15)&O(16)'              @DM06694 03450000
IJBZ&SYSNDX SVC   2                    FETCH $$BRELSE               BSW 03500000
         MEND                                                       BSW 03550000
