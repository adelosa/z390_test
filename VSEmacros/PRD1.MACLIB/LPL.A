         MACRO                                                          00050000
&NAME    LPL   &AREA=,&AREALEN=,&LABLEN=,&FILENAM=,&GROUP=,&STORE=      00100000
.*                                                                      00150000
         AGO   .ZZZ                                            @D51IDHB 00200000
*        LICENSED MATERIALS - PROPERTY OF IBM                  @D51IDHB 00250000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"           @D51IDHB 00300000
*        5686-066 (C) COPYRIGHT IBM CORP. 1977, 1995           @D61ZDWS 00350061
*        SEE COPYRIGHT INSTRUCTIONS                            @D51IDHB 00400000
.ZZZ     ANOP                                                  @D51IDHB 00450000
.*                                                                      00500000
.* ******************************************************************** 00550000
.* MACRO NAME: LPL (DOS/VS)                                             00600000
.*                                                                      00650000
.* DESCRIPTIVE NAME: CREATE A LABEL PARAMETER LIST                      00700000
.*                   AS INTERFACE INFO. FOR SYMBOLIC LABEL ACCESS       00750000
.*                                                                      00800000
.* STATUS: RELEASE 35, RELEASE 1.0                                      00850000
.*                                                                      00900000
.* FUNCTION:                                                            00950000
.*     THIS MACRO CAN BE USED TO DEFINE THE FIELDS OF A                 01000000
.*     LABEL PARAMETER LIST (LPL). THE LPL CONTAINS ALL INFORMATION     01050000
.*     NECESSARY TO EXECUTE ONE OF THE SLA (SYMOLIC LABEL ACCESS)       01100000
.*     FUNCTIONS.                                                       01150000
.*                                                                      01200000
.*     IF ONE OF THE PARAMETERS IS NOT SPECIFIED, THE CORRESPONDING     01250000
.*     FIELD IS INITIALISED TO ZERO OR TO BLANKS. IF THE PARAMETER      01300000
.*     IS SPECIFIED, THE FIELD IS INTIALIZED TO THE VALUE SPECIFIED,    01350000
.*     RESPECTIVELY TO A KEY VALUE, REPRESENTING THE ORIGINAL VALUE.    01400000
.*                                                                      01450000
.* DEPENDENCIES: NONE                                                   01500000
.*                                                                      01550000
.* RESTRICTIONS: NONE                                                   01600000
.*                                                                      01650000
.* PROCESSOR: ASSEMBLER                                                 01700000
.*                                                                      01750000
.* MODULE SIZE: NO SIZE LIMITATIONS                                     01800000
.*                                                                      01850000
.* ATTRIBUTES: REUSABLE                                                 01900000
.*                                                                      01950000
.* REGISTER CONVENTIONS: NONE                                           02000000
.*                                                                      02050000
.* INPUT:                                                               02100000
.* <NAME>  LPL  <AREA = ADDRESS>                                        02150000
.*              <AREALEN = INTEGER>                                     02200000
.*              <LABLEN = INTEGER>                                      02250000
.*              <FILENAM = CHARACTERS>                                  02300000
.*              <GROUP = PARTITION / PN / SYSTEM / CLASS >     @D51IDHB 02350000
.*              <STORE = TEMP /PERM /FREE /DEL /CLASS /CLDEL > @D51IDHB 02400000
.*                                                                      02450000
.* PARAMETER DESCRIPTION                                                02500000
.*                                                                      02550000
.* <AREA = ADDRESS>                                                     02600000
.*     SPECIFIES THE NAME OF THE BUFFER FROM WHICH THE SLA              02650000
.*     IS TO FETCH A LABEL OR WHERE THE SLA SHOULD STORE A LABEL,       02700000
.*     DEPENDING FROM THE SLA MACRO SPECIFIED.                          02750000
.*                                                                      02800000
.* <AREALEN = INTEGER>                                                  02850000
.*     A NUMBER FROM 1 TO 4 DIGITS SPECIFYING THE LENGTH OF THE BUFFER  02900000
.*     DESCRIBED ABOVE                                                  02950000
.*                                                                      03000000
.* <LABLEN = INTEGER>                                                   03050000
.*     A NUMBER FROM 1 TO 4 DIGITS SPECIFYING THE LENGTH OF THE LABEL   03100000
.*     INFORMATION.                                                     03150000
.*                                                                      03200000
.* <FILENAM = CHARACTERS>                                               03250000
.*     SPECIFIES THE FILE NAME (1 TO 8 CHARACTERS) WHICH IS CONTAINED   03300000
.*     IN THE LABEL THAT IS TO BE FETCHED OR STORED.                    03350000
.*                                                                      03400000
.* <GROUP = PARTITION / PN / SYSTEM / CLASS >                  @D51IDHB 03450000
.*     PARTITION SPECIFIES THAT THE MACRO IS TO WORK ON PARTITION       03500000
.*               SPECIFIC LABELS. THE PARTITION IS ALWAYS THE ONE       03550000
.*               UNDER WHICH THE MACRO IS EXECUTED.                     03600000
.*     PN        SPECIFIES THE PARTITION NUMBER WHEN IT SHOULD BE       03650000
.*               DIFFERENT FROM THE ONE IN WHICH THE MACRO IS EXECUTED  03700000
.*               (LOCGRPL,CLRGRPL AND GETNXGL MACROS).         @D36HDWS 03750000
.*     SYSTEM    THE MACRO IS TO HANDLE SYSTEM LABELS.         @D51IDHB 03800000
.*     CLASS     THE MACRO IS TO HANDLE CLASS LABELS.          @D51IDHB 03850000
.*                                                                      03900000
.* <STORE = TEMP / PERM / FREE / DEL / CLASS / CLDEL >         @D51IDHB 03950000
.*     SPECIFIES WHETHER THE LABEL BELONGS TO THE TEMPORARY OR          04000000
.*     PERMANENT LABEL SUBAREA OR TO THE FREE USAGE AREA OF THE         04050000
.*     PARTITION OR TO A CLASS AND WHETHER IT IS TO BE DELETED @D51IDHB 04100000
.*                                                                      04150000
.* OUTPUT:                                                              04200000
.*     FOLLOWING ASSEMBLER STMTS. ARE CREATED:                          04250000
.*                                                                      04300000
.*        LPLAREA  DS     F     ADDRESS OF BUFFER                       04350000
.*        LPLBFLEN DS     H     LENGTH OF BUFFER                        04400000
.*        LPLLBLEN DS     H     LENGTH OF LABEL                         04450000
.*        LPLNAM   DS     CL8   FILE NAME                               04500000
.*        LPLGRP   DS     H     PARTITION NUMBER                        04550000
.*        LPLSTORE DS     X     OPTION CODE                             04600000
.*        LPLINDIC DS     X     INDICATORS IN LPL                       04650000
.*        LPLSEQNM DS     X     EXTENT SEQUENCE NUMBER                  04700000
.*                                                                      04750000
.*     CONTENTS OF THE DEFINED FIELDS (IF NOT ZERO):                    04800000
.*        LPLAREA CONTAINS A VIRTUAL ADDRESS.                           04850000
.*        LPLBFLEN CONTAINS A BINARY VALUE.                             04900000
.*        LPLLBLEN CONTAINS A BINARY VALUE.                             04950000
.*        LPLNAM  CONTAINS AN ALPHAMERIC CHARACTER STRING. IF THE       05000000
.*                FILE NAME IS SMALLER THAN 8 CHARACTERS IT IS LEFT     05050000
.*                ADJUSTED AND PADDED WITH BLANKS.                      05100000
.*        LPLGRP  CONTAINS THE PARTITION NUMBER IN THE LEFT BYTE OF THE 05150000
.*                HALF WORD. IF GROUP = PARTITION IS SPECIFIED THEN THE 05200000
.*                BYTE IS ZERO. FOR GROUP = SYSTEM THE BYTE CONTAINES   05250000
.*                X'FF' AND FOR GROUP = PN THE BYTE CONTAINS THE PART.  05300000
.*                NUMBER IN BINARY (E.G. X'01' FOR PARTITION P1).       05350000
.*                FOR GROUP = CLASS THE LETTER REPRESENTING    @D51IDHB 05400000
.*                THE CLASS IS STORED IN THE LEFT BYTE.        @D51IDHB 05450000
.*                THE RIGHT BYTE OF THE HALFWORD IS RESERVED.           05500000
.*       LPLSTORE CONTAINS X'00' FOR STORE = TEMP, X'01' FOR            05550000
.*                STORE=PERM, X'02' FOR STORE=FREE, X'03' FOR  @D51IDHB 05600000
.*                STORE=DEL, X'04' FOR STORE=CLASS             @D51IDHB 05650000
.*                AND X'05' FOR STORE=DELCL                    @D51IDHB 05700000
.*       LPLINDIC IS ALWAYS INITIALIZED TO X'00'. IF THE EXTENT         05750000
.*                SEQUENCE NUMBER SHOULD BE CHECKED BY SLA, IT          05800000
.*                CONTAINS X'01'.                                       05850000
.*       LPLSEQNM IS INITIALIZED TO X'00' AND CONTAINS THE EXTENT       05900000
.*                SEQUENCE NUMBER (USED FOR GETNXL FUNCTION).           05950000
.*                                                                      06000000
.*     REFERENCES TO THE FIELDS DEFINED:                                06050000
.*       BY CALLING THE LPLDCT MACRO, A DUMMY SECTION IS CREATED WHICH  06100000
.*       CORRESPONDS FIELD BY FIELD TO THE AREA CREATED BY AN LPL       06150000
.*       MACRO. THIS ALLOWS TO MODIFY AN LPL AREA (E.G. CHANGE THE      06200000
.*       FILE NAME) ONCE CREATED BY AN LPL MACRO.                       06250000
.*                                                                      06300000
.* EXIT: CONTINUE CALLING PROGRAM                                       06350000
.*                                                                      06400000
.* CHANGE ACTIVITY:                                                     06450000
.*        @D51IDHB MORE THAN 12 PARTITIONS                     @D51IDHB 06500000
.*        @D51ZDWS REMOVE RESTRICTIONS                         @D61ZDWS 06525061
.* A000000-999999                                              @D35DDWH 06550000
.* ******************************************************************** 06600000
         AIF   ('&AREA' EQ '').AREADS            BR IF AREA NOT SPEC.   06650000
         AIF   (K'&AREA GT 8).ERROR1             BR IF TOO LONG         06700000
         AIF   (T'&AREA EQ 'N').ERROR1           BR IF SELF DEFINING    06750000
         AGO   .AREADC                           BR IF AREA IS SPEC.    06800000
.ARLEN   AIF   ('&AREALEN' EQ '').ARLENDS        BR IF ARLEN NOT SPEC.  06850000
         AIF   (K'&AREALEN GT 4).ERROR2          BR IF LONGER THAN 3    06900000
         AIF   (T'&AREALEN NE 'N').ERROR2        BR IF NOT SELF DEF.    06950000
         AGO   .ARLENDC                          BR IF ARLEN IS SPEC.   07000000
.LBLEN   AIF   ('&LABLEN' EQ '').LBLENDS         BR IF LABLEN NOT SPEC. 07050000
         AIF   (K'&LABLEN GT 4).ERROR2           BR IF LONGER THAN 3    07100000
         AIF   (T'&LABLEN NE 'N').ERROR2         BR IF NOT SELF DEF.    07150000
         AGO   .LBLENDC                          BR IF LABLEN IS SPEC.  07200000
.FILNM   AIF   ('&FILENAM' EQ '').FILNMDS        BR IF FILNM NOT SPEC.  07250000
         AIF   (T'&FILENAM EQ 'N').ERROR3        BR IF SELF DEFINING    07300000
         AIF   (K'&FILENAM GT 8).ERROR3          BR IF TOO LONG         07350000
         AGO   .FILNMDC                          BR IF FILNM IS SPEC.   07400000
.GRP     AIF   ('&GROUP' EQ '').GRPDS            BR IF GRP NOT SPEC.    07450000
         AIF   ('&GROUP' EQ 'SYSTEM').GRPSYS     BR IF GROUP=SYSTEM     07500000
         AIF   ('&GROUP' EQ 'PARTITION').GRPPART BR IF GROUP=PARTITION  07550000
         AIF   ('&GROUP' EQ 'BG').GRPBG          BR IF GROUP=BG         07600000
         AIF   ('&GROUP' EQ 'F1').GRPF1          BR IF GROUP=F1         07650000
         AIF   ('&GROUP' EQ 'F2').GRPF2          BR IF GROUP=F2         07700000
         AIF   ('&GROUP' EQ 'F3').GRPF3          BR IF GROUP=F3         07750000
         AIF   ('&GROUP' EQ 'F4').GRPF4          BR IF GROUP=F4         07800000
         AIF   ('&GROUP' EQ 'F5').GRPF5          BR IF GROUP=F5         07850000
         AIF   ('&GROUP' EQ 'F6').GRPF6          BR IF GROUP=F6         07900000
         AIF   ('&GROUP' EQ 'F7').GRPF7         BR IF GROUP=F7 @D36IDWS 07950000
         AIF   ('&GROUP' EQ 'F8').GRPF8         BR IF GROUP=F8 @D36IDWS 08000000
         AIF   ('&GROUP' EQ 'F9').GRPF9         BR IF GROUP=F9 @D36IDWS 08050000
         AIF   ('&GROUP' EQ 'FA').GRPFA         BR IF GROUP=FA @D36IDWS 08100000
         AIF   ('&GROUP' EQ 'FB').GRPFB         BR IF GROUP=FB @D36IDWS 08150000
         AIF   (K'&GROUP NE 1).ERROR5       BR IF GROUP¬=CLASS @D61ZDWS 08200061
         AIF   ('&GROUP' GT 'B' AND '&GROUP' LE 'Z'                    *08250000
               AND '&GROUP' NE 'F').GRPCL                      @D61ZDWS 08300061
.ERROR5  MNOTE 8,'INVALID SPECIFICATION OF GROUP PARM.'        @D51IDHB 08400000
         MEXIT                                                          08450000
.STORE   AIF   ('&STORE' EQ '').STOREDS          BR IF STORE NOT SPEC.  08500000
         AIF   ('&STORE' EQ 'TEMP').STORETP      BR IF STORE=TEMP       08550000
         AIF   ('&STORE' EQ 'PERM').STOREPM      BR IF STORE=PERM       08600000
         AIF   ('&STORE' EQ 'FREE').STOREFR      BR IF STORE=FREE       08650000
         AIF   ('&STORE' EQ 'DEL').STOREDL    BR IF STORE=DEL  @D36HDWS 08700000
         AIF   ('&STORE' EQ 'CLASS').STORECL  BR IF STORE=CLASS@D51IDHB 08750000
         AIF   ('&STORE' EQ 'CLDEL').STORECD  BR IF STORE=CLDEL@D51IDHB 08800000
         MNOTE 8,'INVALID SPECIFICATION OF STORE PARM.'                 08850000
         MEXIT                                                          08900000
.AREADS  ANOP                                                           08950000
&NAME    DS    0F                                                       09000000
         DC    F'0'                              AREA NOT SPEC.         09050000
         AGO   .ARLEN                            GO TO NEXT PARM        09100000
.AREADC  ANOP                                                           09150000
&NAME    DS    0F                                                       09200000
         DC    A(&AREA)                          GET ADDR OF BUFFER     09250000
         AGO   .ARLEN                            GO TO NEXT PARM        09300000
.ARLENDS ANOP                                                           09350000
         DC    H'0'                              ARLEN NOT SPEC.        09400000
         AGO   .LBLEN                            GO TO NEXT PARM        09450000
.ARLENDC ANOP                                                           09500000
         DC    H'&AREALEN'                       GET LENGTH OF BUFFER   09550000
         AGO   .LBLEN                            GO TO NEXT PARM        09600000
.LBLENDS ANOP                                                           09650000
         DC    H'0'                              LABLEN NOT SPEC.       09700000
         AGO   .FILNM                            GO TO NEXT PARM        09750000
.LBLENDC ANOP                                                           09800000
         DC    H'&LABLEN'                        GET LENGTH OF LABEL    09850000
         AGO   .FILNM                            GO TO NEXT PARM        09900000
.FILNMDS ANOP                                                           09950000
         DC    CL8' '                            FILNM NOT SPEC.        10000000
         AGO   .GRP                              GO TO NEXT PARM        10050000
.FILNMDC ANOP                                                           10100000
         DC    CL8'&FILENAM'                     GET FILENAME           10150000
         AGO   .GRP                              GO TO NEXT PARM        10200000
.GRPDS   ANOP                                                           10250000
         DC    H'0'                              GROUP NOT SPEC.        10300000
         AGO   .STORE                            GO TO NEXT PARM        10350000
.GRPSYS  ANOP                                                           10400000
         DC    X'FF00'                           GROUP=SYSTEM           10450000
         AGO   .STORE                            GO TO NEXT PARM        10500000
.GRPPART ANOP                                                           10550000
         DC    X'0000'                           GROUP=PARTITION        10600000
         AGO   .STORE                            GO TO NEXT PARM        10650000
.GRPBG   ANOP                                                           10700000
         DC    X'0000'                           GROUP=BG               10750000
         AGO   .STORE                            GO TO NEXT LABEL       10800000
.GRPF1   ANOP                                                           10850000
         DC    X'0100'                           GROUP=F1               10900000
         AGO   .STORE                            GO TO NEXT PARM        10950000
.GRPF2   ANOP                                                           11000000
         DC    X'0200'                           GROUP=F2               11050000
         AGO   .STORE                            GO TO NEXT LABEL       11100000
.GRPF3   ANOP                                                           11150000
         DC    X'0300'                           GROUP=F3               11200000
         AGO   .STORE                            GO TO NEXT LABEL       11250000
.GRPF4   ANOP                                                           11300000
         DC    X'0400'                           GROUP=F4               11350000
         AGO   .STORE                            GO TO NEXT LABEL       11400000
.GRPF5   ANOP                                                           11450000
         DC    X'0500'                           GROUP=F5               11500000
         AGO   .STORE                            GO TO NEXT LABEL       11550000
.GRPF6   ANOP                                                           11600000
         DC    X'0600'                           GROUP=F6               11650000
         AGO   .STORE                            GO TO NEXT LABEL       11700000
.GRPF7   ANOP                                                  @D36IDWS 11750000
         DC    X'0700'                           GROUP=F7      @D36IDWS 11800000
         AGO   .STORE                            GO TO NEXT    @D36IDWS 11850000
.GRPF8   ANOP                                                  @D36IDWS 11900000
         DC    X'0800'                           GROUP=F8      @D36IDWS 11950000
         AGO   .STORE                            GO TO NEXT    @D36IDWS 12000000
.GRPF9   ANOP                                                  @D36IDWS 12050000
         DC    X'0900'                           GROUP=F9      @D36IDWS 12100000
         AGO   .STORE                            GO TO NEXT    @D36IDWS 12150000
.GRPFA   ANOP                                                           12200000
         DC    X'0A00'                           GROUP=FA      @D36IDWS 12250000
         AGO   .STORE                            GO TO NEXT    @D36IDWS 12300000
.GRPFB   ANOP                                                           12350000
         DC    X'0B00'                           GROUP=FB      @D36IDWS 12400000
         AGO   .STORE                            GO TO NEXT    @D36IDWS 12450000
.GRPCL   ANOP                                                  @D51IDHB 12500000
         DC    C'&GROUP'                         CLASS         @D51IDHB 12550000
         DC    X'00'                             EMPTY         @D51IDHB 12600000
         AGO   .STORE                            GO TO NEXT    @D51IDHB 12650000
.STOREDS ANOP                                                           12700000
         DC    X'00'                             LPLSTORE NOT SPEC.     12750000
         AGO   .SEQNUM                           GO TO NEXT PARM        12800000
.STORETP ANOP                                                           12850000
         DC    X'00'                             STORE=TEMP             12900000
         AGO   .SEQNUM                           GO TO NEXT PARM        12950000
.STOREPM ANOP                                                           13000000
         DC    X'01'                             STORE=PERM             13050000
         AGO   .SEQNUM                           GO TO NEXT PARM        13100000
.STOREFR ANOP                                                           13150000
         DC    X'02'                             STORE=FREE             13200000
         AGO   .SEQNUM                        GO TO NEXT PARM  @D36HDWS 13250000
.STOREDL ANOP                                                  @D36HDWS 13300000
         DC    X'03'                             STORE=DEL     @D36HDWS 13350000
         AGO   .SEQNUM                        GO TO NEXT PARM  @D51IDHB 13400000
.STORECL ANOP                                                  @D51IDHB 13450000
         DC    X'04'                             STORE=CLASS   @D51IDHB 13500000
         AGO   .SEQNUM                        GO TO NEXT PARM  @D51IDHB 13550000
.STORECD ANOP                                                  @D51IDHB 13600000
         DC    X'05'                             STORE=CLDEL   @D51IDHB 13650000
.SEQNUM  ANOP                                                           13700000
         DC    X'00'                             INDICATORS IN LPL      13750000
         DC    X'00'                             EXTENT SEQUENCE NUMBER 13800000
         MEXIT                                                          13850000
.ERROR1  MNOTE 8,'INVALID SPECIFICATION OF AREA PARM.'                  13900000
         MEXIT                                                          13950000
.ERROR2  MNOTE 8,'INVALID SPECIFICATION OF AREALEN PARM.'               14000000
         MEXIT                                                          14050000
.ERROR3  MNOTE 8,'INVALID SPECIFICATION OF FILENAM PARM.'               14100000
         MEND                                                           14150000
