         MACRO                                                          00050000
&NAME    PCPTAB &MASTER=,&NOMAST1=,&NOMAST2=,&NOMAST3=                  00100000
.*                                                                      00150000
.*                                                                    * 00200000
.*            IBM DISK OPERATING SYSTEM                               * 00250000
.*            VSE/POWER PCPTAB  - 5686-CF7-03                         * 00300001
.*                                                                    * 00350000
.*                                                                    * 00400000
.*                                                                      00450000
.*   DECLARE GLOBALS AND LOCALS                                         00500000
.*                                                                      00550000
     GBLB    &TWICE                 ONLY ONE PER EXEC ASSEMBLY RUN      00600000
     LCLA    &M                     NUMBER OF MASTER ENTRIES            00650000
     LCLA    &NM                    NUMBER OF NON MASTER ENTRIES        00700000
     LCLA    &X                     DECIMEL VALUE OF CHARACTER          00750000
     LCLA    &Y                     COMPACT CHARACTER                   00800000
     LCLA    &TR                    X'00' OF &T                         00850000
     LCLA    &T(255)                REST OF TRANSLATE TEST TABLE        00900000
     LCLA    &RT                    X'00' OF &R                         00950000
     LCLA    &R(255)                REST OF TRANSLATE TABLE             01000000
     LCLA    &DC                    X'00' OF &D                         01050000
     LCLA    &D(255)                REST OF DECOMPACTION TABLE          01100000
     LCLA    &E(252),&F             WORK BUCKETS                        01150000
     LCLA    &A,&B,&C,&K,&Z         WORK COUNTERS                       01200000
     LCLC    &LLL                   WORK BUCKETS                        01250000
.*                                                                      01300000
         AGO   .ZZZ                                                     01350000
*        LICENSED MATERIALS - PROPERTY OF IBM                           01400000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                    01450000
*        5686-CF7 (C) COPYRIGHT IBM CORP. 1979, 2004                    01500001
*        SEE COPYRIGHT INSTRUCTIONS                                     01550000
.ZZZ     ANOP                                                           01600000
.*   ONLY ONE PCPTAB MACRO PER ASSEMBLY IS ALLOWED                      01650000
.*                                                                      01700000
     AIF     (&TWICE).ERR8          ERROR IF TWO IN ONE ASSEMBLY        01750000
     ACTR    4095                   SET ASSEMBLER COUNTER               01800000
.*                                                                      01850000
.*   CHECK IF NUMBER OF MASTERS IS CORRECT                              01900000
.*                                                                      01950000
&M   SETA    N'&MASTER              SET TO NUMBER OF MASTER CHARS       02000000
     AIF     (&M LT 3 OR &M GT 16).ERR0 INVALID VALUE                   02050000
.*                                                                      02100000
.*   GET COUNT OF NON MASTERS                                           02150000
.*                                                                      02200000
&NM  SETA    N'&NOMAST1+N'&NOMAST2+N'&NOMAST3 COUNT THEM UP             02250000
.*                                                                      02300000
.*   INITIALIZE TRT TABLE                                               02350000
.*                                                                      02400000
&TR  SETA    255                    FIRST BYTE OF TRT TABLE             02450000
&A   SETA    1                      SET TO INITIAL VALUE                02500000
.$1  ANOP                           LOOP ENTRY POINT                    02550000
&T(&A) SETA  255                    SET POSITION IN TABLE               02600000
&A   SETA    &A+1                   BUMP VALUE                          02650000
     AIF     (&A LE 255).$1         LOOP THROUGH TABLE                  02700000
.*                                                                      02750000
.*   INITIALIZE DECOMPACTION TABLE                                      02800000
.*                                                                      02850000
&DC  SETA    256                    SET INITIAL VALUE                   02900000
&B   SETA    &M-1                   SET VALUE                           02950000
&A   SETA    1                      SET INITIAL VALUE                   03000000
.$2  ANOP                           LOOP ENTRY POINT                    03050000
&D(&A) SETA  256                    SET TABLE POSITION                  03100000
&A   SETA    &A+1                   BUMP VALUE                          03150000
     AIF     (&A LE &B).$2          LOOP THROUGH LINE                   03200000
&B   SETA    &B+16                  SET TO LINE LENGTH                  03250000
&A   SETA    ((&A+15)/16)*16        POINT TO NEXT LINE                  03300000
     AIF     (&A LT &M*16).$2       FINISH TABLE                        03350000
.*                                                                      03400000
&A   SETA    1                      SET TO INITIAL VALUE                03450000
&Y   SETA    240                    POINT TO PLACE FOR FIRST MASTER     03500000
&Z   SETA    255                    SET TO END OF FIRST LINE            03550000
.*                                                                      03600000
     AIF     (&NM EQ 0 AND &M NE 16).ERR7 DETECT ERROR                  03650000
     AIF     (&M EQ 16 AND &NM EQ 0).$3 IF ONLY MASTERS, BRANCH         03700000
     AIF     (&NM GT (256-&M*&M-&M)).ERR6 TOO MANY SPECIFIED            03750000
     AIF     (&NM NE (256-&M*&M-&M)).ERR9 MUST SPECIFY ALL CHARS        03800000
.*                                                                      03850000
.$3  ANOP                           ENTRY POINT FOR MASTERS             03900000
&E(252) SETA &X                     GET LAST MASTER CHAR                03950000
&C   SETA    1                      GET RETURN POINT                    04000000
     ACTR    4096                   SET CONDITIONAL ASSEMBLE COUNTER    04050000
&LLL SETC    '&MASTER(&A)'          GET MASTER CHAR                     04100000
     AIF     (&Z LT 0).ERR6         ERROR IF NO PLACE                   04150000
&K   SETA    K'&MASTER(&A)          GET NUMBER OF CHARS                 04200000
     AIF     (&A LE N'&MASTER).@0   CHECK CHAR                          04250000
     AIF     (&M EQ 16).$9          WE ARE THROUGH                      04300000
&A   SETA    1                      SET INITIAL VALUE                   04350000
.*                                                                      04400000
.*   GET VALUES FOR NOMAST                                              04450000
.*                                                                      04500000
     AIF     (K'&NOMAST1 EQ 0).$5   IGNORE IF NOT SPECIFIED             04550000
&C   SETA    2                      SET RETURN POINT                    04600000
.$4  ANOP                           NOMAST ENTRY POINT                  04650000
     ACTR    4096                   SET CONDITIONAL ASSEMBLE COUNTER    04700000
&LLL SETC    '&NOMAST1(&A)'         GET NOMAST1 CHAR                    04750000
&K   SETA    K'&NOMAST1(&A)         GET COUNT OF CHAR                   04800000
     AIF     (&Z LT 0 AND &A LE N'&NOMAST1).ERR6 TOO MANY CHARS         04850000
     AIF     (&A LE N'&NOMAST1).@0  CHECK CHAR                          04900000
&A   SETA    1                      SET TO INITIAL VALUE                04950000
.*                                                                      05000000
.*   GET VALUES FOR NOMAST2                                             05050000
.*                                                                      05100000
.$5  AIF     (K'&NOMAST2 EQ 0).$7   IGNORE IF OMITTED                   05150000
&C   SETA    3                      GET RETURN POINT                    05200000
.$6  ANOP                           ENTRY FOR NOMAST2                   05250000
     ACTR    4096                   CONDITIONAL ASSEMBLER COUNTER       05300000
&LLL SETC    '&NOMAST2(&A)'         GET NOMAST2 CHAR                    05350000
&K   SETA    K'&NOMAST2(&A)         GET COUNT OF CHAR                   05400000
     AIF     (&Z LT 0 AND &A LE N'&NOMAST2).ERR6 TOO MANY CHARS         05450000
     AIF     (&A LE N'&NOMAST2).@0   CHECK CHAR                         05500000
&A   SETA    1                      SET TO INITIAL VALUE                05550000
.*                                                                      05600000
.*   GET VALUES FOR NOMAST3                                             05650000
.*                                                                      05700000
.$7  AIF     (K'&NOMAST3 EQ 0).$9   IGNORE IF OMITTED                   05750000
&C   SETA    4                      GET RETURN POINT                    05800000
.$8  ANOP                           ENTRY FOR NOMAST3                   05850000
     ACTR    4096                   CONDITIONAL ASSEMBLY COUNTER        05900000
&LLL SETC    '&NOMAST3(&A)'         GET NOMAST3 CHAR                    05950000
&K   SETA    K'&NOMAST3(&A)         GET COUNT OF CHAR                   06000000
     AIF     (&Z LT 0 AND &A LE N'&NOMAST3).ERR6 TOO MANY CHARS         06050000
     AIF     (&A LE N'&NOMAST3).@0   CHECK CHARACTER                    06100000
     AGO     .$9                    FINISH UP PROCESSING                06150000
.*                                                                      06200000
.@0  ANOP                           ENTRY FOR CHAR CHECK                06250000
.*                                                                      06300000
     AIF     (&K EQ 1).@1           ALPHA SPECIFICATION                 06350000
     AIF     (&K EQ 2).@7           HEX SPECIFICATION                   06400000
     AIF     (&K GT 4 OR &K LT 1).ERR4  TOO MANY CHARS                  06450000
     AIF     (&K EQ 4).@4           CHECK IF QUOTE                      06500000
     AIF     ('&LLL'(1,1) NE '''').ERR4 IS IN QUOTES                    06550000
     AIF     ('&LLL'(3,1) NE '''').ERR4 IS IN QUOTES                    06600000
.*                                                                      06650000
&LLL SETC    '&LLL'(2,1)            GET CHARACTER FROM QUOTES           06700000
.@1  ANOP                           ENTRY POINT IF ALPHA                06750000
&B   SETA    1                      SET INITIAL VALUE                   06800000
     AIF     ('&LLL'(1,1) LT 'A').@3  WHICH TABLE TO SEARCH             06850000
.@2  AIF        ('&LLL'(1,1) EQ 'ABCDEFGHI.......JKLMNOPQR........STUVWX06900000
               XYZ......0123456789'(&B,1)).@6                           06950000
&B   SETA    &B+1                   BUMP POINTER                        07000000
     AIF     (&B LE 57).@2          LIMITS ON LOOP                      07050000
     AGO     .ERR4                  ERROR IF FALL THRU                  07100000
.@3  AIF      ('&LLL'(1,1) EQ '          ¢.<(+|..........!$*);¬-/......X07150000
               ...,%_>?..........:#@.="'(&B,1)).@5                      07200000
&B   SETA    &B+1                   BUMP POINTER                        07250000
     AIF     (&B LE 64).@3          LOOP ON ALL CHARS                   07300000
     AGO     .ERR4                  ERROR IF FALL THRU                  07350000
.@4  ANOP                           ENTRY FOR QUOTE QUOTE               07400000
&X   SETA    125                    VALUE FOR QUOTE                     07450000
     AIF     ('&LLL'(2,1) EQ ''''(1,1)).@F  ACCEPT IF QUOTE             07500000
&X   SETA    80                     GET VALUE FOR AMPERSAND             07550000
     AIF     ('&LLL'(2,1) EQ '&&'(1,1)).@F ACCEPT IF AMPERSAND          07600000
     AGO     .ERR4                  ERROR IF NOT                        07650000
.@5  ANOP                           FOUND CHARACTER IN TABLE            07700000
&X   SETA    &B+63                  GET VALUE OF CHAR                   07750000
     AGO     .@F                    CONTINUE MAIN LINE                  07800000
.*                                                                      07850000
.@6  ANOP                           FOUND CHARACTER                     07900000
&X   SETA    &B+192                 GET VALUE OF CHAR                   07950000
     AGO     .@F                    CONTINUE MAINLINE                   08000000
.*                                                                      08050000
.@7  ANOP                           HEX ENTRY POINT                     08100000
     AIF     ('&LLL'(1,1) EQ ''''(1,1)).@8  IF QUOTE HANDLE             08150000
     AIF     ('&LLL'(1,1) EQ '&&'(1,1)).@9  IF AMPERSAND HANDLE         08200000
     AGO     .@A                    BRANCH AROUND                       08250000
.@8  ANOP                           QUOTE ENTRY                         08300000
&X   SETA    125                    GET VALUE OF QUOTE                  08350000
     AGO     .@F                    CONTINUE MAINLINE                   08400000
.@9  ANOP                           AMPERSAND ENTRY                     08450000
&X   SETA    80                     VALUE OF AMPERSAND                  08500000
     AGO     .@F                    CONTINUE MAINLINE                   08550000
.@A  ANOP                           ENTRY FOR TRUE HEX                  08600000
&B   SETA    1                      SET INITIAL VALUE                   08650000
     ACTR    4096                   SET CONDITIONAL COUNTER             08700000
.@B  AIF     ('&LLL'(1,1) EQ '0123456789ABCDEF'(&B,1)).@C               08750000
&B   SETA    &B+1                   BUMP TO NEXT CHAR                   08800000
     AIF     (&B LE 16).@B          LOOP ON HEX TEXT                    08850000
     AGO     .ERR5                  INVALID HEX                         08900000
.*                                                                      08950000
.@C  ANOP                           FOUND FIRST HEX CHARACTER           09000000
&X   SETA    (&B-1)*16              GET VALUE OF CHAR                   09050000
.*                                                                      09100000
&B   SETA    1                      SET TO INITIAL VALUE                09150000
.@D  AIF     ('&LLL'(2,1) EQ '0123456789ABCDEF'(&B,1)).@E               09200000
&B   SETA    &B+1                   BUMP TO NEXT CHAR                   09250000
     AIF     (&B LE 16).@D          LOOP LIMITS                         09300000
     AGO     .ERR5                  INVALID HEX                         09350000
.*                                                                      09400000
.@E  ANOP                           FOUND SECOND HEX CHAR               09450000
&X   SETA    &X+(&B-1)              GET TOTAL VALUE OF CHAR             09500000
.*                                                                      09550000
.@F  AIF     (&X NE 0).@G           SPECIAL CASE OF ZERO                09600000
&TR  SETA    0                      SET ZERO AREA TO ZERO               09650000
&RT  SETA    &Y                     SET CHAR TO ZERO                    09700000
     AGO     .@H                    CONTINUE                            09750000
.*                                                                      09800000
.@G  AIF     (&T(&X) EQ 0).ERR3     DUPLICATE CHARACTER                 09850000
&T(&X) SETA  0                      INITIALIZE TRT TABLE                09900000
&R(&X) SETA  &Y                     INITIALIZE XLATE TABLE              09950000
.@H  ANOP                           CONTINUE MAIN LINE                  10000000
&F   SETA    &F+1                   BUMP POINTER                        10050000
&E(&F) SETA  &X                     SAVE CHARACTER                      10100000
&D(&Y) SETA  &X                     INITIALIZE DECOMPACT TABLE          10150000
.*                                                                      10200000
&Y   SETA    &Y+1                   BUMP POINTER                        10250000
     AIF     (&Y LE &Z).@J          STILL WITHIN LINE                   10300000
.@I  ANOP                           GET NEXT LINE                       10350000
&Z   SETA    &Z-16                  SET UP VALUE                        10400000
&Y   SETA    ((&Y-17)/16)*16        GET VALUE FOR Y                     10450000
.@J  AIF     (&Y NE 0).@K           SPECIAL CASE IF ZERO                10500000
     AIF     (&Z LT 0).@L           TOO MANY CHARS                      10550000
&Y   SETA    &M-1                   SET TO NUMBER OF MASTERS            10600000
.@K  AIF     (&D(&Y) EQ 0).@L       FIND PLACE                          10650000
     AIF     (&M EQ 16).@L          IF ALL MASTERS, ACCEPT              10700000
&Y   SETA    &Y+1                   BUMP POINTER                        10750000
     AIF     (&Y LE &Z).@K          LOOP TIL FIND START PLACE           10800000
     AGO     .@I                    GET NEXT LINE                       10850000
.@L  ANOP                           FOUND PLACE                         10900000
&A   SETA    &A+1                   BUMP TO NEXT MASTER                 10950000
     AIF     (&C EQ 1).$3           GET RETURN POINT                    11000000
     AIF     (&C EQ 2).$4           RETURN FOR NOMAST1                  11050000
     AIF     (&C EQ 3).$6           RETURN FOR NOMAST2                  11100000
     AGO     .$8                    RETURN FOR NOMAST3                  11150000
.*                                                                      11200000
.*                                                                      11250000
.$9  ANOP                           FINISHED PROCESSING ALL OPERANDS    11300000
&TWICE SETB  1                      NO MORE GENERATION POSSIBLE         11350000
.*                                                                      11400000
.*   GENERATE PHASE CARD                                                11450000
.*                                                                      11500000
     AIF     (K'&NAME GT 4).$A      WE ONLY TAKE 4 CHAR NAMES           11550000
&LLL SETC    '&NAME'                GET NAME                            11600000
     AIF     (K'&NAME NE 0).$B      CONTINUE                            11650000
     MNOTE  8,' NAME OMITTED OR SPECIFIED INCORRECTLY - ''PCPT'' TAKEN X11700000
               AS DEFAULT.'                                             11750000
&LLL SETC    'PCPT'                 SET DEFAULT NAME                    11800000
     AGO     .$B                    CONTINUE MAINLINE                   11850000
.*                                                                      11900000
.$A  MNOTE  8,' LENGTH OF NAME IS GREATER THAN 4 CHARACTERS, TRUNCATED X11950000
               TO 4 CHARACTERS - GENERATION CONTINUED.'                 12000000
&LLL SETC    '&NAME'(1,4)           TAKE FIRST 4 CHARS                  12050000
.$B  ANOP                                                               12100000
&LLL TITLE   'VSE/POWER COMPACTION TABLE GENERATION FOR - ''&LLL''.'    12150000
     SPACE 3                                                            12200000
     PUNCH     ' PHASE &LLL,+0 '    PUNCH OUT PHASE CARD FOR CATAL      12250000
     PRINT   DATA                   LET USER SEE ALL OF TABLES          12300000
.*                                                                      12350000
.*   GENERATE TABLES                                                    12400000
.*                                                                      12450000
.*   GENERATE FORMATE HEADER TYPE 3                                     12500000
.*                                                                      12550000
     SPACE 3                                                            12600000
*            FORMAT TYPE 3 HEADER                                       12650000
     SPACE                                                              12700000
*            THIS TABLE IS USED TO TRANSMIT TO THE SNA TERMINAL TO      12750000
*            ALLOW THE TERMINAL TO BUILD THE CORRECT COMPACT TABLE      12800000
     SPACE                                                              12850000
FMH3 DC      AL1(4+&M+&NM)          LENGTH OF TABLE                     12900000
     DC      B'00000011'            TYPE 3 HEADER                       12950000
     DC      X'02'                  COMPACTION TABLE                    13000000
     DC      AL1(&M)                NUMBER OF MASTERS                   13050000
&A   SETA    1                      SET INITIAL VALUE                   13100000
.$C  DC      AL1(&E(&A),&E(&A+1),&E(&A+2),&E(&A+3),&E(&A+4),&E(&A+5),&EX13150000
               (&A+6),&E(&A+7))                                         13200000
&A   SETA    &A+8                   BUMP POINTER                        13250000
     AIF     (&A LT 248).$C         LOOP THROUGH TABLE                  13300000
     DC      AL1(&E(249),&E(250),&E(251))                               13350000
     DC      AL1(&E(252))           LAST MASTER CHAR                    13400000
     EJECT                                                              13450000
     SPACE 3                                                            13500000
*            TRANSLATE AND TEST TABLE                                   13550000
     SPACE                                                              13600000
*            THIS TABLE IS USED TO DETERMINE WHETHER A CHARACTER        13650000
*            STRING HAS MEMBERS WHICH ARE NOT MEMBERS OF THE            13700000
*            COMPACTIBLE SET.  IF A TRANSLATE AND TEST IS STOPPED       13750000
*            WITHOUT EXHAUSTING THE COUNT OF THE TRT, THEN THE          13800000
*            CHARACTER STRING MAY NOT BE COMPACTED.                     13850000
     SPACE                                                              13900000
.*                                                                      13950000
TRTC DC        AL1(&TR,&T(1),&T(2),&T(3),&T(4),&T(5),&T(6),&T(7))       14000000
&A   SETA      8                    START OF NEXT LINE                  14050000
.$D  DC        AL1(&T(&A),&T(&A+1),&T(&A+2),&T(&A+3),&T(&A+4),&T(&A+5),X14100000
               &T(&A+6),&T(&A+7))                                       14150000
&A   SETA      &A+8                 BUMP TO NEXT LINE                   14200000
     AIF       (&A LT 256).$D       LOOP THROUGH TABLE                  14250000
.*                                                                      14300000
     EJECT                                                              14350000
     SPACE 3                                                            14400000
*            TRANSLATE TABLE                                            14450000
     SPACE                                                              14500000
*            THIS TABLE IS USED TO CONVERT COMPACTIBLE CHARACTERS TO    14550000
*            THEIR PROPER COMPACTIBLE CODE.  THIS IS DONE USING A TR    14600000
*            INSTRUCTION.                                               14650000
     SPACE                                                              14700000
.*                                                                      14750000
XLTC DC        AL1(&RT,&R(1),&R(2),&R(3),&R(4),&R(5),&R(6),&R(7))       14800000
&A   SETA      8                    START OF SECOND LINE                14850000
.$E  DC        AL1(&R(&A),&R(&A+1),&R(&A+2),&R(&A+3),&R(&A+4),&R(&A+5),X14900000
               &R(&A+6),&R(&A+7))                                       14950000
&A   SETA      &A+8                 GET NEXT LINE                       15000000
     AIF       (&A LT 256).$E       LOOP THROUGH TABLE                  15050000
.*                                                                      15100000
     EJECT                                                              15150000
     SPACE 3                                                            15200000
*            DECOMPACTION TABLE                                         15250000
     SPACE                                                              15300000
*            THIS TABLE IS A MIRROR IMAGE OF THE TABLE ABOVE.           15350000
*            THAT IS, THE CHARACTER OBTAINED ABOVE MAY BE               15400000
*            CONVERTED TO ITS PROPER EBCDIC REPRESENTATION BY           15450000
*            USING A TR INSTRUCTION ON THIS TABLE.                      15500000
     SPACE                                                              15550000
.*                                                                      15600000
XLTD DC        AL1(&DC,&D(1),&D(2),&D(3),&D(4),&D(5),&D(6),&D(7))       15650000
&A   SETA      8                    START OF SECOND LINE                15700000
.$F  DC        AL1(&D(&A),&D(&A+1),&D(&A+2),&D(&A+3),&D(&A+4),&D(&A+5),X15750000
               &D(&A+6),&D(&A+7))                                       15800000
&A   SETA      &A+8                 GET NEXT LINE                       15850000
     AIF       (&A LT 256).$F       LOOP THROUGH TABLE                  15900000
.*                                                                      15950000
     MEXIT                                                              16000000
.ERR0 MNOTE 8,' NUMBER OF MASTER CHARACTERS INCORRECTLY SPECIFIED - MUSX16050000
               T BE GREATER THAN 2 AND LESS THAN 17.'                   16100000
     MEXIT                                                              16150000
.ERR3 MNOTE 8,' DUPLICATE CHARACTER ''&LLL'' SPECIFIED - GENERATION TERX16200000
               MINATED.'                                                16250000
     MEXIT                                                              16300000
.ERR4 MNOTE 8,' INVALID CHARACTER ''&LLL'' SPECIFIED - GENERATION TERMIX16350000
               NATED.'                                                  16400000
     MEXIT                                                              16450000
.ERR5 MNOTE 8,' INVALID HEX CHARACTER ''&LLL'' SPECIFIED - GENERATION TX16500000
               ERMINATED.'                                              16550000
     MEXIT                                                              16600000
.ERR6 MNOTE 8,' TOO MANY MASTER AND/OR NON-MASTER CHARACTERS SPECIFIED X16650000
               - GENERATION TERMINATED.'                                16700000
     MEXIT                                                              16750000
.ERR7 MNOTE 8,' NO NON-MASTERS SPECIFIED AND NUMBER OF MASTERS IS NOT EX16800000
               QUAL TO 16 - GENERATION TERMINATED.'                     16850000
     MEXIT                                                              16900000
.ERR8 MNOTE 8,' ONLY ONE MACRO PCPTAB ALLOWED PER ASSEMBLY RUN - MACRO X16950000
               IGNORED.'                                                17000000
     MEXIT                                                              17050000
.ERR9 ANOP                                                              17100000
&K   SETA    256-&M*&M-&M           GET CORRECT COUNT OF NONMASTERS     17150000
     MNOTE  8,' &NM NON-MASTERS SPECIFIED WITH &M MASTERS, &K NON-MASTEX17200000
               RS ARE REQUIRED - GENERATION TERMINATED.'                17250000
     MEND                                                               17300000
