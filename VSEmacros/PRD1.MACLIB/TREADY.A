      MACRO                                                             00050000
&NAME    TREADY  &TASK=,                                               *00100000
               &PART=,                                                 *00150000
               &COND=,                                                 *00200000
               &PU=,                                                   *00250000
               &CBFADR=,                                               *00300000
               &RETURN=,                                               *00325001
               &CODE=                                                   00350000
.*                                                                      00400000
         LCLA  &FC                            FUNCTION CODE             00450000
.*                                                                      00500000
***************************************************************         00550000
*                                                             *@D51MDGL 00600000
.*       IBM VSE / ADVANCED FUNCTIONS                         *@D51MDGL 00650000
*        SUPERVISOR - TREADY  - 5686-CF7-06                             00700002
.*                                                                      00750000
         AGO   .CPRIGHT                                                 00800000
.*                                                                      00850000
*        LICENSED MATERIALS - PROGRAM PROPERTY OF IBM         *@D51MDGL 00900000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"          *@D51MDGL 00950000
*        5686-CF7 (C) COPYRIGHT IBM CORP. 1979, 2004          *@D51MDGL 01000002
*        SEE COPYRIGHT INSTRUCTIONS                           *@D51MDGL 01050000
.*                                                                      01100000
.CPRIGHT ANOP                                                           01150000
*                                                             *@D51MDGL 01200000
***************************************************************         01250000
.*                                                                      01300000
.**** START OF SPECIFICATIONS ***************************************** 01350000
.*                                                                      01400000
.*01* MODULE-TYPE = MACRO                                               01450000
.*                                                                      01500000
.*01* DESCRIPTIVE NAME = READY OR CANCEL A TASK                         01550000
.*                                                                      01600000
.*  STATUS           =  NEW MACRO - FIRST RELEASE 35.0                  01650000
.*                      EXTENDED - RELEASE 1.2                 @D369DWK 01700000
.*                      EXTENDED - RELEASE 1.3                 @D379DWK 01750000
.*                      EXTENDED - RELEASE 2.1                 @D149DWK 01800000
.*  FUNCTION         =  THE MACRO CHECKS THE USER SUPPLIED              01850000
.*                      OPERANDS AND LOADS THE NECESSARY                01900000
.*                      REGISTERS FOR SVC 107.                          01950000
.*  NOTES            =                                                  02000000
.*  DEPENDENCIES     =                                                  02050000
.*  RESTRICTIONS     =                                                  02100000
.*  REGISTER CONVENT.=  REGISTER 0  CONTAINS THE TASK ID.               02150000
.*                      REGISTER 1  CONTAINS THE CANCEL CODE            02200000
.*                                  IF APPLICABLE.                      02250000
.*                      REGISTER 15 CONTAINS THE FUNTION CODE.          02300000
.*  PATCH LABEL      =  NONE                                            02350000
.*  MACRO TYPE       =  ASSEMBLER                                       02400000
.*  PROCESSOR        =  ASM                                             02450000
.*  MACRO SIZE       =                                                  02500000
.*  ATTRIBUTES       =                                                  02550000
.*  ENTRY POINT      =  TREADY                                          02600000
.*  PURPOSE          =  SEE FUNCTION                                    02650000
.*  LINKAGE          =                                                  02700000
.*  CALLER           =                                                  02750000
.*  INPUT            =   (CALLING SEQUENCE)                             02800000
.*    1. ASM :                                                          02850000
.*        LABEL   TREADY   COND= LQ                        OR  @D379DWK 02900000
.*                         COND= VCANCEL                   OR  @D369DWK 02950000
.*                         COND= OCCF                      OR  @D379DFG 03000000
.*                         COND= OCANCEL                   OR  @D31..GN 03050000
.*                         COND= CRTSAV                    OR  @D379DWK 03100000
.*                         COND= HCFCB                     OR  @D379DWK 03150000
.*                         COND= ATTINT                    OR  @D379DWK 03200000
.*                         COND= ALLOCR                    OR  @D149DFG 03250000
.*                         COND= RSGT                          @D149DFG 03300000
.*                         TASK=(NAME1 | (R1) | (0) ),         @D379DWK 03350000
.*                           COND=( NO | IO | VTAM | ICCF|     @D379DWK 03400000
.*                                  OCCFIO )               OR  @D149DWK 03450000
.*                           COND=CANCEL,CODE=(NAME2|(R2)) OR  @D369DWK 03500000
.*                           COND=POWER,PU=(NAME2|(R2)|(1))OR  @D149DWK 03550000
.*                         PART=(NAME3 | (R3) ),               @D369DWK 03600000
.*                           COND=( START | OC )           OR  @D369DWK 03650000
.*                           COND=CANCEL,CODE=(NAME2|(R2)) OR  @D369DWK 03700000
.*                                                                      03750000
.*                      OPERANDS :                                      03800000
.*                      TASK = ID OF THE TASK TO BE POSTED              03850000
.*                             OR CANCELED OR ZERO                      03900000
.*                      PART = ID OF THE PARTITION TO BE       @D369DWK 03950000
.*                             STARTED OR CANCELED.            @D369DWK 04000000
.*                                                             @D379DWK 04050000
.*                      COND = NO                                       04100000
.*                             READY TASK UNCONDITIONALLY               04150000
.*                      COND = IO   (DEFAULT)                           04200000
.*                             READY TASK AFTER WAIT OR WAITM           04250000
.*                      COND = VTAM                                     04300000
.*                             READY TASK AFTER WAIT OR WAITM AND       04350000
.*                             INDICATE VTAM AP EXIT IS TO TAKE         04400000
.*                             AFTER NEXT DISPATCHING.                  04450000
.*                      COND = ICCF                            @D369DWK 04500000
.*                             READY TASK FROM THE ICCF WAIT-  @D369DWK 04550000
.*                             STATE                           @D369DWK 04600000
.*                      COND = OCCFIO                          @D379DWK 04650000
.*                             SYSLOG REQUEST SERVICED BY OCCF @D379DWK 04700000
.*                             POST TASK IF WAIT OR WAITM COND.@D379DWK 04750000
.*                      COND = CANCEL                                   04800000
.*                             CANCEL THE IDENTIFIED TASK               04850000
.*                      COND = LQ                                       04900000
.*                             READY TASKS WAITING FOR LOGGING          04950000
.*                      COND = VCANCEL                                  05000000
.*                             CANCEL ALL TASK RUNNING WITH VTAM        05050000
.*                      COND = OCCF                            @D379DFG 05100000
.*                             READY TASKS WAITING FOR OCCF    @D379DFG 05150000
.*                      COND = OCANCEL                         @D31..GN 05200000
.*                             CANCEL OCCF MAINTASK            @D31..GN 05250000
.*                      COND = CRTSAV                          @D379DWK 05300000
.*                             READY TASKS WAITING FOR SAVED   @D379DWK 05350000
.*                             CRT WORKING AREA                @D379DWK 05400000
.*                      COND = HCFCB                           @D379DWK 05450000
.*                             READY TASKS WAITING FOR         @D379DWK 05500000
.*                             HARD COPY FILE CONTROL BLOCK    @D379DWK 05550000
.*                      COND = ATTINT                          @D379DWK 05600000
.*                             SIMULATE ATTENTION INTERRUPT    @D379DWK 05650000
.*                      COND = ALLOCR                          @D149DFG 05700000
.*                             POST TASK WAITING FOR ACCESS    @D149DFG 05750000
.*                             TO THE SYSTEM 'REAL PARTITION'  @D149DFG 05800000
.*                      COND = RSGT                            @D149DFG 05850000
.*                             POST TASK WAITING FOR ACCESS    @D149DFG 05900000
.*                             TO THE SEGM.TABLE OF R SPACE    @D149DFG 05950000
.*                      COND = START                           @D369DWK 06000000
.*                             REMOVE PARTITION FROM UNBATCH   @D369DWK 06050000
.*                             OR STOP STATE                   @D369DWK 06100000
.*                      COND = OC                              @D369DWK 06150000
.*                             ACTIVATE OPERATOR COMMUNICATION @D369DWK 06200000
.*                             EXIT FOR SPECIFIED PARTITION    @D369DWK 06250000
.*                      COND = CANCEL                          @D369DWK 06300000
.*                             CANCEL THE MAINTASK OF THE SPE- @D369DWK 06350000
.*                             CIFIED PARTITION AND RELATED    @D369DWK 06400000
.*                             SUBTASKS.                       @D369DWK 06450000
.*                      CODE = CANCEL CODE                              06500000
.*                             VALID ONLY FOR COND=CANCEL               06550000
.*                      COND = POWER                           @D149DWK 06600000
.*                             POST OWNER OF THE SPOOLED DEVICE@D149DWK 06650000
.*                             (PU=) AND RESET OWNER FIELD     @D149DWK 06700000
.*                                                             @D149DWK 06750000
.*                      PU   = PUB INDEX OF DEVICE             @D149DWK 06800000
.*                                                                      06850000
.*  OUTPUT(ASSEMBLER)= NECESSARY INSTRUCTIONS TO LOAD THE               06900000
.*                     FOLLOWING REGISTER :                             06950000
.*                                                                      07000000
.*           REGISTER 0  : TASK ID        X'0000NNNN'                   07050000
.*                                                                      07100000
.*           REGISTER 1  : CODE = CC      X'000000CC'                   07150000
.*                                                                      07200000
.*           REGISTER 2  : CODE = CC      X'CCRRRRRR'                   07210001
.*                                        ONLY FOR 'TREADY COND=CANCEL' 07211001
.*                                        AND IF REG1=X'FE'    @DY45849 07212001
.*                                                                      07220001
.*           REGISTER 15 : COND = NN      X'000000XX'          @D369DWK 07250000
.*                                                             @D369DWK 07300000
.* ------------------------------------------------------      @D149DWK 07350000
.* | COND    | TASK | PART | CODE |  PU  | CBF. | FCODE |      @D149DWK 07400000
.* ------------------------------------------------------      @D149DWK 07450000
.* | LQ      | IGN. | ERR. | IGN. | IGN. | IGN  |  00   |      @D149DWK 07500000
.* | NO      |  R   | ERR. | IGN. | IGN. | IGN  |  01   |      @D149DWK 07550000
.* | IO      |  R   | ERR. | IGN. | IGN. | IGN  |  02   |      @D149DWK 07600000
.* | VTAM    |  R   | ERR. | IGN. | IGN. |      |  03   |      @D149DWK 07650000
.* | CANCEL  |  R   | ERR. |  R   | IGN. |      |  04   |      @D149DWK 07700000
.* | VCANCEL | IGN. | ERR. | IGN. | IGN. |      |  05   |      @D149DWK 07750000
.* | START   | ERR. |  R   | IGN. | IGN. |      |  13   |      @D149DWK 07800000
.* | OC      | ERR. |  R   | IGN. | IGN. |      |  14   |      @D149DWK 07850000
.* | CANCEL  | ERR. |  R   |  R   | IGN. |      |  15   |      @D149DWK 07900000
.* | ICCF    |  R   | ERR. | IGN. | IGN. |      |  29   |      @D149DWK 07950000
.* | OCCF    | IGN. | ERR. | IGN. | IGN. |      |  36   |      @D149DWK 08000000
.* | CRTSAV  | IGN. | ERR. | IGN. | IGN. |      |  39   |      @D149DWK 08050000
.* | HCFCB   | IGN. | ERR. | IGN. | IGN. |      |  3A   |      @D149DWK 08100000
.* | ATTINT  | IGN. | ERR. | IGN. | IGN. |      |  3B   |      @D149DWK 08150000
.* | OCCFIO  |  R   | ERR. | IGN. | IGN. |      |  3C   |      @D149DWK 08200000
.* | POWER   |  R   | ERR. | IGN. |  R   |      |  4D   |      @D149DWK 08250000
.* | ALLOCR  | IGN  | ERR. | IGN. | IGN. |      |  5F   |      @D149DFG 08300000
.* | RSGT    | IGN  | ERR. | IGN. | IGN. |      |  60   |      @D149DFG 08350000
.* | OCANCEL | IGN  | ERR. | IGN. | IGN. |      |  6B   |      @D31..GN 08400000
.* ------------------------------------------------------      @D149DWK 08450000
.*                                                                      08500000
.*           ERR. : MNOTE WITH RC 8 WRITTEN IF SPECIFIED       @D369DWK 08550000
.*                  MNOTE WITH RC 8 ALSO WRITTEN WHEN R-       @D369DWK 08600000
.*                  PARAMETER (REQUIRED) NOT SPECIFIED         @D369DWK 08650000
.*           IGN. : MNOTE WITH RC 1 WRITTEN IF SPECIFIED       @D369DWK 08700000
.*           DEFAULT COND PARAMETER IS                         @D369DWK 08750000
.*                   COND = IO IF TASK SPECIFIED               @D369DWK 08800000
.*                   COND = START IF PART SPECIFIED            @D369DWK 08850000
.*                                                                      08900000
.*  MACROS GENERATING SVC 107 :                                @D149DWK 08950000
.*         GETFLD,   MODFLD,   TREADY,   TSTOP,   RLOCK,       @D149DWK 09000000
.*         SRCHFLD,  DEVUSE,   DEVREL,   SENTER,  SLEAVE,      @D149DWK 09050000
.*         VIOPOINT                                            @D149DWK 09100000
.*                                                                      09150000
.* ====> CHECK SUPERVISOR GENERATION MACRO SGTINF FOR HIGHEST  @D149DWK 09200000
.* ====> USED FUNCTION CODE WHEN EXTENDING TREADY              @D149DWK 09250000
.*                                                                      09300000
.*  OUTPUT(EXECUTION)=  TASK CONTROLBLOCKS CHANGED TO THE               09350000
.*                      REQUESTED STATUS.                               09400000
.*                                                                      09450000
.*  EXIT NORMAL      =  COMPILER :                                      09500000
.*                      MACRO EXPANSION                                 09550000
.*                      EXECUTION:                                      09600000
.*                      CONTROL RETURNED TO USER                        09650000
.*                                                                      09700000
.*  EXIT ERROR       =  COMPILER :                                      09750000
.*                      MNOTES INDICATING THE ERROR                     09800000
.*                      EXECUTION:                                      09850000
.*                      NO RETURN CODE SET                              09900000
.*                                                                      09950000
.*                      ILLEGAL SVC IS FORCED WHEN                      10000000
.*                      - REQUESTOR HAS NOT KEY ZERO OR                 10050000
.*                        THE TIK DOES NOT EQUAL THAT IN THE LOGGING    10100000
.*                        TABLE                                         10150000
.*                      - PASSED PARAMETERS INVALID                     10200000
.*                                                                      10250000
.*  EXTERNAL REFEREN.=  NONE                                            10300000
.*  ROUTINES         =  SVC 107 (SUPERVISOR GENERATION MACRO SGTINF)    10350000
.*  DATA AREAS       =  NONE                                            10400000
.*  CONTROL BLOCKS   =  NONE                                            10450000
.*  TABLES           =  NONE                                            10500000
.*  MACROS           =  NONE                                            10550000
.*  CHANGE ACTIVITIES=  NEW MACRO   FIRST RELEASE 35.0                  10600000
.*  CHANGE 01        =  SECURITY HOOKS IN RELEASE 35.0         @DC44DWK 10650000
.*  CHANGE 02        =  RELEASE 1.2  ADAPTION                  @D369DWK 10700000
.*  CHANGE 03        =  RELEASE 1.3  EXTENSIONS                @D379DWK 10750000
.*  CHANGE 04        =  RELEASE 2.1  EXTENSIONS                @D149DWK 10800000
.*  CHANGE 05        =  510 CBFADR PARAMETER FOR COND=OCCFIO   @D51GDGL 10850000
.*  CHANGE 06        =  RETURN=YES FOR CONDITION POWER         @D51RMGL 10875001
.*  CHANGE 07        =  RETURN=YES FOR CONDITION VTAM          @DA41626 10887501
.*  CHANGE 08        =  RETURN=YES FOR CONDITION IO            @D52VDGL 10893701
.*  MODIFIED REGISTER=  REGISTER 0,1 AND 15                             10900000
.*                                                                      10950000
.*  A000000-999999                                             @D352DWK 11000000
.*                                                                      11050000
.** END OF SPECICATIONS *********************************************** 11100000
.*                                                                      11150000
.********************************************************************** 11200000
.*                                                                      11250000
.* MACRO DIAGNOSTIC                                                     11300000
.*                                                                      11350000
.**************************************************************@D51GDGL 11400000
.* CHECK FOR OPERAND COND=CANCEL                               @D51GDGL 11450000
.**************************************************************@D51GDGL 11500000
         AIF   (T'&CBFADR EQ 'O').NOCBF                        @D51GDGL 11550000
         AIF   ('&COND' EQ 'OCCFIO').NOCBF                     @D51GDGL 11600000
         MNOTE 8,'CBFADR OPERAND ILLEGAL FOR COND = &COND'     @D51GDGL 11650000
         MEXIT                                                 @D51GDGL 11700000
.NOCBF   ANOP                                                  @D51GDGL 11750000
.**************************************************************@D51GDGL 11753801
.* CHECK FOR OPERAND RETURN= FOR COND=POWER,VTAM OR IO         @D51RMGL 11757601
.**************************************************************@D51RMGL 11761401
         AIF   (T'&RETURN EQ 'O').LEGRET                       @D51RMGL 11765201
         AIF   ('&COND' EQ 'IO').TSTRET                        @DA41626 11766101
         AIF   ('&COND' EQ 'VTAM').TSTRET                      @DA41626 11767101
         AIF   ('&COND' NE 'POWER').ILLRET                     @D51RMGL 11769001
.TSTRET  AIF   ('&RETURN' EQ 'YES').LEGRET                     @DA41626 11772801
         AIF   ('&RETURN' EQ 'NO').LEGRET                      @D51RMGL 11776601
         MNOTE 8,'RETURN = &RETURN NOT PERMITTED'              @D51RMGL 11780401
         MEXIT                                                 @D51RMGL 11784201
.ILLRET  MNOTE 8,'RETURN OPERAND ILLEGAL FOR COND = &COND'     @D51RMGL 11788001
         MEXIT                                                 @D51RMGL 11791801
.LEGRET  ANOP                                                  @D51RMGL 11795601
.**************************************************************@D369DWK 11800000
.*                                                             @D369DWK 11850000
.* CHECK FOR OPERAND COND=CANCEL                               @D369DWK 11900000
.*                                                             @D369DWK 11950000
.**************************************************************@D369DWK 12000000
         AIF   ('&COND' NE 'CANCEL').CHCONDO  NOT CANCEL,SKIP  @D36IDWK 12050000
         AIF   (T'&CODE EQ 'O').NOTE01        CODE IS REQUIRED @D36IDWK 12100000
         AIF   ('&CODE' EQ '(0)').NOTE02      REG.0 IS INVALID @D36IDWK 12150000
         AIF   (T'&PART EQ 'O').CHKFC04       THEN CHECK TASK  @D36IDWK 12200000
         AIF   (T'&TASK EQ 'O').SETFC15       OK,GO SET FC 15  @D36IDWK 12250000
         AGO   .NOTE03                        TASK A.PART SPEC.@D36IDWK 12300000
.CHKFC04 AIF   (T'&TASK EQ 'O').NOTE04        TASK OR PART REQ.@D36IDWK 12350000
         AGO   .SETFC04                       OK,GO SET FC 04  @D36IDWK 12400000
.**************************************************************@D369DWK 12450000
.*                                                             @D369DWK 12500000
.* CHECK FOR OPERAND COND=OMITTED                              @D369DWK 12550000
.*                                                             @D369DWK 12600000
.**************************************************************@D369DWK 12650000
.CHCONDO AIF   (T'&COND NE 'O').CHCONDL       NOT OMITTED,SKIP @D36IDWK 12700000
         AIF   (T'&CODE EQ 'O').CHKFC13       CODE OMITTED     @D36IDWK 12750000
.WNOTE01 MNOTE 1,'CODE OPERAND IGNORED FOR COND = OMITTED'     @D36IDWK 12800000
.CHKFC13 AIF   (T'&PART EQ 'O').CHKFC02       THEN CHECK TASK  @D36IDWK 12850000
         AIF   (T'&TASK EQ 'O').SETFC13       OK,GO SET FC 13  @D36IDWK 12900000
         AGO   .NOTE03                        TASK A.PART SPEC.@D36IDWK 12950000
.CHKFC02 AIF   (T'&TASK EQ 'O').NOTE09        TASK OR PART REQ.@D36IDWK 13000000
         AGO   .SETFC02                       OK,GO SET FC 02  @D36IDWK 13050000
.**************************************************************@D369DWK 13100000
.*                                                             @D369DWK 13150000
.* CHECK COND OPERANDS WHICH DOES NOT REQUIRE PART OR TASK OP. @D379DWK 13200000
.*                                                             @D369DWK 13250000
.**************************************************************@D369DWK 13300000
.CHCONDL AIF   ('&COND' NE 'LQ').CHVCAN       NOT LQ           @D37CDWK 13350000
&FC      SETA  0                              COND = LQ        @D37CDWK 13400000
         AGO   .CHTASK                        CHECK OTHER OPER.@D37CDWK 13450000
.CHVCAN  AIF   ('&COND' NE 'VCANCEL').CHOCCF  NOT VCANCEL      @D37CDWK 13500000
&FC      SETA  5                              COND = VCANCEL   @D37CDWK 13550000
         AGO   .CHTASK                        CHECK OTHER OPER.@D37CDWK 13600000
.CHOCCF  AIF   ('&COND' NE 'OCCF').CHCRTS     NOT OCCF         @D37CDWK 13650000
&FC      SETA  54                             COND = OCCF      @D37CDWK 13700000
         AGO   .CHTASK                        CHECK OTHER OPER.@D37CDWK 13750000
.CHCRTS  AIF   ('&COND' NE 'CRTSAV').CHHCFC   NOT CRTSAV       @D37CDWK 13800000
&FC      SETA  57                             COND = CRTSAV    @D37CDWK 13850000
         AGO   .CHTASK                        CHECK OTHER OPER.@D37CDWK 13900000
.CHHCFC  AIF   ('&COND' NE 'HCFCB').CHATIN    NOT HCFCB        @D37CDWK 13950000
&FC      SETA  58                             COND = HCFCB     @D37CDWK 14000000
         AGO   .CHTASK                        CHECK OTHER OPER.@D37CDWK 14050000
.CHATIN  AIF   ('&COND' NE 'ATTINT').CHALCR   NOT ATTINT       @D14NDFG 14100000
&FC      SETA  59                             COND = ATTINT    @D37CDWK 14150000
         AGO   .CHTASK                        CHECK OTHER OPER.@D14NDFG 14200000
.CHALCR  AIF   ('&COND' NE 'ALLOCR').CHRSGT   NOT ALLOCR       @D14NDFG 14250000
&FC      SETA  95                             COND = ALLOCR    @D14NDFG 14300000
         AGO   .CHTASK                        CHECK OTHER OPER.@D14NDFG 14350000
.CHRSGT  AIF   ('&COND' NE 'RSGT').CHOCNCL    NOT RSGT         @D31..GN 14400000
&FC      SETA  96                             COND = RSGT      @D14NDFG 14450000
         AGO   .CHTASK                        CHECK OTHER OPER.@D31..GN 14500000
.CHOCNCL AIF   ('&COND' NE 'OCANCEL').CHKOTH  NOT OCANCEL      @D31..GN 14550000
&FC      SETA  107                            COND = OCANCEL   @D31..GN 14600000
.*-------------------------------------------------------------@D379DWK 14650000
.* CHECK TASK, PART AND CODE OPERAND                           @D379DWK 14700000
.*-------------------------------------------------------------@D379DWK 14750000
.CHTASK  AIF   (T'&TASK EQ 'O').CODEIGN       TASK OMITTED     @D37CDWK 14800000
.WNOTE02 MNOTE 1,'TASK OPERAND IGNORED FOR COND = &COND '               14850000
.CODEIGN AIF   (T'&CODE EQ 'O').PARTINV       CODE OMITTED     @D36IDWK 14900000
.WNOTE03 MNOTE 1,'CODE OPERAND IGNORED FOR COND = &COND '               14950000
.PARTINV AIF   (T'&PART NE 'O').NOTE05        PART NOT OMITTED @D36IDWK 15000000
.*-------------------------------------------------------------@D379DWK 15050000
.* CHECK AND SET LABEL                                         @D379DWK 15100000
.*-------------------------------------------------------------@D379DWK 15150000
         AIF   (T'&NAME EQ 'O').GENFC         SET FUNCTION CODE@D37CDWK 15200000
&NAME    DC    0H'0'                                           @D37CDWK 15250000
         AGO   .GENFC                         SET FUNCTION CODE@D37CDWK 15300000
.**************************************************************@D369DWK 15350000
.*                                                             @D369DWK 15400000
.* CHECK COND OPERANDS WHICH REQUIRE PART OR TASK OPERAND      @D379DWK 15450000
.*                                                             @D369DWK 15500000
.**************************************************************@D369DWK 15550000
.CHKOTH  AIF   ('&COND' EQ 'NO').CHKTASK      COND=NO    SPEC. @D36IDWK 15600000
         AIF   ('&COND' EQ 'IO').CHKTASK      COND=IO    SPEC. @D36IDWK 15650000
         AIF   ('&COND' EQ 'VTAM').CHKTASK    COND=VTAM  SPEC. @D36IDWK 15700000
         AIF   ('&COND' EQ 'ICCF').CHKTASK    COND=ICCF  SPEC. @D36GDWK 15750000
         AIF   ('&COND' EQ 'OCCFIO').CHKTASK  COND=OCCFIO SPEC.@D37CDWK 15800000
         AIF   ('&COND' EQ 'START').CHKPART   COND=START SPEC. @D36IDWK 15850000
         AIF   ('&COND' EQ 'OC').CHKPART      COND=OC    SPEC. @D36IDWK 15900000
         AIF   ('&COND' EQ 'POWER').CHKTASK   COND=POWER SPEC. @D14CDWK 15950000
         AGO   .NOTE06                        WRITE MNOTE      @D36IDWK 16000000
.CHKTASK AIF   (T'&TASK EQ 'O').NOTE07        TASK OPERAND REQ.@D36IDWK 16050000
         AIF   (T'&PART EQ 'O').CHCODE        CHECK CODE OPER. @D36IDWK 16100000
         AGO   .NOTE03                        TASK A.PART INVAL@D36IDWK 16150000
.CHKPART AIF   (T'&PART EQ 'O').NOTE08        PART OPERAND REQ.@D36IDWK 16200000
         AIF   (T'&TASK NE 'O').NOTE03        TASK PARM INVALID@D36IDWK 16250000
.CHCODE  AIF   (T'&CODE EQ 'O').CHPU          CODE OMITTED     @D14BDWK 16300000
.WNOTE04 MNOTE 1,'CODE OPERAND IGNORED FOR COND = &COND '               16350000
.CHPU    AIF   ('&COND' NE 'POWER').CHPUOM                     @D14BDWK 16400000
         AIF   (T'&PU NE 'O').SETFC77         COND=POWER       @D14BDWK 16450000
         AGO   .NOTE10                        PU MANDANTORY    @D14BDWK 16500000
.CHPUOM  AIF   (T'&PU EQ 'O').CHCOND          PU OMTTED        @D14BDWK 16550000
.WNOTE05 MNOTE 1,'PU OPERAND IGNORED FOR COND = &COND '        @D14BDWK 16600000
.CHCOND  AIF   ('&COND' EQ 'NO').SETFC01      COND=NO          @D36IDWK 16650000
         AIF   ('&COND' EQ 'IO').SETFC02      COND=IO          @D36IDWK 16700000
         AIF   ('&COND' EQ 'VTAM').SETFC03    COND=VTAM        @D36IDWK 16750000
         AIF   ('&COND' EQ 'ICCF').SETFC29    COND=ICCF  SPEC. @D36GDWK 16800000
         AIF   ('&COND' EQ 'START').SETFC13   COND=START SPEC. @D36IDWK 16850000
         AIF   ('&COND' EQ 'OCCFIO').SETFC60  COND=OCCFIO SPEC.@D37CDWK 16900000
         AGO   .SETFC14                       COND=OC    SPEC. @D36IDWK 16950000
.********************************************************************** 17000000
.*                                                                      17050000
.*  MACRO DIAGNOSTICS: MNOTES RC = 8                           @D369DWK 17100000
.*                                                                      17150000
.********************************************************************** 17200000
.NOTE01  MNOTE 8,'CODE-OPERAND REQUIRED FOR COND=CANCEL '               17250000
         MEXIT                                                          17300000
.NOTE02  MNOTE 8,'REGISTER 0 MUST NOT BE USED FOR THE CODE OPERAND '    17350000
         MEXIT                                                          17400000
.NOTE03  MNOTE 8,'TASK- AND PART-OPERAND SPECIFICATION INVALID'         17450000
         MEXIT                                                 @D36IDWK 17500000
.NOTE04  MNOTE 8,'TASK- OR PART- OPERAND REQUIRED FOR COND= &COND '     17550000
         MEXIT                                                 @D36IDWK 17600000
.NOTE05  MNOTE 8,'PART-OPERAND MUST NOT BE SPECIFIED FOR COND= &COND '  17650000
         MEXIT                                                 @D36IDWK 17700000
.NOTE06  MNOTE 8,'UNKNOWN COND-OPERAND COND= &COND SPECIFIED '          17750000
         MEXIT                                                 @D36IDWK 17800000
.NOTE07  MNOTE 8,'TASK OPERAND REQUIRED FOR COND= &COND'                17850000
         MEXIT                                                 @D36IDWK 17900000
.NOTE08  MNOTE 8,'PART OPERAND REQUIRED FOR COND= &COND'                17950000
         MEXIT                                                 @D36IDWK 18000000
.NOTE09  MNOTE 8,'PART OPERAND REQUIRED FOR DEFAULT COND = START OR'    18050000
         MNOTE 'TASK OPERAND REQUIRED FOR DEFAULT COND = IO '           18100000
         MEXIT                                                 @D36IDWK 18150000
.NOTE10  MNOTE 8,'PU OPERAND REQUIRED FOR COND= &COND'         @D14BDWK 18200000
         MEXIT                                                 @D14BDWK 18250000
.NOTE11  MNOTE 8,'REGISTER 1 MUST NOT BE USED FOR THE TASK OPERAND '    18300000
         MEXIT                                                 @D14BDWK 18350000
.********************************************************************** 18400000
.*                                                                      18450000
.* SET FUNCTION CODE                                                    18500000
.*                                                                      18550000
.********************************************************************** 18600000
.SETFC01 ANOP                                                           18650000
&FC      SETA  1                              COND = NO                 18700000
         AGO   .CHKLBL                        CHECK AND SET LABEL       18750000
.SETFC02 ANOP                                                           18800000
&FC      SETA  2                              COND = IO OR OMITTED      18850000
         AGO   .CHKLBL                        CHECK AND SET LABEL       18900000
.SETFC03 ANOP                                                           18950000
&FC      SETA  3                              COND = VTAM               19000000
         AGO   .CHKLBL                        CHECK AND SET LABEL       19050000
.SETFC04 ANOP                                                           19100000
&FC      SETA  4                              COND = CANCEL TASK        19150000
         AGO   .CHKLBL                        CHECK AND SET LABEL       19200000
.SETFC13 ANOP                                                  @D36IDWK 19250000
&FC      SETA  19                             COND=START  PART.@D36IDWK 19300000
         AGO   .CHKLBL                        CHECK LABEL      @D36IDWK 19350000
.SETFC14 ANOP                                                  @D36IDWK 19400000
&FC      SETA  20                             COND = OC        @D36IDWK 19450000
         AGO   .CHKLBL                        CHECK LABEL      @D36IDWK 19500000
.SETFC15 ANOP                                                  @D36IDWK 19550000
&FC      SETA  21                             COND=CANCEL PART.@D36IDWK 19600000
         AGO   .CHKLBL                        CHECK LABEL      @D36GDWK 19650000
.SETFC29 ANOP                                                  @D36GDWK 19700000
&FC      SETA  41                             COND=ICCF        @D36GDWK 19750000
         AGO   .CHKLBL                        CHECK LABEL      @D36GDWK 19800000
.SETFC60 ANOP                                                  @D37CDWK 19850000
&FC      SETA  60                             COND=OCCFIO      @D37CDWK 19900000
         AGO   .CHKLBL                        CHECK LABEL      @D36GDWK 19950000
.SETFC77 ANOP                                                  @D14CDWK 20000000
&FC      SETA  77                             COND=POWER       @D14CDWK 20050000
.********************************************************************** 20100000
.*                                                                      20150000
.* CHECK AND SET LABEL                                                  20200000
.*                                                                      20250000
.********************************************************************** 20300000
.CHKLBL  ANOP                                                           20350000
         AIF   (T'&NAME EQ 'O').NOLBL         IF LABEL NOT SPECIFIED    20400000
&NAME    DC    0H'0'                                           @D36IDWK 20450000
.NOLBL   ANOP                                 THEN CHECK FC             20500000
         AIF   (&FC EQ 77).SETPU              SET PU OPERAND   @D14BDWK 20550000
         AIF   (&FC GT 21).SETTASK            SET TASK OPERAND @D37CDWK 20600000
         AIF   (&FC GT 5).SETPART             PART REQUEST     @D36IDWK 20650000
         AGO   .SETTASK                       SET TASK OPERAND @D36IDWK 20700000
.*-------------------------------------------------------------@D149DWK 20750000
.*                                                             @D149DWK 20800000
.* SET PU OPERAND                                              @D149DWK 20850000
.*                                                             @D149DWK 20900000
.*-------------------------------------------------------------@D149DWK 20950000
.SETPU   ANOP                                                  @D14BDWK 21000000
         AIF   ('&TASK' EQ '(1)').NOTE11      REG.1 DESTROYED  @D14BDWK 21050000
         AIF   ('&PU'(1,1) EQ '(').RPU        REGISTER NOTATION@D14BDWK 21100000
         LH    1,&PU                          PUB INDEX        @D14BDWK 21150000
         AGO   .SETTASK                       SET TASK OPERAND @D14BDWK 21200000
.RPU     ANOP                                                  @D14BDWK 21250000
         AIF   ('&PU' EQ '(1)').SETTASK       PU ALLREADY LOAD @D14BDWK 21300000
         LR    1,&PU(1)                       PUB INDEX        @D14BDWK 21350000
.********************************************************************** 21400000
.*                                                                      21450000
.* SET TASK OPERAND                                                     21500000
.*                                                                      21550000
.********************************************************************** 21600000
.SETTASK ANOP                                                           21650000
         AIF   ('&TASK'(1,1) EQ '(').RTASK    REGISTER NOTATION USED    21700000
         LH    0,&TASK                        LOAD TASK ID              21750000
         AGO   .CHKFC                         GO, SET CODE OPERAND      21800000
.RTASK   ANOP                                 HANDLE REGISTER NOTATION  21850000
         AIF   ('&TASK' EQ '(0)').CHKFC       REG. 0 ALLREADY LOADED    21900000
         LR    0,&TASK(1)                     LOAD TASK ID              21950000
         AGO   .CHKFC                         SET CODE OPERAND @D36IDWK 22000000
.**************************************************************@D369DWK 22050000
.*                                                             @D369DWK 22100000
.* SET PART OPERAND                                            @D369DWK 22150000
.*                                                             @D369DWK 22200000
.**************************************************************@D369DWK 22250000
.SETPART ANOP                                                  @D36IDWK 22300000
         AIF   ('&PART'(1,1) EQ '(').RPART    REGISTER NOTATION@D36IDWK 22350000
         LH    0,&PART                        LOAD PART ID     @D36IDWK 22400000
         AGO   .CHKFC                         SET CODE OPERAND @D36IDWK 22450000
.RPART   ANOP                                                  @D36IDWK 22500000
         AIF   ('&PART' EQ '(0)').CHKFC       ALLREADY LOADED  @D36IDWK 22550000
         LR    0,&PART(1)                     LOAD PART ID     @D36IDWK 22600000
.********************************************************************** 22650000
.*                                                                      22700000
.* SET CODE OPERAND                                                     22750000
.*                                                                      22800000
.********************************************************************** 22850000
.CHKFC   ANOP                                                           22900000
         AIF   (&FC NE 4 AND &FC NE 21).CHCBF COND NOT CANCEL  @D36IDWK 22950000
         AIF   ('&CODE' EQ '(1)').GENFC       REGISTER ALLREADY LOADED  23000000
         AIF   ('&CODE'(1,1) EQ '(').RCODE    REGISTER NOTATION USED    23050000
         SLR   1,1                            CLEAR REGISTER ONE AND    23100000
         IC    1,&CODE                        INSERT CANCEL CODE        23150000
         AGO   .GENFC                         GO, SET COND OPERAND      23200000
.RCODE   ANOP                                 HANDLE REGISTER NOTATION  23250000
         LR    1,&CODE(1)                     LOAD CANCEL CODE          23300000
.**************************************************************@D51GDGL 23350000
.*                                                             @D51GDGL 23400000
.*  CHECK AND SET CBFADR PARAMETER                             @D51GDGL 23450000
.*                                                             @D51GDGL 23500000
.**************************************************************@D51GDGL 23550000
.CHCBF   ANOP                                                  @D51GDGL 23600000
         AIF   (&FC NE 60).GENFC                               @D51GDGL 23650000
         AIF   (T'&CBFADR  EQ 'O').CHCBF2                      @D51GDGL 23700000
         AIF   ('&CBFADR' EQ '(1)').GENFC                      @D51GDGL 23750000
         AIF   ('&CBFADR'(1,1) EQ '(').CHCBF1                  @D51GDGL 23800000
         L     1,&CBFADR                                       @D51GDGL 23850000
         AGO   .GENFC                                          @D51GDGL 23900000
.CHCBF1  ANOP                                                  @D51GDGL 23950000
         LR    1,&CBFADR(1)                                    @D51GDGL 24000000
         AGO   .GENFC                                          @D51GDGL 24050000
.CHCBF2  ANOP                                                  @D51GDGL 24100000
         SLR   1,1                                             @D51GDGL 24150000
.********************************************************************** 24200000
.*                                                                      24250000
.* GENERATE FUNCTION CODE AND SVC                                       24300000
.*                                                                      24350000
.********************************************************************** 24400000
.GENFC   ANOP                                                           24450000
         AIF   ('&RETURN' NE 'YES').NORET                      @D51RMGL 24456201
         ICM   15,15,*+8                                       @D51RMGL 24462401
         B     *+8                                             @D51RMGL 24468601
         DC    X'80',AL3(&FC)                                  @D51RMGL 24474801
         SVC   107                           HANDLE REQUEST    @D51RMGL 24481001
         MEXIT                                                 @D51RMGL 24487201
.NORET   ANOP                                                  @D51RMGL 24493401
         LA    15,&FC.(,0)                   SET FUNCTION CODE @D14ZDWK 24500000
         SVC   107                           HANDLE REQUEST             24550000
         MEND                                                        */ 24600000
