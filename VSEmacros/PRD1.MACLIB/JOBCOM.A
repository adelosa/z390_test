         MACRO                                                          00050000
&NAME    JOBCOM &FUNCT=,&AREA=,&LENGTH=                                 00100000
         LCLA  &KAP1                                                    00150000
         LCLA  &KAP2                                                    00200000
&KAP1    SETA  K'&AREA                          GET LENGTH OF PARM      00250000
&KAP2    SETA  K'&LENGTH                        GET LENGTH OF PARM      00300000
.*                                                                      00350000
*  JOB CONTROL - JOBCOM - 5686-007-06-C44                               00400000
          AGO   .ZZ                                                     00450000
*        IBM DISK OPERATING SYSTEM                                      00500000
**************************************************************          00550000
* @@COPY                                                     *          00600000
*                                                            *          00650000
*   THIS MACRO IS 'RESTRICTED MATERIALS OF IBM"              *          00700000
*   5686-007 (C) COPYRIGHT IBM CORP. 1979,1989               *          00750000
*   ALL RIGHTS RESERVED                                      *          00800000
*   LICENSED MATERIALS-PROPERTY OF IBM                       *          00850000
*   SEE COPYRIGHT INSTRUCTIONS, G120-2083                    *          00900000
*                                                            *          00950000
**************************************************************          01000000
.ZZ       ANOP                                                          01050000
.*********************************************************************  01100000
.* MACRO NAME: JOBCOM (DOS/VS)                                          01150000
.*                                                                      01200000
.* DESCRIPTIVE NAME: INTERFACE MACRO TO SYMBOLIC LABEL ACCESS           01250000
.*                   USED FOR JOB TO JOB COMMUNICATION                  01300000
.*                                                                      01350000
.* STATUS: RELEASE 35, RELEASE 1.0                                      01400000
.*                                                                      01450000
.* FUNCTION:                                                            01500000
.*      - CREATE A PARAMETER LIST:                                      01550000
.*        4 BYTES CONTAINING ADDRESS OF INTERFACE FIELD                 01600000
.*        2 BYTES CONTAINING LENGTH OF INTERFACE FIELD                  01650000
.*      - LOAD ADDRESS OF PARAMETER LIST INTO REGISTER 1                01700000
.*      - LOAD ADDRESS OF SLA PROGRAM INTO REGISTER 15                  01750000
.*      - LOAD FUNCTION CODE INTO REGISTER 0                            01800000
.*      - LOAD RETURN ADDRESS INTO REGISTER 14                          01850000
.*      - BRANCH TO SYMBOLIC LABEL ACCESS                               01900000
.*                                                                      01950000
.* DEPENDENCIES: THOSE OF SLA PROGRAM WHICH IS CALLED                   02000000
.*                                                                      02050000
.* RESTRICTIONS: THOSE OF SLA PROGRAM WHICH IS CALLED                   02100000
.*                                                                      02150000
.* PROCESSOR: ASSEMBLER                                                 02200000
.*                                                                      02250000
.* MODULE SIZE: NO SIZE LIMITATIONS                                     02300000
.*                                                                      02350000
.* ATTRIBUTES: REENTRANT                                                02400000
.*                                                                      02450000
.* REGISTER CONVENTIONS:                                                02500000
.*    REG. 13 MUST CONTAIN THE ADDRESS OF A REGISTER SAVE AREA          02550000
.*    REG. 14 - 1 ARE DESTROYED                                         02600000
.*                                                                      02650000
.* INPUT:                                                               02700000
.* <NAME>   JOBCOM     FUNCT = PUTCOM / GETCOM,                         02750000
.*                     AREA  = ADDRESS / (REGISTER),                    02800000
.*                     LENGTH = LENGTH / (REGISTER)                     02850000
.*                                                                      02900000
.* PARAMETER DESCRIPTION                                                02950000
.*                                                                      03000000
.* FUNCT= PUTCOM / GETCOM                                               03050000
.*      PUTCOM      SAVE INFORMATION FOR JOB TO JOB COMMUNICATION       03100000
.*      GETCOM      GET  INFORMATION FOR JOB TO JOB COMMUNICATION       03150000
.*                                                                      03200000
.* AREA = ADDRESS / (REGISTER)                                          03250000
.*      THIS KEYWORD OPERAND SPECIFIES  THE ADDRESS  OF THE INTERFACE   03300000
.*      FIELD, CONTAINING THE JOB TO JOB COMMUNICATION INFORMATION.     03350000
.*      IF ADDRESS IS SPECIFIED, THE OPERAND CONTAINS THE ASSEMBLER     03400000
.*      LABEL OF THE INTERFACE FIELD.                                   03450000
.*      IF (REGISTER)  IS  SPECIFIED  THE ITEM  WITHIN  THE             03500000
.*      BRACKETS SPECIFIES A  REGISTER THAT CONTAINS THE  ADDRESS OF    03550000
.*      THE  INTERFACE FIELD AT EXECUTION TIME.                         03600000
.*                                                                      03650000
.* LENGTH = LENGTH / (REGISTER)                                         03700000
.*      THIS KEYWORD OPERAND SPECIFIES THE LENGTH OF THE INTERFACE      03750000
.*      FIELD IN BYTES.                                                 03800000
.*      IF LENGTH IS SPECIFIED, THE OPERAND CONTAINS A DECIMAL VALUE    03850000
.*      UP TO 256 (BYTES).                                              03900000
.*      IF (REGISTER) IS SPECIFIED THE ITEM WITHIN THE BRACKETS         03950000
.*      SPECIFIES A REGISTER THAT CONTAINS A BINARY VALUE.              04000000
.*                                                                      04050000
.* ALL PARAMETERS ARE MANDATORY !!                                      04100000
.* IF ONE OF THEM IS OMITTED OR SPECIFIED                               04150000
.* INVALIDLY, THE MACRO IS IGNORED.                                     04200000
.*                                                                      04250000
.* OUTPUT:                                                              04300000
.*    SEE TOPIC FUNCTION                                                04350000
.*                                                                      04400000
.* EXIT:                                                                04450000
.*    SEE TOPIC FUNCTION                                                04500000
.*    BRANCH TO SLA (SYMBOLIC LABEL ACCESS)                             04550000
.*                                                                      04600000
.* CHANGE ACTIVTIY:                                                     04650000
.* ADAPT TO FAMILY API                                         @D62ADWS 04700000
.* A000000-999999                                              @D35DDWH 04750000
.********************************************************************** 04800000
         AIF   ('&FUNCT' EQ '').ERROR1          PARMS ARE MISSING ?     04850000
         AIF   ('&AREA' EQ '').ERROR1                                   04900000
         AIF   ('&LENGTH' EQ '').ERROR1                                 04950000
&NAME    B     CONT&SYSNDX                                              05000000
CLPL&SYSNDX DS    0F              PARAMETER LIST FOR SLA                05050000
         AIF   ('&AREA'(1,1) EQ '(').ARREG      PARENTH.: REG. NUM      05100000
         AIF   (T'&AREA EQ 'N').ERROR2          DIGIT NOT IN PARENTH.?  05150000
         AIF   (K'&AREA GT 8).ERROR2            CHECK LENGTH OF PARM    05200000
         DC    AL4(&AREA)                       ADDR OF INTERF. FIELD   05250000
         AGO   .LENGTH                                                  05300000
.ARREG   DS    F                  FOR ADDR OF AREA IN PARM LIST         05350000
.LENGTH AIF    ('&LENGTH'(1,1) EQ '(').LENREG   PARENTH.: REG NUM       05400000
         AIF   (T'&LENGTH NE 'N').ERROR2        LENGTH NOT NUMERIC      05450000
         DC    H'&LENGTH'         FOR LENGTH IN PARM LIST               05500000
CONT&SYSNDX NOPR  0                                                     05550000
.GETADDR AIF   ('&AREA'(1,1) NE '(').LOAD       PARENTH.: REG. NUM      05600000
         ST    &AREA,CLPL&SYSNDX  STORE ADDR OF AREA IN PARM LIST       05650000
         AGO   .LOAD                                                    05700000
.LENREG  ANOP                                                           05750000
         DS    H                  LENGTH FIELD IN PARM LIST: 2 BYTES    05800000
CONT&SYSNDX STH    &LENGTH,CLPL&SYSNDX+4 STORE LENGTH IN PARM LIST      05850000
         AGO   .GETADDR                                                 05900000
.LOAD    ANOP                                                           05950000
         MC     2,4               JOB CANCELLED IF RUNNING IN PSEUDO    06000000
.*                          -PARTITION (MCODE 2) BY ETSS (MCLASS 4)     06050000
         ILOAD  $IJBSLA           GET ADDR. OF PHASE IN REG. 1 @D62ADWS 06100000
         LR     15,1              LOAD ADDR INTO REG. 15                06150000
         LA     1,CLPL&SYSNDX     ADDR. OF PARM LIST INTO REG. 1        06200000
         AIF    ('&FUNCT' EQ 'PUTCOM').PUTCOM   CHECK FUNCTION PARM     06250000
         AIF    ('&FUNCT' EQ 'GETCOM').GETCOM                           06300000
         MNOTE 8,'INVAL. SPEC. OF FUNCT PARM. MACRO IGNORED.'           06350000
         MEXIT                                                          06400000
.PUTCOM  LA     0,14              LOAD FUNCTION CODE INTO REG. 0        06450000
         AGO    .END                                                    06500000
.GETCOM  LA     0,15              LOAD FUNCTION CODE                    06550000
         AGO    .END                                                    06600000
.ERROR1  MNOTE 8,'PARAMETER(S) MISSING. MACRO IGNORED.'                 06650000
         MEXIT                                                          06700000
.ERROR2  ANOP                                                           06750000
CONT&SYSNDX NOPR  0               DUMMY INSTR. TO AVOID ASS. ERROR      06800000
         MNOTE 8,'SYNTAX OR CONTENTS OF PARAMETER(S) INVALID.'          06850000
         MEXIT                                                          06900000
.END     BALR   14,15             RETURN ADDR. INTO REG. 14             06950000
         MEND                                                           07000000
