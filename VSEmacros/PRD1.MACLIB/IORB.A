*% GOTO  IORPLS;                /* ASSEMBLER MACRO IN COMMENT           00050000
         MACRO                                                          00100000
&LABEL   IORB  &CCW=,                                                  *00150000
               &DEVICE=,                                               *00200000
               &DSECT=NO,                                              *00250000
               &ECB=,                                                  *00300000
               &FIXFLAG=,                                              *00350000
               &FIXLIST=,                                              *00400000
               &IOFLAG=                                                 00450000
.*                                                                      00500000
         LCLA  &DEV                                                     00550000
         LCLA  &FIXFL                                                   00600000
         LCLA  &IOFL                                                    00650000
         LCLA  &N                                                       00700000
         LCLA  &VAR                                                     00750000
         LCLA  &XIND                                                    00800000
         LCLB  &COM                                                     00850000
         LCLB  &DE                                                      00900000
         LCLB  &ERR                                                     00950000
         LCLB  &FIX                                                     01000000
         LCLB  &FIXERR                                                  01050000
         LCLB  &IOERR                                                   01100000
         LCLB  &SKP                                                     01150000
         LCLB  &CCW1                                           @D52VDAP 01175001
         LCLB  &CCW31                                          @D66ADAP 01176001
         LCLC  &CWORK                                                   01200000
.*       IBM DISK OPERATING SYSTEM                             @D368DAP 01250000
*        SUPERVISOR - IORB - 5686-007-06-65C                   @D66ADAP 01300001
         AGO   .NOCOPY                                         @D368DAP 01350000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"           @D31UDMZ 01400000
*        5686-007 (C) COPYRIGHT IBM CORP. 1979, 2000           @D66ADAP 01450001
*        ALL RIGHTS RESERVED                                   @D31UDMZ 01500000
*        LICENSED MATERIALS-PROPERTY OF IBM                    @D368DAP 01550000
*        SEE COPYRIGHT INSTRUCTIONS, G120-2083                 @D31UDMZ 01600000
.NOCOPY  ANOP                                                  @D368DAP 01650000
.* /* START OF SPECIFICATIONS ***************************************** 01700000
.*                                                                      01750000
.*01* MODULE-TYPE = SUPERVISOR - USER INTERFACE MACRO                   01800000
.*                                                                      01850000
.*01* DECRIPTIVE NAME = INPUT/OUTPUT REQUEST BLOCK                      01900000
.*                                                                      01950000
.*01* NOTES = CHANGE ACTIVITY                                           02000000
.*    NEW CODE FOR RELEASE 35 TO SUPPORT CHANNEL RELOCATION             02050000
.* A000000-999999                                              @D35EDEP 02100000
.* SUPPORT OF FORMAT 1 CCW                                     @D52VDAP 02125001
.**** END OF SPECIFICATIONS ******************************************* 02150000
.*                                                                      02200000
         AIF   ('&DSECT' NE 'YES').NODSECT                              02250000
.*                                                                      02300000
.*       DSECT GENERATION, ALL OTHER OPERANDS IGNORED --------          02350000
.*                                                                      02400000
IORBADR  DSECT ,              I/O REQUEST BLOCK                         02450000
IORBCNT  DS    H              RESIDUAL COUNT                            02500000
IORBCOM1 DS    X              1ST COMM. BYTE AS IN CCB                  02550000
IORBWAIT EQU   X'80'          TRAFFIC/WAIT BIT (I/O COMPLETE)           02600000
IORBEOF  EQU   X'40'          END OF FILE                               02650000
IORBERR  EQU   X'20'          IRRECOVERABLE ERROR                       02700000
IORBPUTR EQU   X'08'          SYSLOG ACTION TYPE MESSAGE       @D61CDAP 02733301
*        EQU   X'17'          FOR USE BY PIOCS                          02766601
IORBCOM2 DS    X              2ND COMM. BYTE AS IN CCB                  02800000
IORBDATC EQU   X'80'          DATA CHECK IN COUNT FIELD                 02850000
IORBTRKO EQU   X'40'          CKD-DASD TRACK OVERFLOW                   02900000
IORBEOC  EQU   X'20'          CKD-DASD END OF CYLINDER                  02950000
IORBDATA EQU   X'10'          CKD-DASD ANY DATA CHECK                   03000000
IORBNRF  EQU   X'08'          CKD-DASD NO RECORD FOUND                  03050000
IORBCHN9 EQU   X'40'          PRINTER CHANNEL 9 OVERFLOW                03100000
*        EQU   X'05'          FOR USE BY PIOCS                          03150000
IORBCSW  DS    0XL2           CSW STATUS BITS                           03200000
IORBDEVS DS    X              CSW DEVICE STATUS BYTE                    03250000
IORBATTN EQU   X'80'          ATTENTION INTERRUPT                       03300000
IORBSMOD EQU   X'40'          STATUS MODIFIER                           03350000
IORBCUE  EQU   X'20'          CONTROL UNIT END                          03400000
IORBBUSY EQU   X'10'          DEVICE BUSY                               03450000
IORBCE   EQU   X'08'          CHANNEL END                               03500000
IORBDE   EQU   X'04'          DEVICE END                                03550000
IORBUC   EQU   X'02'          UNIT CHECK                                03600000
IORBUE   EQU   X'01'          UNIT EXCEPTION                            03650000
IORBCHNS DS    X              CSW CHANNEL STATUS BYTE                   03700000
IORBPCI  EQU   X'80'          PROGRAM CONTROLLED INTERRUPT              03750000
IORBILC  EQU   X'40'          INCORRECT LENGTH CHECK                    03800000
IORBPGMC EQU   X'20'          PROGRAM CHECK                             03850000
IORBPCK  EQU   X'10'          PROTECTION CHECK                          03900000
IORBCHDT EQU   X'08'          CHANNEL DATA CHECK                        03950000
IORBCCC  EQU   X'04'          CHANNEL CONTROL CHECK                     04000000
IORBICC  EQU   X'02'          INTERFACE CONTROL CHECK                   04050000
IORBCHAI EQU   X'01'          CHAINING CHECK                            04100000
IORBPIOC DS    X              FOR USE BY PIOCS                          04150000
         DS    X              FOR USE BY PIOCS                          04200000
IORBLIOC DS    X              RESERVED                         @D61CDAP 04250001
IORBCPAD DS    XL3            CHANNEL PROGRAM ADDRESS          @D37BDAP 04300000
         ORG   IORBLIOC       OVERLAY                          @D66ADAP 04310001
IORBCP31 DS    AL4            CCW ADDRESS ABOVE THE LINE       @D66ADAP 04320001
         DS    XL1            FOR USE BY PIOCS                 @D37BDAP 04350000
IORBCSWW DS    AL3            FAILING CCW ADDRESS                       04400003
         ORG   IORBCSWW       OVERLAY                          @D66ADAP 04401001
IORB31OF DS    AL3            FAILING CCW INFORMATION          @D66ADAP 04410003
         DS    XL4            FOR USE BY PIOCS                 @D14CDAP 04450000
         DS    XL2            IORB VERSION LEVEL               @D14CDAP 04500000
         DS    XL2            SPECIAL PROCESSING FLAGS         @D14CDAP 04550000
IORBEVAR EQU   *              BEGIN OF VARIABLE PART (IF ANY)           04600000
*                                                                       04650000
* SOME INPUT FLAGS TO BE USED ONLY WITH ITS FLAG BYTE                   04700000
*                                                                       04750000
IORBACCF EQU   IORBCOM1       FLAG BYTE FOR IORBACC                     04800000
IORBACC  EQU   X'10'          ACCEPT IRRECOVERABLE I/O ERROR            04850000
IORBIORF EQU   IORBPIOC       FLAG BYTE FOR IORBIORB BIT                04900000
IORBXCPR EQU   X'80'          EXCP-REAL REQUEST                @D37BDAP 04950000
IORBABOV EQU   X'10'          CCW ADDRESS IS ABOVE THE LINE    @D66ADAP 04960001
IORBIORB EQU   X'04'          CONTROL BLOCK IS AN IORB                  05000000
IORBPRLU EQU   X'01'          PROGRAMMER LOGICAL UNIT          @D37BDAP 05050000
IORBXINF EQU   IORBADR+12     FLAG BYTE FOR EXTENSION BIT               05100000
IORBEXT  EQU   X'20'          IORB EXTENSION EXISTS                     05150000
IORBCCW1 EQU   X'01'          IORB WITH FORMAT1 CCW            @D52VDAP 05175001
IORBREQ  EQU   IORBADR+22     FLAG BYTE FOR SPECIAL PROCESSING @D14CDAP 05200000
IORBSF   EQU   X'80'          SYSFIL REQUEST TO FBA DEVICE              05250000
IORBSFNU EQU   X'40'          SYSFIL REQUEST DONT UPDATE DIB   @D14CDAP 05300000
IORBLEN  EQU   IORBEVAR-IORBADR LENGTH OF FIXED PART                    05350000
         ORG   IORBADR        VARIABLE PART DSECT                       05400000
IORBEID  DS    X              EXTENSION ID                              05450000
IORBELST EQU   X'80'          LAST EXTENSION ELEMENT FLAG               05500000
IORBEECB EQU   X'00'          ID = ECB EXTENSION ELEMENT                05550000
IORBEADR DS    AL3            ADDRESS                                   05600000
         ORG                                                            05650000
         MEXIT                                                          05700000
.*                                                                      05750000
.*       NON-DSECT GENERATION --------------------------------          05800000
.*                                                                      05850000
.NODSECT ANOP                                                           05900000
&IOFL    SETA  X'0000'        INITIAL VALUE FOR BYTES 2 & 3             05950000
.*                            FIXFLAG EVALUATION:                       06000000
&N       SETA  0                                                        06050000
.LOOP1   ANOP                 FIXFLAG EVALUATION LOOP                   06100000
&N       SETA  &N+1                                                     06150000
         AIF   (&N GT N'&FIXFLAG).IOF                                   06200000
         AIF   ('&FIXFLAG(&N)' EQ 'FIXED').FIXED                        06250000
         AIF   ('&FIXFLAG(&N)' EQ 'COMPRESSED').COMPR                   06300000
.FIXERR  ANOP                                                           06350000
&FIXERR  SETB  1                                                        06400000
         AGO   .LOOP1                                                   06450000
.*                            IOFLAG EVALUATION:                        06500000
.IOF     ANOP                                                           06550000
&N       SETA  0                                                        06600000
.LOOP2   ANOP                 IOFLAG EVALUATION LOOP                    06650000
&N       SETA  &N+1                                                     06700000
         AIF   (&N GT N'&IOFLAG).N0                                     06750000
         AIF   ('&IOFLAG(&N)' EQ 'POSTDE').PDE                          06800000
         AIF   ('&IOFLAG(&N)' EQ 'POSTERR').PERR                        06850000
         AIF   ('&IOFLAG(&N)' EQ 'SKIPERP').PSKP                        06900000
         AIF   ('&IOFLAG(&N)' EQ 'FORMAT1').PCCW1              @D52VDAP 06925001
         AIF   ('&IOFLAG(&N)' EQ 'ABOVE').PCCWA                @D66ADAP 06926001
.IOERR   ANOP                                                           06950000
&IOERR   SETB  1                                                        07000000
         AGO   .LOOP2                                                   07050000
.*                            OPTION FLAGBIT CALCULATION:               07100000
.FIXED   AIF   (&FIX EQ 1).FIXERR                                       07150000
&FIX     SETB  1                                                        07200000
&FIXFL   SETA  &FIXFL+X'40'                                             07250000
         AGO   .LOOP1                                                   07300000
.COMPR   AIF   (&COM EQ 1).FIXERR                                       07350000
&COM     SETB  1                                                        07400000
&FIXFL   SETA  &FIXFL+X'80'                                             07450000
         AGO   .LOOP1                                                   07500000
.PDE     AIF   (&DE EQ 1).IOERR                                         07550000
&DE      SETB  1                                                        07600000
&IOFL    SETA  &IOFL+X'0400'                                            07650000
         AGO   .LOOP2                                                   07700000
.PERR    AIF   (&ERR EQ 1).IOERR                                        07750000
&ERR     SETB  1                                                        07800000
&IOFL    SETA  &IOFL+X'1000'                                            07850000
         AGO   .LOOP2                                                   07900000
.PSKP    AIF   (&SKP EQ 1).IOERR                                        07950000
&SKP     SETB  1                                                        08000000
&IOFL    SETA  &IOFL+X'0100'                                            08050000
         AGO   .LOOP2                                                   08100000
.PCCWA   AIF   (&CCW1 EQ 1).IOERR                              @D66ADAP 08101104
&VAR     SETA  &VAR+2         INDICATE 31-BIT CCW ADDRESS      @D66ADAP 08101204
&CCW31   SETB  1                                               @D66ADAP 08101301
.PCCW1   AIF   (&CCW1 EQ 1).IOERR                              @D52VDAP 08110001
&VAR     SETA  &VAR+1         INDICATE: FORMAT 1 CCW           @D52VDAP 08120001
&CCW1    SETB  1                                               @D52VDAP 08130001
         AGO   .LOOP2                                          @D52VDAP 08140001
.*                            OPTION ERROR MESSAGES:                    08150000
.N0      AIF   (&FIXERR EQ 0).N1                                        08200000
         MNOTE 4,'FIXFLAG OPTION(S) DUPLICATED OR IN ERROR HAVE BEEN IG*08250000
               NORED.'                                                  08300000
.N1      AIF   (&IOERR EQ 0).N2                                         08350000
         MNOTE 4,'IOFLAG OPTION(S) DUPLICATED OR IN ERROR HAVE BEEN IGN*08400000
               ORED.'                                                   08450000
.**************               ECB PARAMETER CHECKING:                   08500000
.N2      AIF   (T'&ECB EQ 'O').N21                                      08550000
&VAR     SETA  &VAR+X'20'     INDICATE: EXTENSION PRESENT      @D52VDAP 08600003
&XIND    SETA  X'80'          SET ECB IDENTIFIER,IND.LAST ENTRY         08650000
.N21     ANOP                                                           08700000
.**************               DEVICE PARAMETER CHECKING                 08750000
         AIF   (T'&DEVICE EQ 'O').N5                                    08800000
         AIF   ('&DEVICE'(1,3) NE 'SYS').N5                             08850000
         AIF   (T'&CCW EQ 'O').N6                                       08900000
.*                            DEVICE EVALUATION:                        08950000
&CWORK   SETC  '&DEVICE'(4,3)                                           09000000
         AIF   ('&CWORK' LT '000').DESYS                                09050000
&DEV     SETA  &CWORK+X'0100'+&DEV INDICATE PROGRAMMER UNIT    @D66ADAP 09100002
         AGO   .DE1                                                     09150000
.DESYS   ANOP                                                           09200000
&N       SETA  1                                                        09250000
.ILOOP   AIF   ('RDRIPTPCHLSTLOGLNKRESSLBRLBUSERECCLBDMPCAT'(&N,3)     *09300000
               EQ '&CWORK').DESYS1                                      09350000
&N       SETA  &N+3                                                     09400000
         AIF   (&N LT 41).ILOOP                                         09450000
         MNOTE 8,'DEVICE OPERAND SPECIFIES UNKNOWN SYSTEM UNIT,GENERATI*09500000
               ON SUPPRESSED'                                           09550000
         AGO   .N7                                                      09600000
.DESYS1  ANOP                                                           09650000
&DEV     SETA  &N/3+&DEV        ADD SYSTEM LOGICAL UNIT NUMBER @D66ADAP 09700002
.DE1     ANOP                                                           09750000
&DEV     SETA  &DEV+X'0400'     SET IORB INDICATOR (X'04' IN BYTE 6)    09800002
.*                                                                      09850000
.*                            CODE GENERATION:                          09900000
&LABEL   DC    0F'0'               I O R B                              09950000
         DC    AL2(0)              COUNT FIELD                          10000000
         DC    AL2(&IOFL)          COMMUNICATION BYTES                  10050003
         DC    AL2(0)              CHANNEL+DEVICE STATUS                10100003
         DC    AL2(&DEV)           TYPE CODE + UNIT INFO                10150003
         AIF   (NOT &CCW31).NCCW31                             @D66ADAP 10160002
         DC    AL4(&CCW)           CCW ADDRESS IN 31-BIT AREA  @D66ADAP 10170001
         AGO   .YCCW31                                         @D66ADAP 10171001
.NCCW31  ANOP                                                  @D66ADAP 10180002
         DC    AL1(0)              RESERVED                             10200003
         DC    AL3(&CCW)           CCW ADDRESS                          10250000
.YCCW31  ANOP                                                  @D66ADAP 10260001
         DC    AL1(&VAR)           PIOCS FLAGS                          10300000
         DC    AL3(0)              FAILING CCW INFORMATION              10350002
         DC    AL1(&FIXFL)         FIXFLAGS                             10400000
         AIF   (T'&FIXLIST EQ 'O').N3                                   10450000
         DC    AL3(&FIXLIST)       FIXLIST ADDRESS                      10500000
         AGO   .N31                                                     10550000
.N3      ANOP                                                           10600000
         DC    AL3(0)              FIXLIST OMITTED                      10650000
.N31     ANOP                                                           10700000
         DC    X'0000'             IORB VERSION IDENTIFIER              10750000
         DC    X'0000'             REQUESTOR INFORMATION                10800000
         AIF   (T'&ECB EQ 'O').N4                                       10850000
         DC    AL1(&XIND)          LAST ENTRY AND ECB EXTENTION         10900000
         DC    AL3(&ECB)           ADDRESS OF ECB                       10950000
.N4      ANOP                                                           11000000
         MEXIT                                                          11050000
.*                            SEVERE ERROR MESSAGES:                    11100000
.N5      MNOTE 8,'DEVICE PARAMETER MISSING OR NOT STARTING WITH ''SYS''*11150000
               ,GENERATION SUPPRESSED.'                                 11200000
         AGO   .N7                                                      11250000
.N6      MNOTE 8,'CCW PARAMETER MISSING,GENERATION SUPPRESSED.'         11300000
.N7      AIF   ('&LABEL' EQ '').N8                                      11350000
&LABEL   DC    0H'0'          LABEL OF SUPPRESSED IORB MACRO            11400000
.N8      ANOP                                                           11450000
         MEND                                                           11500000
*        */                                                             11550000
*%IORPLS: ;                                                             11600000
*       %IF IOR9999¬=','                /* IF THIS IS THE LAST BLOCK */ 11650000
*          %THEN %IOR9999=';' ;         /*  THEN CLOSE THE DCL STMT  */ 11700000
*       %IF IORLEVEL¬=''                /* IF THIS IS CONCATENATED   */ 11750000
*          %THEN %IORDUM=IORLEVEL||' IORBADR' ; /* USER DCL  @D3CADAP*/ 11800000
*       %ELSE %IORDUM='DCL 1 IORBADR BASED ' ; /* LEVEL 1 DCL@D3CADAP*/ 11850000
*/********************************************************************/ 11900000
*/*     DOS/VS INPUT OUTPUT REQUEST BLOCK                            */ 11950000
*/********************************************************************/ 12000000
*     IORDUM , /* I/O REQUEST BLOCK */                                  12050000
*       5 IORBCNT FIXED BIN(16),   /* RESIDUAL COUNT                 */ 12100000
*       5 IORBCOM1 CHAR(1),        /* 1ST COMM BYTE AS IN CCB        */ 12150000
*         6 IORBWAIT BIT(1),       /* I/O COMPLETE. TRAFFIC/WAIT BIT */ 12200000
*         6 IORBEOF BIT(1),        /* END OF FILE                    */ 12250000
*         6 IORBERR BIT(1),        /* IRRECOVERABLE I/O ERROR        */ 12300000
*         6 IORBACC BIT(1),        /* INPUT FLAG- ACCEPT IORBERR     */ 12350000
*         6 IORBPUTR BIT(1),       /* SYSLOG ACTION TYPE MSG @D61CDAP*/ 12383301
*         6 * BIT(3),              /* FOR USE BY PIOCS       @D61CDAP*/ 12416601
*       5 IORBCOM2 CHAR(1),        /* 2ND COMM BYTE AS IN CCB        */ 12450000
*         6 IORBDATC BIT(1),       /* DATA CHECK IN COUNT FIELD      */ 12500000
*         6 IORBTRKO BIT(1),       /* TRACK OVERFLOW                 */ 12550000
*         6 IORBEOC BIT(1),        /* END OF CYLINDER                */ 12600000
*         6 IORBDATA BIT(1),       /* ANY DATA CHECK                 */ 12650000
*         6 IORBNRF BIT(1),        /* NO RECORD FOUND                */ 12700000
*         6 * BIT(1),              /* FOR USE BY PIOCS               */ 12750000
*         6 IORBCHN9 BIT(1),       /* PRINTER CHAN 9 OVERFLOW        */ 12800000
*         6 IORBCCR  BIT(1),       /* COMMAND CHAINING (FOR ERP)     */ 12850000
*       5 IORBCSW CHAR(2),         /* STATUS FLAGS FROM CSW          */ 12900000
*         6 IORBDEVS CHAR(1),      /* CSW DEVICE STATUS BYTE         */ 12950000
*           7 IORBATTN BIT(1),     /* ATTENTION INTERRUPT            */ 13000000
*           7 IORBSMOD BIT(1),     /* STATUS MODIFIER INTERRUPT      */ 13050000
*           7 IORBCUE BIT(1),      /* CONTROL UNIT END               */ 13100000
*           7 IORBBUSY BIT(1),     /* DEVICE BUSY                    */ 13150000
*           7 IORBCE BIT(1),       /* CHANNEL END                    */ 13200000
*           7 IORBDE BIT(1),       /* DEVICE END                     */ 13250000
*           7 IORBUC BIT(1),       /* UNIT CHECK                     */ 13300000
*           7 IORBUE BIT(1),       /* UNIT EXCEPTION                 */ 13350000
*         6 IORBCHNS CHAR(1),      /* CSW CHANNEL STATUS BYTE        */ 13400000
*           7 IORBPCI BIT(1),      /* PROGRAM CONTROLLED INTERRUPT   */ 13450000
*           7 IORBILC BIT(1),      /* INCORRECT LGT(CHAIN BROKEN)    */ 13500000
*           7 IORBPGMC BIT(1),     /* CHANNEL-PROGRAM CHECK          */ 13550000
*           7 IORBPCK BIT(1),      /* PROTECTION CHECK               */ 13600000
*           7 IORBCHDT BIT(1),     /* CHANNEL DATA CHECK             */ 13650000
*           7 IORBCCC BIT(1),      /* CHANNEL CONTROL CHECK          */ 13700000
*           7 IORBICC BIT(1),      /* INTERFACE CONTROL CHECK        */ 13750000
*           7 IORBCHAI BIT(1),     /* CHAINING CHECK                 */ 13800000
*       5 IORBCLS CHAR(1),         /* INPUT FLAGS                    */ 13850000
*         6 IORBXCPR BIT(1),       /* EXCP-REAL REQUEST      @D37BDAP*/ 13900000
*         6 * BIT(4),              /* FOR USE BY PIOCS       @D68ADAP*/ 13950004
*         6 IORBIORB BIT(1),       /* BLOCK IS NOT A CCB             */ 14000000
*         6 * BIT(1),              /* FOR USE BY PIOCS               */ 14050000
*         6 IORBPRLU BIT(1),       /* PROGRAMMER UNIT        @D37BDAP*/ 14100000
*       5 * CHAR(1),               /* FOR USE BY PIOCS               */ 14150000
*       5 IORBCP31 PTR(31),        /* CCW IN 31-BIT AREA     @D66ADAP*/ 14160001
*         6 IORBLIOC CHAR(1),      /* LIOCS OUTPUT INFO              */ 14200003
*         6 IORBCPAD PTR(24),      /* CHANNEL PROGRAM ADDR.  @D37BDAP*/ 14250001
*       5 IORBXINF CHAR(1),        /* PIOCS FLAG BYTES               */ 14300000
*         6 * BIT(2),              /* FOR USE BY PIOCS               */ 14350000
*         6 IORBEXT BIT(1),        /* IORB EXTENSION EXISTS          */ 14400000
*         6 * BIT(3),              /* FOR USE BY PIOCS               */ 14433304
*         6 IORBABOV BIT(1),       /* FORMAT-1 CCW ABOVE 16M @D68ADAP*/ 14466604
*         6 IORBCCW1 BIT(1),       /* IORB WITH FORMAT1 CCW  @D52VDAP*/ 14466704
*       5 IORBCSWW PTR(24),        /* VIRTUAL ADDR OF CCW IN CSW     */ 14500000
*       5 * CHAR(4),               /* FOR USE BY PIOCS               */ 14550000
*       5 * CHAR(2),               /* IORB VERSION LEVEL             */ 14600000
*       5 IORBREQ CHAR(2),         /* IORB REQUEST INFO      @D14CDAP*/ 14650000
*         6 IORBSF   BIT(1),       /* SYSFIL REQUEST FOR FBA @D14CDAP*/ 14700000
*         6 IORBSFNU BIT(1),       /* SYSFIL REQUEST NO DIB UPDATE   */ 14750000
*         6 * BIT(14),             /* RESERVED               @D14CDAP*/ 14800000
*       5 IORBEVAR CHAR(0),        /* END OF FIXED PART OF IORB      */ 14850000
*       5 *(*) CHAR(4),            /* IORB EXTENSION                 */ 14900000
*         6 *       CHAR(1),       /* EXTENSION BYTE                 */ 14950000
*           7 IORBELST BIT(1),     /* 1=LAST EXTENSION IN LIST       */ 15000000
*           7 IORBEID BIT(7),      /* EXTENSION ID                   */ 15050000
*         6 IORBEADR PTR(24) BDY(BYTE) /* ADDRESS                    */ 15100000
*             IOR9999                                                   15150000
* %IORLEN='LENGTH(IORBADR)';                                            15200000
* %IORBEECB='''0000000''B';        /* ID FOR ECB EXTENSION           */ 15250000
* %IORXLEN='4';                    /* LENGTH OF EXTENTION ELE@D37BDAP*/ 15300000
