         TITLE ' JOBEXAMP'                                              00001001
         PUNCH ' PHASE JOBEXAMP,*'                                      00002001
         SPACE                                                          00003001
*******************************************************************     00004001
***                                                             ***     00005001
***                  J O B E X A M P                            ***     00006001
***                                                             ***     00007001
***            VSE/POWER JOB EXIT: EXAMPLE PROGRAM              ***     00008001
***                                                             ***     00009001
*******************************************************************     00010001
         SPACE 2                                                        00011001
*        THIS PROGRAM - NAMED JOBEXAMP - ACTS AS AN EXAMPLE FOR A       00012001
*        USER WRITTEN JOB EXIT ROUTINE.                                 00013001
         SPACE 2                                                        00014001
*        THIS EXAMPLE GIVES A SKELETON WHICH SHOWS HOW TO CHECK         00015001
*        THE CONTENTS OF JCL AND JECL STATEMENTS. DEPENDING ON THE      00016001
*        NEEDS OF THE CUSTOMERS AN ACTION TYPE MAY BE PROVIDED          00017001
*        BY THE JOB EXIT TO POSITION 80 OF THE STATEMENT.               00018001
*        THIS ACTION TYPE WILL BE INTERPRETED LATER ON AND THE          00019001
*        APPROPRIATE RETURN CODE WILL BE                                00020001
*        SET IN REGISTER 15 TO BE PASSED TO VSE/POWER.                  00021001
         SPACE 2                                                        00022001
*        CONTROL IS GIVEN TO THIS JOB EXIT VIA REGISTER 15              00023001
*        BY THE LOGICAL READER (IPW$$LR).                               00024001
         SPACE 2                                                        00025001
*        THIS JOB EXIT IS ASSUMED TO BE LOADED WITH A WORK AREA         00026001
*        OF 50 BYTES.                                                   00027001
         SPACE 2                                                        00028001
*        THE FOLLOWING ADDRESSABILITY IS ASSUMED AT ENTRY TO            00029001
*        THE JOB EXIT:                                                  00030001
         SPACE                                                          00031001
*        R0  -  ADDRESS OF STATEMENT PASSED BY VSE/POWER                00032001
*        R1  -  LENGTH  OF STATEMENT PASSED BY VSE/POWER                00033001
*        R3  -  ADDRESS OF THE WORKAREA PASSED BY VSE/POWER             00034001
*        RA  -  ADDRESS OF VSE/POWER COMMON ADDRESS TABLE (CAT)         00035001
*        RB  -  ADDRESS OF TASK CONTROL BLOCK (TCB)                     00036001
*        RE  -  RETURN ADDRESS TO VSE/POWER                             00037001
*        RF  -  BASE REGISTER OF THIS ROUTINE                           00038001
         SPACE 2                                                        00039001
*        THIS JOB EXIT PHASE IS LOCATED WITHIN THE                      00040001
*           - PAGEABLE AREA OF THE VSE/POWER PARTITION WHEN LOADED      00041001
*             AT VSE/POWER INITIALIZATION TIME, OR IN THE               00042001
*           - GETVIS AREA OF THE VSE/POWER PARTITION WHEN LOADED        00043001
*             AFTER INITIALIZATION  BY THE 'PLOAD' COMMAND.             00044001
*        FOR DEBUGGING LOCATE THE JOB EXIT IN STORAGE                   00045001
*        BY ITS STORAGE DESCRIPTOR 'JOB-EXIT' AND BY THE POINTER        00046001
*        'CARE' OF THE COMMON ADDRESS TABLE OF                          00047001
*        VSE/POWER.                                                     00048001
         SPACE 2                                                        00049001
*        THE FOLLOWING PIECE OF CODE IS ONLY AN EXAMPLE.                00050001
*        IT IS THE USER'S RESPONSIBILITY TO DEVELOP                     00051001
*        HIS OWN ROUTINE CONCERNING HIS PROBLEM DEFINITION.             00052001
         EJECT                                                          00053001
*        REGISTER USAGE:                                                00054001
*           R0 - ADDRESS OF STATEMENT IN CASE OF INSERT                 00055001
*           R1 - LENGTH OF STATEMENT IN CASE OF INSERT                  00056001
*           R2 - ADDRESS OF STATEMENT IN JOB EXIT                       00057001
*           R3 - ADDRESS OF WORK AREA PASSED BY VSE/POWER               00058001
*           R4 - R7  -  WORK REGISTER                                   00059001
*           R8 - RETURN CODE SET BY JOB EXIT                            00060001
*           R9 - WORK REGISTER                                          00061001
*           RA - RC USED BY VSE/POWER                                   00062001
*           RE - RETURN ADDRESS TO VSE/POWER                            00063001
*           RF - EXIT BASE REGISTER AND RETURN CODE TO VSE/POWER        00064001
         SPACE 2                                                        00065001
JOBEXAMP CSECT 0                   ESTABLISH MAIN CONTROL SECTION       00066001
         USING *,RF                BASE REG ESTABLISHED BY VSE/POWER    00067001
         USING PADS,RA             ADDRESSABILITY OF VSE/POWER CAT      00068001
         USING TCDS,RB             ADDRESSABILITY OF TCB                00069001
         USING CDSECT,R2           ADDRESSABILITY OF STATEMENT          00070001
         USING WDSECT,R3           ADDRESSABILITY FOR WORK AREA         00071001
         B     JEX000              SKIP STORAGE DESCRIPTOR              00072001
         SPACE                                                          00073001
         DC    CL12'JOB-EXIT'      DEFINE STORAGE DESCRIPTOR            00074001
         SPACE                                                          00075001
*******************************************************************     00076001
*                                                                 *     00077001
*        THE FOLLOWING PIECE OF CODE IS USED TO CHECK THE         *     00078001
*        STATEMENT. FOR A * $$ JOB STATEMENT THE ACTION TYPE      *     00079001
*        WILL BE DETERMINED.                                      *     00080001
*        FOR ALL OTHER STATEMENTS CONTROL IS PASSED TO VSE/POWER  *     00081001
*        WITH NORMAL PROCESSING INDICATED IN REGISTER 15.         *     00082001
*                                                                 *     00083001
*        FOR A JOB STATEMENT ACCEPTED BY THE JOB EXIT             *     00084001
*        A NEW STATEMENT WILL BE INSERTED AND THE JOB COUNTER     *     00085001
*        IN THE WORK AREA WILL BE INCREMENTED.                    *     00086001
*        INSERTION OF THE STATEMENT IS INDICATED IN THE WORK      *     00087001
*        AREA TO AVOID LOOPING SINCE THE CURRENT * $$ JOB         *     00088001
*        STATEMENT IS PASSED AGAIN TO THIS JOB EXIT.              *     00089001
*                                                                 *     00090001
*        FIRST THE EXAMPLE CHECKS,WHETHER THE REQUIRED WORKAREA   *     00091001
*        SIZE OF 50 BYTES HAS BEEN SPECIFIED FOR THIS EXIT.       *     00092001
*        IF NOT, WE DO NOT DARE TO USE THE PASSED WORKAREA,       *     00093001
*        INSTEAD THE CENTRAL OPERATOR WILL BE INFORMED BY A       *     00094001
*        WARNING MESSAGE.                                         *     00095001
*                                                                 *     00096001
*******************************************************************     00097001
         SPACE                                                          00098001
JEX000   DS    0H                                                       00099001
         SPACE 2                                                        00100001
*        WHENEVER A WORK AREA IS USED, THE LENGTH OF THIS AREA MUST BE  00100101
*        VERIFIED EACH TIME THE EXIT GAINS CONTROL, BECAUSE             00100201
*        - THE EXIT MIGHT HAVE BEEN LOADED BY OPERATOR WITH A WRONG     00100301
*          LENGTH OF ITS WORK AREA                                      00100401
*        - A NEW VERSION OF THE EXIT MIGHT HAVE BEEN LOADED AND THE     00100501
*          PREVIOUSLY DEFINED WORK AREA IS STILL USED UNTIL THE TASK    00100601
*          ENDS.                                                        00100701
         SPACE 2                                                        00100801
*        SINCE A WORK AREA IS REQUIRED BY THIS ROUTINE, VERIFY IF ONE   00100901
*        IS SPECIFIED. IF NOT, THE VSE/POWER JOB IS FLUSHED.            00101001
         SPACE 1                                                        00102001
         LTR   R3,R3                   WORK AREA SPECIFIED ?            00102101
         BZ    JEX003                  ..NO, WARN OPER. AND FLUSH JOB   00102201
         SPACE 2                                                        00102301
*        THE WORK AREA EXISTS AND CAN NOW BE ADDRESSED. TEST IF         00102401
*        THE WORK AREA IS LARGE ENOUGH.                                 00102501
         SPACE 1                                                        00102601
         L     R4,WDLENGTH             GET SPECIFIED WORK AREA SIZE     00102701
         LA    R5,WDLEN                GET REQU. SIZE OF WORK AREA      00102801
         CR    R4,R5                   WORK AREA TOO SHORT ?            00102901
         BNL   JEX005                  ..NO, CONTINUE EXIT              00103001
         SPACE 1                                                        00104001
JEX003   DS    0H                                                       00105001
         SPACE 1                                                        00106001
*        RESPECT THAT MACRO IPW$WTO DESTROYS REGISTER R0-R3             00107001
         SPACE 1                                                        00108001
         LA    R7,JEXMSG1          GET ADDRESS OF JEXMSG1               00109001
         BAL   R6,SUBWTO           ISSUE TEXMSG1                        00110001
         SPACE 1                                                        00111001
         LA    R7,JEXMSG2          GET ADDRESS OF JEXMSG2               00112001
         BAL   R6,SUBWTO           ISSUE TEXMSG2                        00113001
         SPACE 1                                                        00114001
         LA    R7,JEXMSG3          GET ADDRESS OF JEXMSG3               00115001
         BAL   R6,SUBWTO           ISSUE TEXMSG3                        00115101
         SPACE 1                                                        00115201
         LA    RF,X'10'            PROVIDE RETURN CODE TO FLUSH JOB     00115301
         BR    RE                  AND RETURN TO VSE/POWER              00115401
         SPACE 2                                                        00115501
JEX005   DS    0H                                                       00115601
         LR    R2,R0               SET UP ADDRESS OF STATEMENT          00115701
         CLC   COMPARE,CUSTNEED    * $$ JOB STATEMENT PASSED ?          00115801
         BE    JEX010              .. YES, CONTINUE                     00115901
         B     JEX050              .. NO, CONTINUE                      00116001
         SPACE                                                          00117001
JEX010   DS    0H                                                       00118001
         TM    WDFLAG,WDFLINS      INSERT OF STATEMENT DONE ?           00119001
         BZ    JEX020              ..NO, CONTINUE                       00120001
         L     R4,WDCOUNT          GET CURRENT JOB COUNTER              00121001
         LA    R4,1(R4)            INCREMENT JOB COUNTER                00122001
         ST    R4,WDCOUNT          SAVE NEW VALUE                       00123001
         NI    WDFLAG,X'FF'-WDFLINS RESET INSERT DONE                   00124001
         B     JEX060              CONTINUE                             00125001
         SPACE                                                          00126001
JEX020   DS    0H                                                       00127001
         SPACE                                                          00128001
*******************************************************************     00129001
*                                                                 *     00130001
*        INSERT CODE HERE WHICH DETERMINES THE ACTION FOR         *     00131001
*        THIS JECL STATEMENT AND WHICH SAVES THIS ACTION          *     00132001
*        IN THE ACTION BYTE OF THE WORK AREA.                     *     00133001
*                                                                 *     00134001
*******************************************************************     00135001
         SPACE 4                                                        00136001
*******************************************************************     00137001
*                                                                 *     00138001
*        THE FOLLOWING PIECE OF CODE IS USED TO REACT UPON THE    *     00139001
*        ACTION AND TO GET THE RELATED RETURN CODE FOR            *     00140001
*        VSE/POWER.                                               *     00141001
*        FOR AN ACTION 'FLUSH VSE/POWER JOB' THE APPROPRIATE      *     00142001
*        BIT IS SET IN THE FLAG BYTE OF THE WORK AREA.            *     00143001
*        THIS ACTION IS ONLY ACCEPTED BY VSE/POWER AFTER          *     00144001
*        PROCESSING OF THE * $$ JOB STATEMENT BY VSE/POWER.       *     00145001
*        SINCE THE JOB EXIT IS CURRENTLY PROCESSING THE * $$ JOB  *     00146001
*        STATEMENT FLUSH IS INDICATED WITH THE NEXT               *     00147001
*        STATEMENT.                                               *     00148001
*                                                                 *     00149001
*******************************************************************     00150001
         SPACE                                                          00151001
JEX030   DS    0H                                                       00152001
         LA    R8,4                ASSUME DELETE                        00153001
         TM    WDACTION,WDACDEL    DO WE WANT TO DELETE THIS            00154001
*                                  STATEMENT ?                          00155001
         BO    JEXDONE             IF YES, RETURN TO VSE/POWER          00156001
         SPACE                                                          00157001
         TM    WDACTION,WDACINS    DO WE WANT TO INSERT ?               00158001
         BZ    JEX040              ... BRANCH IF NOT                    00159001
         LA    R8,8                GET PROPER RETURN CODE               00160001
         LA    R0,INSERT           POINT TO CORRECT STATEMENT           00161001
         LA    R1,L'INSERT         GET PROPER LENGTH                    00162001
         OI    WDFLAG,WDFLINS      INDICATE INSERT DONE                 00163001
         B     JEXDONE             RETURN TO VSE/POWER                  00164001
         SPACE                                                          00165001
JEX040   DS    0H                                                       00166001
         OI    WDFLAG,WDFLPOW      ASSUME FLUSH OF VSE/POWER JOB        00167001
         TM    WDACTION,WDACFLH    DO WE WANT TO FLUSH VSE/POWER JOB ?  00168001
         BO    JEX060              ..YES, CONTINUE                      00169001
         SPACE                                                          00170001
         NI    WDFLAG,X'FF'-WDFLPOW RESET FLUSH INDICATION              00171001
         SPACE                                                          00172001
         TM    WDACTION,WDACCHG    DO WE WANT TO CHANGE THIS            00173001
*                                  STATEMENT ?                          00174001
         BZ    JEX060              ..NO, CONTINUE                       00175001
*        MVC   FIELD,NOTCHA        MOVE IN CHANGE INFO                  00176001
         B     JEX060              CONTINUE                             00177001
         SPACE                                                          00178001
JEX050   DS    0H                                                       00179001
         TM    WDFLAG,WDFLPOW      FLUSH VSE/POWER JOB TO DO ?          00180001
         BZ    JEX060              ..NO, CONTINUE                       00181001
         LA    R8,16               INDICATE FLUSH VSE/POWER JOB         00182001
         NI    WDFLAG,X'FF'-WDFLPOW RESET FLUSH VSE/POWER JOB           00183001
         B     JEXDONE                                                  00184001
         SPACE                                                          00185001
JEX060   DS    0H                                                       00186001
         SR    R8,R8               GET NORMAL RETURN CODE               00187001
         SPACE 1                                                        00188001
*                                                                       00189001
*        R E T U R N   T O   V S E / P O W E R                          00190001
*                                                                       00191001
         SPACE                                                          00192001
JEXDONE  DS    0H                                                       00193001
         MVI   WDACTION,X'00'      CLEAR ACTION BYTE                    00194001
         LR    RF,R8               GET RETURN CODE                      00195001
         BR    RE                  AND RETURN TO VSE/POWER LOG.RDR      00196001
         EJECT                                                          00197001
*********************************************************************** 00198001
*        SUBROUTINE TO ISSUE MESSAGE ON OPERATOR CONSOLE              * 00199001
*                                                                     * 00200001
* USAGE OF VSE/POWER MACRO IPW$WTO:                                   * 00201001
*       THIS MACRO, TOGETHER WITH SOME INDICATIONS SET IN THE TCB,    * 00202001
*       CAN BE USED TO DISPLAY A MESSAGE ON THE CENTRAL OPERATOR      * 00203001
*       CONSOLE.                                                      * 00204001
*       THE USAGE OF THIS MACRO DESTROYS THE REGISTER R0 - R3.        * 00205001
*       THE MESSAGE TO BE DISPLAYED SHOULD CONTAIN ALPHAMERIC         * 00206001
*       CHARACTERS ONLY.                                              * 00207001
*                                                                     * 00208001
*********************************************************************** 00209001
         SPACE 2                                                        00210001
SUBWTO   DS    0H                                                       00211001
         STCM  R7,7,TCMW+1             PASS MESSAGE ADDRESS             00212001
         MVI   TCMW,X'00'              CLEAR OPTION BYTE                00213001
         OI    TCMW,TCPCOP             FORCE MESSAGE TO CONSOLE         00214001
         OI    TCMW,TCDNMM             SUPPRESS MESSAGE MODIFICATION    00215001
         SPACE                                                          00216001
         IPW$WTO TCMW                  ISSUE MESSAGE                    00217001
         SPACE                                                          00218001
         MVI   TCMW,X'00'              CLEAR OPTION BYTE                00219001
         BR    R6                      RETURN TO CALLER                 00220001
         SPACE 5                                                        00221001
         DROP  R2,R3               RELEASE ADDRESSABILITY               00222001
         DROP  RA,RB               RELEASE ADDRESSABILITY               00223001
         EJECT                                                          00224001
         SPACE                                                          00225001
*******************************************************************     00226001
*        D E F I N I T I O N S                                    *     00227001
*******************************************************************     00228001
         SPACE                                                          00229001
CUSTNEED DC    CL9'* $$ JOB '      ACTION FOR JOB STATEMENT REQUIRED    00230001
INSERT   DC    CL80'* THIS RECORD IS INSERTED BY JOB EXIT'              00231001
NOTCHA   DC    C'CHANGED'          CHANGE INFO                          00232001
         SPACE 2                                                        00233001
JEXMSG1  DC    AL1(JEXMSG1L-JEXMSG1-1)   LENGTH OF MESSAGE              00234001
         DC    C'INCORRECT SIZE OF WORKAREA GIVEN, 50 BYTES NEEDED'     00235001
JEXMSG1L EQU   *                                                        00236001
         SPACE 1                                                        00237001
JEXMSG2  DC    AL1(JEXMSG2L-JEXMSG2-1)   LENGTH OF MESSAGE              00238001
         DC    C'DISABLE JOBEXIT USING PVARY'                           00239001
JEXMSG2L EQU   *                                                        00240001
         SPACE 1                                                        00241001
JEXMSG3  DC    AL1(JEXMSG3L-JEXMSG3-1)   LENGTH OF MESSAGE              00242001
         DC    C'STOP AND RESTART READER TASK. RELOAD JOBEXIT'          00243001
JEXMSG3L EQU   *                                                        00244001
         SPACE 2                                                        00245001
CDSECT   DSECT                     DSECT FOR JECL SATEMENT              00246001
COMPARE  DS    CL9                 PREFIX OF JECL STATEMENT             00247001
         DS    CL71                FILLER                               00248001
         SPACE                                                          00249001
WDSECT   DSECT ,                   DSECT FOR WORK AREA                  00250001
WDLENGTH DS    F                   LENGTH OF WORK AREA                  00251001
WDCOUNT  DS    F                   COUNTER TO MAINTAIN NUMBER OF        00252001
*                                  JOBS PROCESSED                       00253001
WDACTION DS    X'00'               ACTION BYTE                          00254001
WDACINS  EQU   X'80'               .. INSERT STATEMENT                  00255001
WDACDEL  EQU   X'40'               .. DELETE STATEMENT                  00256001
WDACFLH  EQU   X'20'               .. FLUSH VSE/POWER JOB               00257001
WDACCHG  EQU   X'10'               .. CHANGE STATEMENT                  00258001
WDFLAG   DS    X'00'               FLAG BYTE                            00259001
WDFLINS  EQU   X'80'               .. INSERT DONE                       00260001
WDFLPOW  EQU   X'40'               .. FLUSH VSE/POWER JOB TO DO         00261001
WDAREA   DS    CL40' '             WORK AREA                            00262001
WDLEN    EQU   *-WDSECT            LENGTH OF WORK AREA                  00263001
         SPACE 2                                                        00264001
         IPW$EQU ,                 DEFINE REGISTER EQUATES AS           00265001
*                                  USED IN VSE/POWER CODING             00266001
         SPACE 2                                                        00267001
         IPW$DPA ,                 LAYOUT OF COMMON ADDRESS TABLE       00268001
*                                  (CAT) ALSO CALLED PERMANENT AREA     00269001
*                                  OF VSE/POWER.                        00270001
         SPACE 2                                                        00271001
         IPW$DQC ,                 LAYOUT OF DISK MANAGEMENT BLOCK      00272001
         SPACE 2                                                        00273001
         IPW$DQR ,                 LAYOUT OF INTERNAL QUEUE RECORD      00274001
         SPACE 2                   ADDRESSED BY TCQV OF TCB             00275001
         IPW$DTC ,                 LAYOUT OF TASK CONTROL BLOCK         00276001
         SPACE 2                                                        00277001
*        END                       NOT REQUIRED FOR '.A' COPY BOOK      00278001
