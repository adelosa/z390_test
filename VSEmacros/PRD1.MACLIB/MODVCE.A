         MACRO                                                          00050000
&NAME    MODVCE &CHNUNIT=,                                             *00100000
               &CLASS=DASD,                                    @D21LDRP*00150000
               &DEVTYPE=,                                      @D356DAP*00200000
               &INSTALL=,                                      @D61DDGL*00210003
               &LOGUNIT=,                                      @D369DAP*00250000
               &MTC=,                                          @D61ADAP*00260003
               &PLFINFO=,                                      @D3570  *00270004
               &RESERVE=,                                      @D356DAP*00300000
               &SHARE=                                         @D61ADAP 00333307
         GBLB  &IJBSGEN          SUPERVISOR GENERATION         @D61ADAP 00383301
         LCLA  &ANYCL                                          @D21LDRP 00400000
         LCLA  &COMAND           MTC COMMAND ID                @D61ADAP 00425001
         LCLA  &LOG                                                     00450000
         LCLA  &PHY                                                     00500000
         LCLA  &REG0             FUNCTION CONTROL DATA                  00550001
         LCLA  &REGF             CLASS QUALIFIER               @D21LDRP 00600001
         LCLA  &RLS              DEVICE TO BE RELEASED         @D356DAP 00650003
         LCLA  &RSV              DASD   TO BE RESERVED         @D356DAP 00700003
         LCLA  &SHR              DEVICE IS SHARABLE            @D36SDAP 00750003
         LCLA  &INS                                            @D36SDAP 00775001
         LCLA  &TAPECL                                         @D21LDRP 00800000
         LCLA  &TYP                                            @D356DAP 00850000
         LCLB  &DEVFD                                                   00900000
         LCLC  &IUN                                                     00950000
         LCLC  &IUNMNT                                                  00960003
         LCLC  &OP                                                      01000000
         LCLC  &OPMNT                                                   01010003
.*       IBM DISK OPERATING SYSTEM                             @D356DAP 01050000
*        SUPERVISOR - MODVCE - 5686-066-06-35C                 @D61ADAP 01100001
         AGO   .SKIP                                           @D356DAP 01150000
.*                                                             @D356DAP 01200000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"           @D31UDRP 01250000
*        5686-007 (C) COPYRIGHT IBM CORP. 1979, 1999           @D61ADAP 01300001
*        ALL RIGHTS RESERVED                                   @D31UDRP 01350000
*        LICENSED MATERIALS-PROPERTY OF IBM                    @D31UDRP 01400000
.*       SEE COPYRIGHT INSTRUCTIONS, G120-2083                 @D31UDRP 01450000
*                                                              @D356DAP 01500000
***************************************************************@D356DAP 01550000
.SKIP    ANOP                                                  @D356DAP 01600000
.*                                                             @D356DAP 01650000
.**** START OF SPECIFICATIONS ***************************************** 01700000
.*                                                                    * 01750000
.*01* MODULE-TYPE = SUPERVISOR USER INTERFACE MACRO                   * 01800000
.*                                                                    * 01850000
.*01* DESCRIPTIVE NAME = REQUEST AVR-TABLE ENTRY CHANGE               * 01900000
.*                                                                    * 01950000
.*01* NOTES = CHANGE ACTIVITY                                         * 02000000
.*    NEW MODULE FIRST RELEASE IS 35 FOR AVR SUPPORT           @D35VDEP 02050000
.*    GETVCE/MODVCE FOR TAPE                     (SPE DY37265) @D21LDRP 02100000
.*    3570 SUPPORT                                             @D3570   02110001
.**** END OF SPECIFICATIONS ******************************************* 02150000
.*                                                                      02200000
.*       THE CLASS OPERAND SPECIFIES THE DEVICE CLASS FOR      @D21LDRP 02250000
.*       WHICH THE REQUEST IS DONE.                            @D21LDRP 02300000
.*                                                                      02350000
.*       THE OPERANDS LOGUNIT,CHNUNIT AND DEVTYPE ARE MUTUALLY          02400000
.*       EXCLUSIVE AND OFFER DIFFERENT WAYS OF DEVICE SPECIFICATION.    02450000
.*                                                                      02500000
.*       LOGUNIT OPERAND ALLOWS TO SPECIFY THE ADDRESS OF A             02550001
.*       HALFWORD WHICH CONTAINS THE LOGICAL UNIT IDENTIFICATION        02600001
.*       AS NOTATED IN THE CCB.                                         02650001
.*                                                                      02700000
.*       CHNUNIT OPERAND IS SIMILAR TO LOGUNIT BUT THE HALFWORD CON-    02750000
.*       TAINS THE PHYSICAL ADDRESS IN CHANNEL-UNIT NOTATION (CUU).     02800000
.*                                                                      02850000
.*       DEVTYPE OPERAND IS SIMILAR TO LOGUNIT BUT THE ADDRESS REFERS   02900000
.*       TO A ONE BYTE FIELD CONTAINING THE PUB DEVICE TYPE CODE.       02950000
.*                                                                      03000000
.*       THE OPTION RESERVE DOES PROVIDE THE ABILITY TO KEEP A          03040001
.*       DEVICE WHICH IS CURRENTLY NOT IN USE, RESERVED UNTIL:          03080001
.*          FOR DASD DEVICES, THE DEVICE BECOMES READY.                 03120001
.*          FOR TAPE DEVICES, A MODVCE RESERVE=NO COMMAND IS            03146601
.*          ISSUED.                                                     03173201
.*                                                                      03200000
.*       THE OPTION SHARE INDICATES THAT A DEVICE IS SHARABLE           03250000
.*       BETWEEN DIFFERENT CPU'S. IS ONLY VALID DURING IPL.             03300001
.*       CURRENTLY ALLOWED WITH CLASS=DASD ONLY.                        03350001
.*                                                                      03400000
.*       THE OPTION INSTALL INDICATES THAT A DEVICE WHICH WAS  @D61ADAP 03405601
.*       SENSED IN THE STAND ALONE ENVIRONMENT, BUT WHOSE      @D61ADAP 03411201
.*       DEVICE TYPE CODE COULD NOT BE IDENTIFIED, IS TO BE    @D61ADAP 03416801
.*       ESTABLISHED AND AFFECTED TABLES TO BE UPDATED PROPERLY@D61ADAP 03422401
.*       INVOKATION:                                           @D61DDGL 03428001
.*       MODVCE  CHNUNIT=..,INSTALL=YES                        @D61DDGL 03433601
.*       BUT THE FIELD THAT WOULD NORMALLY CONTAIN THE         @D61DDGL 03435201
.*       CUU ALONE IS NOW EXPANDED TO CONTAIN FURTHER INFO     @D61DDGL 03436801
.*       REQUIRED TO DO THE INSTALLATION. IT HAS THE FOLLOWING @D61DDGL 03438401
.*       FORMAT:                                               @D61DDGL 03440001
.*       <2 BYTE CUU><6 BYTE DEVICE TYPE INFO IN A READABLE    @D61DDGL 03441601
.*       FORMAT AS SPECIFIED IN THE IPL ADD STATEMENT OF A     @D61DDGL 03443201
.*       NORMAL SUPERVISOR>                                    @D61DDGL 03444801
.*                                                                      03446401
.*       ADDRESSING CONVENTIONS:                                        03450000
.*       THE OPERAND ADDRESSES MAY BE GIVEN IN A REGISTER,IN A-TYPE     03500000
.*       FORMAT,OR IN S-TYPE FORMAT NOTATION.                           03550000
.*       THE REGISTER NOTATION IS REGISTER NUMBER OR AN APPROPRIATELY   03600000
.*       EQUATED SYMBOL ENCLOSED IN PARENTHESES.                        03650000
.*       THE TERM A-TYPE FORMAT MEANS A NAME OR AN ADDRESS EXPRESSION   03700000
.*       EXPANDING IN AN A-TYPE ADDRESS CONSTANT.                       03750000
.*       THE TERM S-TYPE MEANS A NAME OR AN ADDRESS EXPRESSION WHICH    03800000
.*       COULD BE EXPANDED IN AN S-TYPE ADDRESS CONSTANT. IT WILL BE    03850000
.*       USED AS OPERAND OF AN INSTRUCTION IN BASE REGISTER,DISPLACE-   03900000
.*       MENT FORMAT. IT IS TO BE WRITTEN AS (S,ADDRESS EXPRESSION).    03950000
.*                                                                      04000000
.*       OUTPUT :                                                       04050000
.*                                                                      04100000
.*       GR 1 CONTAINS THE ADDRESS OF THE FIELD                         04150000
.*       GR 0 CONTAINS FLAGS FOR SVC101:                                04200000
.*            X'00CCN000' = FIELD CONTAINS LOGICAL UNIT (AS IN CCB)     04233301
.*            X'00CCN001' = FIELD CONTAINS PHYSICAL UNIT (CUU)          04266601
.*            X'00CCN002' = FIELD CONTAINS DEVICE TYPE         @D356DAP 04299901
.*            X'00CCN010' = DEVICE IS TO BE RESERVED           @D356DAP 04333201
.*            X'00CCN020' = DEVICE IS TO BE RELEASED           @D356DAP 04366501
.*            X'00CCN040' = DEVICE SHAREABLE                   @D36SDAP 04399801
.*            X'00CCN080' = DEVICE IS TO BE INSTALLED          @D61DDGL 04433101
.*                  N=0   : CLASS = DASD                       @D21LDRP 04466401
.*                  N=8   : CLASS = TAPE                       @D21LDRP 04499701
.*                  N=F   : CLASS = ANY                        @D21LDRP 04533001
.*                CCN=008 : NORMAL MODVCE SERVICE              @D61ADAP 04566301
.*                CCN=018 : GRANT WRITE ACCESS                 @D61ADAP 04599601
.*                CCN=028 : GRANT READ  ACCESS ONLY            @D61ADAP 04632901
.*                CCN=038 : PERFORM REWIND AND UNLOAD          @D61ADAP 04666201
.*                CCN=048 : PERFORM LDSPLY MOUNT               @D3570   04666302
.*                                                                      04700000
.********************************************************************** 04750000
.*                                                                      04800000
.*                                                                      04850000
&LOG     SETA  0                                                        04900000
&PHY     SETA  1                                                        04950000
&TYP     SETA  2                                               @D356DAP 05000000
&RSV     SETA  16                                              @D356DAP 05050000
&RLS     SETA  32                                              @D356DAP 05100000
&SHR     SETA  64                                              @D36SDAP 05150000
&INS     SETA  128                FOR INSTALL=YES              @D61DDGL 05175001
&TAPECL  SETA  128                X'80'   FOR CLASS=TAPE       @D21LDRP 05200000
&ANYCL   SETA  240                X'F0'   FOR CLASS=ANY        @D21LDRP 05250000
.*                                                                      05300000
.*       CLASS   OPERAND EVALUATION                                     05350000
.*                                                                      05400000
         AIF   ('&INSTALL' EQ 'YES').CLANY                     @D61DDGL 05425001
         AIF   (T'&INSTALL NE 'O').ERR0A                       @D61NDAP 05437501
         AIF   ('&CLASS' EQ 'DASD').CLEND                      @D31UDRP 05450000
         AIF   ('&CLASS' NE 'ANY').CL1                         @D21LDRP 05500000
.CLANY   ANOP                                                  @D61DDGL 05525001
&REGF    SETA  &REGF+&ANYCL             INDICATE ANY SPECIFIED @D21LDRP 05550000
         AGO   .CLEND                                          @D31UDRP 05600000
.CL1     AIF   ('&CLASS' NE 'TAPE').ERR07                      @D21LDRP 05650000
&REGF    SETA  &REGF+&TAPECL            INDICATE TAPE SPECIFIED@D21LDRP 05700000
.CLEND   ANOP                                                  @D31UDRP 05750000
.*                                                             @D356DAP 05800000
.*       RESERVE OPERAND EVALUATION                            @D356DAP 05850000
.*                                                             @D356DAP 05900000
         AIF   (T'&RESERVE EQ 'O').RSEND                       @D356DAP 05950000
         AIF   (T'&INSTALL NE 'O').ERR0A                       @D61DDGL 05975001
         AIF   ('&CLASS' EQ 'ANY').ERR08                       @D61NDAP 06012501
         AIF   ('&RESERVE' EQ 'NO').NORSV                      @D356DAP 06050000
         AIF   ('&RESERVE' NE 'YES').ERR06                     @D356DAP 06100000
&REG0    SETA  &REG0+&RSV                                      @D356DAP 06200000
         AGO   .RSEND                                          @D356DAP 06250000
.NORSV   ANOP                                                  @D356DAP 06300000
&REG0    SETA  &REG0+&RLS                                      @D356DAP 06400000
.RSEND   ANOP                                                  @D356DAP 06450000
.*                                                                      06500000
.*       LOGUNIT OPERAND EVALUATION                                     06550000
.*                                                                      06600000
.LU      AIF   (T'&LOGUNIT EQ 'O').CU                                   06650000
         AIF   (T'&INSTALL NE 'O').ERR0A                       @D61DDGL 06675001
         AIF   ('&LOGUNIT'(1,1) EQ '(').LUDYN                           06700000
&OP      SETC  'L'                                                      06750000
&IUN     SETC  '=A(&LOGUNIT)'                                           06800000
         AGO   .LU1                                                     06850000
.LUDYN   AIF   ('&LOGUNIT'(1,3) EQ '(S,').LUSTYP                        06900000
&OP      SETC  'LR'                                                     06950000
&IUN     SETC  '&LOGUNIT'(2,K'&LOGUNIT-2)                               07000000
         AGO   .LU1                                                     07050000
.LUSTYP  ANOP                                                           07100000
&OP      SETC  'LA'                                                     07150000
&IUN     SETC  '&LOGUNIT'(4,K'&LOGUNIT-4)                               07200000
.LU1     ANOP                                                           07250000
&REG0    SETA  &REG0+&LOG                                               07300000
&DEVFD   SETB  1                                                        07350000
.*                                                                      07400000
.*       CHNUNIT OPERAND EVALUATION                                     07450000
.*                                                                      07500000
.CU      AIF   (T'&CHNUNIT NE 'O').CUCONT                               07530001
.INSTCK  AIF   ('&INSTALL' EQ 'YES').ERR0B                     @D61DDGL 07560001
         AGO   .CUEND                                          @D61DDGL 07590001
.CUCONT  AIF   (&DEVFD EQ 1).ERR01                                      07620001
         AIF   ('&CHNUNIT'(1,1) EQ '(').CUDYN                           07650000
&OP      SETC  'L'                                                      07700000
&IUN     SETC  '=A(&CHNUNIT)'                                           07750000
         AGO   .CU1                                                     07800000
.CUDYN   AIF   ('&CHNUNIT'(1,3) EQ '(S,').CUSTYP                        07850000
&OP      SETC  'LR'                                                     07900000
&IUN     SETC  '&CHNUNIT'(2,K'&CHNUNIT-2)                               07950000
         AGO   .CU1                                                     08000000
.CUSTYP  ANOP                                                           08050000
&OP      SETC  'LA'                                                     08100000
&IUN     SETC  '&CHNUNIT'(4,K'&CHNUNIT-4)                               08150000
.CU1     ANOP                                                           08200000
&REG0    SETA  &REG0+&PHY                                               08250000
&DEVFD   SETB  1                                                        08300000
.CUEND   ANOP                                                           08350000
.*                                                             @D356DAP 08400000
.*       DEVTYPE OPERAND EVALUATION                            @D356DAP 08450000
.*                                                             @D356DAP 08500000
         AIF   (T'&DEVTYPE EQ 'O').DTEND                       @D356DAP 08550000
         AIF   (T'&INSTALL NE 'O').ERR0A                       @D61DDGL 08575001
         AIF   (&DEVFD EQ 1).ERR01                             @D356DAP 08600000
         AIF   ('&DEVTYPE'(1,1) EQ '(').DTDYN                  @D356DAP 08650000
&OP      SETC  'L'                                             @D356DAP 08700000
&IUN     SETC  '=A(&DEVTYPE)'                                  @D356DAP 08750000
         AGO   .DT1                                            @D356DAP 08800000
.DTDYN   AIF   ('&DEVTYPE'(1,3) EQ '(S,').DTSTYP               @D356DAP 08850000
&OP      SETC  'LR'                                            @D356DAP 08900000
&IUN     SETC  '&DEVTYPE'(2,K'&DEVTYPE-2)                      @D356DAP 08950000
         AGO   .DT1                                            @D356DAP 09000000
.DTSTYP  ANOP                                                  @D356DAP 09050000
&OP      SETC  'LA'                                            @D356DAP 09100000
&IUN     SETC  '&DEVTYPE'(4,K'&DEVTYPE-4)                      @D356DAP 09150000
.DT1     ANOP                                                  @D356DAP 09200000
&REG0    SETA  &REG0+&TYP                                      @D356DAP 09250000
&DEVFD   SETB  1                                               @D356DAP 09300000
         AIF   ('&CLASS' EQ 'TAPE').DTEND                      @D61ADAP 09325001
         AIF   (T'&RESERVE EQ 'O').DTRSV                       @D356DAP 09350000
         AIF   ('&RESERVE' EQ 'YES').DTEND                     @D356DAP 09400000
         MNOTE 1,'DEVTYPE OPERAND FORCES RESERVE=YES.'         @D356DAP 09450000
.DTRSV   ANOP                                                  @D356DAP 09500000
&REG0    SETA  &REG0+&RSV                                      @D356DAP 09550000
.DTEND   ANOP                                                  @D356DAP 09600000
.*                                                             @D36SDAP 09700000
.*       SHARE OPERAND EVALUATION                              @D36SDAP 09750000
.*                                                             @D36SDAP 09800000
         AIF   (T'&SHARE EQ 'O').SHEND                         @D21LDRP 09850000
         AIF   (T'&INSTALL NE 'O').ERR0A                       @D61DDGL 09875001
         AIF   ('&CLASS' NE 'DASD').ERR09                      @D21LDRP 09900000
         AIF   ('&SHARE' EQ 'NO').SHEND                        @D36SDAP 09950000
         AIF   ('&SHARE' NE 'YES').ERR03                       @D36SDAP 10000000
         AIF   (T'&CHNUNIT EQ 'O').ERR04                       @D36SDAP 10050000
         AIF   (T'&RESERVE EQ 'O').SHR                         @D36SDAP 10100000
         AIF   ('&RESERVE' NE 'NO').ERR05                      @D36SDAP 10150000
.SHR     ANOP                                                  @D36SDAP 10200000
&REG0    SETA  &REG0+&SHR                                      @D36SDAP 10250000
.SHEND   ANOP                                                  @D36SDAP 10300000
.*                                                             @D61ADAP 10302601
.*       MTC     OPERAND EVALUATION                            @D61ADAP 10305201
.*                                                             @D61ADAP 10307801
         AIF   (T'&MTC EQ 'O').NOTAP10                         @D61ADAP 10310401
         AIF   (T'&INSTALL NE 'O').ERR0A                       @D61ADAP 10313001
         AIF   ('&CLASS' NE 'TAPE').ERR0D                      @D61ADAP 10315601
         AIF   (T'&DEVTYPE NE 'O').ERR0F                       @D3570   10315703
         AIF   ('&MTC' EQ 'PLF').YMTC060                       @D3570   10315805
         AIF   ('&MTC' EQ 'RUN').YMTC070                       @D61ADAP 10318201
         AIF   ('&MTC' EQ 'AAC').YMTC080                       @D61ADAP 10320801
         AIF   ('&MTC' EQ 'DAC').YMTC090                       @D61ADAP 10323401
         AGO   .ERR0E                                          @D61ADAP 10326001
.YMTC060 ANOP                                                  @D3570   10327001
         AIF   (T'&PLFINFO EQ 'O').ERR0G                       @D3570   10327104
&COMAND  SETA  &COMAND+256        PLF                          @D3570   10328004
.YMTC070 ANOP                                                  @D61ADAP 10328601
&COMAND  SETA  &COMAND+256        RUN                          @D61ADAP 10331201
.YMTC080 ANOP                                                  @D61ADAP 10333801
&COMAND  SETA  &COMAND+256        AAC=READ                     @D61ADAP 10336401
.YMTC090 ANOP                                                  @D61ADAP 10339001
&COMAND  SETA  &COMAND+256        DAC=WRITE                    @D61ADAP 10341601
&REGF    SETA  &REGF+&COMAND                                   @D61NDAP 10344201
.*                                                             @D61ADAP 10344303
.*       PLFINFO OPERAND EVALUATION                            @D61ADAP 10344404
.*                                                             @D61ADAP 10344503
         AIF   (T'&PLFINFO EQ 'O').NOTAP10                     @D3570   10344604
         AIF   (T'&INSTALL NE 'O').ERR0A                       @D3570   10344703
         AIF   ('&MTC' NE 'PLF').ERR0H                         @D3570   10344804
         AIF   (&DEVFD EQ 1).ERR01                                      10344903
&DEVFD   SETB  1                                                        10345103
&REG0    SETA  &REG0+&PHY                                               10345203
         AIF   ('&PLFINFO'(1,1) EQ '(').MNTREG                 @D3570   10345304
&OP      SETC  'L'                                             @D3570   10345403
&IUN     SETC  '=A(&PLFINFO)'                                  @D3570   10346004
         AGO   .NOTAP10                                        @D3570   10346103
.MNTREG  AIF   ('&PLFINFO'(1,3) EQ '(S,').MNTSTYP              @D3570   10346204
&OP      SETC  'LR'                                            @D3570   10346303
&IUN     SETC  '&PLFINFO'(2,K'&PLFINFO-2)                      @D3570   10346404
         AGO   .NOTAP10                                        @D3570   10346503
.MNTSTYP ANOP                                                  @D3570   10346603
&OP      SETC  'LA'                                            @D3570   10346703
&IUN     SETC  '&PLFINFO'(4,K'&PLFINFO-4)                      @D3570   10346804
.NOTAP10 ANOP                                                  @D61ADAP 10348001
         AIF   (&DEVFD NE 1).ERR02                                      10349006
.*                                                                      10350000
.*       INSTALL OPERAND EVALUATION                                     10356201
.*                                                                      10362401
         AIF   (T'&INSTALL EQ 'O').INSTEND                     @D61DDGL 10368601
         AIF   ('&INSTALL' NE 'YES').ERR0C                     @D61DDGL 10374801
&REG0    SETA  &REG0+&INS                                      @D61DDGL 10381001
.INSTEND ANOP                                                  @D61DDGL 10387201
.*                                                                      10393401
.*       GENERATE CODE                                                  10400000
.*                                                                      10450000
         AIF   ('&NAME' EQ '').GEN10                                    10500000
&NAME    DC    0H'0'                                                    10550000
.GEN10   ANOP                                                  @D61ADAP 10564201
         AIF   (NOT &IJBSGEN).NSUP010                          @D61ADAP 10578401
         L     RD,BASESERI        SGSERI BASE POINTER          @D61ADAP 10592601
         L     RD,ASV101BA-SGSERI(RD) SV101BAL BASE ADDRESS    @D61ADAP 10606801
.NSUP010 ANOP                                                  @D61ADAP 10621001
         AIF   ('&IUN' EQ '1').GEN20                           @D61ADAP 10635201
         &OP   1,&IUN             LOAD FIELD ADDRESS                    10650000
.GEN20   LA    0,&REG0            LOAD FLAG BITS                        10700000
         AIF   (&REGF EQ 0).GEN30                              @D21LDRP 10750000
         LA    15,&REGF           GET CLASS AND FUNCTION       @D21LDRP 10775001
         SLL   15,8               INDICATION                   @D21LDRP 10800001
         AR    0,15               INTO REGISTER 0              @D21LDRP 10825001
         AIF   (NOT &IJBSGEN).NSUP020                                   10850001
         BASSM RC,RD              EXECUTE MODVCE FUNCTION      @D61ADAP 10875001
         AGO   .YSUP020                                                 10900001
.NSUP020 ANOP                                                           10925001
.GEN30   SVC   101                MODIFY VOLUME ENTRY                   10950000
.YSUP020 ANOP                                                           10975001
         MEXIT                                                          11000000
.*                                                                      11050000
.*       ERROR MESSAGES                                                 11100000
.*                                                                      11150000
.ERR01   MNOTE 8,'MULTIPLE DEVICE SPECIFICATION, GENERATION SUPPRESSED' 11200000
         AGO   .DLABEL                                                  11250000
.ERR02   MNOTE 8,'DEVICE SPECIFICATION MISSING, GENERATION SUPPRESSED'  11300000
         AGO   .DLABEL                                                  11350000
.ERR03   MNOTE 8,'SHARE OPERAND INVALID, GENERATION SUPPRESSED'         11400000
         AGO   .DLABEL                                                  11450000
.ERR04   MNOTE 8,'SHARE=YES REQUIRES CHNUNIT SPECIFICATION,            *11500000
               GENERATION SUPPRESSED'                                   11550000
         AGO   .DLABEL                                                  11600000
.ERR05   MNOTE 8,'SHARE=YES AND RESERVE=YES ARE MUTUALLY EXCLUSIVE,    *11650000
               GENERATION SUPPRESSED'                                   11700000
         AGO   .DLABEL                                                  11750000
.ERR06   MNOTE 8,'RESERVE OPERAND INVALID, GENERATION SUPPRESSED'       11800000
         AGO   .DLABEL                                         @D21LDRP 11850000
.ERR07   MNOTE 8,'CLASS=&CLASS INVALID, GENERATION SUPPRESSED' @D21LDRP 11900000
         AGO   .DLABEL                                         @D21LDRP 11950000
.ERR08   MNOTE 8,'RESERVE OPERAND INVALID FOR CLASS=&CLASS'    @D21LDRP 12000000
         AGO   .DLABEL                                         @D21LDRP 12050000
.ERR09   MNOTE 8,'SHARE OPERAND INVALID FOR CLASS=&CLASS'      @D21LDRP 12100000
         AGO   .DLABEL                                         @D21LDRP 12107101
.ERR0A   MNOTE 8,'INSTALL OPERAND INVALID FOR THIS INVOCATION' @D61DDGL 12114201
         AGO   .DLABEL                                         @D21LDRP 12121301
.ERR0B   MNOTE 8,'CHNUNIT OPERAND REQUIRED FOR INSTALL=YES'    @D61DDGL 12128401
         AGO   .DLABEL                                         @D21LDRP 12135501
.ERR0C   MNOTE 8,'YES IS ONLY VALID VALUE FOR INSTALL= PARM'   @D61DDGL 12142601
         AGO   .DLABEL                                         @D21LDRP 12144001
.ERR0D   MNOTE 8,'MTC IS ONLY VALID FOR CLASS=TAPE'            @D61ADAP 12145401
         AGO   .DLABEL                                         @D21LDRP 12146801
.ERR0E   MNOTE 8,'MTC=&MTC INVALID, GENERATION SUPPRESSED'     @D61ADAP 12148201
         AGO   .DLABEL                                         @D21LDRP 12148303
.ERR0F   MNOTE 8,'DEVTYPE TOGETHER WITH MTC=&MTC IS INVALID'   @D3570   12148403
         AGO   .DLABEL                                         @D3570   12148503
.ERR0G   MNOTE 8,'MTC=&MTC REQUIRES PLFINFO OPERAND'           @D3570   12148604
         AGO   .DLABEL                                         @D3570   12148703
.ERR0H   MNOTE 8,'PLFINFO IS ONLY VALID WITH MTC=PLF'          @D3570   12148804
.*                                                                      12150000
.*                                                                      12200000
.DLABEL  AIF   ('&NAME' EQ '').NONAM                                    12250000
&NAME    DC    0H'0'              LABEL OF SUPPRESSED MACRO             12300000
.NONAM   MEND                                                           12350000
