         MACRO                                                 @D23BDWS 00050000
&NAM     IPW$GQR &DW                                           @D23BDWS 00100000
.*                                                                      00150000
.* IBM DISK OPERATING SYSTEM                                            00200000
.* VSE/POWER - 5686-CF7-03                                              00250001
.* IPW$GQR MACRO INSTRUCTION - GET QUEUE RECORD                         00300000
.*                                                                      00350000
.* THE GET QUEUE RECORD MACRO PROVIDES ENTRY TO THE QUEUE FILE          00400000
.* SERVER ROUTINE LOCATED IN THE VSE/POWER NUCLEUS.                     00450000
.* THE ONLY PARAMETER POINTS TO AN I/O REQUEST WORD CONTAINING          00500000
.* A RELATIVE QUEUE RECORD NUMBER IN ITS FIRST FULLWORD AND             00550000
.* THE ADDRESS OF A VIRTUAL QUEUE RECORD INPUT AREA IN ITS              00600000
.* SECOND FULLWORD.                                                     00650000
.* UPON THIS REQUEST THE QUEUE FILE SERVER WILL OBTAIN THE              00700000
.* QUEUE RECORD - DEFINED BY ITS RELATIVE NUMBER - FROM THE             00750000
.* VIO SPACE AND MOVE IT TO THE GIVEN VIRTUAL INPUT AREA.               00800000
.*                                                                      00850000
.* NOTE: THE LENGTH FIELD OF THE I/O REQUEST WORD NEED NOT              00900000
.*       BE SET BY THE USER OF IPW$GQR; THE QUEUE FILE SERVER           00950000
.*       WILL ALWAYS OPERATE WITH THE 'QUEUE RECORD LENGTH'.            01000000
.*                                                                      01050000
         GBLB  &NUSA                                           @D23BDWS 01100000
         LCLC  &RG                 REGISTER LABEL              @D23BDWS 01150000
.*                                                                      01200000
         AGO   .ZZZ                                                     01250000
*        LICENSED MATERIALS - PROPERTY OF IBM                           01300000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                    01350000
*        5686-CF7 (C) COPYRIGHT IBM CORP. 1979, 2004                    01400001
*        SEE COPYRIGHT INSTRUCTIONS                                     01450000
.ZZZ     ANOP                                                           01500000
.*                                                                      01550000
         AIF   (T'&NAM EQ 'O').GQ0                             @D23BDWS 01600000
&NAM     DS    0H                                              @D23BDWS 01650000
.GQ0     ANOP                                                  @D23BDWS 01700000
         AIF   ('&DW' EQ '(R1)').GQ2                           @D23BDWS 01750000
         AIF   ((K'&DW NE 4) OR ('&DW'(1,2) NE '(R')).GQ1      @D23BDWS 01800000
&RG      SETC  '&DW'(2,2)          GET USER DEFINED REGISTER   @D23BDWS 01850000
         LR    R1,&RG              COPY I/O REQUEST WORD ADDR  @D23BDWS 01900000
         AGO   .GQ2                AND CONTINUE                @D23BDWS 01950000
.GQ1     ANOP                                                  @D23BDWS 02000000
         LA    R1,&DW              ADDRESS I/O REQUEST WORD    @D23BDWS 02050000
.GQ2     ANOP                                                  @D23BDWS 02100000
         MVI   IOOP-IODS(R1),IOQGET  INDICATE GET QUEUE RECORD @D23BDWS 02150000
.*                                                                      02200000
         AIF   (&NUSA).GQ3                                     @D23BDWS 02250000
         SPACE                                                          02300000
*        LINK TO NUCLEUS SERVICE FROM OUTSIDE NUCLEUS.  HIGH-ORDER      02350000
*        BYTE OF LINK REG MUST BE NON-ZERO.  'BAL' INSURES THIS.        02400000
         SPACE                                                          02450000
         BAL   R2,QF00             GO TO QUEUE FILE SERVER     @D23BDWS 02500000
         AGO   .GQ4                                            @D23BDWS 02550000
.GQ3     ANOP                                                  @D23BDWS 02600000
         SPACE                                                          02650000
*        LINK TO NUCLEUS SERVICE FROM INSIDE NUCLEUS.  HIGH-ORDER       02700000
*        BYTE OF LINK REG MUST BE ZERO.  'LA' INSURES THIS.             02750000
         SPACE                                                          02800000
         LA    R2,*+8              GET LINK ADDRESS            @D23BDWS 02850000
         B     QF00                GO TO QUEUE FILE SERVER     @D23BDWS 02900000
.GQ4     ANOP                                                  @D23BDWS 02950000
         SPACE                                                          03000000
*        THE CONTENTS OF REGISTERS 0, 1, 2 AND 3 HAVE BEEN DESTROYED    03050000
         MEND                                                  @D23BDWS 03100000
