         MACRO                                                          00010009
&NAME    LABEL &LPLADDR=,&FUNCT=,&SLAADDR=                     @D65BDFM 00020009
.*                                                                      00030009
         AGO   .ZZ                                                      00040009
************************************************************            00050009
*                                                          *            00060009
*         IBM DISK OPERATING SYSTEM                        *            00070009
*         JOB CONTROL - LABEL - 5686-CF7-06-81C            *   @D68KDFM 00080016
*                                                          *            00090009
*         THIS MACRO IS "RESTRICTED MATERIALS OF IBM"      *            00100009
*         ALL RIGHTS RESERVED                              *            00110009
*         5686-CF7 COPYRIGHT IBM CORP 1977,2004            *   @D68KDFM 00120016
*         LICENSED MATERIALS-PROPERTY OF IBM               *            00130009
*         SEE COPYRIGHT INSTRUCTIONS, G120-2083            *            00140009
*                                                          *            00150009
************************************************************            00160009
.ZZ      ANOP                                                           00170009
.*                                                                      00180009
.*********************************************************************  00190009
.* MACRO NAME: LABEL (DOS/VS)                                           00200009
.*                                                                      00210009
.* DESCRIPTIVE NAME: INTERFACE MACRO TO CALL SYMBOLIC LABEL ACCESS      00220009
.*                                                                      00230009
.* STATUS: AF 4.1.0                                                     00240009
.*                                                                      00250009
.* FUNCTION:                                                            00260009
.*      - LOAD FUNCTION CODE INTO REGISTER 0                            00270009
.*      - LOAD ADDRESS OF LABEL PARAMETER LIST INTO REGISTER 1          00280009
.*      - LOAD ADDRESS OF SLA PROGRAM INTO REGISTER 15                  00290009
.*      - LOAD RETURN ADDRESS INTO REGISTER 14                          00300009
.*      - BRANCH TO SYMBOLIC LABEL ACCESS                               00310009
.*                                                                      00320009
.* DEPENDENCIES: THOSE OF SLA PROGRAM WHICH IS CALLED                   00330009
.*                                                                      00340009
.* RESTRICTIONS: THOSE OF SLA PROGRAM WHICH IS CALLED                   00350009
.*                                                                      00360009
.* PROCESSOR: ASSEMBLER                                                 00370009
.*                                                                      00380009
.* MODULE SIZE: NO SIZE LIMITATIONS                                     00390009
.*                                                                      00400009
.* ATTRIBUTES: REENTRANT                                                00410009
.*                                                                      00420009
.* REGISTER CONVENTIONS:                                                00430009
.*    REG. 13 MUST CONTAIN THE ADDRESS OF A REGISTER SAVE AREA          00440009
.*    REG. 14 - 1 ARE DESTROYED                                         00450009
.*                                                                      00460009
.* INPUT:                                                               00470009
.* <NAME>   LABEL      LPLADDR = ADDRESS / (REGISTER),                  00480009
.*                     FUNCT = GETLBL / GETNXL / REPLBL /               00490009
.*                              ADDLBL / ADDNXL / CLRGRPL/              00500009
.*                              ENDLBL / LOCGRPL / GETNXGL/             00510009
.*                              PUTOV / GETOV / PUTPN / GETPN /         00520009
.*                              MODGRPL / SRCLBL / CPCTYLBL /  @D65BDFM 00530009
.*                              DELLBL / QRYGRPL / GETLBLID    @D68KDFM 00530115
.*                              GETLBLIC                       @D68KDFM 00530215
.*                              (REGISTER),                    @D65BDFM 00530314
.*                     SLAADDR = ADDRESS / (REGISTER)          @D65BDFM 00530414
.*                                                             @D65BDFM 00530514
.* PARAMETER DESCRIPTION                                                00530614
.*                                                                      00530714
.* LPLADDR = ADDRESS / (REGISTER)                                       00530814
.*      THIS KEYWORD OPERAND SPECIFIES  THE ADDRESS  OF THE  LPL        00530914
.*      ASSOCIATED WITH THE MACRO.  IF  ADDRESS IS SPECIFIED THIS IS    00531014
.*      THE  ASSEMBLER LABEL  OF THE  LPL.                              00531114
.*      IF (REGISTER)  IS  SPECIFIED  THE ITEM  WITHIN  THE             00531214
.*      BRACKETS SPECIFIES A  REGISTER THAT CONTAINS THE  ADDRESS OF    00532009
.*      THE  LPL AT  EXECUTION  TIME.                                   00533009
.*                                                                      00534009
.* FUNCT= GETLBL / GETNXL / REPLBL / ADDLBL / ADDNXL / CLRGRPL /        00535009
.*        ENDLBL / LOCGRPL / GETNXGL / PUTOV / GETOV / PUTPN / GETPN    00536009
.*        / MODGRPL / SRCLBL / CPCTYLBL / DELLBL / QRYGRPL /   @D67EDFM 00537010
.*        (REGISTER)                                                    00537110
.*                                                                      00538009
.* SLAADDR = ADDRESS / (REGISTER)                              @D65BDFM 00539009
.*      THIS KEYWORD OPERAND SPECIFIES THE START ADDRESS OF    @D65BDFM 00540009
.*      $IJBSLA. IF THIS KEYWORD IS SPECIFIED, THE MACRO       @D65BDFM 00550009
.*      EXPANSION WILL *NOT* CONTAIN A LOAD $IJBSLA BUT BAL(R) @D65BDFM 00560009
.*      TO THE ADDRESS OR REGISTER CONTENT DIRECTLY.           @D65BDFM 00570009
.*      THIS MAY BE A PERFORMANCE BENEFIT FOR PROGRAMS WITH    @D65BDFM 00580009
.*      THE NEED TO EXECUTE THE LABEL MACRO CODE VERY OFTEN.   @D65BDFM 00590009
.*                                                                      00600009
.* GETLBL      GET A LABEL                                              00610009
.* GETNXL      GET NEXT LABEL                                           00620009
.* REPLBL      REPLACE A LABEL                                          00630009
.* ADDLBL      INSERT A LABEL INTO A LABEL SUBAREA                      00640009
.* ADDNXL      INSERT ADDITIONAL LABEL INFORMATION                      00650009
.* CLRGRPL     RESET A GROUP OF LABELS                                  00660009
.* ENDLBL      END OF ADDING TO A LABEL AREA                            00670009
.* LOCGRPL     LOCATE A GROUP OF LABELS                                 00671009
.* GETNXGL     GET NEXT LABEL FROM LOCATED GROUP OF LABELS              00672009
.* PUTOV       SAVE A PROCEDURE OVERWRITE STATEMENT                     00673009
.* GETOV       FETCH A PROCEDURE OVERWRITE STATEMENT                    00674009
.* PUTPN       SAVE A PHASE NAME                                        00675009
.* GETPN       FETCH A PHASE NAME                                       00676009
.* MODGRPL     REACTIVATE A GROUP OF LABELS                    @D36HDWS 00677009
.* SRCLBL      SEARCH FOR SAME LABEL IN GROUP                  @D37DDWS 00678009
.* CPCTYLBL    PROVIDE CAPACITY INFORMATION FOR LABEL AREA     @D65BDFM 00679009
.* DELLBL      DELETE A LABEL FROM A LABEL GROUP               @D66JDFM 00679113
.* QRYGRPL     CHECK WHETHER LABEL GROUP IS IN UPDATE MODE     @D67EDFM 00679213
.* GETLBLID    GET LABEL INFO RECORD PER FILE ID               @D68KDFM 00679315
.* GETLBLIC    GET LABEL INFO RECORD PER FILE ID & CATALOGNAME @D68KDFM 00679415
.*                                                                      00680009
.* THE PARAMETERS LPLADDR= AND FUNCT= ARE MANDATORY !!                  00690009
.* EXCEPTION: FOR ENDLBL FUNCTION THE LPLADDR PARAMETER HAS    @D36HDWS 00700009
.*  NOT BUT MAY BE SPECIFIED                                   @D36HDWS 00710009
.* THE PARAMETER SLAADDR= IS OPTIONAL.                         @D65BDFM 00720009
.* IF ONE OF THE MANDATORY PARAMETERS IS OMITTED OR SPECIFIED           00730009
.* INVALIDLY, THE MACRO IS IGNORED.                                     00740009
.*                                                                      00750009
.* OUTPUT:                                                              00760009
.*    SEE TOPIC FUNCTION                                                00770009
.*                                                                      00780009
.* EXIT:                                                                00790009
.*    SEE TOPIC FUNCTION                                                00800009
.*    BRANCH TO SLA (SYMBOLIC LABEL ACCESS)                             00810009
.*                                                                      00820009
.* CHANGE ACTIVTIY:                                                     00830009
.* ADAPT TO FAMILY API                                         @D62ADWS 00840009
.* PROVIDE SLAADDR OPERAND FOR PERFORMANCE IMPROVEMENT         @D65BDFM 00850009
.* PROVIDE CPCTYLBL FUNCTION FOR CAPACITY CONSIDERATIONS       @D65BDFM 00860009
.* PROVIDE DELLBL FUNCTION FOR LABEL DELETE (US.4)             @D66JDFM 00861009
.* PROVIDE QRYGRPL FUNCTION TO CHECK LABEL GROUP               @D67EDFM 00861110
.* PROVIDE GETLBLID/GETLBLIC TO SEARCH VIA FILEID/CATALAG      @D68KDFM 00861216
.* A000000-999999                                              @D35DDWH 00862009
.********************************************************************** 00863009
         AIF   (T'&FUNCT EQ 'O').ERROR1 FUNCTION CODE MANDATORY@D65BDFM 00864009
         AIF   (T'&LPLADDR EQ 'O' AND '&FUNCT' NE 'ENDLBL').ERROR2      00865009
.*       LPL ADDRESS MANDATORY, UNLESS ENDLBL SPECIFIED        @D65BDFM 00866009
         AIF   (T'&NAME EQ 'O').NONAME     NO LABEL SPECIFIED  @D65BDFM 00867009
&NAME    DS    0H                                              @D65BDFM 00868009
.NONAME  ANOP                                                  @D65BDFM 00869009
         AIF   (T'&LPLADDR EQ 'O' AND                                  C00870009
                T'&SLAADDR EQ 'O').LABEL4,                             C00880009
               (T'&LPLADDR EQ 'O').LABEL3,                             C00890009
               ('&LPLADDR'(1,1) EQ '(' AND                             C00900009
                T'&SLAADDR EQ 'O').LABEL2,                             C00910009
               ('&LPLADDR'(1,1) NE '(' AND                             C00920009
                T'&SLAADDR EQ 'O').LABEL4,                             C00930009
               ('&LPLADDR'(1,1) EQ '(' AND                             C00940009
                '&SLAADDR'(1,1) EQ '(').LABEL1,                        C00950009
               ('&LPLADDR'(1,1) EQ '(' AND                             C00960009
                '&SLAADDR'(1,1) NE '(').LABEL2,                        C00970009
               ('&LPLADDR'(1,1) NE '(' AND                             C00980009
                '&SLAADDR'(1,1) EQ '(').LABEL3,                        C00990009
               ('&LPLADDR'(1,1) NE '(' AND                             C01000009
                '&SLAADDR'(1,1) NE '(').LABEL4                 @D65BDFM 01010009
.LABEL1  ANOP  BOTH ADDRESSES IN REGISTER NOTATION             @D65BDFM 01020009
         AIF   ('&LPLADDR(1)' EQ '1').LABEL11                  @D65BDFM 01030009
         LR    1,&LPLADDR(1)               LPL ADDRESS IN R1   @D65BDFM 01040009
.LABEL11 ANOP                                                  @D65BDFM 01050009
         AIF   ('&SLAADDR(1)' EQ '15').FUNCT                   @D65BDFM 01060009
         LR    15,&SLAADDR(1)              SLA ADDRESS IN R15  @D65BDFM 01070009
         AGO   .FUNCT                                          @D65BDFM 01080009
.LABEL2  ANOP  LPL IN REGISTER NOTATION, SLA IN LABEL NOTATION @D65BDFM 01090009
         AIF   (T'&SLAADDR NE 'O').LABEL21 SLAADDR SPECIFIED?  @D65BDFM 01100009
         LA    14,0&LPLADDR   SAVE CONTENTS OF REG. BEFORE LOAD@D65BDFM 01110009
         ILOAD $IJBSLA        PUTS ADDR OF SLA INTO REG.1      @D62ADWS 01120009
         LR    15,1           GET ADDR OF SLA INTO REG.15               01130009
         LR    1,14           GET ORIGINAL REGISTER VALUE INTO REG.1    01140009
         AGO   .FUNCT                                          @D65BDFM 01150009
.LABEL21 ANOP                                                  @D65BDFM 01160009
         AIF   ('&LPLADDR(1)' EQ '1').LABEL22                  @D65BDFM 01170009
         LR    1,&LPLADDR(1)               LPL ADDRESS IN R1   @D65BDFM 01180009
.LABEL22 ANOP                                                  @D65BDFM 01190009
         L     15,&SLAADDR                 SLA ADDRESS IN R15  @D65BDFM 01200009
         AGO   .FUNCT                                          @D65BDFM 01210009
.LABEL3  ANOP  LPL IN LABEL NOTATION, SLA IN REGISTER NOTATION @D65BDFM 01220009
         AIF   ('&SLAADDR'(1,1) NE '(').LABEL4                 @D65BDFM 01230009
         AIF   ('&FUNCT' EQ 'ENDLBL').LABEL31                  @D65BDFM 01240009
         LA    1,&LPLADDR     GET ADDR OF LPL IF PARM IS A LABEL        01250009
.LABEL31 ANOP                                                  @D65BDFM 01260009
         AIF   ('&SLAADDR(1)' EQ '15').FUNCT                   @D65BDFM 01270009
         LR    15,&SLAADDR(1)              SLA ADDRESS IN R15  @D65BDFM 01280009
         AGO   .FUNCT                                          @D65BDFM 01290009
.LABEL4  ANOP  BOTH LPL AND SLA IN LABEL NOTATION              @D65BDFM 01300009
         AIF   (T'&SLAADDR NE 'O').LABEL41 SLAADDR SPECIFIED?  @D65BDFM 01310009
         ILOAD $IJBSLA        PUTS ADDR OF SLA INTO REG.1      @D62ADWS 01320009
         LR    15,1           GET ADDR OF SLA INTO REG.15               01330009
         AGO   .LABEL42                                        @D65BDFM 01340009
.LABEL41 ANOP                                                  @D65BDFM 01350009
         L     15,&SLAADDR                 SLA ADDRESS IN R15  @D65BDFM 01360009
.LABEL42 ANOP                                                  @D65BDFM 01370009
         AIF   ('&FUNCT' EQ 'ENDLBL').FUNCT                    @D65BDFM 01380009
         LA    1,&LPLADDR     GET ADDR OF LPL IF PARM IS A LABEL        01390009
.FUNCT   AIF   ('&FUNCT'(1,1) EQ '(').REGF                              01400009
.*                            PARM BEGINNING WITH PARENTHESIS  @D36HDWS 01410009
         AGO   .FUNCTA                                         @D36HDWS 01420009
.REGF    LR    0,&FUNCT(1)    LOAD FUNCTION CODE               @D36HDWS 01430009
         AGO   .END                                            @D36HDWS 01440009
.FUNCTA  AIF   ('&FUNCT' EQ 'GETLBL').GETLBL,                          C01450009
               ('&FUNCT' EQ 'GETNXL').GETNXL,                          C01460009
               ('&FUNCT' EQ 'REPLBL').REPLBL,                          C01470009
               ('&FUNCT' EQ 'ADDLBL').ADDLBL,                          C01480009
               ('&FUNCT' EQ 'ADDNXL').ADDNXL,                          C01490009
               ('&FUNCT' EQ 'CLRGRPL').CLRGRPL,                        C01500009
               ('&FUNCT' EQ 'ENDLBL').ENDLBL,                          C01510009
               ('&FUNCT' EQ 'LOCGRPL').LOCGRPL,                        C01520009
               ('&FUNCT' EQ 'GETNXGL').GETNXGL,                        C01530009
               ('&FUNCT' EQ 'PUTOV').PUTOV,                            C01540009
               ('&FUNCT' EQ 'GETOV').GETOV,                            C01550009
               ('&FUNCT' EQ 'PUTPN').PUTPN,                            C01560009
               ('&FUNCT' EQ 'GETPN').GETPN,                            C01570009
               ('&FUNCT' EQ 'MODGRPL').MODGRPL,                        C01580009
               ('&FUNCT' EQ 'SRCLBL').SRCLBL,                          C01590009
               ('&FUNCT' EQ 'CPCTYLBL').CAPACTY,               @D65BDFMC01600009
               ('&FUNCT' EQ 'DELLBL').DELLBL,                  @D67EDFMC01610010
               ('&FUNCT' EQ 'QRYGRPL').QRYGRPL,                @D68KDFMC01611015
               ('&FUNCT' EQ 'GETLBLID').GETLBLI,               @D68KDFMC01612015
               ('&FUNCT' EQ 'GETLBLIC').GETLBLC                @D68KDFM 01613015
         MNOTE 8,'INVAL. SPEC. OF FUNCT PARM. MACRO IGNORED.'           01620009
         MEXIT                                                          01630009
.GETLBL  LA    0,1            LOAD FUNCT CODE 1 FOR GETLBL              01640009
         AGO   .END           GO TO CHECK NEXT PARAMETER                01650009
.GETNXL  LA    0,2            LOAD FUNCT CODE 2 FOR GETNXL              01660009
         AGO   .END                                                     01670009
.REPLBL  LA    0,3            LOAD FUNCT CODE  3 FOR REPLBL             01680009
         AGO   .END                                                     01690009
.ADDLBL  LA    0,4            LOAD FUNCT CODE  4 FOR ADDLBL             01700009
         AGO   .END                                                     01710009
.ADDNXL  LA    0,5            LOAD FUNCT CODE  5 FOR ADDNXL             01720009
         AGO   .END                                                     01730009
.CLRGRPL LA    0,6            LOAD FUNCT CODE  6 FOR CLRGRPL            01740009
         AGO   .END                                                     01750009
.ENDLBL  LA    0,7            LOAD FUNCT CODE  7 FOR ENDLBL    @D65BDFM 01760009
         AGO   .END                                                     01770009
.LOCGRPL LA    0,8            LOAD FUNCT CODE  8 FOR LOCGRPL            01780009
         AGO   .END                                                     01790009
.GETNXGL LA    0,9            LOAD FUNCT CODE  9 FOR GETNXGL            01800009
         AGO   .END                                                     01810009
.PUTOV   LA    0,10           LOAD FUNCT CODE 10 FOR PUTOV              01820009
         AGO   .END                                                     01830009
.GETOV   LA    0,11           LOAD FUNCT CODE 11 FOR GETOV              01840009
         AGO   .END                                                     01850009
.PUTPN   LA    0,12           LOAD FUNCT CODE 12 FOR PUTPN              01860009
         AGO   .END                                                     01870009
.GETPN   LA    0,13           LOAD FUNCT CODE 13 FOR GETPN              01880009
         AGO   .END                                                     01890009
.MODGRPL LA    0,16           LOAD FUNCT CODE 16 FOR MODGRPL   @D36HDWS 01900009
         AGO   .END                                            @D36HDWS 01910009
.SRCLBL  LA    0,17           LOAD FUNCT CODE 17 FOR SRCLBL    @D37DDWS 01920009
         AGO   .END                                            @D37DDWS 01930009
.CAPACTY LA    0,18           LOAD FUNCT CODE 18 FOR CPCTYLBL  @D65BDFM 01940009
         AGO   .END                                            @D65BDFM 01950009
.DELLBL  LA    0,19           LOAD FUNCT CODE 19 FOR DELLBL    @D66JDFM 01960009
         AGO   .END                                            @D66JDFM 01970009
.QRYGRPL LA    0,20           LOAD FUNCT CODE 20 FOR QRYGRPL   @D67EDFM 01971010
         AGO   .END                                            @D67EDFM 01972010
.GETLBLI LA    0,21           LOAD FUNCT CODE 21 FOR GETLBLID  @D68KDFM 01973015
         AGO   .END                                            @D68KDFM 01974015
.GETLBLC LA    0,22           LOAD FUNCT CODE 22 FOR GETLBLIC  @D68KDFM 01975015
         AGO   .END                                            @D68KDFM 01976015
.ERROR1  MNOTE 8,'FUNCT PARAMETER MISSING. MACRO IGNORED.'     @D65BDFM 01980009
         MEXIT                                                          01990009
.ERROR2  MNOTE 8,'LPLADDR PARAMETER MISSING. MACRO IGNORED.'   @D65BDFM 02000009
         MEXIT                                                          02010009
.END     MC    0,4            CHANGE PSW-KEY TO 0 IF MACRO ISSUED       02020009
.*       FROM PSEUDO-PARTITION (MCODE 0) BY ETSS (MCLASS 4)             02030009
         BALR  14,15          RETURN ADDR INTO REG.14                   02040009
         MC    3,4            CHANGE PSW-KEY TO PSW-KEY OF PSEUDO       02050009
.*             -PARTITION (MCODE 3) OF ETSS (MONITORCLASS 4)            02060009
         MEND                                                           02070009
