*%GOTO   PRODEXIT1; /* PLX COMMENT TO AVOID UNMATCHED QUOTE PROBLEMS    00050061
         MACRO                                                          00100061
&NAME    PRODEXIT &FUNC,                                               *00150061
               &AREA=,                                                 *00200061
               &CLASS=,                                                *00250061
               &DYN=,                                          @D61YDHK*00270061
               &ID=,                                                   *00300061
               &RC=                                                     00350061
         GBLB  &IJBSGEN                                                 00400061
         LCLA  &IJBRID                                                  00412561
         LCLB  &IJBB1                                                   00425061
         LCLB  &IJBRC,&IJBAR,&IJBSRC,&IJBVTMP                  @D61YDHK 00465041
         LCLB  &IJBDYN                                         @D61YDFG 00470031
         LCLC  &IJBR1                                                   00475061
.*                                                                      00500061
*        SUPERVISOR - PRODEXIT - 5686-CF7-06                            00550061
         AGO   .NOCOPY                                                  00600061
*********************************************************************** 00650061
*                                                                     * 00700061
*        LICENSED MATERIALS - PROPERTY OF IBM                         * 00750061
*        "RESTRICTED MATERIALS OF IBM"                                * 00800061
*        5686-CF7 (C) COPYRIGHT IBM CORP. 1994, 2003           @D61YDHK 00900051
*                                                                     * 00950061
*********************************************************************** 01000061
.NOCOPY  ANOP                                                           01050061
.* /* START OF SPECIFICATIONS ***************************************** 01100061
.*                                                                    * 01150061
.* MODULE-TYPE = SUPERVISOR SVC/BASSM INTERFACE MACRO          @D61YDHK 01220051
.*                                                                    * 01250061
.* DESCRIPTIVE NAME = EXIT PROCESSING FOR VENDOR PRODUCTS             * 01300061
.*                                                                    * 01350061
.* STATUS = NEW IN VSE/AF 6.1.0, 5686-CF7-06                          * 01400061
.*                                                                    * 01450061
.* SYNTAX OF MACRO                                                    * 01500061
.* <NAME> PRODEXIT (ACTIVATE|CHECK|DEFINE),AREA= ((1)|NAME1)          * 01550061
.*  IN SSTATE:      ACTIVATE,ID=SCLFCT<,AREA=((1)|NAME1)>      @D61YDHK 01574651
.*                                         <,RC=(YES|NO)>             * 01583261
.*                  R13 MUST POINT TO PROGRAM SAVE AREA FOR           * 01600061
.*                                             NON-SUPERVISOR         * 01625061
.* <NAME> PRODEXIT (DELETE|DISABLE)                            @D61YDHK 01672551
.*                  INPUT: PRODEXIT TOKEN IN R1 AND R2                * 01700061
.* <NAME> PRODEXIT (ENABLE|DISABLE)<,DYN=YES,ID=SCLFCT>        @D61YDHK 01703061
.*                  INPUT: PRODEXIT TOKEN IN R1 AND R2         @D61YDHK 01706061
.* <NAME> PRODEXIT (ENABLE)                                    @D61YDHK 01709061
.*                  INPUT: PRODEXIT TOKEN IN R1 AND R2         @D61YDHK 01712061
.* <NAME> PRODEXIT (RETURN)                                           * 01716661
.*                  INPUT: REGISTER 14 MUST CONTAIN THE RETURN ADDRESS* 01733261
.* <NAME> PRODEXIT  DSECT<,CLASS=(BAM|SVC|FSVC|LNG|PSUP|SUP|ALL       * 01750061
.*                                |BINT)>                             * 01800061
.*                                                                    * 01900061
.* REGISTER USAGE (EXCEPT FOR PRODEXIT DSECT):                        * 01950061
.*       REGISTER 15  FUNCTION CODE, RETURN CODE                      * 02000061
.*       REGISTER 0   ADD. FUNCTION CODE, ADD. RETURN CODE            * 02050061
.*       REGISTER 1   POINTER TO INPUT AREA, OR 1ST HALF OF TOKEN     * 02100061
.*       REGISTER 2   2ND HALF OF TOKEN                               * 02150061
.*                                                                    * 02200061
.* FUNCTION CODES FOR SVC 124 IN REGISTER 15 AND 0                    * 02250061
.*                                                                    * 02300061
.* FUNCTION                   REG 15   REG 0                          * 02370051
.* PRODID                                                             * 02400061
.*  AUTH=YES                    0        1                            * 02500051
.*  AUTH=NO                     0        0                            * 02550041
.*  CHECK                       3        -                            * 02600031
.*  DEFINE                      1                                     * 02650021
.*  DELETE                      2                                     * 02700011
.*  DSECT                       -        -                            * 02750001
.*                                                                    * 02800061
.* PRODEXIT                                                           * 02850061
.*  ACTIVATE (PSTATE)           8        -                     @D61YDHK 02930051
.*  ACTIVATE (SSTATE)           D        -                     @D61YDHK 02960041
.*  CHECK                       7        -                            * 02990031
.*  DEFINE                      5        -                            * 03020021
.*  DELETE                      6        -                            * 03050011
.*  DISABLE                     0        3                            * 03080001
.*  DISABLE,DYN=YES,ID=SCLFCT   F        -                     @D61YDHK 03109991
.*  DSECT                       -        -                            * 03139981
.*  ENABLE                      0        2                            * 03169971
.*  ENABLE,DYN=YES,ID=SCLFCT    E        -                     @D61YDHK 03199961
.*  RETURN                      9        -                            * 03229951
.*                                                                    * 03350061
.* CHANGE ACTIVITIES:                                                 * 03400061
.* A000000-999999                                              @D61EDEM 03450061
.* @DY43684 ADDITIONAL VENDOR REQUIREMENTS                     @DY43684 03470061
.* @DY43775 ADD. V. REQUIREMENTS ( EXT AND SVC EXITS )         @DY43775 03486051
.* @D61YDHK VI PERFORMANCE IMPROVEMENT / DYNAMIC SERVICES      @D61YDHK 03492041
.*                                                                    * 03500061
.**** END OF SPECIFICATIONS ******************************************* 03550061
.* * * * * *  CHECK FOR CORRECT FUNCTION/PARAMETERS  * * * * * * * *    03600061
         AIF   (T'&DYN EQ 'O').FUN06                           @D61YDHK 03656051
         AIF   ('&FUNC' EQ 'ENABLE' OR '&FUNC' EQ 'DISABLE').FUN02      03662041
         MNOTE 8,' PARAMETER ''DYN='' NOT ALLOWED FOR FUNC=&FUNC,  GENE*03668031
               RATION SUPPRESSED'                              @D61YDHK 03674021
         SPACE 3                                               @D61YDHK 03680011
         MEXIT                                                 @D61YDHK 03686001
.FUN02   ANOP                                                  @D61YDHK 03691991
         AIF   ('&DYN' EQ 'YES' OR '&DYN' EQ 'NO').FUN06       @D61YDHK 03697981
         MNOTE 8,' DYN=&DYN INVALID, VALID ARE YES OR NO, GENERATION   *03703971
               SUPPRESSED'                                     @D61YDHK 03709961
         SPACE 3                                               @D61YDHK 03715951
         MEXIT                                                 @D61YDHK 03721941
.FUN06   AIF   (T'&FUNC NE 'O').FUN10                          @D61YDHK 03727931
.FUN07   MNOTE 8,' MACRO FUNCTION OMITTED, GENERATION SUPPRESSED'       03733921
         SPACE 3                                                        03750061
         MEXIT                                                          03800061
.FUN10   AIF   (N'&SYSLIST EQ 1).FUN20                                  03850061
.FUN12   MNOTE 8,' MORE THAN ONE POSITIONAL OPERAND, GENERATION SUPPRES*03930051
               SED'                                            @D61YDHK 03960041
         SPACE 3                                                        04000061
         MEXIT                                                          04050061
.FUN20   AIF   ('&FUNC' EQ 'DSECT').DS10                                04100061
         AIF   ('&NAME' EQ '').FUN30                                    04150061
&NAME    DS    0H                                                       04200061
.FUN30   ANOP                                                           04250061
         AIF   ('&FUNC' EQ 'ACTIVATE' AND &IJBSGEN).ACT20               04287561
         AIF   ('&FUNC' EQ 'ACTIVATE' AND T'&ID NE 'O').ACT20           04306261
         AIF   ('&FUNC' EQ 'ACTIVATE').AREA10                           04325061
         AIF   ('&FUNC' EQ 'CHECK' OR '&FUNC' EQ 'DEFINE').AREA10       04362561
         AIF   ('&FUNC' EQ 'DELETE' OR '&FUNC' EQ 'RETURN').NOTH10      04400061
         AIF   ('&FUNC' EQ 'ENABLE' OR '&FUNC' EQ 'DISABLE').NOTH10     04450061
         MNOTE 8,' &FUNC IS NO VALID FUNCTION, GENERATION SUPPRESSED'   04500061
         SPACE 3                                                        04550061
         MEXIT                                                          04600061
.* * * * *  ONLY AREA REQUIRED FOR ACTIVATE, CHECK, DEFINE * * * * *    04650061
.AREA10  ANOP                                                           04700061
         AIF   (T'&AREA NE 'O').AREA20                                  04750061
         MNOTE 8,' ''AREA='' REQUIRED BUT MISSING, GENERATION SUPPRESSE*04783361
               D'                                                       04816661
         SPACE 3                                                        04850061
         MEXIT                                                          04900061
.AREA20  AIF   (T'&CLASS NE 'O' OR T'&ID NE 'O').AREA30                 04937561
         AIF   (T'&RC EQ 'O').AREA40                                    04975061
.AREA30  MNOTE 8,' ''AREA='' IS ONLY VALID PARAMETER FOR &FUNC, GENERAT*05012561
               ION SUPPRESSED'                                          05050061
         SPACE 3                                                        05150061
         MEXIT                                                          05200061
.* * * * * * * * * * *  AREA TO REGISTER 1  * * * * * * * * * * * * * * 05250061
.AREA40  ANOP                                                           05275061
&IJBAR   SETB  1                  AREA PASSED BY CALLER                 05300061
         AIF   ('&AREA' EQ '(1)').AREA60                                05325061
         AIF   ('&AREA'(1,1) EQ '(').AREA50                             05350061
         LA    1,&AREA                                                  05400061
         AGO   .AREA60                                                  05500061
.AREA50  ANOP                                                           05550061
&IJBR1   SETC  '&AREA'(2,K'&AREA-2)                                     05600061
         LR    1,&IJBR1                                                 05650061
.AREA60  AIF   ('&FUNC' EQ 'ACTIVATE' AND &IJBSGEN).ACT50               05683361
         AIF   ('&FUNC' EQ 'ACTIVATE' AND T'&ID NE 'O').ACT60           05699961
         AIF   ('&FUNC' EQ 'ACTIVATE').ACT10                            05716661
         AIF   ('&FUNC' EQ 'CHECK').CHEK10                              05750061
.* * * * * * * * * * * *  FUNC=DEFINE  * * * * * * * * * * * * * *      05800061
.DEF10   LA    15,5               FUNCTION=DEFINE                       05850061
         SVC   124                                                      05900061
         SPACE 3                                                        05950061
         MEXIT                                                          06000061
.* * * * * * * * * * * *  FUNC=CHECK  * * * * * * * * * * * * * *       06050061
.CHEK10  ANOP                                                           06100061
         LA    15,7               FUNCTION=CHECK                        06150061
         SVC   124                                                      06200061
         SPACE 3                                                        06250061
         MEXIT                                                          06300061
.* * * * * * * * * * * *  FUNC=ACTIVATE * * * * * * * * * * * * *       06350061
.* * * * * *  ACTIVATE IN PPSTATE  * * * * * * * * * * * * * *          06400061
.ACT10   LA    15,8               FUNCTION=ACTIVATE                     06450061
         SVC   124                                                      06550061
         SPACE 3                                                        06600061
         MEXIT                                                          06650061
.* * * * * *  ACTIVATE IN SSTATE  * * * * * * * * * * * * * *           06657561
.ACT20   AIF   (T'&ID NE 'O').ACT25                                     06665061
         MNOTE 8,' ACTIVATE IN SSTATE REQUIRES ''ID='', GENERATION SUPP*06672561
               RESSED'                                                  06680061
         SPACE 3                                                        06687561
         MEXIT                                                          06695061
.ACT25   ANOP                                                           06695861
         AIF   (T'&RC EQ 'O' OR '&RC' EQ 'NO').ACT30                    06696661
&IJBRC   SETB  1                  CALLER EXPECTS RETURN CODE IN R15     06697461
         AIF   ('&RC' EQ 'YES').ACT30                                   06698261
         MNOTE 8,' RC=&RC INVALID, VALID ARE YES OR NO, GENERATION SUPP*06699061
               RESSED'                                                  06699861
         SPACE 3                                                        06700661
         MEXIT                                                          06701461
.ACT30   ANOP                                                           06702561
         AIF   (T'&CLASS EQ 'O').ACT40                                  06710061
         MNOTE 8,' ''CLASS='' INVALID FOR ACTIVATE, GENERATION SUPPRESS*06717561
               ED'                                                      06725061
         SPACE 3                                                        06732561
         MEXIT                                                          06740061
.ACT40   AIF   (T'&AREA NE 'O').AREA40                                  06747561
         AIF   (NOT &IJBSGEN).ACT60                                     06749361
.* * * *   SSTATE ACTIVATE WITHIN SUPERVISOR * * * * * * * * * *        06751261
.* SET RID                                                              06752461
.*                                                                      06753661
.ACT50   ANOP                                                           06755061
&IJBRID  SETA  0                  RID 0 EXIT                            06756161
         AIF ('&ID' NE 'SVC' AND '&ID' NE 'EXT' AND                    X06757551
               '&ID' NE 'PCKEX').ACT52                         @D61NDHK 06757841
         SR    9,9                MAY NOT BE CLEARED BY IJBVEND         06758361
.ACT52   AIF ('&ID' EQ 'POSTSIO' OR '&ID' EQ 'IOINT').ACT54             06760561
         AIF ('&ID' EQ 'PCKEX' OR '&ID' EQ 'EXT').ACT54                 06761661
         AIF ('&ID' EQ 'FSVC').ACT54                           @DY43684 06763251
&IJBRID  SETA  4                  RID 4 EXIT                            06763861
.ACT54   ANOP                                                           06765561
         AIF ('&ID' NE 'PCKEX' AND '&ID' NE 'SVC').ACT55       @DY43684 06765631
&IJBSRC  SETB  1                  SET FLAG FOR SPECIAL TREATMNT@DY43684 06765701
.ACT55   ANOP                                                  @DY43684 06765771
         STM   13,15,VSEVISA      SAVE REG 13,14,15                     06765861
         AIF ('&ID' NE 'POSTSIO').ACT56                                 06766161
         IPM   13                 INSERT CC AND PROGRAM MASK            06766561
         ST    13,PRODXSCC        SAVE IT                               06767561
.ACT56   ANOP                                                           06768761
         L     13,APRODACT        POINT TO BOOTSTRAP FOR PRODEXIT ACT   06773061
*        BASSM 14,13                                                    06777461
         DC    0H'0',X'0C',AL.4(14,13)  BASSM 14,13                     06781161
         DC    AL4(&ID)           TYPE OF CLASS, SUBCLASS               06785061
         DC    AL3(&ID.C)         ZERO BYTE OFFSET AND MASK             06788061
         DC    B'&IJBSRC.&IJBDYN.000&IJBSGEN.&IJBRC.&IJBAR.'   @D61YDHK 06791741
*           X'80' SPECIAL RC TREATMENT DURING ACTIVATE         @DY43684 06792041
*           X'40' DYN. SERVICES ( ENABLE / DISABLE )           @D61YDHK 06792241
*           X'04' GENERATED WITHIN SUPERVISOR                  @DY43684 06792531
*           X'02' RC ACCEPTED                                  @DY43684 06793021
*           X'01' AREA PASSED DURING ACTIVATE                  @DY43684 06793511
         DC    AL2(&IJBRID)       RID IN WHICH EXIT WILL RUN            06794561
         AIF ('&ID' NE 'POSTSIO').ACT58                                 06795061
         L     13,PRODXSCC        GET CC AND PROGRAM MASK               06795661
         SPM   13                 SET CC AND PROGRAM MASK               06796061
.ACT58   ANOP                                                           06796561
         LM    13,15,VSEVISA      RESTORE USER REG 13,14,15             06798261
         SPACE 3                                                        06800061
         MEXIT                                                          06850061
.* * * *   SSTATE ACTIVATE NOT WITHIN SUPERVISOR * * * * * * * * * *    06851761
.ACT60   ANOP                                                           06853461
         AIF ('&ID' NE 'PCKEX' AND '&ID' NE 'SVC').ACT65       @DY43684 06853661
&IJBSRC  SETB  1                  SET FLAG FOR SPECIAL TREATMNT@DY43684 06853861
.ACT65   ANOP                                                  @DY43684 06854061
&IJBRID  SETA  4                                                        06854261
         SAVE  (14,12)            SAVE PROGRAM'S REG.S WITH R13         06855161
         ASYSCOM 14                                                     06856861
         L     14,IJBSPAVT-SYSCOM(,14) POINT TO SUPAVT                  06859261
         L     14,ASUPAVTE-SUPAVT(,14) POINT TO SUPAVTEX                06861661
         L     14,AVSEVISA-SUPAVTEX(,14) POINT TO REG SAVE AREA         06864061
         ST    13,0(,14)          SAVE POINTER TO PROGRAM'S SAVE AREA   06866461
         L     13,APRODACT-SYSS00(,0) POINT TO ACTIVATE ROUTINE         06868861
*        BASSM 14,13              BRANCH TO IT                          06871561
         DC    0H'0',X'0C',AL.4(14,13)  BASSM 14,13                     06872661
         DC    AL4(&ID)           TYPE OF CLASS, SUBCLASS               06873861
         DC    AL3(&ID.C)         ZERO BYTE OFFSET AND MASK             06874261
         DC    B'&IJBSRC.&IJBDYN.000&IJBSGEN.&IJBRC.&IJBAR.'   @D61YDHK 06874781
*           X'80' SPECIAL RC TREATMENT DURING ACTIVATE         @DY43684 06874841
*           X'40' DYN. SERVICES ( ENABLE / DISABLE )           @D61YDHK 06874881
*           X'04' GENERATED WITHIN SUPERVISOR                  @DY43684 06874931
*           X'02' RC ACCEPTED                                  @DY43684 06875021
*           X'01' AREA PASSED DURING ACTIVATE                  @DY43684 06875111
         DC    AL2(&IJBRID)       RID IN WHICH EXIT WILL RUN            06875261
         ASYSCOM 14               RESTORE USER REG.S UPON RETURN FROM   06875561
         L     14,IJBSPAVT-SYSCOM(,14) POINT TO SUPAVT                  06878461
         L     14,ASUPAVTE-SUPAVT(,14) POINT TO SUPAVTEX                06881361
         L     14,AVSEVISA-SUPAVTEX(,14) POINT TO REG SAVE AREA         06884261
         L     13,0(,14)          POINT TO REG SAVE AREA OF PROGRAM     06887161
         LM    14,12,12(13)       RELOAD REGISTERS                      06890361
         SPACE 3                                                        06894261
         MEXIT                                                          06895961
.*                                                             @D61YDHK 06896961
.*  * * SELECT ENABLE/DISABLE WITH ADDITIONAL PARAMETERS       @D61YDHK 06897961
.*                                                                      06900061
.*  * * * * * * * NOTHING FOR DELETE, RETURN * * * * * * * * * @D61YDHK 06980051
.NOTH10  AIF   (T'&AREA NE 'O' OR T'&CLASS NE 'O' OR                   *07010041
               T'&RC NE 'O').NOTH20                            @D61YDHK 07040031
         AIF   (T'&ID EQ 'O'AND T'&DYN EQ 'O').NOTH30          @D61YDHK 07070021
         AIF   ('&FUNC' EQ 'ENABLE' OR '&FUNC' EQ 'DISABLE').NOTH40     07100011
.NOTH20  MNOTE 8,' &FUNC EITHER REQUIRES NO ADDITIONAL PARAMETERS OR ON*07130001
               E OR MORE PARAMETERS ARE INVALID FOR &FUNC, GENERATION  *07159991
               SUPPRESSED'                                     @D61YDHK 07189981
         SPACE 3                                                        07250061
         MEXIT                                                          07300061
.NOTH30  AIF   ('&FUNC' EQ 'RETURN').RET10                              07350061
.NOTH40  AIF   ('&FUNC' EQ 'ENABLE').ENAB10                    @D61YDHK 07420051
         AIF   ('&FUNC' EQ 'DISABLE').DIS10                             07450061
.* * * * * * * * * * * *  FUNC=DELETE  * * * * * * * * * * * * * *      07500061
.DEL10   ANOP                                                           07550061
         LA    15,6               FUNCTION=DELETE                       07600061
         SVC   124                                                      07650061
         SPACE 3                                                        07700061
         MEXIT                                                          07750061
.* * * * * * * * * * * *  ENABLE FUNCTIONS * * * * * * * * * * @D61YDHK 07820051
.ENAB10  ANOP                                                           07850061
         AIF   ('&DYN' NE 'YES' AND T'&ID EQ 'O').ENAB40       @D61YDHK 07851061
         AIF   ('&DYN' EQ 'YES' AND T'&ID EQ 'O').DISER10      @D61YDHK 07852061
         AIF   ('&DYN' NE 'YES' AND T'&ID NE 'O').DISER20      @D61YDHK 07853061
.*                                                             @D61YDHK 07854061
.* * * * * * * * FUNC=ENABLE,DYN=YES,ID=SCLFCT   * * * * * * * @D61YDHK 07855061
.* * *   ID=SVC IS THE ONLY SCLFCT CURRENTLY SUPPORTED!  * * * @D61YDHK 07856061
.*                                                             @D61YDHK 07857061
&IJBDYN SETB   1                  FLAG FOR DYN. EN/DIS SERVICE @D61YDHK 07858061
         L     15,X'25C'(,0)                                   @D61YDHK 07859061
.* GET TDCPUPTR FROM LOW CORE. NOTE: LOWCORE DPLMNT DEPENDENCY @D61YDHK 07860061
         L     15,X'18'(,15)      GET CPU SAVE AREA ADDR       @D61YDHK 07861061
.* GET CPU SAVE AREA ADDR      NOTE: TCPUADR DPLMNT DEPENDENCY @D61YDHK 07862061
.* X'18' => TCPUWRK-TCPUADR                                    @D61YDHK 07863061
         STM   0,14,X'0'(15)      SAVE VENDOR REGISTERS        @D61YDHK 07864061
.* X'0'  => TWSAVE-TWCPADR     NOTE: TWCPADR DPLMNT DEPENDENCY @D61YDHK 07865061
&IJBRID  SETA  4                                               @D61YDHK 07866061
         LA    15,14              FUNCTION=ENABLE,DYN=YES      @D61YDHK 07867061
         L     13,X'ED0'(,0)      POINT TO VENDOR EXIT ROUTINE @D61YDHK 07868061
.* GET APRODACT FROM LOW CORE. NOTE: LOWCORE DPLMNT DEPENDENCY @D61YDHK 07869061
         BASSM 14,13              BRANCH TO IT                 @D61YDHK 07870061
         DC    0H'0',X'0C',AL.4(14,13)  BASSM 14,13            @D61YDHK 07871061
         DC    AL4(&ID)           TYPE OF CLASS, SUBCLASS      @D61YDHK 07872061
         DC    AL3(&ID.C)         ZERO BYTE OFFSET AND MASK    @D61YDHK 07873061
         DC    B'&IJBSRC.&IJBDYN.000&IJBSGEN.&IJBRC.&IJBAR.'   @D61YDHK 07874061
*           X'80' SPECIAL RC TREATMENT DURING ACTIVATE         @DY43684 07875061
*           X'40' DYN. SERVICES ( ENABLE / DISABLE )           @D61YDHK 07876061
*           X'04' GENERATED WITHIN SUPERVISOR                  @DY43684 07877061
*           X'02' RC ACCEPTED                                  @DY43684 07878061
*           X'01' AREA PASSED DURING ACTIVATE                  @DY43684 07879061
         DC    AL2(&IJBRID)       RID IN WHICH EXIT WILL RUN   @D61YDHK 07880061
         L     15,X'25C'(,0)                                   @D61YDHK 07881061
.* GET TDCPUPTR FROM LOW CORE. NOTE: LOWCORE DPLMNT DEPENDENCY @D61YDHK 07882061
         L     15,X'18'(,15)      GET CPU SAVE AREA ADDR       @D61YDHK 07883061
.* GET CPU SAVE AREA ADDR      NOTE: TCPUADR DPLMNT DEPENDENCY @D61YDHK 07884061
.* X'18' => TCPUWRK-TCPUADR                                    @D61YDHK 07885061
         LM    0,14,X'0'(15)      LOAD  VENDOR REGISTERS       @D61YDHK 07886061
.* X'0'  => TWSAVE-TWCPADR     NOTE: TWCPADR DPLMNT DEPENDENCY @D61YDHK 07887061
         SPACE 3                                               @D61YDHK 07888061
         MEXIT                                                 @D61YDHK 07889061
.*                                                             @D61YDHK 07890061
.* * * * * * * * * * * *  FUNC=ENABLE  * * * * * * * * * * * * * *      07891061
.*                                                             @D61YDHK 07892061
.ENAB40  ANOP                                                  @D61YDHK 07893061
         SR    15,15              INDICATE FAST PATH                    07900061
         LA    0,2                FUNCTION=ENABLE                       07950061
         SVC   124                                                      08000061
         SPACE 3                                                        08050061
         MEXIT                                                          08100061
.*                                                             @D61YDHK 08157051
.* * * * * * * * * * * * * ERROR MSGS. * * * * * * * * * * * * @D61YDHK 08164041
.*                                                             @D61YDHK 08171031
.*                                                             @D61YDHK 08178021
.* * * * * * * * * * DISABLE FUNCTIONS * * * * * * * * * * * * @D61YDHK 08185011
.*                                                             @D61YDHK 08192001
.DIS10   ANOP                                                           08200061
         AIF   ('&DYN' NE 'YES' AND T'&ID EQ 'O').DIS20        @D61YDHK 08201061
         AIF   ('&DYN' EQ 'YES' AND T'&ID EQ 'O').DISER10      @D61YDHK 08202061
         AIF   ('&DYN' NE 'YES' AND T'&ID NE 'O').DISER20      @D61YDHK 08203061
.*                                                             @D61YDHK 08204061
.* * * * * * * * FUNC=DISABLE,DYN=YES,ID=SCLFCT * * * * * * *  @D61YDHK 08205061
.* * *   ID=SVC IS THE ONLY SCLFCT CURRENTLY SUPPORTED!  * * * @D61YDHK 08206061
.*                                                             @D61YDHK 08207061
&IJBDYN SETB   1                  FLAG FOR DYN. EN/DIS SERVICE @D61YDHK 08208061
         L     15,X'25C'(,0)                                   @D61YDHK 08209061
.* GET TDCPUPTR FROM LOW CORE. NOTE: LOWCORE DPLMNT DEPENDENCY @D61YDHK 08210061
         L     15,X'18'(,15)      GET CPU SAVE AREA ADDR       @D61YDHK 08211061
.* GET CPU SAVE AREA ADDR      NOTE: TCPUADR DPLMNT DEPENDENCY @D61YDHK 08212061
.* X'18' => TCPUWRK-TCPUADR                                    @D61YDHK 08213061
         STM   0,14,X'0'(15)      SAVE VENDOR REGISTERS        @D61YDHK 08214061
.* X'0'  => TWSAVE-TWCPADR     NOTE: TWCPADR DPLMNT DEPENDENCY @D61YDHK 08215061
&IJBRID  SETA  4                                               @D61YDHK 08216061
         LA    15,15              FUNCTION=DISABLE,DYN=YES     @D61YDHK 08217061
         L     13,X'ED0'(,0)      POINT TO VENDOR EXIT ROUTINE @D61YDHK 08218061
.* GET APRODACT FROM LOW CORE. NOTE: LOWCORE DPLMNT DEPENDENCY @D61YDHK 08219061
         BASSM 14,13              BRANCH TO IT                 @D61YDHK 08220061
         DC    0H'0',X'0C',AL.4(14,13)  BASSM 14,13            @D61YDHK 08221061
         DC    AL4(&ID)           TYPE OF CLASS, SUBCLASS      @D61YDHK 08222061
         DC    AL3(&ID.C)         ZERO BYTE OFFSET AND MASK    @D61YDHK 08223061
         DC    B'&IJBSRC.&IJBDYN.000&IJBSGEN.&IJBRC.&IJBAR.'   @D61YDHK 08224061
*           X'80' SPECIAL RC TREATMENT DURING ACTIVATE         @DY43684 08225061
*           X'40' DYN. SERVICES ( ENABLE / DISABLE )           @D61YDHK 08226061
*           X'04' GENERATED WITHIN SUPERVISOR                  @DY43684 08227061
*           X'02' RC ACCEPTED                                  @DY43684 08228061
*           X'01' AREA PASSED DURING ACTIVATE                  @DY43684 08229061
         DC    AL2(&IJBRID)       RID IN WHICH EXIT WILL RUN   @D61YDHK 08230061
         L     15,X'25C'(,0)                                   @D61YDHK 08231061
.* GET TDCPUPTR FROM LOW CORE. NOTE: LOWCORE DPLMNT DEPENDENCY @D61YDHK 08232061
         L     15,X'18'(,15)      GET CPU SAVE AREA ADDR       @D61YDHK 08233061
.* GET CPU SAVE AREA ADDR      NOTE: TCPUADR DPLMNT DEPENDENCY @D61YDHK 08234061
.* X'18' => TCPUWRK-TCPUADR                                    @D61YDHK 08235061
         LM    0,14,X'0'(15)      LOAD  VENDOR REGISTERS       @D61YDHK 08236061
.* X'0'  => TWSAVE-TWCPADR     NOTE: TWCPADR DPLMNT DEPENDENCY @D61YDHK 08237061
         SPACE 3                                               @D61YDHK 08238061
         MEXIT                                                 @D61YDHK 08239061
.*                                                             @D61YDHK 08240061
.* * * * * * * * * * * *  FUNC=DISABLE * * * * * * * * * * * * * *      08241061
.*                                                             @D61YDHK 08242061
.DIS20   ANOP                                                  @D61YDHK 08243061
         SR    15,15              INDICATE FAST PATH                    08250061
         LA    0,3                FUNCTION=DISABLE                      08300061
         SVC   124                                                      08350061
         SPACE 3                                                        08400061
         MEXIT                                                          08450061
.*                                                             @D61YDHK 08453061
.* * * * * * * * * * * * * * ERROR MSGS. * * * * * * * * * * * @D61YDHK 08456061
.*                                                             @D61YDHK 08459061
.DISER10 ANOP                                                  @D61YDHK 08462061
         MNOTE 8,' DYN=&DYN INVALID W/O ID=''SCLFCT'' BEING   SPECIFIED*08465061
                FOR FUNC=&FUNC, GENERATION SUPPRESSED'         @D61YDHK 08468061
         SPACE 3                                               @D61YDHK 08471061
         MEXIT                                                 @D61YDHK 08474061
.DISER20 ANOP                                                  @D61YDHK 08477061
         MNOTE 8,' ID=&ID INVALID W/O DYN=YES BEING SPECIFIED TOO FOR  *08480061
               FUNC=&FUNC, GENERATION SUPPRESSED'              @D61YDHK 08483061
         SPACE 3                                               @D61YDHK 08486061
         MEXIT                                                 @D61YDHK 08489061
.* * * * * * * * * * * *  FUNC=RETURN  * * * * * * * * * * * * * *      08500061
.RET10   ANOP                                                           08550061
         AMODESW RETURN           RETURN FROM EXIT                      08600061
         SPACE 3                                                        08950061
         MEXIT                                                          09000061
.* * * * * * * * * * * *  FUNC=DSECT  * * * * * * * * * * * * * * *     09050061
.DS10    ANOP                                                           09100061
         AIF   (T'&AREA NE 'O' OR T'&ID NE 'O' OR T'&DYN NE 'O').DS15   09138151
         AIF   (T'&RC EQ 'O').DS20                                      09156261
.DS15    MNOTE 8,' ONLY ''CLASS='' MAY BE SPECIFIED WITH DSECT, GENERAT*09184361
               ION SUPPRESSED'                                          09212561
         SPACE 3                                                        09250061
         MEXIT                                                          09300061
.DS20    AIF   ('&NAME' EQ '').DS30                                     09308961
&NAME    DSECT                                                          09317861
         AGO   .DS40                                                    09326761
.DS30    ANOP                                                           09335661
         DSECT                                                          09344561
.DS40    ANOP                                                           09353461
         AIF   (T'&CLASS NE 'O').DS50  GENERATE PRODEXIT DSECT          09362361
IJBVLN   DS    H                  LENGTH OF PRODEXIT AREA               09371261
IJBVCLS  DS    0XL4               CLASS DEFINITION BYTES       @D61YDHK 09382151
IJBVCLS1 DS    X                  CLASS 1 - 8   DEFINITIONS    @D61YDHK 09384141
IJBVSUP  EQU   X'80'               EXIT CLASS   SUP                     09389061
IJBVPSUP EQU   X'40'               EXIT CLASS   PSUP                    09397961
IJBVSVC  EQU   X'20'               EXIT CLASS   SVC                     09406861
IJBVFSV  EQU   X'10'               EXIT CLASS   FAST PATH SVC           09415761
IJBVBAM  EQU   X'08'               EXIT CLASS   BAM                     09424661
IJBVAIT  EQU   X'04'               EXIT CLASS   ATTENTION               09433561
IJBVDOC  EQU   X'02'               EXIT CLASS   CONSOLE SSTATE          09446451
IJBVLNG  EQU   X'01'               EXIT CLASS   LANGUAGES               09451361
IJBVCLS2 DS    X                  CLASS 9 - 16  DEFINITIONS    @D61YDHK 09460851
IJBVDOCP EQU   X'80'               EXIT CLASS   CONSOLE PSTATE @D61YDHK 09461241
IJBVCLS3 DS    X                  CLASS 17 - 24 DEFINITIONS    @D61YDHK 09461631
IJBVCLS4 DS    X                  CLASS 25 - 32 DEFINITIONS    @D61YDHK 09462021
***  END OF CLASS DEFINITIONS  ***                             @D61YDHK 09462411
IJBVSCL  DS    XL32               SUBCLASS DEFINITIONS         @D61YDHK 09462801
*  IJBVSUP  EXIT CLASS  ( 1 )  SUP    SUBCLASSES               @D61YDHK 09463191
IJBVSPIO EQU   X'80'               POST SIO/SSCH               @D61YDHK 09463581
IJBVSIOI EQU   X'40'               I/O INTERRUPT               @D61YDHK 09463971
IJBVSPCK EQU   X'20'               PROG. CHECK                 @D61YDHK 09464361
IJBVSEXT EQU   X'10'               EXT. INTERRUPT              @D61YDHK 09464751
IJBVSEXP EQU   X'08'               EXCHANGE PHASE              @D61YDHK 09465141
IJBVSPRF EQU   X'04'               PREFETCH                    @D61YDHK 09465531
IJBVSPOF EQU   X'02'               POSTFETCH                   @D61YDHK 09465921
*  IJBVPSUP EXIT CLASS  ( 2 )  PSUP   SUBCLASSES               @D61YDHK 09466311
IJBVPEOT EQU   X'80'               END OF TASK                 @D61YDHK 09466701
*  IJBVSVC  EXIT CLASS  ( 3 )  SVC    SUBCLASSES               @D61YDHK 09467091
*  IJBVFSV  EXIT CLASS  ( 4 )  FSVC   SUBCLASSES               @D61YDHK 09467481
*  IJBVBAM  EXIT CLASS  ( 5 )  BAM    SUBCLASSES               @D61YDHK 09467871
IJBVBPRO EQU   X'80'               PRE-OPEN                    @D61YDHK 09468261
IJBVBPRC EQU   X'40'               PRE-CLOSE                   @D61YDHK 09468651
IJBVBEOX EQU   X'20'               END OF EXTENT               @D61YDHK 09469041
IJBVBCVH EQU   X'10'               COMMON VTOC HANDLER         @D61YDHK 09469431
IJBVBPOO EQU   X'08'               POST OPEN                   @D61YDHK 09469821
IJBVBPOC EQU   X'04'               POST CLOSE                  @D61YDHK 09470211
*  IJBVAIT  EXIT CLASS  ( 6 )  AIT    SUBCLASSES               @D61YDHK 09470601
IJBVAPRE EQU   X'80'               PRE-SCAN                    @D61YDHK 09470991
IJBVAPOE EQU   X'40'               POST-SCAN                   @D61YDHK 09471381
*  IJBVDOC  EXIT CLASS  ( 7 )  DOC    SUBCLASSES               @D61YDHK 09471771
IJBVDMSG EQU   X'80'               MESSAGE PROC.               @D61YDHK 09472161
IJBVDCR  EQU   X'40'               COMMAND REPLY               @D61YDHK 09472551
*  IJBVLNG  EXIT CLASS  ( 8 )  LNG    SUBCLASSES               @D61YDHK 09472941
IJBVLOPN EQU   X'80'               LNG-OPEN                    @D61YDHK 09473331
*  IJBVDOCP EXIT CLASS  ( 9 )  DOCP   SUBCLASSES               @D61YDHK 09473721
IJBVDFSI EQU   X'80'               FULL SCREEN INPUT SCL       @D61YDHK 09474111
***  END OF SUBCLASS DEFINITIONS  ***                          @D61YDHK 09474501
IJBVCLEN EQU   *                  END OF PRODEXIT CHECK INPUT           09478061
IJBVSCP  DS    X                  FLAG BYTE - SCOPE OF EXIT             09486961
IJBVSYS  EQU   X'80'               SYSTEM WIDE                          09495861
IJBVPAR  EQU   X'40'               PARTITION                            09504761
IJBVTSK  EQU   X'20'               TASK                                 09513661
IJBVFLAG DS    X                  FLAG BYTE                             09522561
IJBVFUPD EQU   X'80'               EXIT WILL UPDATE INPUT               09531461
IJBVCRIT EQU   X'40'               CRITICAL EXIT                        09544761
IJBVDYN  EQU   X'20'               DYNAMIC ENABLE/DISABLE SUPP.@D61YDHK 09547761
IJBVSUPD EQU   X'08'               UPDATE EXIT HAS BEEN SKIPPED@DY43684 09550761
IJBVRC   DS    F                  VENDOR EXIT RETURN CODE               09558161
IJBVINF  DS    F                  POINTER TO INPUT INFORMATION          09567061
IJBVEXT  DS    F                  EXIT ROUTINE ADDRESS                  09575961
IJBVPRIO DS    X                  EXIT PRIORITY                         09584861
         DS    3X                 RESERVED                              09593761
IJBVUSW  DS    XL8                USER WORD, E.G. ANCHOR FOR WORK AREA  09602661
IJBVTOK  DS    XL8                PRODID TOKEN                          09611561
IJBVETK  DS    XL8                PRODEXIT TOKEN                        09620461
IJBVCEXT DS    XL4                CLASS EXT. POINTER                    09629361
IJBVLEN  EQU   *                  END OF PRODEXIT AREA                  09638261
         SPACE 3                                                        09650061
         MEXIT                                                          09700061
.* * * * * * * * * *  FUNC=DSECT, CLASS=  * * * * * * * * * * * *       09720061
.DS50    AIF   ('&CLASS' EQ 'SVC' OR '&CLASS' EQ 'FSVC').DS60           09740061
         AIF   ('&CLASS' EQ 'SUP' OR '&CLASS' EQ 'PSUP').DS60           09760061
         AIF   ('&CLASS' EQ 'LNG' OR '&CLASS' EQ 'BAM').DS60            09780061
         AIF   ('&CLASS' EQ 'BINT').DS110                               09790061
&IJBB1   SETB  1                                                        09800061
         AIF   ('&CLASS' EQ 'ALL').DS60                                 09820061
         MNOTE 8,' CLASS=&CLASS IS NO VALID VALUE, GENERATION SUPPRESSE*09840061
               D'                                                       09860061
         SPACE 3                                                        09880061
         MEXIT                                                          09900061
.DS60    ANOP                                                           09920061
* * * * * * * * * * *  COMMON EXTENSION  * * * * * * * * * * * * * *    09940061
IJBVLENV DS    H                  LENGTH OF VAR. PART                   09960061
IJBVPITI DS    0XL4               OVERLAY FOR NEXT TWO HALFWORDS        09970061
IJBVPIK  DS    H                  PIK OF CURRENT TASK                   09980061
IJBVTIK  DS    H                  TID OF CURRENT TASK                   10000061
* DO NOT REARRANGE PREVIOUS TWO HALFWORDS                               10010061
         DS    H                  RESERVED                              10020061
IJBVELEN EQU   *                  LENGTH OF FIXED PART                  10026961
IJBVELN1 EQU   *-IJBVLENV         LENGTH OF FIXED PART                  10033861
         AIF   ('&CLASS' NE 'LNG' AND NOT &IJBB1).DS65                  10040861
* * * * * * * * * * *   LNG EXTENSION    * * * * * * * * * * * * * *    10041661
         ORG   IJBVELEN           LNG EXTENSION                         10042461
IJBVLLVL DS    F                  LEVEL NUMBER OF THE PARAMETER LIST    10043261
IJBVLANG DS    CL8                LANG.INVOKING EXIT: COBOL,PL/I,LE     10044061
IJBVLNAM DS    CL8                NAME FROM COBOL ASSIGN STMT, OR       10044861
*                                 PL/I FILE DECLARE STATEMENT           10045661
IJBVLLUN DS    H                  LOGICAL UNIT NUM. FROM CBL ASSIGN STM 10046461
*                                 OR PL/I FILE DCL STMT, ZERO IF OMITT. 10047261
IJBVLOPM DS    X                  PROPOSED OPEN MODE:                   10048061
IJBVLIN  EQU   X'01'              - INPUT                               10048861
IJBVLIO  EQU   X'02'              - I-O (EG. TYPEFLE=INPUT+UPDATE=YES   10049661
IJBVLOUT EQU   X'03'              - OUTPUT                              10050461
IJBVLEXT EQU   X'04'              - EXTEND (APPEND)                     10051261
IJBVLWRK EQU   X'05'              - WORK (EG. COMPILER WORK FILE)       10052061
IJBVLINW EQU   X'06'              - INPUT WORK FILE            @D68DDMZ         
IJBVLIOW EQU   X'07'              - I-O WORK FILE              @D68DDMZ         
IJBVLOUW EQU   X'08'              - OUTPUT WORK FILE           @D68DDMZ         
IJBVLEXW EQU   X'09'              - EXTEND WORK FILE           @D68DDMZ         
IJBVLDEV DS    X                  DEVICE TYPE FOR FILE:                 10052861
IJBVLDSD EQU   X'01'              - SAM DISK                            10053661
IJBVLDDA EQU   X'02'              - DAM DISK                            10054461
IJBVLDVS EQU   X'03'              - VSAM DISK                           10055261
IJBVLDUT EQU   X'04'              - UNLABELED TAPE                      10056061
IJBVLDLT EQU   X'05'              - LABELED TAPE                        10056861
IJBVLDCD EQU   X'06'              - CARD                                10057661
IJBVLDPR EQU   X'07'              - PRINTER                             10058461
IJBVLDUA EQU   X'08'              - UNASSIGNED (UA)                     10059261
IJBVLDIG EQU   X'09'              - ASSIGNED TO IGNORE (IGN)            10060061
IJBVLLBA DS    A                 A(DBLB/TLBL) GOT BY LABEL FUNCT=GETLBL 10060861
IJBVLLBL DS    F                  LENGTH OF DLBL OR TLBL                10061661
IJBVLLBB DS    F                  LEN(BUFFER) CONTAINING DLBL/TLBL      10062461
IJBVLOPT DS    XL2                OTHER OPEN OPTIONS.                   10063261
IJBVLORV EQU   X'80'              - OPEN REVERSED SPECIFIED             10064061
IJBVLNRW EQU   X'40'              - OPEN NO REWIND SPECIFIED            10064861
IJBVLASC EQU   X'20'              - ASCII TAPE FILE                     10065661
IJBVLOPF EQU   X'10'              - OPTIONAL FILE (COBOL ONLY)          10066461
IJBVLCBM EQU   X'08'              - MODE=C (COLUMN BINARY MODE)         10067261
*                                   (3505 AND 3525 CARD DEVICES ONLY)   10068061
IJBVLOMR EQU   X'04'              - MODE=O (OPTICAL MARK READ)          10068861
*                                   (3505 CARD DEVICES ONLY)            10069661
IJBVLRCE EQU   X'02'              - MODE=R (READ COLUMN ELIMINATE)      10070461
*                                   (3505 AND 3525 CARD DEVICES ONLY)   10071261
IJBVLPUB DS    X                  PUB DEVTYPE UNLAB TAPE/CARD /PRT      10072061
IJBVLFEX DS    X                  FILE EXISTS FLAG                      10072861
*                                 - X'00' IF NOT SET                    10073661
*                                 - X'01' IF FILE CURRENTLY EXISTS, OR  10074461
*                                 - X'02' IF FILE DOES NOT EXIST        10075261
IJBVLLUO DS    H                  LOGICAL UNIT NUM. DETERMINED BY EXIT  10076061
IJBVLFID DS    CL44               FILE ID FOR FILE, ANY THIRD PARTY     10076861
*                                 DISK/TAPE MGR CONTR.CHAR.S REMOVED    10077661
IJBVLLEX DS    H                  EXTENT NUM. OF LAST VOLUME, SAM ONLY  10078461
IJBVLRFI DS    H                  RECORD FORMAT FROM PROGRAM:           10079261
IJBVLRFF EQU   X'80'              - FIXED                               10080061
IJBVLRFV EQU   X'40'              - VARIABLE                            10080861
IJBVLRFU EQU   X'20'              - UNDEFINED                           10081661
IJBVLRFB EQU   X'10'              - BLOCKED                             10082461
IJBVLRFS EQU   X'08'              - SPANNED                             10083261
IJBVLRFA EQU   X'04'              - ASA CONTROL CHARACTERS              10084061
IJBVLRFM EQU   X'02'              - MACHINE CONTROL CHARACTERS          10084861
*                                 EG. X'90' FOR FIXED BLOCKED.          10085661
IJBVLRSI DS    F                  REC. LENGTH FROM PROGRAM              10086461
IJBVLBSI DS    F                  BLOCK SIZE FROM PROGRAM               10087261
IJBVLRFO DS    H                  REC FORMAT OF EXIST. FILE,            10088061
*                                 FLAGS AS IJBVLRFI.                    10088861
IJBVLRSO DS    F                  REC LENGTH OF EXIST FILE IF AVAIL.    10089661
IJBVLBSO DS    F                  BLOCKSIZE OF EXSIT.FILE IF AVAIL.     10090461
IJBVLLMA DS    A                  A(LIOCS MODULE) IN DTF AFTER OPEN     10091261
IJBVLLMS DS    A                  ADDRESS WHERE TO STORE A(LIOCS MOD)   10092061
*                                 PRIOR TO BE OVERWRITTEN WITH ABOVE    10092861
.DS65    AIF   ('&CLASS' NE 'BAM' AND NOT &IJBB1).DS70                  10093661
* * * * * * * * * * *   BAM EXTENSION  * * * * * * * * * * * * * *      10106861
         ORG   IJBVELEN           BAM EXTENSION                         10120061
IJBVBEXI DS    CL16                                                     10170061
IJBVBREG DS    XL64               SAVE AREA FOR PP REGISTERS   @D67BDMZ         
         ORG   IJBVBEXI                                                 10177761
IJBVBFLG DS    X                  FLAG BYTE                             10185461
IJBVBJCT EQU   X'80'               REQUEST FOR JOB COMTROL              10193161
         DS    3X                 RESERVED                              10200861
IJBVBDTF DS    F                  DTF POINTER                           10208561
         ORG   IJBVBDTF                                                 10216261
IJBVBCPL DS    F                  ADDRESS OF CVH PARM.LIST              10223961
         ORG   ,                                                        10231661
IJBVBLEN EQU   *                  LENGHT OF BAM EXTENSION               10240061
.DS70    ANOP                                                           10260061
         AIF   ('&CLASS' NE 'SVC' AND NOT &IJBB1).DS80                  10280061
* * * * * * * * * * *  SVC EXTENSION   * * * * * * * * * * * * * *      10300061
         ORG   IJBVELEN           SVC EXTENSION                         10320061
IJBVCPSW DS    CL8                OLD SVC PSW                           10340061
IJBVCIC  DS    CL4                LOW CORE BYTE 136-139                 10360061
IJBVCRID DS    CL2                RID OF INTERRUPTED TASK               10366661
         DS    CL2                RESERVED                              10373261
IJBVCARG DS    CL64               ACCESS REGISTERS                      10380061
IJBVCGRG DS    CL64               GENERAL REGISTERS                     10400061
IJBVCVIN DS    CL8                INFO RETURNED BY VENDOR EXIT          10413361
IJBVCLNG EQU   *-IJBVCPSW         LENGTH OF SVC EXTENSION               10426661
.DS80    ANOP                                                           10440061
         AIF   ('&CLASS' NE 'FSVC' AND NOT &IJBB1).DS90                 10460061
* * * * * * * * * * *  FAST SVC EXTENSION  * * * * * * * * * * * * *    10480061
         ORG   IJBVELEN           EXTENSION FOR FAST SVC                10500061
IJBVFPSW DS    CL8                OLD SVC PSW                           10520061
IJBVFIC  DS    CL4                LOW CORE BYTE 136-139                 10540061
IJBVFRID DS    CL2                RID OF INTERRUPTED TASK               10560061
         DS    CL2                RESERVED                              10580061
IJBVFARG DS    CL64               ACCESS REGISTERS                      10600061
IJBVFGRG DS    CL64               GENERAL REGISTERS                     10620061
IJBVFVIN DS    CL8                INFO RETURNED BY VENDOR EXIT          10633361
IJBVFLNG EQU   *-IJBVFPSW         LENGTH OF FSVC EXTENSION              10646661
.DS90    ANOP                                                           10660061
         AIF   ('&CLASS' NE 'PSUP' AND NOT &IJBB1).DS100                10680061
* * * * * * * * * * *  PSUP EXTENSION   * * * * * * * * * * * * * *     10700061
         ORG   IJBVELEN           EXTENSION FOR PSUP                    10720061
IJBVPCC1 DS    X                  FIRST CANCEL CODE                     10740061
IJBVPCC2 DS    X                  SECOND CANCEL CODE                    10760061
IJBVPFL1 DS    X                  FLAG BYTE                             10780061
IJBVPFMT EQU   X'80'               MAIN TASK TERMINATION                10786661
IJBVPFST EQU   X'40'               SUBTASK TERMINATION                  10793261
         DS    X                  RESERVED                              10800061
IJBVPLEN EQU   *                  LENGTH OF PSUP EXTENSION              10820061
.DS100   ANOP                                                           10840061
         AIF   ('&CLASS' NE 'SUP' AND NOT &IJBB1).DS110                 10860061
* * * * * * * * * * * *   SUP EXTENSION   * * * * * * * * * * * * * *   10873061
*********************************************************************   10876261
*    FETCH EXITS                                                        10879461
*********************************************************************   10882661
         ORG   IJBVELEN            EXT. SUP SUBCL. XPHASE/PRE/POSTFCH   10886061
IJBVSFOP DS    CL8                ORIGINAL PHASENAME                    10912061
IJBVSFEP DS    CL8                EXCHANGED PHASENAME (BY EXIT)         10925061
IJBVSFSC DS    X                  SVC CODE                              10933661
IJBVSFL1 EQU   *-IJBVSFOP         LENGTH OF VARIABLE PART FOR EXPHASE   10942361
IJBVSFLI DS    CL7                NAME OF LIBRARY                       10951061
IJBVSFSL DS    CL8                NAME OF SUBLIBRARY                    10964061
IJBVSFDE DS    CL40               DIRECTORY ENTRY                       10977061
IJBVSFRC DS    X                  RETCODE (SET ONLY FOR POSTFCH EXIT)   10990061
IJBVSFLN EQU   *-IJBVSFOP         LENGTH OF VARIABLE PART               10998161
*********************************************************************   11006261
*    POSTSIO EXIT                                                       11009461
*********************************************************************   11012661
         ORG   IJBVELEN            EXT. FOR SUP SUBCLASS POSTSIO        11016061
IJBVSCCB DS    CL4                CCB ADDRESS                           11029061
IJBVSCU1 DS    CL2                CUU OF DEVICE I/O WAS ISSUED TOO      11042061
IJBVSPOL EQU   *-IJBVSCCB         LENGTH OF VARIABLE PART               11055061
*********************************************************************   11058261
*    IOINT EXIT                                                         11061461
*********************************************************************   11064661
         ORG   IJBVELEN            EXT. FOR SUP SUBCLASS IOINT          11068061
IJBVSCSW DS    CL8                CSW OF DEVICE CAUSING INTERR          11081061
IJBVSCU2 DS    CL2                CUU OF DEVICE CAUSING INTERRUPT       11094061
IJBVSIOL EQU   *-IJBVSCSW         LENGTH OF VARIABLE IOINT PART         11107061
*********************************************************************   11110261
*    EXT EXIT                                                           11113461
*********************************************************************   11116661
         ORG   IJBVELEN            EXT. FOR SUP SUBCLASS EXT            11120061
IJBVSESW DS    CL8                OLD EXTERNAL INTERRUPT PSW            11140061
IJBVSEIC DS    CL4                EXT.INTERRUPT CODE X'84'-X'87'        11160061
IJBVSETI DS    CL2                TID OF TASK WHOSE TIMER EXPIRED       11172061
IJBVSERI DS    CL2                RID                                   11184061
IJBVSEIP DS    CL4                EXT. INTERR. PARAMETER WORD  @DY43775 11190061
IJBVSELN EQU   *-IJBVSESW         LENGTH                                11196061
*********************************************************************   11199061
*    PCKEX EXIT                                                         11202061
*********************************************************************   11205061
         ORG   IJBVELEN            EXT. FOR SUP SUBCLASS PCKEX          11208061
IJBVSPSW DS    CL8                OLD PROGRAM CHECK PSW                 11220061
IJBVSPIC DS    CL4                PROGRAM INTER. CODE X'8C'-X'8F'       11240061
IJBVSPFA DS    CL4                ADDRESS CAUSING PAGE FAULT X'90'      11260061
IJBVSPAR DS    CL64               ACCESS REGISTERS                      11276661
IJBVSPGR DS    CL64               GENERAL PURPOSE REGISTERS             11293261
IJBVSPRI DS    CL2                RID AT INTERRUPTION TIME              11309861
IJBVSPAI DS    CL1                EXECEPTION ACCESS IDENT. X'A0'        11326461
IJBVSPLN EQU   *-IJBVSPSW         LENGTH OF SUBCLASS PCKEX              11343061
IJBVSLEN EQU   *                  MAX. LENGTH OF EXT. FOR CLASS SUP     11360061
.DS110   ANOP                                                           11361661
         AIF   ('&CLASS' NE 'BINT').DSFIN                               11364861
IJBVBLN  DS    CL2                LENGTH OF BAM EXTENSION               11368061
IJBVBCRI DS    CL2                COMREG INFO TO BE SAVED               11369661
IJBVBNXT DS    CL8                NAME OF NEXT PHASE TO BE FETCHED      11371261
IJBVBLTA DS    CL16               LTA INFO TO BE SAVED                  11372861
IJBVBR15 DS    CL4                REG 15 SAVE AREA                      11374461
         DS    CL4                RESERVED                              11376061
IJBVBLSA DS    CL120              LTA SAVE AREA                         11376861
IJBVBLNG EQU   *                  LENGHT OF EXTENSION                   11377661
.DSFIN   ANOP                                                           11380061
         SPACE 3                                                        11400061
         MEND                                                           11450061
**********************************************************************  11500061
*                                                                       11550061
* PL/S PART OF MACRO FOLLOWS                                            11600061
*                                                                       11650061
**********************************************************************  11700061
* END OF PLX COMMENT */ %PRODEXIT1 : ;                                  11750061
*%PRODEXIT:                                                             11800061
* MACRO KEYS(AREA,CLASS,ID,RC) TRACE;                                   11850061
*                                                                       11900061
* /* LIST INVOCATION IN SOURCE                                      */  11950061
* ANS('/*'||MACLABEL||' '||MACNAME||' '||MACLIST||' '||MACKEYS||        12000061
*     '*/') COL(MACLMAR);                                               12050061
*                                                                       12100061
* ANS('/*                                            */') COL(MACLMAR); 12150061
* ANS('/* SUPERVISOR - PRODEXIT - 5686-066-06        */') COL(MACLMAR); 12200061
* ANS('/*                                            */') COL(MACLMAR); 12250061
*                                                                       12300061
* /*                                                                */  12350061
* /* DECLARE LOCAL VARIABLES                                        */  12400061
* /*                                                                */  12450061
*                                                                       12500061
* DCL (LABEL) CHAR;                                                     12550061
* DCL BASE    CHAR;                                                     12575061
*                                                                       12600061
* IF MACLIST='' THEN                                                    12650061
*   DO;                                                                 12700061
*     ANS MSG('SERVICE NOT SPECIFIED');                                 12750061
*     RETURN CODE(8);                                                   12800061
*   END;                                                                12850061
*                                                                       12900061
* IF MACLIST(1) = 'DSECT' THEN                                         12933361
*  IF MACLIST(1) = 'CSECT' THEN                                        12966661
*   DO;                                                                 13000061
*     ANS MSG(MACLIST(1) || 'SPECIFIED SERVICE INVALID');               13050061
*     RETURN CODE(8);                                                   13100061
*   END;                                                                13150061
*                                                                       13200061
* IF AREA(1) = '' THEN GOTO MSG;                                       13201361
* IF RC(1) = '' THEN GOTO MSG;                                         13202661
* IF ID(1) = '' THEN GOTO MSG;                                         13203961
* ELSE  GOTO CONT;                                                      13205261
*  MSG: ;                                                               13206561
*   DO;                                                                 13207861
*     ANS MSG('CLASS IS ONLY VALID PARAMETER WITH DSECT');              13209161
*     RETURN CODE(8);                                                   13210461
*   END;                                                                13211761
* CONT: ;                                                               13213061
*                                                                       13214361
* IF MACLIST(1)='DSECT' THEN BASE =' BASED '; ELSE BASE =' ';           13216661
* IF CLASS(1)='' THEN GOTO DSECT;                                       13224961
* IF CLASS(1)='BAM' THEN GOTO BAM;                                      13233261
*   DO;                                                                 13234861
*     ANS MSG('DSECT FOR CLASS=' || CLASS(1) || ' NOT SUPPORTED');      13236461
*     RETURN CODE(8);                                                   13238061
*   END;                                                                13239661
*                                                                       13241661
* /*----------------------------------------------------------------*/  13250061
* /* PROCESS DSECT REQUEST                                          */  13300061
* /*----------------------------------------------------------------*/  13350061
*                                                                       13400061
* DSECT: ;                                                              13450061
*                                                                       13500061
* ANS ('DCL 1 PRODEXIT BASE BDY(WORD),')COL(MACLMAR);                   13506561
* ANS (COMMENT('PRODEXIT AREA                '))COL(40);                13513061
* ANS ('2 IJBVLN  BIT(16),')COL(8);                                     13519561
* ANS (COMMENT(' LENGTH OF AREA              '))COL(40);                13526061
* ANS ('2 IJBVCLS  BIT(32),')COL(8) ;                      /*@D61YDHK*/ 13535551
* ANS (COMMENT(' CLASS DEFINITION BYTES      '))COL(40);   /*@D61YDHK*/ 13538541
* ANS ('3 IJBVCLS1  BIT(8),')COL(8) ;                      /*@D61YDHK*/ 13541531
* ANS (COMMENT(' CLASS 1 - 8   DEFINITIONS   '))COL(40);   /*@D61YDHK*/ 13544521
* ANS ('4 IJBVSUP BIT(1),')COL(10) ;                       /*@D61YDHK*/ 13547511
* ANS (COMMENT(' SUP EXIT CLASS              '))COL(40);   /*@D61YDHK*/ 13550501
* ANS ('4 IJBVPSUP BIT(1),')COL(10) ;                      /*@D61YDHK*/ 13553491
* ANS (COMMENT(' PSUP EXIT CLASS             '))COL(40);   /*@D61YDHK*/ 13556481
* ANS ('4 IJBVSVC BIT(1),')COL(10) ;                       /*@D61YDHK*/ 13559471
* ANS (COMMENT(' SVC EXIT CLASS              '))COL(40);   /*@D61YDHK*/ 13562461
* ANS ('4 IJBVFSV BIT(1),')COL(10) ;                       /*@D61YDHK*/ 13565451
* ANS (COMMENT(' FAST SVC EXIT CLASS         '))COL(40);   /*@D61YDHK*/ 13568441
* ANS ('4 IJBVBAM BIT(1),')COL(10) ;                       /*@D61YDHK*/ 13571431
* ANS (COMMENT(' BAM EXIT CLASS              '))COL(40);   /*@D61YDHK*/ 13574421
* ANS ('4 IJBVAIT BIT(1),')COL(10) ;                       /*@D61YDHK*/ 13577411
* ANS (COMMENT(' AIT EXIT CLASS              '))COL(40);   /*@D61YDHK*/ 13580401
* ANS ('4 IJBVDOC BIT(1),')COL(10) ;                       /*@D61YDHK*/ 13583391
* ANS (COMMENT(' DOC EXIT CLASS              '))COL(40);   /*@D61YDHK*/ 13586381
* ANS ('4 IJBVLNG BIT(1),')COL(10) ;                       /*@D61YDHK*/ 13589371
* ANS (COMMENT(' LNG EXIT CLASS              '))COL(40);   /*@D61YDHK*/ 13592361
* ANS ('3 IJBVCLS2  BIT(8),')COL(8) ;                      /*@D61YDHK*/ 13595351
* ANS (COMMENT(' CLASS 9 - 16  DEFINITIONS   '))COL(40);   /*@D61YDHK*/ 13598341
* ANS ('4 IJBVDOCP BIT(1),')COL(10) ;                      /*@D61YDHK*/ 13601331
* ANS (COMMENT(' DOCP EXIT CLASS             '))COL(40);   /*@D61YDHK*/ 13604321
* ANS ('4 *       BIT(7),')COL(8) ;                        /*@D61YDHK*/ 13607311
* ANS (COMMENT(' RESERVED                    '))COL(40);   /*@D61YDHK*/ 13610301
* ANS ('3 IJBVCLS3  BIT(8),')COL(8) ;                      /*@D61YDHK*/ 13613291
* ANS (COMMENT(' CLASS 17 - 24 DEFINITIONS   '))COL(40);   /*@D61YDHK*/ 13616281
* ANS ('3 IJBVCLS4  BIT(8),')COL(8) ;                      /*@D61YDHK*/ 13619271
* ANS (COMMENT(' CLASS 25 -32  DEFINITIONS   '))COL(40);   /*@D61YDHK*/ 13622261
*                                                                       13650061
* ANS ('2 IJBVSCL BIT(256),')COL(8) ;                                   13680061
* ANS (COMMENT(' SUBCLASS DEFINITION BYTES   '))COL(40);                13710061
*  /*  ALL X'80' BIT FOR THE FIRST BYTE OF IJBVSCL     */               13725061
* ANS ('3 IJBVPEOT BIT(1),')COL(10) ;                                   13740061
* ANS (COMMENT(' END-OF-TASK IF CLASS=PSUP   '))COL(40);                13770061
* ANS ('4 IJBVBPRO BIT(1),')COL(11) ;                                   13796561
* ANS (COMMENT(' PRE-OPEN IF CLASS=BAM       '))COL(40);                13823061
* ANS ('5 IJBVSPIO BIT(1),')COL(12) ;                                   13824461
* ANS (COMMENT(' POST SIO/SSCH IF CLASS=SUP  '))COL(40);                13825861
* ANS ('6 IJBVDMSG BIT(1),')COL(13) ;                                   13827261
* ANS (COMMENT(' MESSAGE PROC. IF CLASS=DOC  '))COL(40);                13828661
* ANS ('7 IJBVAPRE BIT(1),')COL(14) ;                                   13830061
* ANS (COMMENT(' PRE-SCAN IF CLASS=AIT       '))COL(40);                13831461
* ANS ('8 IJBVLOPN BIT(1),')COL(14) ;                                   13831861
* ANS (COMMENT(' LNG-OPEN IF CLASS=LNG       '))COL(40);                13832261
* ANS ('9 IJBVDFSI BIT(1),')COL(14) ;                      /*@D61YDHK*/ 13832461
* ANS (COMMENT(' FULL SCREEN INPUT CLS=DOCP  '))COL(40);   /*@D61YDHK*/ 13832661
*  /*  ALL X'40' BIT FOR THE FIRST BYTE OF IJBVSCL     */               13832861
* ANS ('3 IJBVBPRC BIT(1),')COL(10) ;                                   13834561
* ANS (COMMENT(' PRE-CLOSE IF CLASS=BAM      '))COL(40);                13846061
* ANS ('4 IJBVSIOI BIT(1),')COL(11) ;                                   13847461
* ANS (COMMENT(' I/O INTERRUPT IF CLASS=SUP  '))COL(40);                13848861
* ANS ('5 IJBVDCR BIT(1),')COL(12) ;                                    13850261
* ANS (COMMENT(' COMMAND REPLY IF CLASS=DOC  '))COL(40);                13851661
* ANS ('6 IJBVAPOE BIT(1),')COL(13) ;                                   13853061
* ANS (COMMENT(' POST-SCAN IF CLASS=AIT      '))COL(40);                13854461
*  /*  ALL X'20' BIT FOR THE FIRST BYTE OF IJBVSCL     */               13855861
* ANS ('3 IJBVBEOX BIT(1),')COL(10) ;                                   13857561
* ANS (COMMENT(' END OF EXTENT IF CLASS=BAM  '))COL(40);                13869061
* ANS ('4 IJBVSPCK BIT(1),')COL(11) ;                                   13871861
* ANS (COMMENT(' PROG. CHECK IF CLASS=SUP    '))COL(40);                13874661
*  /*  ALL X'10' BIT FOR THE FIRST BYTE OF IJBVSCL     */               13877461
* ANS ('3 IJBVBCVH BIT(1),')COL(10) ;                                   13880561
* ANS (COMMENT(' CVH IF CLASS=BAM            '))COL(40);                13892061
* ANS ('4 IJBVSEXT BIT(1),')COL(11) ;                                   13894861
* ANS (COMMENT(' EXT. INTERRUPT IF CLASS=SUP '))COL(40);                13897661
*  /*  ALL X'08' BIT FOR THE FIRST BYTE OF IJBVSCL     */               13900461
* ANS ('3 IJBVBPOO BIT(1),')COL(10) ;                                   13903561
* ANS (COMMENT(' POST OPEN IF CLASS=BAM      '))COL(40);                13915061
* ANS ('4 IJBVSEXP BIT(1),')COL(11) ;                                   13917861
* ANS (COMMENT(' EXCHANGE PHASE IF CLASS=SUP '))COL(40);                13920661
*  /*  ALL X'04' BIT FOR THE FIRST BYTE OF IJBVSCL     */               13923461
* ANS ('3 IJBVBPOC BIT(1),')COL(10) ;                                   13926561
* ANS (COMMENT(' POST CLOSE IF CLASS=BAM     '))COL(40);                13938061
* ANS ('4 IJBVSPRF BIT(1),')COL(11) ;                                   13988061
* ANS (COMMENT(' PREFETCH IF CLASS=SUP       '))COL(40);                14550061
*  /*  ALL X'02' BIT FOR THE FIRST BYTE OF IJBVSCL     */               14575061
* ANS ('3 IJBVSPOF BIT(1),')COL(10) ;                                   14600061
* ANS (COMMENT(' POSTFETCH IF CLASS=SUP      '))COL(40);                14650061
*                                                                       15400061
* ANS ('2 IJBVSCP BIT(8),')COL(8) ;                                     15400761
* ANS (COMMENT(' SCOPE OF EXIT               '))COL(40);                15401461
*                                                                       15402161
* ANS ('3 IJBVSYS BIT(1),')COL(10) ;                                    15402261
* ANS (COMMENT(' SYSTEM SCOPE                '))COL(40);                15402361
* ANS ('3 IJBVPAR BIT(1),')COL(10) ;                                    15402461
* ANS (COMMENT(' PARTITION SCOPE             '))COL(40);                15402561
* ANS ('3 IJBVTSK BIT(1),')COL(10) ;                                    15402661
* ANS (COMMENT(' TASK SCOPE                  '))COL(40);                15402761
* ANS ('3 *       BIT(1),')COL(10) ;                                    15402861
* ANS (COMMENT(' RESERVED                    '))COL(40);                15402961
* ANS ('3 *       BIT(1),')COL(10) ;                                    15403061
* ANS (COMMENT(' RESERVED                    '))COL(40);                15403161
* ANS ('3 *       BIT(1),')COL(10) ;                                    15403261
* ANS (COMMENT(' RESERVED                    '))COL(40);                15403361
* ANS ('3 *       BIT(1),')COL(10) ;                                    15403461
* ANS (COMMENT(' RESERVED                    '))COL(40);                15403561
* ANS ('3 *       BIT(1),')COL(10) ;                                    15403661
* ANS (COMMENT(' RESERVED                    '))COL(40);                15403761
*                                                                       15404261
* ANS ('2 IJBVFLAG BIT(8),')COL(8) ;                                    15404961
* ANS (COMMENT(' FLAG BYTE                   '))COL(40);                15405661
*                                                                       15406361
* ANS ('3 IJBVFUPD BIT(1),')COL(10) ;                                   15406461
* ANS (COMMENT(' EXIT DEFINED FOR UPDATE     '))COL(40);                15406561
* ANS ('3 IJBVCRIT BIT(1),')COL(10) ;                                   15406661
* ANS (COMMENT(' CRITICAL EXIT               '))COL(40);                15406761
* ANS ('3 IJBVDYN BIT(1),')COL(10) ;                       /*@D61YDHK*/ 15406911
* ANS (COMMENT(' DYN. ENABLE/DISABLE SERVICE'))COL(40);    /*@D61YDHK*/ 15406961
* ANS ('3 *       BIT(1),')COL(10) ;                                    15407061
* ANS (COMMENT(' RESERVED                    '))COL(40);                15407161
* ANS ('3 IJBVSUPD BIT(1),')COL(10) ;                      /*@DY43684*/ 15407311
* ANS (COMMENT(' UPDATE EXIT HAS BEEN SKIPPED'))COL(40);   /*@DY43684*/ 15407361
* ANS ('3 *       BIT(1),')COL(10) ;                                    15407461
* ANS (COMMENT(' RESERVED                    '))COL(40);                15407561
* ANS ('3 *       BIT(1),')COL(10) ;                                    15407661
* ANS (COMMENT(' RESERVED                    '))COL(40);                15407761
* ANS ('3 *       BIT(1),')COL(10) ;                                    15407861
* ANS (COMMENT(' RESERVED                    '))COL(40);                15407961
*                                                                       15408461
* ANS ('2 IJBVRC   FIXED(32),')COL(8) ;                                 15408561
* ANS (COMMENT(' THE RETURN CODE             '))COL(40);                15408661
* ANS ('2 IJBVINF  PTR(31),')COL(8) ;                                   15408761
* ANS (COMMENT(' ADR OF ARE PREPARED BY CLASS'))COL(40);                15408861
* ANS ('2 IJBVEXT  FIXED(32),')COL(8) ;                                 15408961
* ANS (COMMENT('                             '))COL(40);                15409061
* ANS ('2 IJBVPRIO BIT(8),')COL(8) ;                                    15409161
* ANS (COMMENT(' PRIORITY VALUE : 0 - 99     '))COL(40);                15409261
* ANS ('2 *        CHAR(3),')COL(8) ;                                   15409361
* ANS (COMMENT(' RESERVED                    '))COL(40);                15409461
* ANS ('2 IJBVUSW  CHAR(8),')COL(8) ;                                   15409561
* ANS (COMMENT(' 8-BYTE USER WORD            '))COL(40);                15409661
* ANS ('2 IJBVTOK  CHAR(8),')COL(8) ;                                   15409761
* ANS (COMMENT(' TOKEN RETURNED BY PRODID    '))COL(40);                15409861
* ANS ('2 IJBVETK  CHAR(8),')COL(8) ;                                   15409961
* ANS (COMMENT(' TOKEN RETURNED BY PRODEXIT  '))COL(40);                15410061
* ANS ('2 IJBVCEXT  PTR(31);')COL(8) ;                                  15410161
* ANS (COMMENT('                             '))COL(40);                15410261
* RETURN CODE(0);                                                       15410361
* GOTO ENDE;                                                            15410461
* BAM: ;                                                                15410561
* /*----------------------------------------------------------------*/  15410661
* /* PROCESS DSECT,BAM REQUEST                                      */  15410761
* /*----------------------------------------------------------------*/  15410861
*                                                                       15410961
*                                                                       15412661
* ANS ('DCL 1 PRODBVIN BASE BDY(WORD),')COL(MACLMAR) ;                  15413361
* ANS (COMMENT('PRODEXIT BAM VENDOR INFO AREA'))COL(40);                15414061
*                                                                       15414761
* ANS ('2 IJBVLENV  BIT(16),')COL(8) ;                                  15414861
* ANS (COMMENT(' LENGTH OF AREA              '))COL(40);                15414961
* ANS ('2 IJBVPIK BIT(16),')COL(8) ;                                    15415061
* ANS (COMMENT(' PIK OF CURRENT TASK         '))COL(40);                15415161
* ANS ('2 IJBVTIK BIT(16),')COL(8) ;                                    15415261
* ANS (COMMENT(' TIK OF CURRENT TASK         '))COL(40);                15415361
* ANS ('2 *       BIT(16),')COL(8) ;                                    15415461
* ANS (COMMENT(' RESERVED                    '))COL(40);                15415561
* ANS ('2 IJBVBEXI CHAR(16) BDY(WORD),')COL(8) ;                        15415661
* ANS (COMMENT(' BAM EXTENTION               '))COL(40);                15441261
* ANS ('3 IJBVBFLG BIT(8),')COL(10) ;                                   15466861
* ANS (COMMENT(' FLAGBYTE                    '))COL(40);                15516861
* ANS ('4 IJBVBJCT BIT(1),')COL(10) ;                                   15566861
* ANS (COMMENT(' FREQUEST FROM JOB CONTROL   '))COL(40);                15616861
* ANS ('3 *       CHAR(3),')COL(10) ;                                   15666861
* ANS (COMMENT(' RESERVED                    '))COL(40);                15716861
* ANS ('3 IJBVBDTF PTR(31);')COL(10) ;                                  15766861
* ANS (COMMENT(' POINTER TO DTF              '))COL(40);                15816861
* ANS ('DCL IJBVBCPL PTR(31)DEFINED IJBVBDTF;')COL(10) ;                15866861
* ANS (COMMENT(' POINTER TO CVH PARAM. LIST  '))COL(40);                15916861
* RETURN CODE(0);                                                       15966861
* ENDE: ;                                                               22725061
*%END;                                                                  22750061
