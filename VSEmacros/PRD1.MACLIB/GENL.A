         MACRO                                                 @DL29ZCJ 00050000
&LABEL   GENL  ,&ADDRESS=,&LENGTH=,&ERREXIT=,&DE=OLD                    00100000
         GBLB  &GNB                                            @D14CDVB 00150000
         LCLA  &A,&B,&C,&DIRL,&DIRH                            @D36JDVB 00200000
         LCLC  &T,&P(200),&DIRC,&DIRID,&DIRID1                 @D14CDVB 00250000
         LCLA  &TAR,&TEX                                       @D35XDGS 00300000
         LCLB  &GETVIS,&DYN,&BNLIB                             @D14CDVB 00350000
         LCLC  &IAR,&IEX,&LNG,&RT,&RS,&RL                      @D35XDGS 00400000
&RT      SETC  '15'                TARGET POINTER REG          @D35XDGS 00450000
&RS      SETC  '14'                SOURCE POINTER REG          @D35XDGS 00500000
&RL      SETC  '0'                 LIMIT POINTER REG           @D35XDGS 00550000
&DIRL    SETA  38                                              @D36JDVB 00600000
&DIRID   SETC  '000000'                                        @D14CDVB 00650000
.*-------------------------------------------------------------@D368DVB 00700000
.*                                                             @D368DVB 00750000
.*       IBM DISK OPERATING SYSTEM                             @D368DVB 00800000
*        SUPERVISOR - GENL - 5686-007-06-C44                   @D14CDVB 00850000
         AGO   .ZZZ                                            @D368DVB 00900000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"           @D31UDMZ 00950000
*        5686-007 (C) COPYRIGHT IBM CORP 1981,1988             @D31UDMZ 01000000
*        ALL RIGHTS RESERVED                                   @D31UDMZ 01050000
*        LICENSED MATERIALS-PROPERTY OF IBM                    @D368DVB 01100000
.*       SEE COPYRIGHT INSTRUCTIONS, G120-2083                 @D31UDMZ 01150000
.ZZZ     ANOP                                                  @D368DVB 01200000
.*-------------------------------------------------------------@D368DVB 01250000
.*                                                                      01300000
.* /* START OF SPECIFICATIONS ***************************************** 01350000
.*                                                                      01400000
.*01* MODULE-TYPE = SUPERVISOR GENERATION AND USER INTERFACE MACRO      01450000
.*                                                                      01500000
.*01* DECRIPTIVE NAME = GENERATE PRIVATE LOAD DIRECTORY MACRO           01550000
.*                                                                      01600000
.*01* NOTES = CHANGE ACTIVITY                                           01650000
.*    SUPPORT OF REENTRANT PROGRAMMING                         @D35XDGS 01700000
.*    SUPPORT OF CONCATENATED LIBRARIES                        @D36JDVB 01750000
.**** END OF SPECIFICATIONS ******************************************* 01800000
.*                                                                      01850000
.*             THIS MACRO GENERATES AN INLINE LIST USEABLE     @DL29ZCJ 01900000
.*             BY THE LIST SCAN FORM OF LOAD OR FETCH.         @DL29ZCJ 01950000
.*             SORT ORDER IS SET UP BY THE MACRO.  AT MOST     @DL29ZCJ 02000000
.*             200 PHASENAMES PER LIST WILL BE ACCEPTED.       @DL29ZCJ 02050000
.*             THE FORM OF THE CALL IS:                        @DL29ZCJ 02100000
.* LABEL GENL  NAME1,NAME2,...,NAMEN                           @DL29ZCJ 02150000
.*                                                             @DL29ZCJ 02200000
&BNLIB   SETB  ('&DE'(1,3) EQ 'VSE')                           @D14CDVB 02250000
         AIF   (&BNLIB OR '&DE' EQ 'OLD').CONT0                @D14CDVB 02300000
         MNOTE 3,'** DE TERM MUST BE OLD OR VSE --- OLD ASSUMED **'     02350000
.CONT0   AIF   (NOT &BNLIB).CONT05                             @D14CDVB 02400000
&DIRID   SETC  'FFFFFF'                                        @D14CDVB 02450000
&DIRL    SETA   40                                             @D14CDVB 02500000
.CONT05  ANOP                                                  @D14CDVB 02550000
&DIRH    SETA  (&DIRL-12)/2                                    @D36JDVB 02600000
&DIRC    SETC  '&DIRL'                                         @D36JDVB 02650000
&A       SETA  N'&SYSLIST                                      @DL29ZCJ 02700000
&B       SETA  &A*&DIRL+2              CALCULATE LIST LENGTH   @D36JDVB 02750000
         AIF   (&A EQ 0).GENL1         ERROR IF NO OPERANDS    @DL29ZCJ 02800000
         AIF  (T'&ADDRESS NE 'O' OR T'&LENGTH NE 'O').EXTENT   @D35XDGS 02850000
.CONT    ANOP                                                  @D35XDGS 02900000
&LABEL   DC    H'&B'                   LIST LENGTH             @DL29ZCJ 02950000
.CONT1   ANOP                                                  @D35XDGS 03000000
.*                                                             @DL29ZCJ 03050000
.*       THE FOLLOWING CODE BUILDS A SORT LIST CONTAINING THE  @DL29ZCJ 03100000
.*       INPUT PHASENAMES.  CHECKS ARE MADE FOR VALID ENTRIES. @DL29ZCJ 03150000
.*                                                             @DL29ZCJ 03200000
&B       SETA  &A+1                                            @DL29ZCJ 03250000
.GENL2   ANOP                                                  @DL29ZCJ 03300000
&B       SETA  &B-1                                            @DL29ZCJ 03350000
         AIF   (K'&SYSLIST(&B) LE 8).GENL6                     @DL29ZCJ 03400000
         MNOTE 1,'** OPERAND # &B TRUNCATED TO 8 CHARACTERS **'         03450000
.GENL6   AIF   (T'&SYSLIST(&B) NE 'O').GENL8                   @DL29ZCJ 03500000
         MNOTE 1,'** OPERAND # &B OMITTED - BLANK ENTRY GENERATED **'   03550000
.GENL8   ANOP                                                  @DL29ZCJ 03600000
&P(&B)   SETC  '&SYSLIST(&B).        '(1,8)   SET NEXT PARAM   @DL29ZCJ 03650000
         AIF   (&B NE 1).GENL2         LOOP IF MORE            @DL29ZCJ 03700000
.*                                                             @DL29ZCJ 03750000
.*       START OF OUTER SORT LOOP.  ONE PASS MOVES THE LOWEST  @DL29ZCJ 03800000
.*       PHASENAME IN COLLATING SEQUENCE TO THE CURRENT TOP    @DL29ZCJ 03850000
.*       OF THE SORT LIST, AND GENERATES ITS ENTRY IN THE      @DL29ZCJ 03900000
.*       OUTPUT LIST.  SUBSEQUENT PASSES EACH START ONE ENTRY  @DL29ZCJ 03950000
.*       LOWER IN THE SORT LIST.  WHEN A PASS RESULTS IN NO    @DL29ZCJ 04000000
.*       SORT ENTRIES BEING MOVED, THE SORT IS TERMINATED AND  @DL29ZCJ 04050000
.*       THE REST OF THE OUTPUT LIST IS GENERATED.             @DL29ZCJ 04100000
.*                                                             @DL29ZCJ 04150000
.GENL7   ANOP                                                  @DL29ZCJ 04200000
&C       SETA  &A                      POINT TO LAST ENTRY     @DL29ZCJ 04250000
&T       SETC  ' '                     RESET FLIP SWITCH       @DL29ZCJ 04300000
         AIF   (&B EQ &A).GENL3        IF LAST ENTRY           @DL29ZCJ 04350000
.*                                                             @DL29ZCJ 04400000
.*       START OF INNER SORT LOOP.                             @DL29ZCJ 04450000
.*       THE NEXT TWO ENTRIES ARE EXAMINED, AND REVERSED IF    @DL29ZCJ 04500000
.*       OUT OF SORT SEQUENCE.  ANY REVERSE SETS A SWITCH, &T, @DL29ZCJ 04550000
.*       CAUSING THE OUTER SORT LOOP TO INITIATE ANOTHER PASS. @DL29ZCJ 04600000
.*                                                             @DL29ZCJ 04650000
.GENL5   AIF   ('&P(&C-1)' LE '&P(&C)').GENL4                  @DL29ZCJ 04700000
&T       SETC  '&P(&C-1)'              SET FLIP SWITCH         @DL29ZCJ 04750000
&P(&C-1) SETC  '&P(&C)'                                        @DL29ZCJ 04800000
&P(&C)   SETC  '&T'                    FLIP POSITION PTRS      @DL29ZCJ 04850000
.GENL4   ANOP                                                  @DL29ZCJ 04900000
&C       SETA  &C-1                                            @DL29ZCJ 04950000
         AIF   (&C GT &B).GENL5        LOOP IF MORE ENTRIES    @DL29ZCJ 05000000
.*                                                             @DL29ZCJ 05050000
.*       END OF INNER SORT LOOP.                               @DL29ZCJ 05100000
.*                                                             @DL29ZCJ 05150000
.GENL3   ANOP                                                  @DL29ZCJ 05200000
         DC    CL8'&P(&B)'             PHASENAME               @DL29ZCJ 05250000
         AIF   (&DYN).GENL31                                   @D35XDGS 05300000
         DC    XL3'&DIRID'                                     @D14CDVB 05350000
         DC    AL1(&DIRH)              N                       @D36JDVB 05400000
         DC    XL(&DIRL-12)'0'         PHASE INFORMATION       @D36JDVB 05450000
.GENL31  ANOP                                                  @D35XDGS 05500000
&B       SETA  &B+1                                            @DL29ZCJ 05550000
         AIF   ('&T' NE ' ').GENL7     LOOP IF SORT INCOMPLETE @DL29ZCJ 05600000
.*                                                             @DL29ZCJ 05650000
.*       END OF OUTER SORT LOOP.                               @DL29ZCJ 05700000
.*                                                             @DL29ZCJ 05750000
         AIF   (&B LE &A).GENL3        LOOP IF MORE ENTRIES    @DL29ZCJ 05800000
         MEXIT                                                 @D14CDVB 05850000
.*------------------------------------------------------------*@DL29ZCJ 05900000
.GENL1   MNOTE 8,'NO NAMES SPECIFIED, GENERATION SUPPRESSED.'  @D35XDGS 05950000
         AGO   .ERREXIT                                        @D35XDGS 06000000
.*                                                             @D35XDGS 06050000
.EXTENT  AIF   (T'&ADDRESS NE 'O').EXT1                        @D35XDGS 06100000
         MNOTE 2,'LENGTH OPERAND IGNORED BECAUSE ADDRESS OPERAND OMITTE-06150000
               D.'                                             @D35XDGS 06200000
         AGO   .CONT                                           @D35XDGS 06250000
.EXT1    ANOP                                                  @D35XDGS 06300000
&DYN     SETB  1              INDICATE DYNAMIC LIST GENERATION @D35XDGS 06350000
         AIF   (N'&SYSLIST LE 200).LNG0                        @D35XDGS 06400000
         MNOTE 8,'MORE THAN 200 DYNAMIC LIST ELEMENTS,MACRO GENERATION *06450000
               SUPPRESSED.' STATIC:200*8 < 4095,DYNAMIC:200*38 < 32767  06500000
         AGO   .ERREXIT                                        @D35XDGS 06550000
.*                                                             @D35XDGS 06600000
.*       LENGTH OPERAND EVALUATION ----------------------------@D35XDGS 06650000
.*                                                             @D35XDGS 06700000
.LNG0    AIF   (T'&LENGTH NE 'O').LNG2                         @D35XDGS 06750000
         AIF   ('&ADDRESS' NE 'DYN' AND '&ADDRESS' NE 'DYNAMIC').LNG1   06800000
&GETVIS  SETB 1                                                @D35XDGS 06850000
         AGO   .AR1                                            @D35XDGS 06900000
.LNG1    MNOTE 8,'LENGTH OPERAND OMITTED, GENERATION SUPPRESSED.'       06950000
         AGO   .ERREXIT                                        @D35XDGS 07000000
.LNG2    ANOP                                                  @D35XDGS 07050000
&LNG     SETC  '&LENGTH'                                       @D35XDGS 07100000
.*                                                             @D35XDGS 07150000
.*       ADDRESS OPERAND EVALUATION ---------------------------@D35XDGS 07200000
.*                                                             @D35XDGS 07250000
.AR      AIF   ('&ADDRESS'(1,1) EQ '(').ARDYN                  @D35XDGS 07300000
&IAR     SETC  '&ADDRESS'                                      @D35XDGS 07350000
&TAR     SETA  1              1= A-TYPE                        @D35XDGS 07400000
         AGO   .AR1                                            @D35XDGS 07450000
.ARDYN   AIF   ('&ADDRESS'(1,3) EQ '(S,').ARSTYP               @D35XDGS 07500000
&IAR     SETC  '&ADDRESS(1)'                                   @D35XDGS 07550000
&TAR     SETA  2              2= REGISTER                      @D35XDGS 07600000
         AGO   .AR1                                            @D35XDGS 07650000
.ARSTYP  ANOP                                                  @D35XDGS 07700000
&IAR     SETC  '&ADDRESS'(4,K'&ADDRESS-4)                      @D35XDGS 07750000
&TAR     SETA  4              4= S-TYPE                        @D35XDGS 07800000
.AR1     ANOP                                                  @D35XDGS 07850000
.*                                                             @D35XDGS 07900000
.*       ERREXIT OPERAND EVALUATION ------------------------   @D35XDGS 07950000
.*                                                             @D35XDGS 08000000
&TEX     SETA  0              0= OMITTED                       @D35XDGS 08050000
         AIF   (T'&ERREXIT NE 'O' OR NOT &GETVIS).EX           @D35XDGS 08100000
         MNOTE 1,'DYNAMIC STORAGE REQUEST SPECIFIED AND ''ERREXIT'' OPE-08150000
               RAND MISSING, GETVIS ACTION WITH ERREXIT -> CANCEL ASSUM-08200000
               ED.'                                            @D35XDGS 08250000
         AGO   .EX1                                            @D35XDGS 08300000
.EX      AIF   (T'&ERREXIT EQ 'O').EX1                         @D35XDGS 08350000
         AIF   (&GETVIS).EX0                                   @D35XDGS 08400000
         MNOTE 4,'''ADDRESS'' AND ''ERREXIT'' PARAMETERS SPECIFIED,''ER*08450000
               REXIT'' HAS BEEN IGNORED.'                      @D35XDGS 08500000
         AGO   .EX1                                            @D35XDGS 08550000
.EX0     AIF   ('&ERREXIT'(1,1) EQ '(').EXDYN                  @D35XDGS 08600000
&IEX     SETC  '&ERREXIT'                                      @D35XDGS 08650000
&TEX     SETA  1              1= A-TYPE                        @D35XDGS 08700000
         AGO   .EX1                                            @D35XDGS 08750000
.EXDYN   AIF   ('&ERREXIT'(1,3) EQ '(S,').EXSTYP               @D35XDGS 08800000
&IEX     SETC  '&ERREXIT'(2,K'&ERREXIT-2)                      @D35XDGS 08850000
&TEX     SETA  2              2= REGISTER                      @D35XDGS 08900000
         AGO   .EX1                                            @D35XDGS 08950000
.EXSTYP  ANOP                                                  @D35XDGS 09000000
&IEX     SETC  '&ERREXIT'(4,K'&ERREXIT-4)                      @D35XDGS 09050000
&TEX     SETA  4              4= S-TYPE                        @D35XDGS 09100000
.EX1     ANOP                                                  @D35XDGS 09150000
.*                                                             @D35XDGS 09200000
.*       GENERATE MACRO EXPANSION --------------------------   @D35XDGS 09250000
.*                                                             @D35XDGS 09300000
.*       GENERATE INSTRUCTIONS -----                           @D35XDGS 09350000
.*                                                             @D35XDGS 09400000
&DIRID1  SETC  '&DIRID'(1,2)                                   @D14CDVB 09450000
         AIF   (&GETVIS).M0                                    @D35XDGS 09500000
         DS    0H                  GENERATE PRIVATE DIRECTORY LIST      09550000
&LABEL   DS    0CL(&LNG-&B+1)      IF THIS RESULTS IN AN ASSEMBLY ERROR 09600000
*                                  THEN LENGTH OF GENL IS TOO SHORT     09650000
         AGO   .N00                                            @D35XDGS 09700000
.M0      ANOP                                                  @D35XDGS 09750000
&LABEL   SR    15,15               CLEAR RETURN CODE REG       @D35XDGS 09800000
         LA    0,&B                SET REQUIRED LENGTH         @D35XDGS 09850000
         SVC   61                  GETVIS SVC                  @D35XDGS 09900000
         LTR   15,15               CHECK SUCCESS               @D35XDGS 09950000
         AIF   (&TEX NE 2).M1                                  @D35XDGS 10000000
         BNZR  &IEX                NO -> ERREXIT               @D35XDGS 10050000
         AGO   .N03                                            @D35XDGS 10100000
.M1      AIF   (&TEX NE 4).M2                                  @D35XDGS 10150000
         BNZ   &IEX                NO -> ERREXIT               @D35XDGS 10200000
         AGO   .N03                                            @D35XDGS 10250000
.M2      AIF   (&TEX NE 1).M3                                  @D35XDGS 10300000
         LR    0,1                 SAVE REG 1 CONTENT          @D35XDGS 10350000
         L     1,=A(&IEX)          LOAD ERREXIT ADDR           @D35XDGS 10400000
         BNZR  1                   NO -> ERREXIT               @D35XDGS 10450000
         LR    1,0                 RESTORE REG 1               @D35XDGS 10500000
         AGO   .N03                                            @D35XDGS 10550000
.M3      ANOP                                                  @D35XDGS 10600000
         BZ    *+6                 YES -> GENERATE             @D35XDGS 10650000
         SVC   6                   NO  -> CANCEL               @D35XDGS 10700000
         AGO   .N03                                            @D35XDGS 10750000
.N00     AIF   (&TAR NE 1).N01                                 @D35XDGS 10800000
         L     1,=A(&IAR)          LOAD ADDRESS                @D35XDGS 10850000
         AGO   .N03                                            @D35XDGS 10900000
.N01     AIF   (&TAR NE 2).N02                                 @D35XDGS 10950000
         AIF   ('&IAR' EQ '1').N03                             @D35XDGS 11000000
         LR    1,&IAR              LOAD ADDRESS                @D35XDGS 11050000
         AGO   .N03                                            @D35XDGS 11100000
.N02     ANOP                                                  @D35XDGS 11150000
         LA    1,&IAR              LOAD ADDRESS                @D35XDGS 11200000
.N03     ANOP                                                  @D35XDGS 11250000
         MVC   0(2,1),@C&SYSNDX    MOVE LENGTH FIELD           @D14CDVB 11300000
         LA    &RT,2(1)            INIT TARGET POINTER         @D35XDGS 11350000
         LA    &RS,@C&SYSNDX+2     INIT SOURCE POINTER         @D14CDVB 11400000
         LA    &RL,&A*8(&RS)       INIT END OF SOURCE LIMIT    @D35XDGS 11450000
@A&SYSNDX MVC  0(8,&RT),0(&RS)     MOVE NAME                   @D35XDGS 11500000
         XC    8(&DIRL-8,&RT),8(&RT) CLEAR REMAINING ENTRY     @D35XDGS 11550000
         MVI   8(&RT),X'&DIRID1'                               @D14CDVB 11600000
         MVI   9(&RT),X'&DIRID1'                               @D14CDVB 11650000
         MVI   10(&RT),X'&DIRID1'                              @D14CDVB 11700000
         MVI   11(&RT),&DIRH       INITIALIZE N FIELD          @D36JDVB 11750000
         LA    &RT,&DIRL.(&RT)     INCREMENT TARGET POINTER    @D36JDVB 11800000
         LA    &RS,8(&RS)          INCREMENT SOURCE POINTER    @D35XDGS 11850000
         CR    &RS,&RL             TEST FOR END OF LIST        @D35XDGS 11900000
         BL    @A&SYSNDX           NO, BUILD NEXT ENTRY        @D14CDVB 11950000
         BR    &RS                 YES, BRANCH AROUND SOURCE   @D35XDGS 12000000
@C&SYSNDX DC   H'&B'               LENGTH OF COMPLETE LIST     @D14CDVB 12050000
         AGO   .CONT1                                          @D35XDGS 12100000
.ERREXIT AIF   ('&LABEL' EQ '').ERRXT10                        @D35XDGS 12150000
&LABEL   DC    0H'0'          LABEL OF SUPPRESSED GENL MACRO   @D149DVB 12200000
.ERRXT10 AIF   (&GNB).N8                                       @D14CDVB 12250000
&GNB     SETB  1                                               @D14CDVB 12300000
*---------------------------------------------------------------------* 12350000
*  NOTES TO GENL MACRO:                                               * 12400000
*                                                                     * 12450000
*  --->  THE SYNTAX OF THE GENL MACRO IS AS FOLLOWS:                  * 12500000
*                                                                     * 12550000
*   LABEL GENL PHASENAME1,..,PHASENAMEN     ... MANDATORY             * 12600000
*              <,ADDRESS=AREA|(S,AREA)|(R1)    ..                     * 12650000
*               ,LENGTH=NUMBER>                ..                     * 12700000
*              <,ADDRESS=DYN(-AMIC)            .. OPTIONAL            * 12750000
*              <,ERREXIT=ADDR|(S,ADDR)|(R2)>>  ..                     * 12800000
*              <,DE=OLD|VSE>                   ..                     * 12850000
*                                                                     * 12900000
*  --->  PHASENAME.:  LIST OF UP TO 200 PHASENAMES                    * 12950000
*                     WHICH IS ALPHAMERICALLY SORTED BY THE MACRO EX- * 13000000
*                     PANSION.                                        * 13050000
*        ADDRESS=...,LENGTH=.. :                                      * 13100000
*                     ADDRESS AND LENGTH OF A STORAGE AREA PROVIDED   * 13150000
*                     TO GET A REENTRANT MACRO EXPANSION WHICH CONTAINS 13200000
*                     BOTH THE PHASENAME_LIST AND THE CODE TO BUILD   * 13250000
*                     A THE LOCAL_DIR_LIST IN THE STORAGE AREA        * 13300000
*                     PROVIDED BY THE OPERANDS ADDRESS AND LENGTH     * 13350000
*        ADDRESS=DYN,ERREXIT=..:                                      * 13400000
*                     THE MACRO EXPANSION ITSELF GIVES A GETVIS TO    * 13450000
*                     GET THE STORAGE REQUIRED TO BUILT THE LOCAL_DIR_* 13500000
*                     _LIST. THE MACRO EXPANSION IS REENTRANT.        * 13550000
*                     ERREXIT IS THE ADDRESS OF ROUTINE TO BE EXECUTED* 13600000
*                     IF THE IMPLICIT GETVIS FAILS.                   * 13650000
*        ADDRESS OPERAND OMITTED :   THE MACRO EXPANSION              * 13700000
*                     RESULTS IN A LOCAL_DIRECTORY_LIST ONLY.         * 13750000
*        DE=OLD|VSE(-FORM): DIRECTORY ENTRY PROVIDED                  * 13800000
*                     'OLD' DIR_ENTRY HAS  CONVENTIONAL FORMAT        * 13850000
*                     'VSE' DIR_ENTRY HAS  N L I B - FORMAT           * 13900000
*                                                                     * 13950000
*  --->  R1, R2  MUST BE DIFFERENT REGISTERS. THE MACRO               * 14000000
*                SERVICE DESTROYS THE GENERAL REGISTERS 0,1,14,15     * 14050000
*                                                                     * 14100000
*  --->   V S E - DIRECTORY ENTRY FORMAT                              * 14150000
*                                                                     * 14200000
*            0   PHASENAME   CL8                                      * 14250000
*            8   DIRVSEID    XL3'FF'     ID FOR NEW FORMAT            * 14300000
*           11   DIRN        XL1'0E'     LENGTH OF DE                 * 14350000
*           12   DIRLMBR     F           LENGTH OF TXT IN BYTES       * 14400000
*           16   DIRC        XL1         FLAGS                        * 14450000
*           17               XL3         RESERVED                     * 14500000
*           20   DIRACOPY    A           INTEGRITY INFORMATION        * 14550000
*           24   DIRALPT     A           LOADPOINT AT LINKEDT TIME    * 14600000
*           28   DIRAEPT     A           ENTRYPOINT AT LINKEDT TIME   * 14650000
*           32   DIRAPART    A           BEG PARTITION                * 14700000
*           36   DIRAVSA     A           ENTRYPOINT IN SVA            * 14750000
*                                                                     * 14800000
*  --->   CONVENTIONAL DIR_ENTRY FORMAT                               * 14850000
*                                                                     * 14900000
*            0   PHASENAME   CL8                                      * 14950000
*            8   DIRCOPY     XL3'0'      INTEGRITY INFORMATION        * 15000000
*           11   DIRN        XL1         LENGTH OF DE                 * 15050000
*           12   DIRTT       H           NUMBER OF 1K LIBRARY BLOCKS  * 15100000
*           14   DIRLL       H           TXT_LENGTH IN BYTES IN LAST  * 15150000
*                                                    TXT BLOCK        * 15200000
*           16   DIRC        XL1         FLAGS                        * 15250000
*           17               XL1         RESERVED                     * 15300000
*           18   DIRPPP      AL3         LOADPOINT AT LINKEDT TIME    * 15350000
*           21   DIREEE      AL3         ENTRYPOINT AT LINKEDT TIME   * 15400000
*           24               XL3         *** NOT SUPPORTED ***        * 15450000
*           26   DIRAAA      AL3         PARTITION START ADDR         * 15500000
*           30   DIRASVA     A           ENTRYPOINT IN SVA            * 15550000
*           34               A           *** NOT SUPPORTED ***        * 15600000
*                                                                     * 15650000
*    --->THE 'DIRC' FLAGS CAN BE USED TO TEST THE STATE OF THE PHASE  * 15700000
*                X'80'        SELFRELOCATABLE                         * 15750000
*                X'40'        RELOCATABLE                             * 15800000
*                X'20'        SVA - ELIGIBLE                          * 15850000
*                             ..( A SVA_PHASE IS RELOCATABLE AND SVA  * 15900000
*                                 - ELIGIBLE )                        * 15950000
*                X'10'        PHASE LOCATED IN SVA                    * 16000000
*                X'01'        SLMAP AVAILABLE                         * 16050000
*    ---> MOREOVER, 'DIRC' FLAGS REFLECT  THE STATE OF THE IN_CORE_   * 16100000
*         DIR_ENTRY                                                   * 16150000
*                X'08'        NON_SYSLIB ENTRY                        * 16200000
*                X'04'        ENTRY NOT FOUND                         * 16250000
*                X'02'        ENTRY ACTIVE   ( FETCH/LOAD SERVICE     * 16300000
*                                   HAS ALREADY PROCESSED DIR_ENTRY ) * 16350000
*    ---> N O T E :  THE 'DIRC' FLAGS ARE BITS AND SHOULD BE TESTED   * 16400000
*                    BY 'TM' - ASSEMBLER INSTRUCTIONS                 * 16450000
*    ---> N O T E :  'DIRC' MUST BE INITIALIZED AS X'00'              * 16500000
*                    'DIRN' INDICATES THE NUMBER OF HALFWORDS AFTER   * 16550000
*                           'DIRN' TO BE PROVIDED BY THE FETCH SERVICE* 16600000
*                           E.G    X'02' LE 'DIRN' LE X'0B'           * 16650000
*                           'DIRN' = X'00' ... NO RETURN_INFO PROVIDED* 16700000
*                                                                     * 16750000
*---------------------------------------------------------------------* 16800000
.N8      ANOP                                                  @D35XDGS 16850000
         MEND                                                  @D35XDGS 16900000
