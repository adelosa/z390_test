         MACRO                                                          00050000
&N       PVTOC &FN,&IOA=,&TYPE=,&NAME=,&COVBYPS=,&SCRBYPS=,&RETAREA=,  X00100000
               &PRTBYPS=,&NEW=,&DLIST=,&MF=                             00150000
.*A000000-999999                                               @D35XE32 00200000
.*                                                                      00250000
.** START OF SPECIFICATIONS ******************************************* 00300000
.*                                                                    * 00350000
.*  MODULE-NAME = PVTOC                                               * 00400000
.*                                                                    * 00450000
.*  DESCRIPTIVE-NAME = PROCESS VTOC                                   * 00500000
.*                                                                    * 00550000
.* COPYRIGHT = 5686-007 (C) COPYRIGHT IBM CORP. 1977,1992      @D52VDAK 00600052
.*             ALL RIGHTS RESERVED                             @U31TUJS 00650000
.*             LICENSED MATERIALS-PROPERTY OF IBM              @U31TUJS 00700000
.*             SEE COPYRIGHT INSTRUCTIONS, G120-2083           @U31TUJS 00750000
.*                                                                    * 00800000
.*  STATUS= DOS/VS RELEASE 35                                         * 00850000
.*                                                                    * 00900000
.*  FUNCTION = THIS MACRO IS USED TO GENERATE THE INTERFACE TO THE    * 00950000
.*             COMMON VTOC HANDLER (CVH) FOR PROCESSING A VTOC.       * 01000000
.*                                                                    * 01050000
.*  MODULE-TYPE = MACRO                                               * 01100000
.*                                                                    * 01150000
.*    PROCESSOR = ASSEMBLER                                           * 01200000
.*                                                                    * 01250000
.*  NOTES = MUCH OF THE LOGIC IN THIS MACRO IS BASED ON CERTAIN       * 01300000
.*          ASSUMPTIONS AND RULES ABOUT THE INTERRELATIONSHIP OF THE  * 01350000
.*          PARAMETERS:                                               * 01400000
.*          1.  FOR THE STANDARD FORM, SUFFICIENT PARAMETERS MUST     * 01450000
.*              BE SPECIFIED TO CONSTITUTE A COMPLETE CVH INTERFACE.  * 01500000
.*          2.  FOR THE LIST AND EXECUTE FORM, NO ADDITIONAL PARA-    * 01550000
.*              METERS ARE REQUIRED AND ANY SUBSET OF NON-EXCLUSIVE   * 01600000
.*              PARAMETERS MAY BE SPECIFIED.  THE ONE EXCEPTION IS    * 01650000
.*              THAT IF TYPE=ADDR IS SPECIFIED, THEN EITHER READ OR   * 01700000
.*              WRITE MUST ALSO BE SPECIFIED.                         * 01750000
.*          3.  THE THREE BYPASS PARAMETERS ARE MUTUALLY EXCLUSIVE.   * 01800000
.*          4.  FOR THE EXECUTE FORM, A NULL VALUE FOR A PARAMETER    * 01850000
.*              INDICATES THAT ITS CORRESPONDING FLAG OR FIELD MUST   * 01900000
.*              NOT BE MODIFIED UNLESS A MUTUALLY EXCLUSIVE PARAMETER * 01950000
.*              IS SPECIFIED.                                         * 02000000
.*                                                                    * 02050000
.*  INPUT =                                                           * 02100000
.*                                                                    * 02150000
.*       THE FIRST (AND ONLY) POSITIONAL OPERAND SPECIFIES THE KIND   * 02200000
.*       OF CVH REQUEST.  THE PERMISSIBLE VALUES ARE:                 * 02250000
.*         READ - READ A LABEL FROM THE VTOC OR READ THE VOL1 LABEL   * 02300000
.*         WRITE - WRITE ONE OR MORE LABELS INTO THE VTOC             * 02350000
.*         COV - CHECK EXTENTS IN NEW LABELS FOR OVERLAP WITH EXTENTS * 02400000
.*               IN EXISTING LABELS, AND CHECK FOR DUPLICATE FILE     * 02450000
.*               NAME                                                 * 02500000
.*         SCRATCH - ERASE A LABEL IN THE VTOC                        * 02550000
.*         RENAME - CHANGE THE 44-BYTE LABEL NAME                     * 02600000
.*                                                                    * 02650000
.*       THE IOA OPERAND SPECIFIES THE ADDRESS OF A USER-SUPPLIED I/O * 02700000
.*       AREA CONTAINING THE LABEL(S) TO BE PROCESSED.                * 02750000
.*                                                                    * 02800000
.*       THE TYPE OPERAND SPECIFIES THE PARTICULAR READ OR WRITE      * 02850000
.*       FUNCTION TO BE PERFORMED.  THE PERMISSIBLE VALUES FOR READ   * 02900000
.*       ARE:                                                         * 02950000
.*         F4   - READ THE FORMAT 4 LABEL                             * 03000000
.*         F1   - READ A PARTICULAR FORMAT 1 LABEL                    * 03050000
.*         ADDR - READ THE LABEL AT A GIVEN DISK ADDRESS              * 03100000
.*         NEXT - READ THE FIRST LABEL AFTER A GIVEN DISK ADDRESS     * 03150000
.*         VOL1 - READ THE VOL1 LABEL                                 * 03200000
.*       THE PERMISSIBLE VALUES FOR WRITE TYPE ARE:                   * 03250000
.*         ADDR    - WRITE THE GIVEN LABEL AT THE GIVEN DISK ADDRESS  * 03300000
.*         ANYSLOT - WRITE THE GIVEN LABEL IN ANY AVAILABLE FREE SLOT * 03350000
.*                                                                    * 03400000
.*       THE NAME OPERAND SPECIFIES THE ADDRESS OF A 44-BYTE LABEL    * 03450000
.*       NAME.                                                        * 03500000
.*                                                                    * 03550000
.*       COVBYPS=YES SPECIFIES THAT CHECK FOR OVERLAPS IS TO BE       * 03600000
.*       BYPASSED WHEN PROCESSING THE WRITE IN ANYSLOT REQUEST.       * 03650000
.*                                                                    * 03700000
.*       THE SCRBYPS OPERAND SPECIFIES WHETHER OR NOT TO BYPASS       * 03750000
.*       SCRATCHING OVERLAPPED, EXPIRED, NON-PROTECTED FILES.         * 03800000
.*                                                                    * 03850000
.*       THE RETAREA OPERAND SPECIFIES THE ADDRESS OF A 44-BYTE AREA  * 03900000
.*       TO CONTAIN THE NAME OF THE OVERLAPPED FILE IF ONE IS FOUND.  * 03950000
.*                                                                    * 04000000
.*       THE PRTBYPS OPERAND SPECIFIES WHETHER OR NOT TO SCRATCH OR   * 04050000
.*       RENAME A FILE EVEN IF IT IS PROTECTED.                       * 04100000
.*                                                                    * 04150000
.*       THE NEW OPERAND SPECIFIES THE ADDRESS OF A NEW 44-BYTE NAME  * 04200000
.*       ON A RENAME REQUEST.                                         * 04250000
.*                                                                    * 04300000
.*       THE DLIST OPERAND SPECIFIES THE ADDRESS OF A VOLUME DESCRIP- * 04350000
.*       TOR LIST.                                                    * 04400000
.*                                                                    * 04450000
.*       THE MF OPERAND SPECIFIES WHETHER THE MACRO IS TO GENERATE    * 04500000
.*       LIST OR EXECUTE FORMS.  IF NOT SPECIFIED, THE STANDARD FORM  * 04550000
.*       IS GENERATED.                                                * 04600000
.*                                                                    * 04650000
.*  OUTPUT = FOR STANDARD AND LIST FORMS, THE CVH PROCESSING PARA-    * 04700000
.*           METER LIST IS GENERATED.  FOR STANDARD AND EXECUTE FORMS,* 04750000
.*           CODE TO INVOKE THE CVH IS GENERATED.  FOR THE EXECUTE    * 04800000
.*           FORM, CODE MAY ALSO BE GENERATED TO MODIFY THE EXISTING  * 04850000
.*           PARAMETER LIST.                                          * 04900000
.*                                                                    * 04950000
.*  CHANGE-ACTIVITY = NONE                                            * 05000000
.*                                                                    * 05050000
.** END OF SPECIFICATIONS ********************************************* 05100000
.*                                                                      05150000
         LCLA  &DISP                                                    05200000
         LCLB  &B1,&B2,&B3,&B4,&B5,&B6,&B7,&B8,&B9                      05250000
         LCLB  &T1,&T2,&T3,&T4,&T5,&T6,&T7                              05300000
         LCLB  &F1,&F2,&F3,&COMB                                        05350000
         LCLC  &X0,&X1,&X2,&X4,&X8,&X12,&X16,&X20                       05400000
         LCLC  &P1,&P2,&P4,&P8,&P12,&P16,&P20                           05450000
.*                                                                      05500000
         ACTR  10000                                                    05550000
.*                                                                      05600000
&B1      SETB  ('&IOA' NE '')                                           05650000
&B2      SETB  ('&TYPE' NE '')                                          05700000
&B3      SETB  ('&NAME' NE '')                                          05750000
&B4      SETB  (('&COVBYPS' NE '') AND ('&COVBYPS' NE 'NO'))            05800000
&B5      SETB  (('&SCRBYPS' NE '') AND ('&SCRBYPS' NE 'NO'))            05850000
&B6      SETB  ('&RETAREA' NE '')                                       05900000
&B7      SETB  (('&PRTBYPS' NE '') AND ('&PRTBYPS' NE 'NO'))            05950000
&B8      SETB  ('&NEW' NE '')                                           06000000
&B9      SETB  ('&DLIST' NE '')                                         06050000
&T1      SETB  ('&TYPE' EQ 'F4')                                        06100000
&T2      SETB  ('&TYPE' EQ 'F1')                                        06150000
&T3      SETB  (('&TYPE' EQ 'ADDR') AND ('&FN' EQ 'READ'))              06200000
&T4      SETB  ('&TYPE' EQ 'NEXT')                                      06250000
&T5      SETB  ('&TYPE' EQ 'VOL1')                                      06300000
&T6      SETB  (('&TYPE' EQ 'ADDR') AND ('&FN' EQ 'WRITE'))             06350000
&T7      SETB  ('&TYPE' EQ 'ANYSLOT')                                   06400000
&F1      SETB  ('&FN' EQ 'COV')                                         06450000
&F2      SETB  ('&FN' EQ 'SCRATCH')                                     06500000
&F3      SETB  ('&FN' EQ 'RENAME')                                      06550000
.*                                                                      06600000
.********************************************************************** 06650000
.*                                                                    * 06700000
.*****          CHECK FOR MUTUALLY EXCLUSIVE PARAMETERS           ***** 06750000
.*                                                                    * 06800000
.********************************************************************** 06850000
.*                                                                      06900000
         AIF   ('&FN' NE 'READ').C10   NOT READ                         06950000
         AIF   (&B4 OR &B5 OR &B6 OR &B7 OR &B8).ER1 NOT ALLOWED W/READ 07000000
         AIF   (NOT(&T2) AND (&B3)).ER6 NAME NOT ALLOWED EXCEPT WITH F1 07050000
         AGO   .C55                                                     07100000
.C10     ANOP                                                           07150000
         AIF   ('&FN' NE 'WRITE').C20  NOT WRITE                        07200000
         AIF   (&B3 OR &B5 OR &B7 OR &B8).ER2 NOT ALLOWED WITH WRITE    07250000
         AIF   (&B4 AND &B6).ER3       COVBYPS AND RETAREA EXCLUSIVE    07300000
         AIF   (('&TYPE' EQ 'ADDR') AND (&B4 OR &B6)).ER7 NOT ALLOWED   07350000
         AGO   .C55                                                     07400000
.C20     ANOP                                                           07450000
         AIF   (NOT(&F1)).C30          NOT COV                          07500000
         AIF   (&B2 OR &B3 OR &B4 OR &B7 OR &B8).ER4 NOT ALLOWED W/COV  07550000
         AGO   .C55                                                     07600000
.C30     ANOP                                                           07650000
         AIF   (NOT(&F2)).C40          NOT SCRATCH                      07700000
         AIF   (&B1 OR &B2 OR &B4 OR &B5 OR &B6 OR &B8).ER5 NOT ALLOWED 07750000
         AGO   .C55                                                     07800000
.C40     ANOP                                                           07850000
         AIF   (NOT(&F3)).C50          NOT RENAME                       07900000
         AIF   (&B1 OR &B2 OR &B4 OR &B5 OR &B6).ER23 NOT ALLOWED W/REN 07950000
         AGO   .C55                                                     08000000
.C50     ANOP                                                           08050000
         AIF   ((&B1) AND (&B7 OR &B8)).ER21 CHECK FOR MUTUALLY EXCLU-  08100000
         AIF   ((&B2) AND (&B5 OR &B7 OR &B8)).ER21 SIVE PARAMETERS BUT 08150000
         AIF   ((&B3) AND (&B4 OR &B5 OR &B6)).ER21 NO FUNCTION GIVEN   08200000
         AIF   ((&B4) AND (&B5 OR &B6 OR &B7 OR &B8)).ER21              08250000
         AIF   ((&B5) AND (&B7 OR &B8)).ER21                            08300000
         AIF   ((&B6) AND (&B7 OR &B8)).ER21                            08350000
.C55     ANOP                                                           08400000
.*                                                                    * 08450000
.*   BYPASS OPTIONS ARE MUTUALLY EXCLUSIVE, EVEN IF SET TO NO.        * 08500000
.*                                                                    * 08550000
         AIF   (('&COVBYPS' EQ 'NO') AND ('&SCRBYPS' EQ 'NO')).ER21     08600000
         AIF   (('&COVBYPS' EQ 'NO') AND ('&PRTBYPS' EQ 'NO')).ER21     08650000
         AIF   (('&SCRBYPS' EQ 'NO') AND ('&PRTBYPS' EQ 'NO')).ER21     08700000
         AIF   (('&COVBYPS' EQ 'NO') AND (&B5 OR &B7)).ER21             08750000
         AIF   (('&SCRBYPS' EQ 'NO') AND (&B4 OR &B7)).ER21             08800000
         AIF   (('&PRTBYPS' EQ 'NO') AND (&B4 OR &B5)).ER21             08850000
.*                                                                      08900000
         AIF   ('&MF' NE 'L').C100     NOT LIST FORM, SO REGS OK        08950000
         AIF   (NOT &B1).C60           IOA NOT SPECIFIED                09000000
         AIF   ('&IOA'(1,1) EQ '(').ER8 REGISTER NOTATION NOT ALLOWED   09050000
.C60     ANOP                                                           09100000
         AIF   (NOT &B3).C70           NAME NOT SPECIFIED               09150000
         AIF   ('&NAME'(1,1) EQ '(').ER8 REGISTER NOTATION NOT ALLOWED  09200000
.C70     ANOP                                                           09250000
         AIF   (NOT &B6).C80           RETAREA NOT SPECIFIED            09300000
         AIF   ('&RETAREA'(1,1) EQ '(').ER8 REG NOTATION NOT ALLOWED    09350000
.C80     ANOP                                                           09400000
         AIF   (NOT &B8).C90           NEW NOT SPECIFIED                09450000
         AIF   ('&NEW'(1,1) EQ '(').ER8 REGISTER NOTATION NOT ALLOWED   09500000
.C90     ANOP                                                           09550000
         AIF   (NOT &B9).C100          DLIST NOT SPECIFIED              09600000
         AIF   ('&DLIST'(1,1) EQ '(').ER8 REGISTER NOTATION NOT ALLOWED 09650000
.*                                                                      09700000
.********************************************************************** 09750000
.*                                                                    * 09800000
.*****             CHECK FOR INVALID SINGLE PARAMETERS            ***** 09850000
.*                                                                    * 09900000
.********************************************************************** 09950000
.*                                                                      10000000
.C100    ANOP                                                           10050000
         AIF   ('&FN' EQ '').C110      FUNCTION NOT SPECIFIED           10100000
         AIF   (('&FN' NE 'READ') AND ('&FN' NE 'WRITE') AND           X10150000
               (NOT(&F1 OR &F2 OR &F3))).ER9 INCORRECT POSITIONAL PARM  10200000
.C110    ANOP                                                           10250000
         AIF   ('&TYPE' EQ '').C130    TYPE NOT SPECIFIED               10300000
         AIF   ('&FN' EQ 'WRITE').C120 TYPE SPECIFIED WITH WRITE        10350000
         AIF   ('&FN' EQ 'READ').C115  TYPE SPECIFIED WITH READ         10400000
         AIF   (NOT(&T1 OR &T2 OR &T4 OR &T5 OR &T7)).ER10 TYPE IS BAD  10450000
         AGO   .C130                   GO ON                            10500000
.C115    ANOP                                                           10550000
         AIF   (NOT(&T1 OR &T2 OR &T3 OR &T4 OR &T5)).ER10 TYPE INVALID 10600000
.*                                     FOR READ                         10650000
         AGO   .C130                   GO ON                            10700000
.C120    ANOP                                                           10750000
         AIF   (NOT(&T6 OR &T7)).ER10  TYPE INVALID FOR WRITE           10800000
.C130    ANOP                                                           10850000
         AIF   (NOT &B4).C140          COVBYPS NOT SPECIFIED OR NO      10900000
         AIF   ('&COVBYPS' NE 'YES').ER11 COVBYPS VALUE INCORRECT       10950000
.C140    ANOP                                                           11000000
         AIF   (NOT &B5).C150          SCRBYPS NOT SPECIFIED OR NO      11050000
         AIF   ('&SCRBYPS' NE 'YES').ER12 SCRBYPS VALUE INCORRECT       11100000
.C150    ANOP                                                           11150000
         AIF   (NOT &B7).C160          PRTBYPS NOT SPECIFIED OR NO      11200000
         AIF   ('&PRTBYPS' NE 'YES').ER13 PRTBYPS VALUE INCORRECT       11250000
.C160    ANOP                                                           11300000
         AIF   ('&MF' EQ '').C200      STD FORM--CHK FOR MISSING PARMS  11350000
         AIF   (('&MF' NE 'L') AND ('&MF(1)' NE 'E')).ER14              11400000
.*                                     BAD MF OPERAND                   11450000
         AGO   .SLE1                   GO TO GENERATE CODE COMMON TO    11500000
.*                                     ALL FORMS                        11550000
.*                                                                      11600000
.********************************************************************** 11650000
.*                                                                    * 11700000
.*****               CHECK FOR MISSING PARAMETERS                 ***** 11750000
.*                                                                    * 11800000
.********************************************************************** 11850000
.*                                                                      11900000
.C200    ANOP                                                           11950000
         AIF   ('&FN' EQ '').ER9       FUNCTION NOT SPECIFIED           12000000
         AIF   (NOT &B9).ER15          DLIST PARAMETER MISSING          12050000
         AIF   ('&FN' NE 'READ').C210  NOT READ FUNCTION                12100000
         AIF   (NOT(&B1 AND &B2)).ER16 IOA OR TYPE MISSING              12150000
         AIF   (&T2 AND (NOT &B3)).ER16 NAME NEEDED IF F1               12200000
         AGO   .SLE1                   GO TO GENERATE STATEMENTS        12250000
.C210    ANOP                                                           12300000
         AIF   ('&FN' NE 'WRITE').C220 NOT WRITE FUNCTION               12350000
         AIF   (NOT(&B1 AND &B2)).ER17 IOA OR TYPE MISSING              12400000
         AIF   (&T7 AND (NOT (&B4 OR &B6))).ER17                        12450000
.*                                     COVBYPS OR RETAREA NEEDED IF     12500000
.*                                     ANYSLOT WRITE TYPE               12550000
         AGO   .SLE1                   GO TO GENERATE STATEMENTS        12600000
.C220    ANOP                                                           12650000
         AIF   (&F1 AND (NOT (&B1 AND &B6))).ER18 IOA AND RETAREA       12700000
.*                                     NEEDED IF COV                    12750000
         AIF   (&F2 AND (NOT &B3)).ER19 NAME NEEDED ON SCRATCH REQUEST  12800000
         AIF   (&F3 AND (NOT (&B3 AND &B8))).ER20 NAME AND NEW NEEDED   12850000
.*                                     IF RENAME                        12900000
.*                                                                      12950000
.********************************************************************** 13000000
.*                                                                    * 13050000
.*****            GENERATE STATEMENT COMMON TO ALL FORMS          ***** 13100000
.*                                                                    * 13150000
.********************************************************************** 13200000
.*                                                                      13250000
.SLE1    ANOP                                                           13300000
         CNOP  0,4                                                      13350000
         AIF   ('&N' EQ '').SSLLEE1                                     13400000
&N       DS    0H                                                       13450000
.SSLLEE1 ANOP                                                           13500000
         AIF   ('&MF' EQ '').S1        STANDARD FORM                    13550000
         AIF   ('&MF' EQ 'L').SL1      LIST FORM                        13600000
         AGO   .E1                     EXECUTE FORM                     13650000
.*                                                                      13700000
.********************************************************************** 13750000
.*                                                                    * 13800000
.*****  GENERATE BRANCH AROUND PARAMETER LIST FOR STANDARD FORM   ***** 13850000
.*                                                                    * 13900000
.********************************************************************** 13950000
.*                                                                      14000000
.S1      ANOP                                                           14050000
         B     *+28                    BRANCH AROUND PARAMETER LIST     14100000
.*                                                                      14150000
.********************************************************************** 14200000
.*                                                                    * 14250000
.*****     GENERATE STATEMENTS FOR THE STANDARD AND LIST FORMS    ***** 14300000
.*                                                                    * 14350000
.********************************************************************** 14400000
.*                                                                      14450000
.SL1     ANOP                                                           14500000
         DC    B'00&T1.&T2.&T3.&T4.&T5.0' FUNCTION FLAGS                14550000
         DC    B'&T6.&T7.&F1.&F2.&F3.&B4.&B5.&B7' FN AND OPTION FLAGS   14600000
         DC    AL2(0)                  OPTION FLAGS AND RESERVED BYTE   14650000
         AIF   (NOT(&B9)).SL2          DLIST NOT GIVEN                  14700000
         AIF   ('&DLIST'(1,1) EQ '(').SL2 DLIST ADDRESS IN A REG        14750000
         DC    AL4(&DLIST)             DLIST ADDRESS                    14800000
         AGO   .SL3                    GO ON                            14850000
.SL2     ANOP                                                           14900000
         DC    AL4(0)                  DLIST ADDRESS                    14950000
.SL3     ANOP                                                           15000000
         AIF   (NOT(&B1)).SL4          IOA NOT GIVEN                    15050000
         AIF   ('&IOA'(1,1) EQ '(').SL4 IOA ADDRESS IN A REG            15100000
         DC    AL4(&IOA)               I/O AREA ADDRESS                 15150000
         AGO   .SL5                    GO ON                            15200000
.SL4     ANOP                                                           15250000
         DC    AL4(0)                  I/O AREA ADDRESS                 15300000
.SL5     ANOP                                                           15350000
         AIF   (NOT(&B3)).SL6          NAME NOT GIVEN                   15400000
         AIF   ('&NAME'(1,1) EQ '(').SL6 NAME ADDRESS IN A REG          15450000
         DC    AL4(&NAME)              LABEL NAME ADDRESS               15500000
         AGO   .SL7                    GO ON                            15550000
.SL6     ANOP                                                           15600000
         DC    AL4(0)                  LABEL NAME ADDRESS               15650000
.SL7     ANOP                                                           15700000
         AIF   (NOT(&B8)).SL8          NEW NAME NOT GIVEN               15750000
         AIF   ('&NEW'(1,1) EQ '(').SL8 NEW NAME ADDRESS IN A REG       15800000
         DC    AL4(&NEW)               NEW NAME ADDRESS                 15850000
         AGO   .SL9                    GO ON                            15900000
.SL8     ANOP                                                           15950000
         DC    AL4(0)                  NEW NAME ADDRESS                 16000000
.SL9     ANOP                                                           16050000
         AIF   (NOT(&B6)).SL10         RETURN AREA NOT GIVEN            16100000
         AIF   ('&RETAREA'(1,1) EQ '(').SL10 RETURN AREA ADDR IN A REG  16150000
         DC    AL4(&RETAREA)           RETURN AREA ADDRESS              16200000
         AGO   .SL11                   GO ON                            16250000
.SL10    ANOP                                                           16300000
         DC    AL4(0)                  RETURN AREA ADDRESS              16350000
.SL11    ANOP                                                           16400000
         AIF   ('&MF' EQ '').S20       IF STD FORM, GO TO GENERATE MORE 16450000
         MEXIT                         END OF LIST FORM GENERATION      16500000
.*                                                                      16550000
.********************************************************************** 16600000
.*                                                                    * 16650000
.*****        GENERATE INSTRUCTIONS FOR THE STANDARD FORM         ***** 16700000
.*                                                                    * 16750000
.********************************************************************** 16800000
.*                                                                      16850000
.S20     ANOP                                                           16900000
&DISP    SETA  24                      SET INITIAL DISPLACEMENT FROM    16950000
.*                                     START OF PARAMETER LIST          17000000
         AIF   ('&DLIST'(1,1) NE '(').S30 DLIST NOT IN A REGISTER       17050000
         ST    &DLIST(1),*+4-&DISP     STORE DLIST ADDRESS IN LIST      17100000
&DISP    SETA  &DISP+4                 BUMP DISPLACEMENT TO LIST START  17150000
.S30     ANOP                                                           17200000
         AIF   (NOT(&B1)).S40          IOA NOT GIVEN                    17250000
         AIF   ('&IOA'(1,1) NE '(').S40 IOA ADDRESS NOT IN A REGISTER   17300000
         ST    &IOA(1),*+8-&DISP       STORE I/O AREA ADDRESS IN LIST   17350000
&DISP    SETA  &DISP+4                 BUMP DISPLACEMENT TO LIST START  17400000
.S40     ANOP                                                           17450000
         AIF   (NOT(&B3)).S50          NAME NOT GIVEN                   17500000
         AIF   ('&NAME'(1,1) NE '(').S50 NAME ADDRESS IN A REGISTER     17550000
         ST    &NAME(1),*+12-&DISP     STORE NAME ADDRESS IN LIST       17600000
&DISP    SETA  &DISP+4                 BUMP DISPLACEMENT TO LIST START  17650000
.S50     ANOP                                                           17700000
         AIF   (NOT(&B8)).S60          NEW NOT GIVEN                    17750000
         AIF   ('&NEW'(1,1) NE '(').S60 NEW ADDRESS NOT IN A REGISTER   17800000
         ST    &NEW(1),*+16-&DISP      STORE NEW NAME ADDRESS IN LIST   17850000
&DISP    SETA  &DISP+4                 BUMP DISPLACEMENT TO LIST START  17900000
.S60     ANOP                                                           17950000
         AIF   (NOT(&B6)).S70          RETAREA NOT GIVEN                18000000
         AIF   ('&RETAREA'(1,1) NE '(').S70 RETAREA ADDR NOT IN A REG   18050000
         ST    &RETAREA(1),*+20-&DISP  STORE RETURN AREA ADDR IN LIST   18100000
&DISP    SETA &DISP+4                  BUMP DISPLACEMENT TO LIST START  18150000
.S70     ANOP                                                           18200000
         LA    1,*-&DISP               GET LIST ADDRESS IN REGISTER 1   18250000
         AGO   .SE1                    GO TO GENERATE CVH LINKAGE       18300000
.*                                                                      18350000
.********************************************************************** 18400000
.*                                                                    * 18450000
.*****        GENERATE INSTRUCTIONS FOR THE EXECUTE FORM          ***** 18500000
.*                                                                    * 18550000
.********************************************************************** 18600000
.*                                                                      18650000
.E1      ANOP                                                           18700000
         AIF   ('&MF(2)'(1,1) EQ '(').E2 LIST ADDRESS IN A REGISTER     18750000
&X0      SETC  ''                                                       18800000
&X1      SETC  ''                      SET UP LOCAL CHARACTER STRINGS   18850000
&X2      SETC  ''                      TO DYNAMICALLY BUILD THE         18900000
&X4      SETC  ''                      NEEDED INSTRUCTIONS INVOLVING    18950000
&X8      SETC  ''                      THE LIST ADDRESS                 19000000
&X12     SETC  ''                                                       19050000
&X16     SETC  ''                                                       19100000
&X20     SETC  ''                                                       19150000
&P1      SETC  '+1'                                                     19200000
&P2      SETC  '+2'                                                     19250000
&P4      SETC  '+4'                                                     19300000
&P8      SETC  '+8'                                                     19350000
&P12     SETC  '+12'                                                    19400000
&P16     SETC  '+16'                                                    19450000
&P20     SETC  '+20'                                                    19500000
         AGO   .E3                                                      19550000
.E2      ANOP                                                           19600000
&X0      SETC  '0'                                                      19650000
&X1      SETC  '1'                                                      19700000
&X2      SETC  '2'                                                      19750000
&X4      SETC  '4'                                                      19800000
&X8      SETC  '8'                                                      19850000
&X12     SETC  '12'                                                     19900000
&X16     SETC  '16'                                                     19950000
&X20     SETC  '20'                                                     20000000
&P1      SETC  ''                                                       20050000
&P2      SETC  ''                                                       20100000
&P4      SETC  ''                                                       20150000
&P8      SETC  ''                                                       20200000
&P12     SETC  ''                                                       20250000
&P16     SETC  ''                                                       20300000
&P20     SETC  ''                                                       20350000
.*                                                                      20400000
.E3      ANOP                                                           20450000
.*                                                                    * 20500000
.* ALWAYS SET BYTE 2 OF FLAG BYTES TO ZERO (OPEN OPTIONS)             * 20550000
.*                                                                    * 20600000
         MVI   &X2.&MF(2).&P2,X'00'    SET OPEN OPTIONS OFF             20650000
         AIF   (&T1 OR &T2 OR &T3 OR &T4 OR &T5 OR &T6 OR &T7 OR &F1 ORX20700000
                &F2 OR &F3).E70        A FUNCTION IS SPECIFIED          20750000
.*                                                                    * 20800000
.* PROCESS BYTES 0 AND 1 FOR THE CASE WHERE NO FUNCTIONS ARE SPECIFIED* 20850000
.*                                                                    * 20900000
         NI    &X0.&MF(2),X'3F'        SET OPEN/CLOSE BITS OFF          20950000
         AIF   (NOT(&B4 OR &B5 OR &B7)).E40 NO BYPASS OPTIONS           21000000
         OI    &X1.&MF(2).&P1,B'00000&B4.&B5.&B7' SET BYPASS OPTION     21050000
         AGO   .E500                   GO TO PROCESS DLIST FIELD        21100000
.E40     ANOP                                                           21150000
         AIF   (NOT('&SCRBYPS' EQ 'NO')).E50 LEAVE SCRBYPS OPTION ALONE 21200000
         NI    &X1.&MF(2).&P1,X'FD'    SET SCRBYPS OPTION OFF           21250000
         AGO   .E500                   GO TO PROCESS DLIST FIELD        21300000
.E50     ANOP                                                           21350000
         AIF   (NOT(('&COVBYPS' EQ 'NO') OR &B6)).E60 LEAVE COVBYPS     21400000
.*                                     OPTION ALONE                     21450000
         NI    &X1.&MF(2).&P1,X'FB'    SET COVBYPS OPTION OFF           21500000
         AGO   .E500                   GO TO PROCESS DLIST FIELD        21550000
.E60     ANOP                                                           21600000
         AIF   (NOT('&PRTBYPS' EQ 'NO')).E500 LEAVE PRTBYPS OPT ALONE   21650000
         NI    &X1.&MF(2).&P1,X'FE'    SET PRTBYPS OPTION OFF           21700000
         AGO   .E500                   GO TO PROCESS DLIST FIELD        21750000
.E70     ANOP                                                           21800000
         AIF   (NOT(&T1 OR &T2 OR &T3 OR &T4 OR &T5)).E100 GO TO PRO-   21850000
.*                                     CESS BYTES 0 AND 1 FOR THE NON-  21900000
.*                                     READ FUNCTION CASE               21950000
.*                                                                    * 22000000
.*  PROCESS BYTES 0 AND 1 FOR THE CASE WHERE A READ FUNCTION IS       * 22050000
.*  SPECIFIED                                                         * 22100000
.*                                                                    * 22150000
         MVI   &X1.&MF(2).&P1,X'00'    SET NON-READ OPTIONS OFF         22200000
         MVI   &X0.&MF(2),B'00&T1.&T2.&T3.&T4.&T5.0'                    22250000
*                                      SET READ TYPE INDICATOR          22300000
         AGO   .E500                   GO TO PROCESS DLIST FIELD        22350000
.*                                                                    * 22400000
.*  PROCESS BYTES 0 AND 1 FOR THE CASE WHERE A NON-READ FUNCTION IS   * 22450000
.*  SPECIFIED                                                         * 22500000
.*                                                                    * 22550000
.E100    ANOP                                                           22600000
         MVI   &X0.&MF(2),X'00'        SET OPEN/CLOSE/READ FLAGS OFF    22650000
.*                                                                    * 22700000
.*  IF NO BYPASS OPTION AND NO RETAREA AND NO WRITE BY ADDRESS, THEN  * 22750000
.*  GO TO GENERATE OI AND NI.  OTHERWISE, THE WHOLE BYTE CAN BE SET   * 22800000
.*  WITH A MVI BECAUSE THESE FIVE THINGS ARE MUTUALLY EXCLUSIVE SO    * 22850000
.*  FLAGS FOR THOSE WHICH ARE NOT SPECIFIED SHOULD BE SET TO ZERO.    * 22900000
.*                                                                    * 22950000
         AIF   (('&COVBYPS' EQ '') AND ('&SCRBYPS' EQ '') AND          X23000000
               ('&PRTBYPS' EQ '') AND NOT(&T6) AND NOT(&B6)).E110       23050000
         MVI   &X1.&MF(2).&P1,B'&T6.&T7.&F1.&F2.&F3.&B4.&B5.&B7'        23100000
*                                      SET FUNCTION INDICATOR           23150000
         AGO   .E500                   GO TO PROCESS DLIST FIELD        23200000
.E110    ANOP                                                           23250000
&COMB    SETB  (&F2 OR &F3)            SCRATCH OR RENAME                23300000
         NI    &X1.&MF(2).&P1,B'00000&T7.&F1.&COMB'                     23350000
*                                      SET MUTUALLY EXCLUSIVE FLAGS OFF 23400000
         OI    &X1.&MF(2).&P1,B'0&T7.&F1.&F2.&F3.000' SET FUNCTION FLAG 23450000
.*                                                                    * 23500000
.*  PROCESS ADDRESS FIELDS IN THE PARAMETER LIST                      * 23550000
.*                                                                    * 23600000
.E500    ANOP                                                           23650000
         AIF   (NOT(&B9)).E520         DLIST NOT GIVEN                  23700000
         AIF   ('&DLIST'(1,1) EQ '(').E510 DLIST ADDRESS IN A REGISTER  23750000
         LA    0,&DLIST                GET DLIST ADDRESS                23800000
         ST    0,&X4.&MF(2).&P4        STORE DLIST ADDRESS IN LIST      23850000
         AGO   .E520                   GO ON                            23900000
.E510    ANOP                                                           23950000
         ST    &DLIST(1),&X4.&MF(2).&P4 STORE DLIST ADDRESS IN LIST     24000000
.E520    ANOP                                                           24050000
         AIF   (NOT(&B1)).E540         IOA NOT GIVEN                    24100000
         AIF   ('&IOA'(1,1) EQ '(').E530 IOA ADDRESS IN A REGISTER      24150000
         LA    0,&IOA                  GET I/O AREA ADDRESS             24200000
         ST    0,&X8.&MF(2).&P8        STORE I/O AREA ADDRESS IN LIST   24250000
         AGO   .E540                   GO ON                            24300000
.E530    ANOP                                                           24350000
         ST    &IOA(1),&X8.&MF(2).&P8  STORE I/O AREA ADDRESS IN LIST   24400000
.E540    ANOP                                                           24450000
         AIF   (NOT(&B3)).E560         NAME NOT GIVEN                   24500000
         AIF   ('&NAME'(1,1) EQ '(').E550 NAME ADDRESS IN A REGISTER    24550000
         LA    0,&NAME                 GET NAME ADDRESS                 24600000
         ST    0,&X12.&MF(2).&P12      STORE NAME ADDRESS IN LIST       24650000
         AGO   .E560                   GO ON                            24700000
.E550    ANOP                                                           24750000
         ST    &NAME(1),&X12.&MF(2).&P12 STORE NAME ADDRESS IN LIST     24800000
.E560    ANOP                                                           24850000
         AIF   (NOT(&B8)).E580         NEW NAME NOT GIVEN               24900000
         AIF   ('&NEW'(1,1) EQ '(').E570 NEW NAME ADDRESS IN A REGISTER 24950000
         LA    0,&NEW                  GET NEW NAME ADDRESS             25000000
         ST    0,&X16.&MF(2).&P16      STORE NEW NAME ADDRESS IN LIST   25050000
         AGO   .E580                                                    25100000
.E570    ANOP                                                           25150000
         ST    &NEW(1),&X16.&MF(2).&P16 STORE NEW NAME ADDRESS IN LIST  25200000
.E580    ANOP                                                           25250000
         AIF   ('&COVBYPS' NE 'YES').E590 COV BYPASS NOT YES, GO ON     25300000
         XR    0,0                     GET ZEROS FOR RETURN AREA ADDR   25350000
         AGO   .E600                   GO TO STORE ZEROS                25400000
.E590    ANOP                                                           25450000
         AIF   (NOT(&B6)).E620         RETURN AREA NOT SPECIFIED        25500000
         AIF   ('&RETAREA'(1,1) EQ '(').E610 RETARA ADDR IN A REGISTER  25550000
         LA    0,&RETAREA              GET RETURN AREA ADDRESS          25600000
.E600    ANOP                                                           25650000
         ST    0,&X20.&MF(2).&P20      STORE RETURN AREA ADDR IN LIST   25700000
         AGO   .E620                   GO ON                            25750000
.E610    ANOP                                                           25800000
         ST    &RETAREA(1),&X20.&MF(2).&P20 STORE RETAREA ADDR IN LIST  25850000
.*                                                                      25900000
.E620    ANOP                                                           25950000
         AIF   ('&MF(2)'(1,1) NE '(').E630 LIST ADDR NOT IN A REGISTER  26000000
         AIF   ('&MF(2)' EQ '(1)').SE1 LIST ADDR ALREADY IN REGISTER 1  26050000
         LR    1,&MF(2)                GET LIST ADDRESS IN REGISTER 1   26100000
         AGO   .SE1                    GO TO GENERATE BALR TO CVH       26150000
.E630    ANOP                                                           26200000
         LA    1,&MF(2)                GET LIST ADDRESS IN REGISTER 1   26250000
.*                                                                      26300000
.********************************************************************** 26350000
.*                                                                    * 26400000
.*****  GENERATE INSTRUCTIONS FOR BOTH STANDARD AND EXECUTE FORM  ***** 26450000
.*                                                                    * 26500000
.********************************************************************** 26550000
.*                                                                      26600000
.SE1     ANOP                                                           26650000
         L     15,4(1)                 GET DLIST ADDRESS FROM PARM LIST 26700000
         L     15,32(15)               GET CVH ADDRESS FROM DLIST       26750000
         XR    14,14               SET LENGTH CODE TO 1        @D52VDAK 26757152
         BALR  14,0           GET NEXT INSTRUCTION ADDRESS AND @D52VDAK 26764252
*                             SET HIGH ORDER BIT IF AMODE 31   @D52VDAK 26771352
         LTR   14,14          RUNNING IN 24 BIT MODE ?         @D52VDAK 26778452
         BP    8(,14)         YES,OK                           @D52VDAK 26785552
         SVC   130            EXECUTION MODE VIOLATION         @D52VDAK 26792652
         BALR  14,15                   CALL CVH                         26800000
         MEXIT                                                          26850000
.ER1     ANOP                                                           26900000
         MNOTE 8,'PARAMETER INVALID WITH READ FUNCTION'                 26950000
         MEXIT                                                          27000000
.ER2     ANOP                                                           27050000
         MNOTE 8,'PARAMETER INVALID WITH WRITE FUNCTION'                27100000
         MEXIT                                                          27150000
.ER3     ANOP                                                           27200000
         MNOTE 8,'COVBYPS AND RETAREA ARE MUTUALLY EXCLUSIVE'           27250000
         MEXIT                                                          27300000
.ER4     ANOP                                                           27350000
         MNOTE 8,'PARAMETER INVALID WITH COV FUNCTION'                  27400000
         MEXIT                                                          27450000
.ER5     ANOP                                                           27500000
         MNOTE 8,'PARAMETER INVALID WITH SCRATCH FUNCTION'              27550000
         MEXIT                                                          27600000
.ER6     ANOP                                                           27650000
         MNOTE 8,'NAME PARAMETER NOT ALLOWED WITH READ TYPE SPECIFIED'  27700000
         MEXIT                                                          27750000
.ER7     ANOP                                                           27800000
         MNOTE 8,'PARAMETER NOT ALLOWED WITH ADDR WRITE TYPE'           27850000
         MEXIT                                                          27900000
.ER8     ANOP                                                           27950000
         MNOTE 8,'REGISTER NOTATION NOT ALLOWED WITH LIST FORM'         28000000
         MEXIT                                                          28050000
.ER9     ANOP                                                           28100000
         MNOTE 8,'FUNCTION PARAMETER MISSING OR INVALID'                28150000
         MEXIT                                                          28200000
.ER10    ANOP                                                           28250000
         MNOTE 8,'TYPE PARAMETER INVALID OR NOT ALLOWED ALONE'          28300000
         MEXIT                                                          28350000
.ER11    ANOP                                                           28400000
         MNOTE 8,'INVALID COVBYPS PARAMETER SPECIFIED'                  28450000
         MEXIT                                                          28500000
.ER12    ANOP                                                           28550000
         MNOTE 8,'INVALID SCRBYPS PARAMETER SPECIFIED'                  28600000
         MEXIT                                                          28650000
.ER13    ANOP                                                           28700000
         MNOTE 8,'INVALID PRTBYPS PARAMETER SPECIFIED'                  28750000
         MEXIT                                                          28800000
.ER14    ANOP                                                           28850000
         MNOTE 8,'INVALID MF PARAMETER SPECIFIED'                       28900000
         MEXIT                                                          28950000
.ER15    ANOP                                                           29000000
         MNOTE 8,'DLIST PARAMETER MISSING'                              29050000
         MEXIT                                                          29100000
.ER16    ANOP                                                           29150000
         MNOTE 8,'REQUIRED PARAMETER MISSING ON READ REQUEST'           29200000
         MEXIT                                                          29250000
.ER17    ANOP                                                           29300000
         MNOTE 8,'REQUIRED PARAMETER MISSING ON WRITE REQUEST'          29350000
         MEXIT                                                          29400000
.ER18    ANOP                                                           29450000
         MNOTE 8,'REQUIRED PARAMETER MISSING ON COV REQUEST'            29500000
         MEXIT                                                          29550000
.ER19    ANOP                                                           29600000
         MNOTE 8,'REQUIRED PARAMETER MISSING ON SCRATCH REQUEST'        29650000
         MEXIT                                                          29700000
.ER20    ANOP                                                           29750000
         MNOTE 8,'REQUIRED PARAMETER MISSING ON RENAME REQUEST'         29800000
         MEXIT                                                          29850000
.ER21    ANOP                                                           29900000
         MNOTE 8,'MUTUALLY EXCLUSIVE PARAMATERS SPECIFIED'              29950000
         MEXIT                                                          30000000
.ER22    ANOP                                                           30050000
         MNOTE 8,'TYPE PARAMETER INVALID OR NOT ALLOWED ALONE'          30100000
         MEXIT                                                          30150000
.ER23    ANOP                                                           30200000
         MNOTE 8,'PARAMETER INVALID WITH RENAME FUNCTION'               30250000
         MEND                                                           30300000
