.*------------------------------------------------------------*         EZA00010
.*                                            October 1992 RS *         EZA00020
.* Macro name:                                                *         EZA00030
.*      EZASMIS                                               *         EZA00040
.*                                                            *         EZA00050
.* COPYRIGHT:                                                 *         EZA00060
.*   LICENSED MATERIALS - PROPERTY OF IBM                     *         EZA00070
.*   THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"      *         EZA00080
.*   5655-HAL (C) COPYRIGHT IBM CORP. 1993.                   *         EZA00090
.*   ALL RIGHTS RESERVED.                                     *         EZA00100
.*   US GOVERNMENT USERS RESTRICTED RIGHTS -                  *         EZA00110
.*   USE, DUPLICATION OR DISCLOSURE RESTRICTED BY GSA ADP     *         EZA00120
.*   SCHEDULE CONTRACT WITH IBM CORP.                         *         EZA00130
.*   SEE IBM COPYRIGHT INSTRUCTIONS.                          *         EZA00140
.*                                                            *         EZA00150
.* Function:                                                  *         EZA00160
.*      Analyze arguments presented to the EZASMI macro       *         EZA00170
.*      and generate code to place their address in R0.       *         EZA00180
.*      This macro handles arguments that can have values.    *         EZA00190
.*      MACRO ARGUMENTS                                       *         EZA00200
.*      &NAME - Field being analyzed                          *         EZA00210
.*      &SIZE - Size of field H=Halfword  F=Fullword          *         EZA00220
.*      &MAX  - Maximum value permitted in field. This is     *         EZA00230
.*              checked only for literal values               *         EZA00240
.*      &NUMBER - Sequence number to assign to constant       *         EZA00250
.*                developed in this macro                     *         EZA00260
.*      &INDEX  - Value of &SYSNDX passed from EZASMI         *         EZA00270
.*      &REG  - Return Register                               *         EZA00280
.*                                                            *         EZA00290
.*------------------------------------------------------------*         EZA00300
         MACRO                                                          EZA00310
         EZASMIS &NAME,&SIZE,&MAX,&NUMBER,&INDEX,&REG                   EZA00320
         GBLC  &CONST             with specified numbers                EZA00330
         LCLC  &TEMPCH,&TVAL,&TREG                                      EZA00340
&CONST   SETC  ''                 Clear constant                        EZA00350
&TREG    SETC  '0'                Set default return register           EZA00360
         AIF   (K'&REG EQ 0).R0000  Was REG value specified             EZA00370
&TREG    SETC  '&REG'             Yes, reset return register            EZA00380
.R0000   ANOP                                                           EZA00390
         AIF   (K'&NAME NE 0).N0000 Was value specified                 EZA00400
         SLR   &TREG,&TREG        No field specified                    EZA00410
         MEXIT                                                          EZA00420
.N0000   ANOP                                                           EZA00430
         AIF   ('&NAME'(1,1) NE '(').N0001 Was address in register      EZA00440
&TVAL    SETC  '&NAME'(2,K'&NAME-2)                                     EZA00450
         LR    &TREG,&TVAL  Put address of field in Reg &TREG           EZA00460
         AGO   .N0007                                                   EZA00470
.N0001   ANOP                                                           EZA00480
         AIF   ('&NAME'(1,1) NE '*').N0002 Using indirect addressing?   EZA00490
&TEMPCH  SETC  '&NAME'(2,K'&NAME-1)     Get field name                  EZA00500
         L     &TREG,&TEMPCH                                            EZA00510
         MEXIT                                                          EZA00520
.N0002   ANOP                                                           EZA00530
         AIF   ('&NAME'(1,1) NE '''').N0003 Was a value specified?      EZA00540
&TVAL    SETC  '&NAME'(2,K'&NAME-2)                                     EZA00550
         AGO   .N0004                                                   EZA00560
.N0003   ANOP                                                           EZA00570
         AIF   ('&NAME'(1,1) LT '0').N0006 Was a numeric                EZA00580
         AIF   ('&NAME'(1,1) GT '9').N0006 literal specified?           EZA00590
&TVAL    SETC  '&NAME'             Yes, use it as value                 EZA00600
.N0004   ANOP                                                           EZA00610
         AIF   ('&TVAL' LT '&MAX').N0005  Was value too large?          EZA00620
         MNOTE 12,'EZASMXXX, VALUE SPECIFIED EXCEEDS MAXIMUM'           EZA00630
         MEXIT                                                          EZA00640
.N0005   ANOP   Value specified                                         EZA00650
&CONST   SETC  '&SIZE'.''''.'&TVAL'''  Create constant                  EZA00660
&TEMPCH  SETC  'E$'.'&SIZE'.'&NUMBER'.'&INDEX'                          EZA00670
         LA    &TREG,&TEMPCH                                            EZA00680
         MEXIT                                                          EZA00690
.N0006   ANOP                                                           EZA00700
         LA    &TREG,&NAME                                              EZA00710
.N0007   ANOP                                                           EZA00700
         MEND                                                           EZA00720
