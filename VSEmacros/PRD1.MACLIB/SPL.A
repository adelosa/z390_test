*%GOTO PLSSPL;                                                 @D02PIXR 00050000
*/*                                                                     00100000
         MACRO                                                          00150000
&LABEL   SPL   &TYPE=MAP,&JOBN=DUMMY,&REQ=,&CBUF=0,&PBUF=0,&PBUFL=88,  *00200000
               &NEWVAL=0,&ICRXECB=,&SPMXECB=,&CLASS=,&DISP=,   @DA29965*00250000
               &PWD=,&USERID=,                                 @D03PIXW*00300000
               &DUMMY=DUMMY                                    @D02PIXS 00350000
.*                                                                      00400000
         LCLC  &REQV,&CLV,&DISPV                                        00450000
         LCLC  &PW,&MOD                                        @D02PIXS 00500000
         LCLC  &US                                             @D03PIXW 00550000
         LCLC  &CLASS1                                         @DA29965 00600000
         LCLC  &DISP1                                          @DA29965 00650000
.*                                                                      00700000
&CLASS1  SETC 'A'      DEFAULT CLASS=                          @DA29965 00750000
&DISP1   SETC 'K'      DEFAULT DISP=                           @DA29965 00800000
         AGO   .ZZZ                                                     00850000
*        LICENSED MATERIALS - PROPERTY OF IBM                           00900000
*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                    00950000
*        5686-CF7 (C) COPYRIGHT IBM CORP. 1979, 1996                    01000004
*        SEE COPYRIGHT INSTRUCTIONS                                     01050000
.*                                                                    * 01060001
.*    CHANGE ACTIVITY  =  AS FOLLOWS                                  * 01070001
.*                                                                    * 01080001
.*   VSE/POWER 6.4 SECURITY ENHANCEMENT                        @D64CDSW 01090002
.*                                                                    * 01091001
.ZZZ     ANOP                                                           01100000
.*MACRO NAME:                 SPL                                       01150000
.*FUNCTION:                   1) CREATE A DSECT FOR SPL CONTROL         01200000
.*                               BLOCKS, DEFINING THE LABELS, OPTIONS,  01250000
.*                               AND FIELDS TO BE USED FOR THE ACTION   01300000
.*                               MACROS GETSPOOL, PUTSPOOL, AND         01350000
.*                               CTLSPOOL. OPTIONALLY, DSECTS ARE       01400000
.*                               CREATED FOR THE SPMXECB AND ICRXECB.   01450000
.*                            2) CREATE A SPL CONTROL BLOCK,            01500000
.*                               INITIALIZING FIXED FIELDS AND ANY USER 01550000
.*                               SPECIFIED FIELDS AS DESCRIBED BELOW    01600000
.*                               IN THE PARAMETER SECTION.              01650000
.*GENERAL DESCRIPTION:        EITHER A DSECT(S) IS CREATED, OR A        01700000
.*                            SPL IS CREATED, DEPENDING ON THE TYPE     01750000
.*                            PARAMETER DESCRIBED BELOW.                01800000
.*PARAMETERS:                 LABEL               REQUIRED              01850000
.*                               SPECIFIES THE LABEL TO BE USED         01900000
.*                               FOR THE CREATED DSECT OR SPL.          01950000
.*                            TYPE=MAP/DEFINE     OPTIONAL              02000000
.*                               IF TYPE=MAP (THE DEFAULT) A DSECT      02050000
.*                               IS CREATED. THIS OPTION MUST BE        02100000
.*                               CHOSEN ONCE PER PROGRAM USING THE      02150000
.*                               ACTION MACROS GETSPOOL, PUTSPOOL,      02200000
.*                               AND/OR CTLSPOOL TO GET THE APPROPRIATE 02250000
.*                               LABELS AND EQUATES DEFINED.  OPTIONAL  02300000
.*                               DSECTS ARE CREATED DEPENDING ON        02350000
.*                               WHETHER THE SPMXECB AND/OR ICRXECB     02400000
.*                               PARAMETERS ARE ALSO SPECIFIED.  NO     02450000
.*                               OTHER PARAMETERS ARE PERTINENT TO      02500000
.*                               THIS OPTION.                           02550000
.*                               IF TYPE=DEFINE, AN SPL IS CREATED AND  02600000
.*                               INITIALIZED, EITHER WITH DEFAULT       02650000
.*                               VALUES OR USER OVERRIDES FOR THE       02700000
.*                               PARAMETERS DESCRIBED BELOW.  THE       02750000
.*                               SPMXECB AND ICRXECB PARAMETERS ARE     02800000
.*                               INVALID FOR THIS FORM OF THE CALL.     02850000
.*                            JOBN=NAME           OPTIONAL              02900000
.*                               DEFAULT IS 'DUMMY'                     02950000
.*                            PWD=NAME            OPTIONAL              03000000
.*                               DEFAULT IS BLANK                       03050000
.*                            USERID=NAME         OPTIONAL              03100000
.*                               DEFAULT IS BLANK                       03150000
.*                            DISP=CHARACTER      OPTIONAL              03200000
.*                               DEFAULT IS 'K'                         03250000
.*                               >> NO LONGER SUPPORTED FOR    @DA29965 03300000
.*                                  PUTSPOOL <<                @DA29965 03350000
.*                            CLASS=CHARACTER     OPTIONAL              03400000
.*                               >> NO LONGER SUPPORTED FOR    @DA29965 03450000
.*                                  PUTSPOOL <<                @DA29965 03500000
.*                            REQ=KEYWORD         OPTIONAL              03550000
.*                               DEFAULT IS ZERO                        03600000
.*                            CBUF=LABEL          OPTIONAL              03650000
.*                               DEFAULT IS ZERO                        03700000
.*                            PBUF=LABEL          OPTIONAL              03750000
.*                               DEFAULT IS ZERO                        03800000
.*                            PBUFL=VALUE         OPTIONAL              03850000
.*                               DEFAULT IS 88                          03900000
.*                            NEWVAL=VALUE        OPTIONAL              03950000
.*                               DEFAULT IS ZERO                        04000000
.*                               VALUE MAY BE NUMERIC (IMPLIED DECIMAL) 04050000
.*                               OR HEX (FORM X'VALUE')                 04100000
.*                               OR CHARACTER (FORM C'CHARACTER')       04150000
.*NOTE: TYPE=DEFINE MAY BE USED AS OFTEN AS DESIRED. EACH CALL WILL     04200000
.*      RESULT IN THE GENERATION OF A SPL CONTROL BLOCK, INITIALIZED    04250000
.*      WITH FIXED FIELDS AND OVERRIDES AS SPECIFIED BY THE USER.       04300000
.*      THE USER IS RESPONSIBLE FOR ASSURING THAT THE APPROPRIATE XECB  04350000
.*      SPL POINTER (THE LAST THREE BYTES OF THE FOUR BYTE AREA         04400000
.*      IMMEDIATELY FOLLOWING THE XECB ITSELF - SEE THE EXPANSION OF    04450000
.*      THE SPMXECB OR ICRXECB OPTION) IS SET TO POINT TO THE SPL       04500000
.*      TO BE USED FOR A GIVEN REQUEST USING THE ACTION MACROS.         04550000
.*      TYPE=MAP MUST BE USED EXACTLY ONCE IN A GIVEN PROGRAM USING     04600000
.*      ANY OF THE ACTION MACROS.                                       04650000
.*                                                                      04700000
.* IBM DISK OPERATING SYSTEM                                            04750000
.*                                                                      04800000
*   VSE/POWER - SPL -  5686-CF7-03                                      04850004
.* GIVE WARNING FOR CLASS= AND DISP= PARAMETERS                         04900000
         AIF   (K'&CLASS EQ 0 AND K'&DISP EQ 0).ZZ2                    X04950000
                           BR IF CLASS OR DISP NOT SPEC'D      @DA29965 05000000
*   >>>NOTE TO PROGRAMMER:                                <<<  @DA29965 05050000
*   >>>  IF CLASS= OR DISP= SPECIFIED FOR PUTSPOOL, PLEASE<<<  @DA29965 05100000
*   >>>  NOTE THAT NEITHER IS SUPPORTED ANYMORE. TO SPECIFY<<  @DA29965 05150000
*   >>>  A JOB OUTPUT CLASS OR DISPOSITION, INSERT A LST  <<<  @DA29965 05200000
*   >>>  JECL STATEMENT IN THE JOB STREAM. SEE MANUAL.    <<<  @DA29965 05250000
         AIF   (K'&CLASS EQ 0).ZZ1 BR IF CLASS NOT SPEC'D      @DA29965 05300000
&CLASS1  SETC '&CLASS'                                         @DA29965 05350000
.ZZ1     ANOP                                                  @DA29965 05400000
         AIF   (K'&DISP EQ 0).ZZ2  BR IF DISP NOT SPEC'D       @DA29965 05450000
&DISP1   SETC '&DISP'                                          @DA29965 05500000
.ZZ2     ANOP                                                  @DA29965 05550000
.*                                                                      05600000
         AIF   (K'&LABEL EQ 0).ERLBL    LABEL REQUIRED                  05650000
         AIF   ('&TYPE' NE 'MAP').DEFINE     TO GENERATE REAL SPL       05700000
         AIF   ('&ICRXECB' NE 'YES').CKSPMX BR IF NO ICRXECB            05750000
         SPACE 1                                                        05800000
         DSECT ,              XECB FOR PUTSPOOL                @DM15213 05850000
ICRXECBU DS    F              XECB                                      05900000
ICRERROR DS    X              ERROR RETURN CODE                         05950000
ICRSPLP  DS    AL3            SPL POINTER                               06000000
.CKSPMX  AIF   ('&SPMXECB' NE 'YES').GENSPL  BR IF NO SPMXECB           06050000
         SPACE 1                                                        06100000
         DSECT ,              XECB FOR GETSPOOL AND CTLSPOOL   @DA10550 06150000
SPMXECBU DS    F              XECB                                      06200000
SPMERROR DS    X              ERROR RETURN CODE                         06250000
SPMSPLP  DS    AL3            SPL POINTER                               06300000
.*                                                                      06350000
.********************************************************************** 06400000
.*             DSECT GENERATION                                       * 06450000
.********************************************************************** 06500000
.*                                                                      06550000
.GENSPL  SPACE 1                                                        06600000
&LABEL   DSECT ,              SPOOL PARAMETER LIST                      06650000
SPLS     DS    0F             FOR ALIGNMENT                             06700000
SPLB     DS    X              LENGTH OF SPOOL PARAMETER LIST   @D02PIXS 06750000
SPHD     DS    CL3            SPL HEADER (= 'SPL')                      06800000
SPJB     DS    CL8            UNIQUE VSE/POWER JOBNAME                  06850000
SPJN     DS    CL2            VSE/POWER JOB NUMBER             @D02PIXS 06900000
SPPW     DS    CL8            PASSWORD                         @D02PIXS 06950000
SPUS     DS    CL8            USER-ID OF ISSUER                @D02PIXS 07000000
         SPACE 1                                                        07050000
SPER     DS    X              ERROR FEEDBACK                            07100000
         SPACE 1                                                        07150000
SPIA     EQU   X'80'          ..INVALID ADDRESS                         07200000
SPLA     EQU   X'08'+SPIA     ..PARAMETER LIST                          07250000
SPPA     EQU   X'04'+SPIA     ..VSE/POWER BUFFER (SPPB)                 07300000
SPBA     EQU   X'02'+SPIA     ..DATA BUFFER CHAIN (SPCB/IBNX)           07350000
         SPACE 1                                                        07400000
SPPP     EQU   X'40'          ..DIAGNOSTIC LOGGED BY VSE/POWER          07450000
SPFP     EQU   X'09'+SPPP     ..WAIT ON QFILE/AFILE SPACE      @DA15044 07500000
SPSP     EQU   X'08'+SPPP     ..SUBMIT PROCESSING                       07550000
SPLP     EQU   X'04'+SPPP     ..LOAD PROCESSING                         07600000
SPCP     EQU   X'02'+SPPP     ..COMMAND PROCESSING                      07650000
SPAP     EQU   X'01'+SPPP     ..VSE/POWER TERMINATED                    07700000
         SPACE 1                                                        07750000
SPUE     EQU   X'20'          ..USER PROCESSING ERROR                   07800000
SPLE     EQU   X'08'+SPUE     ..SPOOL REQUEST CONFLICT                  07850000
SPBE     EQU   X'04'+SPUE     ..TOO MANY BUFFERS, POSSIBLE LOOP         07900000
SPPE     EQU   X'02'+SPUE     ..LOAD POSITIONING ERROR                  07950000
SPSE     EQU   X'01'+SPUE     ..BUFFER AREA TOO SMALL                   08000000
         SPACE 1                                                        08050000
SPPI     EQU   X'10'          ..PARAMETER INVALID                       08100000
SPJI     EQU   X'08'+SPPI     ..JOB NAME INVALID                        08150000
SPPWI    EQU   X'07'+SPPI     ..PASSWORD INVALID               @D02PIXS 08200000
SPQI     EQU   X'06'+SPPI     ..QUEUE ID INVALID               @D02PIXS 08250000
SPUI     EQU   X'05'+SPPI     ..USERID INVALID                 @DA36449 08300000
SPCI     EQU   X'04'+SPPI     ..CLASS INVALID                           08350000
SPDI     EQU   X'02'+SPPI     ..DISPOSITION INVALID                     08400000
SPOI     EQU   X'01'+SPPI     ..COMMAND INVALID                @DA11226 08450000
         SPACE 1                                                        08500000
SPNR     EQU   X'00'          ..NORMAL RETURN                           08550000
SPFR     EQU   X'09'+SPNR     .. NOTE:WAIT ON QFILE/AFILE OC'D @DA15044 08600000
SPLR     EQU   X'08'+SPNR     ..END-OF-DATA ON LOAD                     08650000
         SPACE 1                                                        08700000
SPER2    DS    X              ERROR FEEDBACK BYTE 2            @D02PIXS 08750000
SPAI     EQU   X'80'          ..ACCESS INHIBITED (WRONG PWD)   @D02PIXR 08800000
SPAC     EQU   X'40'          ..ACCESS INHIBITED (ACCESS CONTROL       /08810001
                                  PROTECTION VIOLATION)        @D64CDSW 08820002
SPDDR    EQU   X'02'          ..3540 DATA MODE RECORD          @D03PIXW 08850000
SPME     EQU   X'01'          ..MULIPLE ENTRY                  @D03PIXH 08900000
         SPACE 1                                                        08950000
SPR1     DS    X              PUTSPOOL REQUEST BYTE                     09000000
SPEJ     EQU   X'40'          ..END READER JOB CODE                     09050000
         SPACE 1                                                        09100000
SPR2     DS    X              CTLSPOOL REQUEST BYTE                     09150000
SPRP     EQU   X'01'          ..ROUTE PRIORITY     (PALTER)             09200000
SPRD     EQU   X'02'          ..ROUTE DISPOSITION  (PALTER)             09250000
SPRC     EQU   X'04'          ..ROUTE CLASS        (PALTER)             09300000
SPRJ     EQU   X'08'          ..ROUTE REMOTE ID    (PALTER)             09350000
SPCX     EQU   X'10'          ..CANCEL INPUT  (PDELETE RDR)             09400000
SPSC     EQU   X'20'          ..SCRATCH OUTPUT(PDELETE LST)             09450000
SPST     EQU   X'40'          ..DISPLAY STATUS   (PDISPLAY)             09500000
SPPC     EQU   X'80'          ..VSE/POWER COMMAND                       09550000
         SPACE 1                                                        09600000
SPR3     DS    X              GETSPOOL REQUEST BYTE                     09650000
SPLD     EQU   X'01'          ..LOAD PRINT DATA                         09700000
SPPO     EQU   X'02'          ..RESERVED FOR VSE/POWER                  09750000
SPBR     EQU   X'04'          ..BROWSE OUTPUT QUEUE                     09800000
SPCO     EQU   X'08'          ..FEED BACK COMMAND CODES                 09850000
SPBG     EQU   X'10'          ..BUFFERED GETSPOOL              @D02PIXS 09900000
         SPACE 1                                                        09950000
SPR4     DS    X              CTLSPOOL REQUEST BYTE 2          @D02PIXS 10000000
SPOO     EQU   X'80'          ..SPOOL QUEUE DISPLAY            @D02PIXR 10050000
SPQR     EQU   X'20'          ..LOOKUP REQUEST                 @D03PIXH 10100000
         SPACE 1                                                        10150000
SPXR     DS    CL8            RDR X-PARTITION ECB NAME                  10200000
SPXL     DS    CL8            LST/CMD X-PARTITION ECB NAME              10250000
SPCB     DS    A              ADDR(CURRENT BUFFER AREA)                 10300000
         SPACE 1                                                        10350000
SPPB     DS    A              ADDR(VSE/POWER BUFFER)                    10400000
SPMO     EQU   28             .. MSG DISP IN BUF FROM 0TH BYTE @D35BIQI 10450000
         SPACE 1                                                        10500000
SPBL     DS    F              VSE/POWER BUFFER LENGTH                   10550000
SPRL     DS    F              DATA RECORD LENGTH                        10600000
SPRS     DS    0F             BROWSE (RESTART) CONTROL                  10650000
SPSN     DS    X              SIGNED START CONTROL                      10700000
SPCT     DS    XL3            LINE COUNT OFFSET                         10750000
SPCL     DS    C              CLASS                                     10800000
SPDP     DS    C              DISPOSITION                               10850000
SPCC     DS    X              CONTROL CHARACTER                         10900000
SPSQ     DS    C              STATUS QUEUE                              10950000
*                             .. N = NOTHING TO DISPLAY                 11000000
*                             .. R = FOUND ON READER QUEUE              11050000
*                             .. L = FOUND ON LIST QUEUE                11100000
*                             .. P = FOUND ON PUNCH QUEUE      @D02PIXS 11150000
*                             .. X = FOUND ON XMIT QUEUE       @D03PIXW 11200000
SPQD     DS    C              DISPOSITION ON QUEUE                      11250000
         DS    XL2            UNUSED                           @D02PIXS 11300000
SPNV     DS    X              NEW ROUTE VALUE                           11350000
*                             NEW ROUTE PRI=                            11400000
*                             NEW ROUTE DISP=                           11450000
*                             NEW ROUTE CLASS=                          11500000
*                             NEW ROUTE REMOTE=                         11550000
         SPACE 1                                                        11600000
SPLC     DS    F              NUMBER OF LINES/CARDS            @D02PIXS 11650000
         DS    CL4            RESERVED FOR FUTURE USE          @D02PIXS 11700000
SPLN     EQU   *-&LABEL       SPOOL PARAMETER LIST LENGTH               11750000
.*                                                                      11800000
         MEXIT                                                          11850000
.*                                                                      11900000
.********************************************************************** 11950000
.*             VARIOUS PARAMETER CHECKING                               12000000
.********************************************************************** 12050000
.*                                                                      12100000
.DEFINE  AIF   ('&TYPE' NE 'DEFINE').ERTYPE                             12150000
         AIF   (K'&ICRXECB EQ 0).CKSPM  BR IF NO ICRXECB PARAM          12200000
         MNOTE 4,'ICRXECB PARAM IGNORED WHEN TYPE=DEFINE'      @D35BIQI 12250000
.CKSPM   AIF   (K'&SPMXECB EQ 0).CHKJNM BR IF NO SPMXECB PARAM          12300000
         MNOTE 4,'SPMXECB PARAM IGNORED WHEN TYPE=DEFINE'      @D35BIQI 12350000
.*                                                                      12400000
.CHKJNM  ANOP  ,                                               @D02PIXS 12450000
         AIF   (K'&JOBN LT 9).CHKPW   BRANCH IF JOBNAME OK     @D02PIXS 12500000
         MNOTE 4,'JOBN TRUNCATED TO 8 CHARACTERS'              @D35BIQI 12550000
.*                                                                      12600000
.CHKPW   ANOP                                                  @D02PIXS 12650000
&PW      SETC  ' '                 SET DEFAULT PASSWORD        @D02PIXS 12700000
         AIF   ('&PWD' EQ '').CHKREG     BR IF OMITTED         @D02PIXS 12750000
         AIF   (K'&PWD LT 9).CHKPW2                            @D02PIXS 12800000
         MNOTE 4,'PWD SPECIFICATION INVALID, DEFAULT ASSUMED'          *12850000
                                                               @D02PIXS 12900000
         AGO   .CHKREG             AND CONTINUE                @D02PIXS 12950000
.CHKPW2  ANOP                                                  @D02PIXS 13000000
&PW      SETC  '&PWD'              SET USER SPECIFIED PASSWORD @D02PIXS 13050000
.*                                                                      13100000
.CHKREG  ANOP  ,                                               @D02PIXS 13150000
&REQV    SETC  '00'                                                     13200000
         AIF   (K'&REQ EQ 0).SETREQ  BR IF NO REQ PARAM                 13250000
&REQV    SETC  '01'                                                     13300000
         AIF   ('&REQ' EQ 'PRI').SETREQ  BR IF PRI REQUEST              13350000
&REQV    SETC  '02'                                                     13400000
         AIF   ('&REQ' EQ 'DISP').SETREQ  BR IF DISP REQUEST            13450000
&REQV    SETC  '04'                                                     13500000
         AIF   ('&REQ' EQ 'CLASS').SETREQ  BR IF CLASS REQUEST          13550000
&REQV    SETC  '08'                                                     13600000
         AIF   ('&REQ' EQ 'REMOTE').SETREQ  BR IF REMOTE REQUEST        13650000
&REQV    SETC  '10'                                                     13700000
         AIF   ('&REQ' EQ 'CANCEL').SETREQ  BR IF CANCEL REQUEST        13750000
&REQV    SETC  '20'                                                     13800000
         AIF   ('&REQ' EQ 'SCRATCH').SETREQ  BR IF SCRATCH REQUEST      13850000
&REQV    SETC  '40'                                                     13900000
         AIF   ('&REQ' EQ 'STATUS').SETREQ  BR IF STATUS REQUEST        13950000
&REQV    SETC  '80'                                                     14000000
         AIF   ('&REQ' EQ 'COMMAND').SETREQ  BR IF COMMAND REQUEST      14050000
&REQV    SETC  '00'                                                     14100000
         AIF   ('&REQ' NE 'LOOKUP').REQER1    BR IF NOT LOOKUP @D03PIXH 14150000
&MOD     SETC  '20'                                            @D03PIXH 14200000
         AGO   .SETREQX                                        @D03PIXH 14250000
.*                                                                      14300000
.REQER1  MNOTE 4,'INVALID REQ SPECIFICATION-PARAMETER IGNORED' @D35BIQI 14350000
.*                                                                      14400000
.SETREQ  ANOP  ,                                               @D02PIXS 14450000
&MOD     SETC  '00'                SET DEFAULT CTL REQ BYTE 2  @D02PIXS 14500000
.SETREQX ANOP  ,                                               @D03PIXH 14550000
.*                                                                      14600000
&US      SETC  ' '                 SET DEFAULT USER ID         @D03PIXW 14650000
         AIF   ('&USERID' EQ '').CHKUSX     BR IF OMITTED      @D03PIXW 14700000
         AIF   (K'&USERID LT 9).CHKUS2                         @D03PIXW 14750000
         MNOTE 4,'USERID SPECIFICATION INVALID, DEFAULT ASSUMED'       *14800000
                                                               @D03PIXW 14850000
         AGO   .CHKUSX             AND CONTINUE                @D03PIXW 14900000
.CHKUS2  ANOP                                                  @D03PIXW 14950000
&US      SETC  '&USERID'           TAKE USER'S USER ID         @D03PIXW 15000000
.CHKUSX  ANOP                                                  @D03PIXW 15050000
.*                                                                      15100000
.*                                                                      15150000
.*                                                                      15200000
.********************************************************************** 15250000
.*             GENERATE SPOOL PARAMATER LIST  (SPL)                   * 15300000
.********************************************************************** 15350000
.*                                                                      15400000
.SPLGEN  ANOP                                                  @D02PIXS 15450000
         SPACE 1                                                        15500000
&LABEL   DS    0F             ALIGN SPOOL PARAMETER LIST                15550000
         DC    AL1(88)        LENGTH OF PARAMETER LIST         @D02PIXS 15600000
         DC    CL3'SPL'       SPL HEADER                                15650000
         DC    CL8'&JOBN'     UNIQUE VSE/POWER JOBNAME         @D02PIXS 15700000
         DC    XL2'0000'      VSE/POWER JOBNUMBER              @D02PIXS 15750000
         DC    CL8'&PW'       PASSWORD                         @D02PIXS 15800000
         DC    CL8'&US'       USER ID OF ISSUER                @D02PIXS 15850000
         DC    X'00'          ERROR FEEDBACK                            15900000
         DC    X'00'          ERROR FEEDBACK BYTE 2            @D02PIXS 15950000
         DC    X'00'          PUTSPOOL REQUEST BYTE                     16000000
         DC    X'&REQV'       CTLSPOOL REQUEST BYTE                     16050000
         DC    X'00'          GETSPOOL REQUEST BYTE                     16100000
         DC    X'&MOD'        CTLSPOOL REQUEST BYTE 2          @D02PIXS 16150000
         DC    CL8'ICRXECB'   RDR X-PARTITION ECB NAME                  16200000
         DC    CL8'SPMXECB'   LST/CMD X-PARTITION ECB NAME              16250000
         DC    A(&CBUF)       ADDR(CURRENT BUFFER AREA)                 16300000
         DC    A(&PBUF)       ADDR(POWER BUFFER)                        16350000
         DC    F'&PBUFL'      POWER BUFFER LENGTH                       16400000
         DC    F'0'           DATA BUFFER LENGTH                        16450000
         DS    0F             BROWSE (RESTART) CONTROL                  16500000
         DC    X'04'          SIGNED START CONTROL                      16550000
         DC    XL3'00'        LINE COUNT OFFSET                         16600000
         DC    CL1'&CLASS1'   CLASS                            @DA29965 16650000
         DC    CL1'&DISP1'    DISPOSITION                      @DA29965 16700000
         DC    X'00'          CONTROL CHARACTER                         16750000
         DC    C' '           STATUS QUEUE                              16800000
         DC    C' '           DISPOSITION ON QUEUE                      16850000
         DS    XL2            UNUSED                           @D02PIXS 16900000
         AIF   ('&NEWVAL'(1,1) EQ 'C').NVC  BR IF CHAR SPECIFICATION    16950000
         AIF   ('&NEWVAL'(1,1) EQ 'X').NVX  BR IF HEX SPECIFICATION     17000000
         DC    AL1(&NEWVAL)   NEW ROUTE VALUE                           17050000
.MEXIT   ANOP                                                           17100000
         DC    A(0)           LINE/CARD COUNTER                @D02PIXS 17150000
         DC    CL4' '         RESERVED FOR FUTURE USE          @D02PIXS 17200000
         MEXIT                                                          17250000
.NVX     AIF   (K'&NEWVAL EQ 5).NVOK  BR IF LENGTH OK                   17300000
.NVC     AIF   (K'&NEWVAL EQ 4).NVOK  BR IF LENGTH OK                   17350000
.NVERR   MNOTE 4,'INVALID NEWVAL PARAMETER - X''00'' ASSUMED'  @D35BIQI 17400000
         DC    X'00'          NEW ROUTE VALUE                           17450000
         AGO .MEXIT                                            @D35BIQI 17500000
.NVOK    AIF   ('&NEWVAL'(2,1) NE '''' OR '&NEWVAL'(K'&NEWVAL,1)       X17550000
               NE '''').NVERR  BR IF INVALID FORM                       17600000
         DC    &NEWVAL        NEW ROUTE VALUE                           17650000
         AGO .MEXIT                                            @D35BIQI 17700000
.*                                                                      17750000
.*                                                                      17800000
.ERTYPE  MNOTE 12,'INVALID TYPE SPECIFICATION'                 @D35BIQI 17850000
         MEXIT                                                          17900000
.*                                                                      17950000
.ERLBL   MNOTE 12,'LABEL REQUIRED'                             @D35BIQI 18000000
         MEXIT                                                          18050000
         MEND                                                           18100000
*                                                            @D02PIXR*/ 18150000
*%PLSSPL:;                                                              18200000
*/********************************************************************/ 18250000
*/*                                                                  */ 18300000
*/*  IBM DISK OPERATING SYSTEM                                       */ 18350000
*/*  VSE/POWER - 5686-033-01                                         */ 18400000
*/*  SPL MACRO INSTRUCTION                                           */ 18450000
*/*                                                                  */ 18500000
*/*  DEFINE SPOOL PARAMETER LIST  (SPL)                              */ 18550000
*/*                                                                  */ 18600000
*/****************************************************/                 18650000
*/*                                                  */                 18700000
*/*   LICENSED MATERIALS - PROPERTY OF IBM           */                 18750000
*/*   THIS MACRO IS "RESTRICTED MATERIALS OF IBM"    */                 18800000
*/*   5686-033 (C) COPYRIGHT IBM CORP. 1979, 1993    */                 18850001
*/*   SEE COPYRIGHT INSTRUCTIONS                     */                 18900000
*/*                                                  */                 18950000
*/****************************************************/                 19000000
*/********************************************************************/ 19050000
* DECLARE                                                  /*@D02PIXR*/ 19100000
*  1 SPL BASED (RB->TCBPLA) ,                              /*@D02PIXR*/ 19150000
*    2 SPLLB CHAR(1),    /* LENGTH OF SPOOL PARAMETER LIST   @D02PIXR*/ 19200000
*    2 SPLHD CHAR(3),    /* SPL HEADER (='SPL')              @D02PIXR*/ 19250000
*    2 SPLJB CHAR(8),    /* UNIQUE VSE/POWER JOBNAME         @D02PIXR*/ 19300000
*    2 SPLJN FIXED(16),  /* VSE/POWER JOB NUMBER             @D02PIXR*/ 19350000
*    2 SPLPW CHAR(8),    /* PASSWORD                         @D02PIXR*/ 19400000
*    2 SPLUS CHAR(8),    /* USER-ID OF ISSUER                @D03PIXW*/ 19450000
*    2 SPLER CHAR(1),    /* ERROR FEEDBACK                   @D02PIXR*/ 19500000
*    2 SPLER2 CHAR(1),   /* ERROR FEEDBACK 2                 @D02PIXR*/ 19550000
*    2 SPLR1 CHAR(1),    /* PUTSPOOL REQUEST BYTE            @D02PIXR*/ 19600000
*    2 SPLR2 CHAR(1),    /* CTLSPOOL REQUEST BYTE            @D02PIXR*/ 19650000
*    2 SPLR3 CHAR(1),    /* GETSPOOL REQUEST BYTE            @D02PIXR*/ 19700000
*    2 SPLR4 CHAR(1),    /* CTLSPOOL REQUEST BYTE 2          @D02PIXR*/ 19750000
*    2 SPLXR CHAR(8),    /* RDR X-PARTITION ECB NAME         @D02PIXR*/ 19800000
*    2 SPLXL CHAR(8),    /* LST/CMD X-PARTITION ECB NAME     @D02PIXR*/ 19850000
*    2 SPLCB CHAR(4),    /* ADDR(CURRENT BUFFER AREA)        @D02PIXR*/ 19900000
*    2 SPLPB CHAR(4),    /* ADDR(VSE/POWER BUFFER)           @D02PIXR*/ 19950000
*    2 SPLBL FIXED(31),  /* VSE/POWER BUFFER LENGTH          @D02PIXR*/ 20000000
*    2 SPLRL FIXED(31),  /* DATA RECORD LENGTH               @D02PIXR*/ 20050000
*    2 SPLRS,            /* BROWSE (RESTART) CONTROL         @D02PIXR*/ 20100000
*      3 SPLSN FIXED(8), /* SIGNED START CONTROL             @D02PIXR*/ 20150000
*      3 SPLCT FIXED(24),  /* LINE COUNT OFFSET              @D02PIXR*/ 20200000
*    2 SPLCL CHAR(1),    /* CLASS                            @D02PIXR*/ 20250000
*    2 SPLDP CHAR(1),    /* DISPOSITION                      @D02PIXR*/ 20300000
*    2 SPLCC CHAR(1),    /* CONTROL CHARACTER                @D02PIXR*/ 20350000
*    2 SPLSQ CHAR(1),    /* STATUS QUEUE                     @D02PIXR*/ 20400000
*    2 SPLQD CHAR(1),    /* DISPOSITION ON QUEUE             @D02PIXR*/ 20450000
*    2 * CHAR(2),        /* UNUSED                           @D02PIXR*/ 20500000
*    2 SPLNV CHAR(1),    /* NEW ROUTE VALUE                  @D02PIXR*/ 20550000
*    2 SPLLC FIXED(31),  /* LINE/CARD COUNTER                @D02PIXS*/ 20600000
*    2 * CHAR(4);        /* RESERVED FOR FUTURE USE          @D02PIXR*/ 20650000
*                        /* END OF DEFINITIONS               @D02PIXR*/ 20700000
