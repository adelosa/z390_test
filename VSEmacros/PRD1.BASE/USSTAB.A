         MACRO                                                          00025100
&NAME    USSTAB &TABLE=0,&FORMAT=,&DATEDLM=,                           X00050200
               &TIMEDLM=,&DATEFRM=                                 @KGC 00075300
.********************* START OF SPECIFICATIONS *********************    00100700
.*                                                                      00101400
.* MACRO NAME(S): USSTAB                                                00102100
.*                                                                      00102800
.* DESCRIPTIVE NAME:  Define USS definition table macro.                00103500
.*                                                                      00104200
.* STATUS: ACF/VTAM VERSION 4 RELEASE 2                                 00104900
.*                                                                      00105600
.* COPYRIGHT:  LICENSED MATERIALS - PROPERTY OF IBM                     00106500
.*                                                                      00107400
.*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"      00107500
.*                                                                      00107600
.*             5686-065 (C) COPYRIGHT IBM CORP. 1984, 1994              00107700
.*             ALL RIGHTS RESERVED.                                     00107800
.*                                                                      00107900
.*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -                00108000
.*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY             00108100
.*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.                 00108200
.*                                                                      00108300
.*             SEE COPYRIGHT INSTRUCTIONS.                              00108400
.*                                                                      00108500
.* MACRO USER'S GUIDE:                                                  00108600
.*                                                                      00108700
.*    FUNCTION: This macro is used to declare the beginning of a USS    00109100
.*              definition table.  This macro must be invoked before    00109800
.*              any entries can be made in the table.                   00110500
.*                                                                      00111200
.*    MACRO FORMAT:                                                     00111900
.*                                                                      00112600
.*       <label> USSTAB <TABLE=name> <FORMAT=DYNAMIC|OLD>               00113300
.*                                                                      00114000
.*    DESCRIPTION OF OPERANDS:                                          00114700
.*                                                                      00115400
.*       label          If a label is specified, it is used as a CSECT  00116100
.*                      name for the USS definition table to be         00116800
.*                      generated; otherwise, no CSECT name is          00117500
.*                      generated.                                      00118200
.*                                                                      00118900
.*       TABLE          Specifies a translation table to be used        00119600
.*                      to translate character-coded commands.  If the  00120300
.*                      specified table is not part of the module       00121000
.*                      containing USSTAB, an EXTRN statement must be   00121700
.*                      coded for the specified name.                   00122400
.*                                                                      00123100
.*       FORMAT         Specifies the version of VTAM the USS Table     00123200
.*                      will be used on.  If FORMAT is not specified,   00123300
.*                      FORMAT=OLD is used.                             00123400
.*                                                                      00123500
.*                      Use FORMAT=DYNAMIC to take advantage of dynamic 00123600
.*                      table replacement through use of the MODIFY     00123700
.*                      TABLE command and also realize a significant    00123800
.*                      reduction in CPU utilization searching for VTAM 00123900
.*                      operator messages.                              00124000
.*                                                                      00124100
.*                      Use FORMAT=OLD if the goal is to transport an   00124200
.*                      up-level USS table to a pre-V3R2 VTAM.          00124300
.*                                                                      00124400
.*                      FORMAT=DYNAMIC is equivalent to specifying      00124500
.*                      FORMAT=V3R2.                                    00124600
.*                                                                      00124700
.*                                                                      00124800
.*       DATEFRM       Specifies the format of the date.  Formats are:  00124900
.*                          MDY                                         00125000
.*                          DMY                                         00125100
.*                          YMD                                         00125200
.*                                                                      00125300
.*       DATEDLM        Specifies the delimeter between the month, day  00125400
.*                      and year in the date.  Can be any single        00125500
.*                      character.  Slash ('/') is the default.         00125600
.*                                                                      00125700
.*       TIMEDLM        Specifies the delimeter between the hours,      00125800
.*                      minutes, and seconds in the time.  Can be any   00125900
.*                      single character.  Colon (':') is the defualt.  00126000
.*                                                                      00126100
.*                                                                      00126200
.*    CODING EXAMPLES:                                                  00126300
.*                                                                      00126400
.*       (1) Code the beginning of a USS definition named "MYTABLE"     00126500
.*           and with a translation to be used located at "TRANS".      00126600
.*           Use ':' as the time delimeter and '-' as the date          00126700
.*           delimeter.  Put the date in MMDDYY form.  This gives       00126800
.*           the following date and time:                               00126900
.*                                                                      00127000
.*                    HH:MM:SS     MM-DD-YY                             00127100
.*                                                                      00127200
.*              MYTABLE  USSTAB  TABLE=TRANS  DATEFRM=MMDDYY            00127300
.*                               TIMEDLM=':' DATEDLM='-'                00127400
.*                                                                      00128000
.*    RESTRICTIONS / DEPENDENCIES:                                      00128700
.*                                                                      00129400
.*          *********** IMPORTANT NOTE ***************                  00129500
.*                                                                      00129600
.*           FORMAT keyword of USSTAB should not be changed for         00129700
.*           any new release after V3R2.  The V3R2 option represents    00129800
.*           Message processing in V3R2 and all following releases,     00129900
.*           unless noted.  OLD signifies Message processing in         00130000
.*           releases previous to V3R2.                                 00130100
.*                                                                      00130200
.*    OUTPUT: The beginning of a USS definition table is declared.      00130300
.*                                                                      00130800
.*    NOTES: If no translation table is specified, VTAM uses the        00131500
.*           translation table associated with the IBM supplied table,  00132200
.*           or its user written replacement.  If neither of these has  00132900
.*           a translation table, then no translation is performed.     00133600
.*                                                                      00134300
.*                                                                      00135000
.*  COMMENT: The following shows the format of the FORMAT=DYNAMIC       00135500
.*           USS table prefix and header generated                      00136000
.*           by the USSTAB macro.                                       00136500
.*                                                                      00137100
.*               ------------------------------------------             00137800
.*               |           USS TABLE PREFIX             |             00138500
.*               |----------------------------------------|             00139200
.*               |       ADDR(TRANSLATE TABLE) OR 0       |             00139900
.*               |----------------------------------------|             00140600
.*               |    ADDR(1ST COMMAND DEFINITION) OR 0   |             00141300
.*               |----------------------------------------|             00142000
.*               | ADDR(1ST VTAM MESSAGE DEFINITION) OR 0 |             00142700
.*               |----------------------------------------|             00143400
.*               |  ADDR(1ST USS MESSAGE DEFINITION) OR 0 |             00144100
.*               ------------------------------------------             00144800
.*                                                                      00145500
.*                                                                      00145600
.*  COMMENT: The following shows the format of the FORMAT=OLD           00145700
.*           USS Table header generated by the USSTAB macro.            00145800
.*           There is no prefix.                                        00145900
.*                                                                      00146000
.*               ------------------------------------------             00146100
.*               |       ADDR(TRANSLATE TABLE) OR 0       |             00146200
.*               |----------------------------------------|             00146300
.*               |    ADDR(1ST COMMAND DEFINITION) OR 0   |             00146400
.*               |----------------------------------------|             00146500
.*               | ADDR(1ST MESSAGE DEFINITION) OR 0      |             00146600
.*               |  (this could be a VTAM or USS message) |             00146700
.*               ------------------------------------------             00146800
.*                                                                      00146900
.*  CHANGE ACTIVITY:                                                    00147000
.*                                                                      00147100
.*     08/07/86  COMPLETE REWRITE OF MACRO.                  @R506400   00147600
.*     04/19/88  Added copyright information.                @P055566   00147900
.*                                                                      00148300
.*  $MAC(USSTAB),COMP(USER),PROD(VTAM): Define USS table                00148400
.*                                                                      00148500
.*  FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS               00148600
.*  ---- -------- ------- ------ ------  ---------------------------    00148900
.*  $R1= R0@USCLD HVT3202 871214 151508: USS Table Update               00149200
.*  $02= OY19144  HVT3202 871221 391074: ALLOW BUFFER=/FORMAT=V3R2      00149300
.*  $Q1= P070078  HVT3303 890617 087112: FORMAT=DYNAMIC                 00149400
.*  $03= DY38477  HVT3303 890727 013298: CORRECT ASSEMBLY ERRORS WITH   00149500
.*                                       BUFFER=                        00149600
.*  $D1= HB3@@CLD VM330   880601 161033: V3R3 LEN BOUNDRY FUNCTION      00149700
.*  $B1= ACC@@AMS HVT3401 890414 010056: V4 MVS/ESA PLAS Conversion     00155900
.*  $A1= T1@@@F01 HVT3401 890907 981735: LAN Channel Station support    00162100
.*  $N1= P077151  HVT3401 891208 577393: Changed version in copyright   00168300
.*  $N2= P078129  HVT3401 900116 699466: Changed T1@@@CLD to T1@@@F01   00174500
.*  $B2= A3@@@CLD HVT3401 900516 385097: National Language Support      00177600
.*  $X1= P085404  HVT3431 901003 991843: V3R4.1 MVS/ESA Release Startup 00178300
.*  $V1= P089486  HVT3401 901023 182401: Remove USS 15 message          00179100
.*  $04= DY41327  VSE3400 920130 787924: DO NOT ALLOW MULTIPLE USSTAB   00179600
.*  $KF= SAM@@CLD HVT4201 901003 379307: APPN CONNECTION MANAGEMENT     00179800
.*                                       MACROS                         00180100
.*  $KG= P099029  HVT4100 911101 553624: NLS support for USSMSG         00182300
.*                                       @@@@DATE and @@@@TIME          00184500
.*  $U1= P106973  EMERALD 931005 699466: Remove ISTGLBAL reference      00185700
.*  $H1= VS@@@MST VSE/ESA 930513 544872: VSE/ESA CONVERSION             00186000
.*  $21= OW07973  EMERALD 941115 410106: Indicate V4R2 in prefix        00186300
.*                                                                      00186900
.*********************************************************************  00193100
            GBLA   &TMSG             CURRENT USSMSG INDEX          @R1A 00200000
            GBLA   &NMSG             NEXT USSMSG INDEX             @R1A 00250000
            GBLA   &TCMD             CURRENT USSCMD INDEX               00300000
            GBLA   &NCMD             NEXT USSCMD INDEX                  00350000
            GBLA   &TPARM            CURRENT USSPARM INDEX              00400000
            GBLA   &NPARM            NEXT USSPARM INDEX                 00450000
            GBLA   &I                USSMG ARRAY INDEX             @03A 00475000
            GBLA   &FMT              CURRENT USS VERSION           @R1A 00500000
            GBLB   &Z                1=USSTAB WAS PROCESSED             00550000
            GBLC   &USSMG(15)        ONE ENTRY/USS MESSAGE (0-14)  @V1C 00575000
            GBLB   &YESDTP           1= DATEFRM WAS CODED          @KGA 00575600
            GBLB   &YESDDLM          1= DATEDLM WAS CODED          @KGA 00576200
            GBLB   &YESTDLM          1= TIMEDLM WAS CODED          @KGA 00576800
            GBLB   &DATTPB0          BIT ONE FOR DATE TYPE         @KGA 00577400
            GBLB   &DATTPB1          BIT TWO FOR DATE TYPE         @KGA 00578000
            GBLC   &VALDDLM          DATE DELIMETER VALUE          @KGA 00578600
            GBLC   &VALTDLM          TIME DELIMETER VALUE          @KGA 00579200
            AIF    (&Z NE 1).BEGIN   USSTAB ALREADY SPECIFIED      @04A 00580000
            MNOTE 8,'MULTIPLE USSTAB MACROS SPECIFIED'             @04A 00585000
            AGO    .MMEND            EXIT                          @04A 00590000
.BEGIN      ANOP                                                   @04A 00595000
&TMSG       SETA   0                 CURRENT MESSAGE               @R1A 00600000
&NMSG       SETA   1                 NEXT MESSAGE                  @R1A 00650000
&TCMD       SETA   0                 1ST USSCMD EXPANSION INDEX         00700000
&NCMD       SETA   1                 2ND USSCMD EXPANSION INDEX         00750000
&TPARM      SETA   0                 1ST USSPARM EXPANSION INDEX        00800000
&NPARM      SETA   1                 2ND USSPARM EXPANSION INDEX        00850000
&Z          SETB   1                 1=USSTAB WAS PROCESSED             00900000
            AIF    ('&NAME' EQ '').NONAME    NO CSECT NAME              04000000
&NAME       CSECT  ,                                                    04050000
.NONAME     ANOP                                                        04100000
&I          SETA   1                   SET ARRAY INDEX             @03A 04103300
.LPINIT     ANOP                       LOOP TO CLEAR ARRAY         @03A 04103400
            AIF    (&I GT 15).LPEND                           @03A @V1C 04103500
&USSMG(&I)  SETC   '0'                 SET ENTRY TO NULLS          @03A 04103600
&I          SETA   &I+1                INCREMENT TO NEXT ENTRY     @03A 04103700
            AGO    .LPINIT                                         @03A 04103800
.LPEND      ANOP                                                   @03A 04103900
            AIF    ('&DATEFRM' EQ '').CHKDDLM   IS DATEFRM  CODED  @KGA 04104000
&YESDTP     SETB   1                 YES, DATEFRM  IS CODED        @KGA 04104100
            AIF    ('&DATEFRM' EQ 'MDY').MDY       CHECK FOR       @KGA 04104200
            AIF    ('&DATEFRM' EQ 'DMY').DMY         WHICH FORMAT  @KGA 04104300
            AIF    ('&DATEFRM' EQ 'YMD').YMD         WAS CODED     @KGA 04104400
            MNOTE 8,'INVALID DATEFRM, USE MDY, DMY, OR YMD'             04104500
            AGO    .MMEND            NO CORRECT DATEFRM  CODED     @KGA 04104600
.MDY        ANOP                     DATEFRM  IS MDY               @KGA 04104700
&DATTPB0    SETB   0                 SET DATEFRM  BITS TO 00       @KGA 04104800
&DATTPB1    SETB   0                                               @KGA 04104900
            AGO    .CHKDDLM                                        @KGA 04105000
.DMY        ANOP                     DATEFRM  IS DMY               @KGA 04105100
&DATTPB0    SETB   0                 SET DATEFRM  BITS TO 01       @KGA 04105200
&DATTPB1    SETB   1                                               @KGA 04105300
            AGO    .CHKDDLM                                        @KGA 04105400
.YMD        ANOP                     DATEFRM  IS YMD               @KGA 04105500
&DATTPB0    SETB   1                 SET DATEFRM  BITS TO 10       @KGA 04105600
&DATTPB1    SETB   0                                               @KGA 04105700
.CHKDDLM    ANOP                     CHECK DATE DELIMITER          @KGA 04105800
&VALDDLM    SETC   '/'               ASSUME DEFUALT FOR DATEDLM    @KGA 04105900
            AIF    ('&DATEDLM' EQ '').CHKTDLM IS DATEDLIM CODED?   @KGA 04106000
&YESDDLM    SETB   1                 YES, DATE DILIMITER IS CODED  @KGA 04106100
            AIF    (K'&DATEDLM EQ 3).DDLIMOK  LENGTH OF DDLIM 1?   @KGA 04106200
            MNOTE 8,'INVALID DATE DELIMITER, USE SINGLE CHARACTER' @KGA 04106300
            AGO    .MMEND            NO CORRECT DATEDLM CODED      @KGA 04106400
.DDLIMOK    ANOP                     HERE IF DDLIM IS SINGLE CHAR       04106500
&VALDDLM    SETC   '&DATEDLM'(2,1)   SET DDLIM VALUE TO INPUT VAL  @KGA 04106600
.CHKTDLM    ANOP                     CHECK TIME DELIMITER          @KGA 04106700
&VALTDLM    SETC   ':'               ASSUME DEFAULT FOR TIMEDLM    @KGA 04106800
            AIF    ('&TIMEDLM' EQ '').CHKFRM  IS TIMEDLM CODED?    @KGA 04106900
&YESTDLM    SETB   1                 YES, TIME DLIM IS CODE        @KGA 04107000
            AIF    (K'&TIMEDLM EQ 3).TDLIMOK                       @KGA 04107100
            MNOTE 8,'INVALID TIME DELIMITER, USE SINGLE CHARACTER' @KGA 04107200
            AGO    .MMEND            NO CORRECT TIMEDLM CODED      @KGA 04107300
.TDLIMOK    ANOP                     TIME DELIMITER IS OK          @KGA 04107400
&VALTDLM    SETC   '&TIMEDLM'(2,1)   SET TDLIM TO INPUT VALUE      @KGA 04107500
.CHKFRM     AIF    ('&FORMAT' NE '').CHKVER   IS FORMAT CODED @R1A @KGC 04107600
&FMT        SETA   0                 USE FORMAT=OLD DEFAULT        @R1A 04108200
            AGO    .OLDFMT           GENERATE TABLE WITHOUT PREFIX @R1A 04112300
.CHKVER     ANOP                                                   @R1A 04116400
&FMT        SETA   0                 ASSUME DEFAULT FORMAT=OLD     @R1A 04120500
            AIF    ('&FORMAT' EQ 'OLD').OLDFMT                     @R1A 04124600
&FMT        SETA   1                 ASSUME FORMAT=DYNAMIC         @R1A 04127800
            AIF    ('&FORMAT' EQ 'V3R2').FMTDYNA    FORMAT=V3R2    @R1A 04131000
            AIF    ('&FORMAT' EQ 'DYNAMIC').FMTDYNA FORMAT=DYNAMIC @Q1A 04134200
            MNOTE 8,'INVALID KEYWORD, USE FORMAT=DYNAMIC OR FORMAT=OLD' 04137400
            AGO    .MMEND                                          @R1A 04141000
.FMTDYNA    ANOP                                                   @R1A 04145100
**********************************************************************  04150000
*           PREFIX TO FORMAT=DYNAMIC USS TABLE                       *  04200000
**********************************************************************  04250000
            DC     X'BD'               CONTROL BLOCK PREFIX             04300000
            DC     X'0420'             V4R2 COPY OF TABLE     @KFC @21C 04350000
            DC     X'00'               PAD PREFIX TO WORD BOUNDARY      04400000
**********************************************************************  04450000
*           OFFSETS TO HEADER FOR FORMAT=DYNAMIC USS TABLE           *  04500000
**********************************************************************  04550000
            DC     A(&TABLE)           TRANSLATION TABLE                04570400
            DC     A(ISTC1)            FIRST USSCMD BLOCK               04590800
            DC     A(ISTM0001)         INDEX TABLE FOR VTAM MESSAGES    04611200
            DC     A(ISTUSS)           FIRST USS MESSAGE                04631600
            DC     B'&YESDTP.&YESDDLM.&YESTDLM.000&DATTPB0.&DATTPB1'    04652000
            DC     C'&VALDDLM'                                     @KGA 04692800
            DC     C'&VALTDLM'                                     @KGA 04713200
            DC     X'00'               NOT USED -- AVAILABLE       @KGA 04733600
            AGO    .MMEND                                          @R1A 04775000
**********************************************************************  04800000
*                                                                    *  04825000
**********************************************************************  04850000
*           OFFSETS TO HEADER FOR FORMAT=OLD USS TABLE               *  04855500
**********************************************************************  04861000
.OLDFMT     ANOP                                                   @R1A 04866500
            DC     A(&TABLE)            TRANSLATION TABLE          @R1A 04872000
            DC     A(ISTC1)             FIRST USSCMD BLOCK         @R1A 04877500
            DC     A(ISTM&NMSG)         FIRST USSMSG BLOCK         @R1A 04883000
**********************************************************************  04888500
.MMEND      ANOP                                                   @R1A 04894000
            MEND                                                        04900000
