         MACRO                                                          00050000
&NAME    COS     &VR=,&SUBSTUT=                              @J1C @X1C  00100000
.************** START OF SPECIFICATIONS ******************************* 00150000
.*  MACRO NAME: COS                                                   * 00200000
.*                                                                    * 00250000
.*  FUNCTION: TO PROVIDE ENTRIES FOR THE CLASS OF SERVICE TABLE.      * 00300000
.*                                                                    * 00350000
.*  STATUS: ACF/VTAM VERSION 3 RELEASE 4.1                            * 00366600
.*                                                                    * 00383200
.*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM                   * 00400000
.*                                                                    * 00550000
.*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"    * 00562500
.*                                                                    * 00575000
.*             5685-085 (C) COPYRIGHT IBM CORP. 1984, 1992.           * 00587500
.*             ALL RIGHTS RESERVED.                                   * 00600000
.*                                                                    * 00612500
.*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -              * 00625000
.*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY           * 00637500
.*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.               * 00650000
.*                                                                    * 00662500
.*             SEE COPYRIGHT INSTRUCTIONS.                            * 00675000
.*                                                                    * 00687500
.*  REGISTER USAGE: NONE.                                             * 00700000
.*                                                                    * 00750000
.*  PARAMETERS:                                                       * 00800000
.*                                                                    * 00850000
.*   KEYS:                                                            * 00900000
.*         VR=(X,Y)   REQUIRED.                                       * 00950000
.*                    THESE ARE THE VALUES OF THE VIRTUAL ROUTE       * 01000000
.*                    AND TRANSMISSION PRIORITY NUMBERS.              * 01050000
.*                    THEY ARE ALWAYS PASSED IN PAIRS AND             * 01100000
.*                    A MAXIMUM OF 24 PAIRS IS ALLOWED.               * 01150000
.*                    IF AN ODD NUMBER OF PAIRS IS PASSED,            * 01200000
.*                    A DUMMY PAIR OF FFFF IS ADDED TO PAD            * 01250000
.*                    OUT THE ELEMENT TO A FULL-WORD BOUNDRY.         * 01300000
.*         SUBSTUT    OPTIONAL (ONLY ALLOWED FOR 1 COS ENTRY IN A @X1C* 01312500
.*                    COS TABLE). INDICATES THAT THIS COS ENTRY       * 01325000
.*                    IS THE SUBSTITUTE COS ENTRY.                    * 01337500
.*                                                                    * 01350000
.*  MACRO NOTES: A UNIQUE LABEL IS REQUIRED ON EACH INVOCATION        * 01400000
.*                    OF THE COS MACRO. IT MAY BE OMITTED ONCE.       * 01450000
.*                    IF SPECIFIED, THIS IS THE NAME OF THE           * 01500000
.*                    CLASS OF SERVICE. OTHERWISE,                    * 01550000
.*                    BLANKS WILL INDICATE THE DEFAULT CLASS.         * 01600000
.*                                                                    * 01650000
.*  DATA-AREA: THE FOLLOWING GLOBAL MACRO VARIABLES ARE USED:         * 01700000
.*             &ERRCK   = INDICATES IF ERROR CONDITION ENCOUNTERED.   * 01750000
.*             &ENTRYCT = COUNTS THE NUMBER OF ENTRIES CREATED.       * 01800000
.*             &BLANK   = ENSURES BLANK LABEL PASSED ONLY ONCE.       * 01850000
.*             &ERRSUBS = INDICATES IF MULTIPLE SUBSTUT=YES CODED.    * 01875000
.*                                                                    * 01900000
.*  CONTROL BLOCKS: NONE.                                             * 01950000
.*                                                                    * 02000000
.*  INNER MACROS:   NONE.                                             * 02050000
.*                                                                    * 02100000
.*  CREATION DATE: 02/16/79                                  @G98BKMC * 02150000
.*                                                                    * 02200000
.*  CHANGE ACTIVITY:                                         @G98BKMC * 02210000
.*                                                                    * 02220000
.*     04/19/88  ADDED COPYRIGHT INFORMATION                  @P055566* 02230000
.*                                                                    * 02240000
.*  $MAC(COS),COMP(USER),PROD(VTAM): DEFINE COS ENTRY WITH COS TABLE  * 02242100
.*                                                                    * 02244200
.*  FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS             * 02245600
.*  ---- -------- ------- ------ ------  ----------------             * 02247000
.*  $BB= ACC@@AMS HVT3401 891025 615520: PL/AS CONVERSION             * 02248400
.*  $J1= JN@@@CLD NQNAME1 910521 450526: ENHANCED NQN SUPPORT         * 02248800
.*  $X1= P102498  VM341   920625 590029: XF ASSMBLER CAN'T DEAL WITH  * 02249200
.*                                       &SUBSTUTE BECAUSE OF LENGTH  * 02249600
.************ END OF SPECIFICATIONS *********************************** 02250000
.********************************************************************** 02300000
.*       DECLARE LOCALS AND GLOBALS                                     02350000
.********************************************************************** 02400000
         GBLB  &ERRCK              INDICATES ERROR CONDITION            02450000
         GBLB  &ERRSUBS            MULTIPLE SUBSTUT=YES CODED @J1A @X1C 02475000
         GBLA  &ENTRYCT            COUNT OF ENTRIES                     02500000
         GBLA  &BLANK              SET TO ONE WHEN BLANK LABEL PASSED   02550000
         LCLB  &SUBVAL             VALUE FOR SUBSTUT= KEYWORD @J1A @X1C 02575000
         LCLC  &WORK               TO WORK WITH COSNAME PADDING         02600000
         LCLC  &FLAG               SET ON TO IND VALID ENTRY            02650000
         LCLC  &PAIRS              TO WORK WITH PAIR VALUES             02700000
         LCLC  &CHAR1,&CHAR2       TO HOLD PAIRS                        02750000
         LCLA  &LEN                NUMBER CHARS IN VR PARAMETER         02800000
         LCLA  &PAIRLEN            CURRENT LENGTH OF PAIRS              02850000
         LCLA  &I                  INDEX FOR COSNAME AND VR PARM        02900000
         LCLA  &J                  INDICATES WHETHER TO PAD WITH FF     02950000
         LCLA  &K                  TO COMPUTE LENGTH OF THIS ENTRY      03000000
.********************************************************************** 03050000
.*       IF COSTAB WAS NOT PREVIOUSLY INVOKED, INDICATE ERROR           03100000
.********************************************************************** 03150000
         AIF   ('&ERRCK' EQ '1').ENTHEAD  THIS IS A VALID MACRO CALL    03200000
         MNOTE 8,'COSTAB MACRO WAS NOT ISSUED COS IS VOID'              03250000
         AGO .BOMB                                                      03300000
.********************************************************************** 03350000
.*       IF A LABEL WAS PASSED, GENERATE CL8 FOR ITS VALUE              03400000
.*          OTHERWISE, GENERATE A CL8 OF BLANKS,                        03450000
.*          ISSUING AN ERROR MESSAGE IF A DUPLICATE IS ENCOUNTERED.     03500000
.********************************************************************** 03550000
.ENTHEAD ANOP                                                           03600000
&PAIRLEN SETA  0                                                        03650000
         AIF   ('&NAME' NE '').HERE                                     03700000
&WORK    SETC  '        '                                               03750000
         AIF   (&BLANK EQ 1).DUPE                                       03800000
&BLANK   SETA  1                                                        03850000
         AGO   .ENTER                                                   03900000
.DUPE    ANOP                                                           03950000
         MNOTE 4,'BLANK LABEL PREVIOUSLY PASSED - NO ENTRY GENERATED'   04000000
         AGO   .BOMB                                                    04050000
.HERE    ANOP                                                           04100000
         AIF   (K'&NAME LE 8).HEREE                                     04150000
         MNOTE 4,'LABEL ON MACRO IS TOO LONG - NO ENTRY GENERATED'      04200000
         AGO   .BOMB                                                    04250000
.HEREE   ANOP                                                           04300000
&WORK    SETC  '&NAME'.'       '                                        04350000
&WORK    SETC  '&WORK'(1,8)                                             04400000
.********************************************************************** 04450000
.*       GENERATE CODE FOR THE VR KEYWORD PAIRS                         04500000
.********************************************************************** 04550000
.ENTER   ANOP                                                           04600000
         AIF   ('&VR' NE '').VROK                                       04650000
         MNOTE 8,'REQUIRED KEYWORD -VR- NOT SPECIFIED'                  04700000
         AGO   .BOMB                                                    04750000
.VROK    ANOP                                                           04800000
         AIF   (N'&SYSLIST EQ 0).VROK2                                  04850000
         MNOTE 8,'OPERAND INCORRECTLY SPECIFIED - NO ENTRY GENERATED'   04900000
         AGO   .BOMB                                                    04950000
.********************************************************************** 04962500
.*       PROCESS THE SUBSTUT KEYWORD                          @J1A @X1C 04975000
.********************************************************************** 04987500
.VROK2   ANOP                                                           05000000
&SUBVAL  SETB  0                               DEFAULT VALUE       @J1A 05002700
         AIF   ('&SUBSTUT' EQ '').SUBOK        SUBSTUT NOT CODED   @X1C 05005400
         AIF   ('&SUBSTUT' EQ 'NO').SUBOK      SUBSTUT=NO CODED    @X1C 05008100
         AIF   ('&SUBSTUT' EQ 'YES').DOSUB     SUBSTUT=YES CODED   @X1C 05010800
         MNOTE 8,'SUBSTUT INCORRECTLY SPECIFIED - NO ENTRY GENERATED'   05013500
         AGO   .BOMB                                          @J1A @X1C 05016200
.DOSUB   ANOP                                  PROCESS SUBSTUT     @J1A 05018900
         AIF   ('&ERRSUBS' NE '1').MKSUB       DUPLICATE SUBSTUT   @J1A 05021600
         MNOTE 4,'DUPLICATE SUBSTUT=YES IN TABLE - NO ENTRY GENERATED'  05024300
         AGO   .SUBOK                                         @J1A @X1C 05027000
.MKSUB   ANOP                                  VALID SUBSTUT=YES   @J1C 05029700
&SUBVAL  SETB  1                               INDICATE YES CODED  @J1A 05032400
&ERRSUBS SETB  1                               INDICATE 1ST TIME   @J1A 05035100
.SUBOK   ANOP                                  SUBSTUT NOT GIVEN   @J1A 05037800
.*                                                                      05040500
.*                                                                      05043200
.*                                                                      05045900
&I       SETA  1                               INITIALIZE INDEX POINTER 05050000
&LEN     SETA  K'&VR                           VALUE LENGTH             05100000
         AIF   (('&VR'(1,2) NE '((')).LOOPC                             05150000
         AIF   (('&VR'(&LEN-1,2) NE '))')).SYNERR                       05200000
&LEN     SETA  &LEN-1                                                   05250000
.LOOPB   ANOP                                                           05300000
&I       SETA  &I+1                                                     05350000
.LOOPC   ANOP                                                           05400000
         AIF   (('&VR'(&I,1) NE '(')).SYNERR                            05450000
         AIF   (('&VR'(&I+2,1) NE ',')).SYNERR                          05500000
         AIF   (('&VR'(&I+4,1) NE ')')).SYNERR                          05550000
&PAIRS   SETC  '&PAIRS'.'&VR'(&I+1,1)                                   05600000
&PAIRS   SETC  '&PAIRS'.'&VR'(&I+3,1)                                   05650000
&PAIRLEN SETA  &PAIRLEN+2                                               05700000
&I       SETA  &I+4                                                     05750000
         AIF   (&I EQ &LEN).PROC                                        05800000
&I       SETA  &I+1                                                     05850000
         AIF   (('&VR'(&I,1) NE ',')).SYNERR                            05900000
         AGO   .LOOPB                                                   05950000
.SYNERR  ANOP                                                           06000000
         MNOTE 8,'SYNTAX ERROR IN SPECIFICATION OF VR PAIRS'            06050000
         AGO   .FINIS                                                   06100000
.PROC    ANOP                                                           06150000
&LEN     SETA  &PAIRLEN                        VALUE LENGTH             06200000
&I       SETA  1                                                        06250000
.PROCA   ANOP                                                           06300000
&CHAR1   SETC  '&PAIRS'(&I,1)                  GET FIRST DIGIT OF PAIR  06350000
&CHAR2   SETC  '&PAIRS'(&I+1,1)                GET SECOND DIGIT OF PAIR 06400000
         AIF   (('&CHAR1' GE '0') AND                                  *06450000
               ('&CHAR1' LE '7')).OK1          FIRST DIGIT VALID ?      06500000
         MNOTE 4,'VR PAIR &CHAR1,&CHAR2 IGNORED - ROUTE IS INVALID'     06550000
         AIF   (('&CHAR2' GE '0') AND                                  *06600000
               ('&CHAR2' LE '2')).OK2          SECOND DIGIT VALID ?     06650000
         MNOTE 4,'VR PAIR &CHAR1,&CHAR2 IGNORED - PRIORITY IS INVALID'  06700000
.OK2     ANOP                                                           06750000
         AGO   .CHKLEN                                                  06800000
.OK1     ANOP                                                           06850000
         AIF   (('&CHAR2' GE '0') AND                                  *06900000
               ('&CHAR2' LE '2')).OK3          SECOND DIGIT VALID ?     06950000
         MNOTE 4,'VR PAIR &CHAR1,&CHAR2 IGNORED - PRIORITY IS INVALID'  07000000
         AGO   .CHKLEN                                                  07050000
.OK3     ANOP                                                           07100000
         AIF   (&K NE 24).OK4                                           07150000
         MNOTE 4,'TOO MANY VR PAIRS - TRUNCATED AT 24'                  07200000
         AGO   .FINIS                                                   07250000
.OK4     ANOP                                                           07300000
&K       SETA  &K+1                            BUMP ENTRY COUNT         07350000
         AIF   ('&FLAG' EQ 'ON').BYPASS                                 07400000
&FLAG    SETC  'ON'                                                     07450000
&ENTRYCT SETA  &ENTRYCT+1                      BUMP ENTRY COUNT         07500000
&WORK    DC    XL2'0'                          LENGTH OF THIS ENTRY     07550000
         DC    B'&SUBVAL.0000000'              FLAGS              @J1A  07583300
         DC    XL1'0'                          RESERVED           @J1C  07616600
         DC   CL8'&WORK'                       CLASS OF SERVICE NAME    07650000
.BYPASS  ANOP                                                           07700000
         DC    XL1'&CHAR1'                     VR NUMBER                07750000
         DC    XL1'&CHAR2'                     TRANSMISSION PRIORITY    07800000
         AIF   (&J EQ 0).SET1                  TEST EVEN PAIR CNTR      07850000
&J       SETA  0                               FLIP TO 0                07900000
         AGO .CHKLEN                           GO TO GET NEXT PAIR      07950000
.SET1    ANOP                                                           08000000
&J       SETA  1                               FLIP TO 1                08050000
.CHKLEN  ANOP                                                           08100000
         AIF   (&I+1 EQ &LEN).CHKPAD           END OF VR PARAMETERS ?   08150000
&I       SETA  &I+2                            NO                       08200000
         AGO .PROCA                            GO TO GET NEXT PAIR      08250000
.CHKPAD  AIF   (&J EQ 0).FINIS                 EVEN PAIR - NO PAD       08300000
&K       SETA  &K+1                            BUMP ENTRY COUNTER       08350000
         DC    XL2'FFFF'                       PAD TO FULL WORD         08400000
.FINIS   ANOP                                                           08450000
         AIF   ('&FLAG' EQ 'ON').ENTOK                                  08500000
         MNOTE 4,'NO VALID VR PAIRS - NO ENTRY GENERATED'               08550000
         AGO   .BOMB                                                    08600000
.ENTOK   ANOP                                                           08650000
&K       SETA  &K*2+12                         COMPUTE ENTRY LENGTH     08700000
         ORG   *-&K                                                     08750000
         DC    AL2(&K)                         STORE ENTRY LENGTH       08800000
         ORG                                                            08850000
.BOMB    ANOP                                                           08900000
         MEND                                                           08950000
