            MACRO                                                       00050000
&NAME       USSEND                                                      00100000
.********************* START OF SPECIFICATIONS *********************    00150000
.*                                                                      00200000
.* MACRO NAME(S): USSEND                                                00250000
.*                                                                      00300000
.* DESCRIPTIVE NAME: Terminate USS definition table macro.              00350000
.*                                                                      00400000
.* STATUS: ACF/VTAM VERSION 4 RELEASE 2                                 00450000
.*                                                                      00500000
.* COPYRIGHT:  LICENSED MATERIALS - PROPERTY OF IBM                     00550000
.*                                                                      00600000
.*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"      00616600
.*                                                                      00633200
.*             5686-065 (C) COPYRIGHT IBM CORP. 1984, 1994.             00634800
.*             ALL RIGHTS RESERVED.                                     00636400
.*                                                                      00638000
.*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -                00639600
.*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY             00641200
.*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.                 00642800
.*                                                                      00644400
.*             SEE COPYRIGHT INSTRUCTIONS.                              00646000
.*                                                                      00647600
.* MACRO USER'S GUIDE:                                                  00650000
.*                                                                      00700000
.*    FUNCTION: This macro declares the end of a USS table.  The entire 00750000
.*              table is aligned on a full word boundary.               00800000
.*                                                                      00850000
.*    MACRO FORMAT:                                                     00900000
.*                                                                      00950000
.*       <label> USSEND                                                 01000000
.*                                                                      01050000
.*    DESCRIPTION OF OPERANDS:                                          01100000
.*                                                                      01150000
.*    RESTRICTIONS / DEPENDENCIES: NONE                                 01200000
.*                                                                      01250000
.*       label          Optional label associated with the macro        01300000
.*                      invocation statement.                           01350000
.*                                                                      01400000
.*    OUTPUT: Declares the end of the USS table.                        01450000
.*                                                                      01500000
.*    NOTES: The following picture shows how messages are mapped        01537500
.*           by the USSTAB, USSCMD, USSMSG, and USSEND macros,          01575000
.*           if FORMAT=DYNAMIC was coded on USSTAB.                     01612500
.*                                                                      01650000
.*          ------------------------------------------                  01700000
.*          |           USS TABLE PREFIX             |                  01750000
.*          |----------------------------------------|                  01800000
.*          |       ADDR(TRANSLATE TABLE) OR 0       |                  01850000
.*          |----------------------------------------|                  01900000
.*          |    ADDR(1ST USSCMD DEFINITION) OR 0    |                  01950000
.*          |----------------------------------------|                  02000000
.*  --------- ADDR(1ST VTAM MESSAGE DEFINITION) OR 0 |                  02050000
.*  |       |----------------------------------------|                  02100000
.*  |   ------ ADDR(1ST USS MESSAGE DEFINITION) OR 0 |                  02150000
.*  |   |   ------------------------------------------                  02200000
.*  |   |                                                               02250000
.*  |   |                                                               02300000
.*  |   --> --------------------                                        02350000
.*  |       | ADDR(USS0) OR 0  |                                        02400000
.*  |       |------------------|                                        02450000
.*  |       | ADDR(USS1) OR 0  |                                        02500000
.*  |       |------------------|                                        02550000
.*  |       |         .        |                                        02600000
.*  |       |         .        |                                        02650000
.*  |       |------------------|                                        02700000
.*  |       | ADDR(USS14) OR 0 |                                        02750000
.*  |       --------------------                                        02800000
.*  |                                                                   02850000
.*  |       -------------------                                         02855500
.*  |------>| 4-DIGIT FLAG    |    THIS TABLE IS ONLY CREATED IF        02861000
.*          |-----------------|    A 4-DIGIT MESSAGE EXISTS             02866500
.*  |-------| OFFSET 000-999  |                                         02872000
.*  |       |---------------- |                                         02877500
.*  |       | OFFSET 1000-1999|                                         02883000
.*  |       |                 |                                         02888500
.*  |       -------------------                                         02894000
.*  |                                                                   02900000
.*  |                                                                   02916600
.*  |                                                                   02933200
.*  -> ------------------    ------------------    ----------------     02950000
.*     | OFFSET TO TABLE|--->| OFFSET TO TABLE|--->| ADDR(MSG000) |     03000000
.*     |   FOR 000-099  |    |   FOR 000-009  |    |     OR 0     |     03050000
.*     |     OR  0      |    |     OR 0       |    |--------------|     03100000
.*     |----------------|    |----------------|    | ADDR(MSG001) |     03150000
.*     | OFFSET TO TABLE|    | OFFSET TO TABLE|    |     OR 0     |     03200000
.*     |   FOR 100-199  |    |   FOR 010-019  |    |--------------|     03250000
.*     |     OR  0      |    |     OR 0       |    |       .      |     03300000
.*     |----------------|    |----------------|    |       .      |     03350000
.*     |       .        |    |       .        |    |       .      |     03400000
.*     |       .        |    |       .        |    |--------------|     03450000
.*     |----------------|    |----------------|    | ADDR(MSG009) |     03500000
.*     | OFFSET TO TABLE|    | OFFSET TO TABLE|    |     OR 0     |     03550000
.*     |   FOR 900-999  |    |   FOR 090-099  |    ----------------     03600000
.*     |     OR  0      |    |     OR 0       |                         03650000
.*     ------------------    ------------------                         03700000
.*                                                                      03750000
.*                                                                      03751300
.*  COMMENT: The following shows the format of the USS message          03752600
.*           generated by the USSMSG macro, if FORMAT=OLD is coded on   03753900
.*           USSTAB.  There is no prefix.                               03755200
.*                                                                      03756500
.* ATCINCNO                                                             03757800
.* |                                                                    03759100
.* |                                                                    03760400
.* +----> +----------------------+                                      03761700
.* +------|    USS Table Header  |                                      03763000
.* |      +----------------------+                                      03764300
.* |                                                                    03765600
.* +----> +----------------------+                                      03766900
.*        |    Address of next   |                                      03768200
.*        |       Message        |---->                                 03769500
.*        +----------------------+                                      03770800
.* +------|   Address of Message |                                      03772100
.* |      |      ID Definition   |                                      03773400
.* |      |----------------------|                                      03774700
.* |      |   Address of Message |                                      03776000
.* |      |       Prototype      |----+                                 03777300
.* +----> |----------------------|    |                                 03778600
.*        | Length of Message ID |    |                                 03779900
.*        |----------------------|    |                                 03781200
.*        |      Message ID      |    |                                 03782500
.*        +----------------------+    |                                 03783800
.*                  .                 |                                 03785100
.*         Other message id maps      |                                 03786400
.*         for messages with same     |                                 03787700
.*        text (e.g. 457I = USS 0)    |                                 03789000
.*         Point to same prototype    |                                 03790300
.*                  .                 |                                 03791600
.*        +----------------------+    |                                 03792900
.*        |   Message Prototype  | <--+                                 03794200
.*        +----------------------+                                      03795500
.*                                                                      03796800
.* CHANGE ACTIVITY:                                                     03800000
.*                                                                      03850000
.*     08/07/86  Complete rewrite of macro.                  @R506400   03900000
.*     02/12/88  Corrected last digit and buffer problem.    @P053496   03925000
.*     04/19/88  Added copyright information                 @P055566   03937500
.*     04/26/88  Fixed problem with DOS assembly             @P054619   03943700
.*                                                                      03950000
.*                                                                      03970800
.* $MAC(USSEND),COMP(USER),PROD(VTAM): USS TABLE TERMINATOR             03991600
.*                                                                      04002000
.* FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS                04008700
.* ---- -------- ------- ------ ------  ---------------------------     04015400
.* $R1= R0@USCLD HVT3202 871214 151508: USS Table Update                04022100
.* $F1= F4M@@CLD HVT3303 890210 042852: Allow 4-digit messages          04028800
.* $Q1= P069469  HVT3303 890210 042852: Create table w/ 4-digits        04035500
.* $Q2= P070249  HVT3303 890616 042852: Table must be on word boundary  04042200
.* $Q3= P070078  HVT3303 890622 087112: FORMAT=DYNAMIC                  04048900
.* $01= DY38477  HVT3303 890727 013298: Correct Assembly errors with    04055600
.*                                      BUFFER=                         04062300
.* $Q4= P070149  HVT3303 890815 042852: BUFFER= code not deleted        04069000
.*                                      for DY38477                     04075700
.* $BB= ACC@@AMS HVT3401 891025 615520: PL/AS CONVERSION                04082400
.* $B1= A3@@@CLD HVT3401 900517 385097: National Language Support       04089100
.* $V1= P089486  HVT3401 901023 182401: Remove USS 15 message           04095800
.* $02= DY41327  VSE3400 920130 787924: Do not allow multiple USSTAB    04102500
.*                                      macros                          04109200
.* $U1= P111665  EMERALD 931105 699466: P106973 missed hit              04112800
.* $Z1= P111582  VSE/ESA 931109 544872: Change high order bit setting   04131400
.********************** END OF SPECIFICATIONS **********************    04150000
            GBLA  &FMT                           CURRENT VERSION   @R1A 04200000
            GBLA  &NMSG                          NEXT MESSAGE DEF  @R1A 04250000
            GBLA  &NCMD                          NEXT USSCMD  INDEX     04350000
            GBLA  &NPARM                         NEXT USSPARM INDEX     04400000
            GBLB  &YESUSS                        1=USS MESSAGES FOUND   04450000
            GBLB  &YESVTAM                       1=VTAM MSGS FOUND      04500000
            GBLB  &ONE(2000)                     1/VTAM MSG(000-1999)   04527700
.*                                                                 @F1A 04555400
            GBLC  &USSMG(15)                     1/USS MESSAGE (0-14)   04583300
.*                                                   @P053496 @B1C @V1C 04616600
            GBLB  &Z                             1=USSTAB CODED         04650000
            LCLA  &G                             TEMPORARY VAR     @F1A 04666600
            LCLA  &H                             TEMPORARY VAR     @F1A 04683200
            LCLA  &I                             TEMPORARY VARIABLE     04700000
            LCLA  &J                             TEMPORARY VARIABLE     04750000
            LCLA  &K                             TEMPORARY VARIABLE     04800000
            LCLA  &L                             TEMPORARY VARIABLE     04825000
            LCLA  &M                             TEMPORARY VARIABLE     04850000
            LCLA  &N                             TEMPORARY VARIABLE     04900000
            LCLA  &X                             TEMPORARY VARIABLE     04950000
            LCLA  &Y                             TEMPORARY VAR     @F1A 04980000
            LCLB  &THSND(2)                      THOUSANDS ARRAY   @F1A 05010000
            LCLB  &HDRED(20)                     HUNDREDS ARRAY    @F1C 05040000
            LCLB  &TENS(200)                     TENS ARRAY        @F1C 05070000
            LCLC  &TTD                                                  05100000
            LCLA  &COUNTER                                              05112500
&Z          SETB  0                             USSTAB BIT OFF     @02A 05118700
&COUNTER    SETA  9000                                                  05125000
            ACTR  &COUNTER                       ACTR COUNTER      @F1A 05137500
ISTC&NCMD   EQU   0                              END OF USSCMD CHAIN    05150000
ISTP&NPARM  EQU   0                              END OF USSPARM CHAIN   05200000
            AIF   (&FMT NE 0).DODYNA        USSTAB FORMAT=DYNAMIC  @Q3C 05206200
ISTM&NMSG   EQU 0                                                  @R1A 05212400
            AGO   .MMEND                                           @R1A 05218600
.********************************************************************** 05224800
.*       INITIAL  PROCESSING FOR FORMAT=DYNAMIC                       * 05231000
.********************************************************************** 05237200
.DODYNA     ANOP                                                   @Q3C 05243400
.********************************************************************   05250000
.* IF AT LEAST ONE USS MESSAGE HAS BEEN DEFINED, GENERATE A TABLE   *   05300000
.* WITH ONE ENTRY PER USS MESSAGE.                                  *   05350000
.*                                                                  *   05400000
.* OTHERWISE, INDICATE NO USS MESSAGES DEFINED.                     *   05450000
.********************************************************************   05500000
            AIF   (&YESUSS NE 0).DOUSS           PROCESS USS MESSAGES   05550000
ISTUSS      EQU   0                              NO USS MESSAGES        05600000
            AGO   .DOVTAM                        DO VTAM MESSAGES       05650000
.DOUSS      ANOP                                 DECLARE USS MESSAGES   05700000
            DS    0F                                                    05750000
ISTUSS      EQU   *                              DEFINE USS MESSAGES    05800000
&I          SETA  0                                                     05850000
.LPUSS      ANOP                                                        05900000
&I          SETA  &I+1                           LOOP FOR ALL MSGS      05950000
            AIF   (&I GT 15).DOVTAM                               @V1C  06000000
            AIF   ('&USSMG(&I)' EQ '0').GEN0     MESSAGE UNDEFINED      06033300
.*                                                                @01A  06066600
&M          SETA  &I-1                                                  06100000
            AIF   ('&USSMG(&I)' EQ ' ').NOBUF    NOT BUFFER   @P053496  06101000
.*                                                                @01A  06102000
            DC    AL4(&USSMG(&I))                            @01A @Z1C  06104100
            ORG   *-4                  Top of char string PTR     @Z1A  06106200
            DC    AL.1(1)              Set high order bit         @Z1A  06108300
            ORG   ,                    Continue                   @Z1A  06110400
            AGO   .LPUSS                         NEXT USS MSG @P053496  06112800
.NOBUF      ANOP                                              @P053496  06114400
            AIF   (&M GT 9).TWODIG                                      06116600
            DC    A(USS00&M)                     USS MESSAGE            06133200
            AGO   .LPUSS                                          @R1A  06149800
.TWODIG     ANOP                                                  @R1A  06166400
            DC    A(USS0&M)                      USS MSG          @R1A  06183000
            AGO   .LPUSS                         NEXT USS MSG     @R1A  06200000
.GEN0       ANOP                                                  @R1A  06250000
            DC    A(0)                           MSG UNDEFINED    @R1A  06300000
            AGO   .LPUSS                         NEXT USS MESSAGE @R1A  06350000
.********************************************************************   06400000
.* GENERATE THE VTAM MESSAGE TABLE, WHICH CONSISTS OF 3 LEVELS OF   *   06450000
.* INDEXING.  THE TABLE WILL NOT BE PRESENT IF NO VTAM MESSAGES     *   06500000
.* WERE ENCOUNTERED BY THE USSMSG MACRO (E.G. IN ISTINCDT).         *   06550000
.********************************************************************   06600000
.DOVTAM     ANOP                                                        06650000
            AIF   (&YESVTAM NE 0).GENVTAM        PROCESS VTAM MSGS      06700000
ISTM0001    EQU   0                              NO VTAM MESSAGES       06750000
            AGO   .MMEND                         ALL DONE               06800000
.********************************************************************   06850000
.* THE ADVANTAGE OF THIS APPROACH FOR DEFINING MESSAGES IS THAT     *   06900000
.* INDEXES AND INDEX TABLES ARE ONLY DEFINED FOR MESSAGES THAT      *   06950000
.* WERE ENCOUNTERED BY THE USSMSG MACRO.  IF A RANGE OF MESSAGES    *   07000000
.* IS ABSENT FROM THE USS TABLE (E.G IST900I-IST950I), NO STORAGE   *   07050000
.* WILL BE WASTED IN DEFINING UNUSED TABLES.                        *   07100000
.*                                                                  *   07150000
.* THREE MACRO ARRAYS ASSIST IN DECIDING WHETHER TO GENERATE THE    *   07200000
.* VARIOUS INDEX TABLES:                                            *   07250000
.*                                                                  *   07300000
.*    ONE   - THERE IS ONE ENTRY FOR EACH VTAM MESSAGE (0-999). A   *   07350000
.*            NON-ZERO VALUE INDICATES THAT THE CORRESPONDING       *   07400000
.*            MESSAGE WAS PROCESSED BY THE USSMSG MACRO.            *   07450000
.*    TENS  - THERE IS ONE ENTRY FOR EACH HUNDREDS/TENS PAIR IN A   *   07500000
.*            MESSAGE (E.G. 45 IN IST452I).  A NON-ZERO VALUE       *   07550000
.*            INDICATES THAT A 2ND LEVEL INDEX TABLE SHOULD BE      *   07600000
.*            GENERATED FOR MESSAGES AT THIS LEVEL (E.G. IST450I-   *   07650000
.*            IST459I).                                             *   07700000
.*    HDRED - THERE IS ONE ENTRY FOR EACH HUNDREDS DIGIT IN A       *   07750000
.*            MESSAGE (E.G. 4 IN IST452I).  SINCE ALL ENTRIES IN    *   07800000
.*            THE FIRST LEVEL TABLE ARE GENERATED, A NON-ZERO       *   07850000
.*            VALUE IN HDRED MEANS THAT THE CORRESPONDING 1ST LEVEL *   07900000
.*            TABLE VALUE SHOULD BE NON-ZERO (MEANING THAT THERE    *   07950000
.*            ARE MESSAGES WITH THIS HUNDREDS DIGIT).               *   08000000
.*                                                                  *   08050000
.* THE FOLLOWING CODE FILLS IN THE HDRED AND TENS ARRAYS, BASED     *   08100000
.* ON VALUES IN THE ONES ARRAY -- WHICH WAS FILLED IN BY THE        *   08150000
.* USSMSG MACRO.                                                    *   08200000
.********************************************************************   08250000
.GENVTAM    ANOP                                 DECLARE VTAM MSGS      08300000
            DS    0F                                                    08350000
ISTM0001    EQU   *                              DEFINE VTAM MSGS       08400000
&I          SETA  0                              INITIALIZE LOOP INDEX  08450000
.L1LP1      ANOP                                 SCAN ALL MESSAGES      08500000
&I          SETA  &I+1                           NEXT ARRAY ENTRY       08550000
            AIF   (&I GT 2000).DL1LP1            DONE WITH LOOP         08600000
            AIF   (&ONE(&I) NE 1).L1LP1          MESSAGE NOT FOUND      08650000
&L          SETA  ((&I-1)/1000)                  THOUSANDS DIGIT  @F1A  08683300
&M          SETA  ((&I-1)-(&L*1000))/100         HUNDREDS DIGIT         08716600
&N          SETA  ((&I-1)-(&L*1000)-(&M*100))/10 TENS DIGIT             08749900
&J          SETA  &L+1                           DETERMINE INDEX   @F1A 08783200
&THSND(&J)  SETB  1                              # OF THOUSANDS    @F1A 08816500
&J          SETA  ((&L*10)+&M+1)                 DETERMINE INDEX   @F1C 08849800
&HDRED(&J)  SETB  1                              # OF HUNDREDS     @F1C 08883100
&J          SETA  (&L*100)+(&M*10)+&N+1          DETERMINE INDEX   @F1C 08916400
&TENS(&J)   SETB  1                              # OF FORM ISTXX FOUND  08950000
            AGO   .L1LP1                         SCAN ALL MESSAGES      09000000
.DL1LP1     ANOP                                                        09050000
.********************************************************************   09100000
.* GENERATE THE HEADER FOR A TABLE CONTAINING FOUR_DIGIT MESSAGES.  *   09103100
.********************************************************************   09106200
            AIF   (&THSND(2) EQ 0).NO4DIG       4-DIGIT MESSAGES   @F1A 09109300
            DC    XL2'FFFF'                     4-DIGIT FLAG       @F1A 09112400
            DC    AL2(L0000-*)                  IST000-IST999      @F1A 09115500
            DC    AL2(L1000-*)                  IST1000-IST1999    @F1A 09118600
.NO4DIG     ANOP                                                   @Q1C 09120100
&H          SETA  0                             THSND INDEX        @F1A 09121700
.L1P2       ANOP                                REPEAT THOUSANDS   @F1A 09124800
&H          SETA  &H+1                          NEXT THOUSANDS     @F1A 09127900
            AIF (&H GT 2).MMEND                 NO MORE MESSAGES   @F1A 09131000
&Y          SETA  &H-1                                             @F1A 09134100
&TTD        SETC 'L'.'&Y'.'000'                 FIRST TABLE        @F1A 09137200
&TTD        EQU   *                             FIRST LEVEL TABLE  @F1A 09140300
.********************************************************************   09146500
.* GENERATE THE 1ST LEVEL INDEX TABLE.  THERE ARE 10 ENTRIES IN     *   09150000
.* THIS TABLE, ONE FOR EACH HUNDREDS DIGIT IN A MESSAGE IDENTIFIER. *   09200000
.* THE ENTRY WILL BE NON-ZERO IF THE USSMSG MACRO ENCOUNTERED AT    *   09250000
.* LEAST ONE MESSAGE THAT HAD THAT HUNDREDS DIGIT (E.G. 3 IN        *   09300000
.* IST341I); THIS STATE IS INDICATED BY THE CORRESPONDING BIT       *   09350000
.* BEING ON IN THE 'HDRED' ARRAY.                                   *   09400000
.********************************************************************   09450000
&I          SETA  0                              INIT LOOP INDEX  @F1C  09500000
.L1LP2      ANOP                                 SCAN HDRED ARRAY       09550000
&I          SETA  &I+1                           NEXT ARRAY ENTRY       09600000
            AIF   (&I GT 10).DL1LP2              SCAN COMPLETE    @F1C  09650000
            AIF   (&HDRED(((&H-1)*10)+&I) EQ 1).GEN0XX            @F1C  09700000
&TTD        SETC  '0'                            NO 2ND LEVEL TABLE     09750000
            AGO   .COM0XX                                               09800000
.GEN0XX     ANOP                                                        09850000
&J          SETA  &I-1                                                  09900000
&TTD        SETC  'L'.'&Y'.'&J'.'00UP-*'         2ND LEVEL NAME   @F1C  09950000
.COM0XX     ANOP                                                        10000000
            DC    AL2(&TTD)                      2ND LEVEL TABLE        10050000
            AGO   .L1LP2                         SCAN HDRED ARRAY       10100000
.DL1LP2     ANOP                                 DONE WITH SCAN         10150000
.********************************************************************   10200000
.* GENERATE THE 2ND LEVEL INDEX TABLE.  THIS TABLE WILL ONLY BE     *   10250000
.* GENERATED IF THE CORRESPONDING HUNDREDS LEVEL ENTRY IS NON-ZERO. *   10300000
.*                                                                  *   10350000
.* NOTE THAT THE LABEL FOR THE 2ND LEVEL TABLE MUST BE THE SAME     *   10400000
.* AS THAT REFERENCED IN THE 1ST LEVEL TABLE AND WILL BE GENERATED  *   10450000
.* ONLY ONCE, AT THE START OF THE 2ND LEVEL TABLE.  ALSO, EVEN IF   *   10500000
.* ONLY ENTRY IN THE 2ND LEVEL TABLE IS REQUIRED, THE OTHER 9 TABLE *   10550000
.* ENTRIES MUST ALSO BE GENERATED.                                  *   10600000
.********************************************************************   10650000
&I          SETA  0                              OUTER LOOP INDEX       10700000
.L2LP1      ANOP                                 SCAN HDRED ARRAY       10750000
&I          SETA  &I+1                           NEXT LOOP INDEX        10800000
            AIF   (&I GT 10).DL2LP1              DONE WITH OUTER LOOP   10850000
            AIF   (&HDRED(((&H-1)*10)+&I) EQ 0).SKPLP2             @F1C 10900000
&K          SETA  &I-1                                                  10950000
&TTD        SETC  'L'.'&Y'.'&K'.'00UP'           2ND LEVEL NAME    @F1C 11000000
&TTD        EQU   *                                                     11050000
&J          SETA  0                              INNER LOOP INDEX  @F1C 11100000
.L2LP2      ANOP                                 SCAN TENS ARRAY        11150000
&J          SETA  &J+1                           NEXT LOOP INDEX        11200000
            AIF   (&J GT 10).DL2LP2              DONE WITH INNER LOOP   11250000
&N          SETA  ((&H-1)*100)+((&I-1)*10)+&J                      @F1A 11283300
            AIF   (&TENS(&N) EQ 0).SKPX0Y        NO MSGS IN RANGE  @F1A 11316600
&X          SETA  &J-1                                                  11350000
&TTD        SETC  'X'.'&Y'.'&K'.'&X'.'0UP-*'     3RD LEVEL NAME    @F1C 11400000
            AGO   .COMX0Y                                               11450000
.SKPX0Y     ANOP                                                        11500000
&TTD        SETC  '0'                            NO 3RD LEVEL TABLE     11550000
.COMX0Y     ANOP                                                        11600000
            DC    AL2(&TTD)                      3RD LEVEL TABLE        11650000
            AGO   .L2LP2                         SCAN TENS ARRAY        11700000
.DL2LP2     ANOP                                                        11750000
.SKPLP2     ANOP                                                        11800000
            AGO   .L2LP1                         SCAN HDRED ARRAY       11850000
.DL2LP1     ANOP                                                        11900000
.********************************************************************   11950000
.* GENERATE THE 3RD LEVEL INDEX TABLE.  THIS TABLE WILL ONLY BE     *   12000000
.* GENERATED IF THE CORRESPONDING HUNDREDS AND TENS LEVEL TABLE     *   12050000
.* ENTRIES WERE NON-ZERO.                                           *   12100000
.*                                                                  *   12150000
.* NOTE THAT THE LABEL FOR THE 3RD LEVEL TABLE MUST BE THE SAME     *   12200000
.* AS THAT REFERENCED IN THE 2ND LEVEL TABLE AND WILL BE GENERATED  *   12250000
.* ONLY ONCE, AT THE START OF THE 3RD LEVEL TABLE.  ALSO, EVEN IF   *   12300000
.* ONLY ENTRY IN THE 3RD LEVEL TABLE IS REQUIRED, THE OTHER 9 TABLE *   12350000
.* ENTRIES MUST ALSO BE GENERATED.                                  *   12400000
.********************************************************************   12450000
&I          SETA  0                              OUTER LOOP INDEX       12500000
            DS    0F                             WORD BOUNDARY     @Q2A 12525000
.L3LP1      ANOP                                 SCAN HDRED ARRAY       12550000
&I          SETA  &I+1                           NEXT LOOP INDEX        12600000
            AIF   (&I GT 10).DL3LP1              HDRED SCAN COMPLETE    12650000
            AIF   (&HDRED(((&H-1)*10)+&I) EQ 0).SKPLP3             @F1C 12700000
&J          SETA  0                              MIDDLE LOOP INDEX      12750000
.L3LP2      ANOP                                 SCAN TENS ARRAY        12800000
&J          SETA  &J+1                           NEXT LOOP INDEX        12850000
            AIF   (&J GT 10).DL3LP2              TENS SCAN COMPLETE     12900000
&N          SETA  ((&H-1)*100)+((&I-1)*10)+&J                      @F1A 12933300
            AIF   (&TENS(&N) EQ 0).SKPGEN2       NO MSGS IN RANGE  @F1A 12966600
&K          SETA  &I-1                                                  13000000
&X          SETA  &J-1                                                  13050000
&TTD        SETC  'X'.'&Y'.'&K'.'&X'.'0UP'       3RD LEVEL NAME    @F1C 13100000
&TTD        EQU   *                                                     13150000
&M          SETA  0                              INNER LOOP INDEX       13200000
.L3LP3      ANOP                                 SCAN ONE ARRAY         13250000
&M          SETA  &M+1                                                  13300000
            AIF   (&M GT 10).DL3LP3              ONE SCAN COMPLETE      13350000
&N          SETA  ((&H-1)*1000)+((&I-1)*100)+((&J-1)*10)+&M        @F1C 13400000
            AIF   (&ONE(&N) EQ 0).SKPXY0         MESSAGE NOT FOUND      13450000
&N          SETA  &M-1                                                  13500000
            AIF   (&H EQ 1).DIGIT3               3 OR 4 DIGITS     @F1A 13516600
&TTD        SETC  'MSG'.'&Y'.'&K'.'&X'.'&N'      ADDR(MESSAGE)     @F1C 13533200
            AGO   .COMXY0                                          @F1A 13549800
.DIGIT3     ANOP                                 3-DIGIT MESSAGE   @F1A 13566400
&TTD        SETC  'MSG'.'&K'.'&X'.'&N'           ADDR(MESSAGE)     @F1C 13583000
            AGO   .COMXY0                                               13600000
.SKPXY0     ANOP                                                        13650000
&TTD        SETC  '0'                            MESSAGE NOT FOUND      13700000
.COMXY0     ANOP                                                        13750000
            DC    A(&TTD)                                               13800000
            AGO   .L3LP3                         SCAN ONE ARRAY         13850000
.DL3LP3     ANOP                                                        13900000
.SKPGEN2    ANOP                                                        13950000
            AGO   .L3LP2                         SCAN TEN ARRAY         14000000
.DL3LP2     ANOP                                                        14050000
.SKPLP3     ANOP                                                        14100000
            AGO   .L3LP1                         SCAN HDRED ARRAY       14150000
.DL3LP1     ANOP                                                        14200000
            AIF   (&THSND(2) EQ 0).MMEND         NO 4-DIGITS       @F1A 14216600
            AGO   .L1P2                                            @F1A 14233200
.MMEND      ANOP                                                        14250000
            DC    0F'0'                                           @R1A  14275000
            MEND                                                        14300000
