         MACRO                                                          00001000
         SOCKOPT &TYPE,            CSECT or DSECT                      X00002000
               &BSDCFG1=$OPTMECB,  Socket options flag 1               X        
               &BSDCFG2=00,        Socket options flag 2               X        
               &SYSID=00,          System identifier                   X        
               &SSLLIBN=X,         SSL library name                    X00005000
               &SSLSUBN=X,         SSL sublib name                     X00006000
               &SSLMEMN=X,         SSL certificate member name         X00007000
               &SSLDEBG=00,        SSL debugging flag                  X00008000
               &SSLFLG1=00,        SSL flag 1                          X00009000
               &SSLFLG2=00,        SSL flag 2                          X00010000
               &SSLFLG3=00,        SSL flag 3                          X        
               &SSLSTOR=80,        SSL storage usage                   X        
               &AESKEYC=0,         AES key cache size                  X        
               &CSRT=59,           Closed socket reuse time            X        
               &CHKT=900,          Check for dead sockets interval     X        
               &CLST=10,           Time to wait for close to complete  X        
               &SNOWMAX=262144,    Max send no wait unacked allowed    X        
               &BSDXPHS=IPNRBSDC   Name of BSD/C phase                          
         LCLC  &DX                                                              
         AIF   ('&TYPE' EQ 'DSECT').DSECT                               00013000
         AIF   ('&TYPE' EQ 'CSECT').CSECT                               00014000
         MNOTE 8,'TYPE MUST BE DSECT OR CSECT'                          00015000
         MEXIT                                                          00016000
.DSECT   ANOP  ,                                                        00017000
SOCKOPT  DSECT                                                          00018000
&DX      SETC 'DS'                                                      00019000
         AGO   .A1000                                                   00020000
.CSECT   ANOP  ,                                                        00021000
&DX      SETC 'DC'                                                      00022000
.A1000   ANOP  ,                                                        00023000
*                                                                       00024000
* * THIS PROGRAM IS USED FOR SETTING DEBUG FLAGS FOR THE                00025000
* * BSDC and SSL API INTERFACEs.                                        00026000
$OPTNAME DC    CL8'$SOCKOPT'       PHASE NAME                           00027000
$OPTVERS DC    CL8'01.05.00'       VERSION AND RELEASE                          
$OPTADAT DC    CL8'&SYSDATE'       ASSEMBLED DATE                       00029000
$OPTATIM DC    CL8'&SYSTIME'       ASSEMBLED TIME                       00030000
$OPTBCF1 DC    AL1(&BSDCFG1)       BSDC options flag                            
$OPTMECB EQU   X'80'               Allow multiple user ecbs in selectex         
$OPTDLAF EQU   X'40'               Do not re-listen after accept fails          
$OPTLOCK EQU   X'20'               Lock socket table                    00034000
$OPTSNWT EQU   X'10'               Do not wait after send               00035000
$OPTBNDX EQU   X'08'               Have bind check external partitions          
$OPTXNBK EQU   X'04'               Extended non-blocking to all calls           
$OPTUSYD EQU   X'02'               Use Sysid from $SOCKOPT not SYSPARM          
$OPTNBSD EQU   X'01'               Cdload and call &BSDXPHS                     
$OPTSSLL DC    CL7'&SSLLIBN'       SSL library name                             
$OPTSSLS DC    CL8'&SSLSUBN'       SSL sublib name                      00037000
$OPTSSLM DC    CL8'&SSLMEMN'       SSL certificate member name          00038000
$OPTSTOR DC    F'&SSLSTOR'         SSL storage useage                   00039000
$OPTSSLD DC    AL1(&SSLDEBG)       SSL debug flag                               
$OPTSDBG EQU   X'80'               SSL debugging turned on by $SOCKLST  00041000
$OPTSFG1 DC    AL1(&SSLFLG1)       SSL flag 1                                   
$OPTSRQC EQU   X'80'               SSL require close_notify alert       00043000
$OPTSFG2 DC    AL1(&SSLFLG2)       SSL flag 2                                   
$OPTSNHC EQU   X'80'               SSL do not use hw-crypto             00045000
$OPTSNZA EQU   X'40'               SSL do not use Z/architecture insts          
$OPTSFZA EQU   X'20'               SSL force  use Z/architecture insts          
$OPTSFG3 DC    AL1(&SSLFLG3)       SSL flag 3                                   
$OPTSPCR EQU   X'80'               SSL use cryto-server for RSA                 
$OPTSYID DC    CL2'00'             TCP/IP partition sysid                       
$OPTBCF2 DC    AL1(&BSDCFG2)       BSDC options flag 2                          
$OPTCHKR EQU   X'80'               Check IPNRSO blocks                          
$OPTFOPC EQU   X'40'               Check force opened sockets closed            
         DS    XL1                 Unused                                       
$OPTCSRT DC    A($OPTCEQU)         Closed socket reuse time in seconds          
$OPTCLST DC    A($OPTCLTM)         Time to wait for close to complete           
$OPTMAXS DC    F'8192'             Maximum number of sockets                    
$OPTCHKT DC    A(&CHKT)            Check sockets time interval                  
$OPTXPHS DC    CL8'&BSDXPHS'       Name of phase to call                        
$OPTSNOW DC    A(&SNOWMAX)         Max unacked with send nowait                 
$OPTAESK DC    F'&AESKEYC'         AES key cache storage                        
         DS    40F                 RESERVED FOR FUTURE USE                      
$OPTL    EQU   *-$OPTNAME          Length of options area                       
$OPTCEQU EQU   &CSRT+1             Seconds before socket reuse                  
$OPTCLTM EQU   &CLST               Seconds to wait for close                    
.DONE    ANOP  ,                                                                
         MEND                                                           00052000
