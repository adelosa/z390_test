*********************************************************************
* Copyright 2006 Automated Software Tools Corporation               *
* This source code is part of z390 assembler/emulator package       *
* The z390 package is distributed under GNU general public license  *
* Author - Don Higgins                                              *
*********************************************************************
* 05/08/06 RPI 308 CONVERT DEMOGUI4 TO TESTGUI4 EXEC CICS TN3270       
*********************************************************************
DFHEISTG DSECT , ADDED TO TRIGGER DSA
MSG3     TN3270 WCC_KD,(SBA,6,10),(SF,ATT_PA)       
REPLY    DC    CL40' '
         TN3270 (SF,ATT_PA)
REPLYL   EQU   *-REPLY
MSG3L    EQU   *-MSG3
TESTGUI4 CSECT , CHANGED FROM SUBENTRY
         EXEC CICS SEND FROM(MSG1) LENGTH(MSG1L)
LOOP     EQU   *
         EXEC CICS SEND FROM(MSG2) LENGTH(MSG2L)
         EXEC CICS RECEIVE INTO(INPUT) LENGTH(L'INPUT)
         CLI   INPUT,PF3     EXIT ON PF3
         BE    EXIT
         CLI   INPUT,ENTER   IGNORE IF NOT ENTRY WITH TEXT FIELD
         BNE   LOOP
         LA    R2,REPLY
         LA    R3,REPLYL
         LA    R4,INPUT+3+3  FIRST FIELD DATA AFTER (KEY,SBA,SF,SBA)
         LR    R5,R1
         AHI   R5,-6
         ICM   R5,8,=C' '    PAD REPLY WITH SPACES
         MVCL  R2,R4
         MVC   MSG3(MSG3DL),MSG3D  COPY MSG3 TEXT TO DSA   
         EXEC CICS SEND FROM(MSG3) LENGTH(MSG3L)
         CLC   REPLY(3),=C'END'
         BNE   LOOP          EXIT IF REPLY IS END
EXIT     EQU   *
         SUBEXIT
INPUT    DS    XL100
         PRINT DATA
MSG1     TN3270 ESC,WRT_EW,WCC_KD,(SBA,2,10),(SF,ATT_PA)
         DC    C'TESTGUI4 TEST TN3270 GUI INTERFACE'
MSG1L    EQU   *-MSG1
MSG2     TN3270 WCC_KD
         TN3270 (SBA,4,10),(SF,ATT_PA,C'TEXT OR END=')
         TN3270 (SF,ATT_UA,C'???'),(SF,ATT_PA)
         TN3270 (SBA,4,24),IC
MSG2L    EQU   *-MSG2
MSG3D    TN3270 WCC_KD,(SBA,6,10),(SF,ATT_PA)       
MSG3DL   EQU   *-MSG3D
         TN3270 SYMBOLS DEFINE TN3290 CONTROL AND ATTRIBUTE CODES
         EQUREGS
         END
