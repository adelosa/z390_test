.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/05/06 RPI 308 CICS RETURN CALLED FROM EXEC
.*********************************************************************
         MACRO
         CICS_RETURN
         LCLC  &TRANSID,&COMMAREA,&LENGTH
&NP      SETA  N'&SYSLIST
&IP      SETA  0
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  &SYSLIST(&IP)
         AIF   ('&PARM'(1,8) EQ 'TRANSID(').TRANSID
         AIF   ('&PARM'(1,9) EQ 'COMMAREA(').COMMAREA
         AIF   ('&PARM'(1,7) EQ 'LENGTH(').LENGTH
         AGO   .PLOOP
.TRANSID ANOP
&TRANSID SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.COMMAREA ANOP
&COMMAREA SETC '&PARM'(10,K'&PARM-10)
         AGO   .PLOOP
.LENGTH  ANOP
&LENGTH  SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.ELOOP   ANOP
         MVC   EIBFN,=X'0E08'     SET EIBFN
.* IF JUST RETURN, WE ARE FINISHED
         AIF   (&TRANSID EQ '' AND &COMMAREA EQ '' AND &LENGTH EQ '').R-
               ETDONE
.* IT'S NOT JUST EXEC CICS RETURN
.* IF LINK-LEVEL NOT ONE, AND OTHER PARMS, THEN IT'S AN ERROR
         CLC   TCTTELNK,=H'1'     ARE WE AT ROOT LEVEL ?
         BE    CICS_RETURN_&SYSNDX._01 EXIT IF WE ARE
.* ### SET EIBRCODE/EIBRESP2 LATER, INVREQ,2
         LG    R15,=CL8'INVREQ'
         DC    H'0'
CICS_RETURN_&SYSNDX._01 DS 0H
         AIF   (&TRANSID NE '').GOTTRAN
         XC    TCTTETC,TCTTETC    CLEAR PRESET TRANSID
         AGO   .DOCOMM
.GOTTRAN ANOP
         AIF   ('&TRANSID'(1,1) EQ '''').STRING
         MVC   TCTTETC,&TRANSID   PRESET TRANSID
         AGO   .DOCOMM
.STRING  ANOP
&TRANLEN SETA  K'&TRANSID
         AIF   (&TRANLEN LT 3 OR &TRANLEN GT 6).BADTRAN
         MVC   TCTTETC,=CL4&TRANSID
.DOCOMM  ANOP
         AIF   (&COMMAREA NE '').COMMCHK
         AIF   (&LENGTH EQ '').COMMCHK
         MNOTE 12,'LENGTH WITHOUT COMMAREA'
         MEXIT
.COMMCHK ANOP
         AIF   (&COMMAREA NE '').GOTCOMM
         XC    TCTTECA,TCTTECA    CLEAR COMMAREA ADDRESS
         AGO   .EIRET
.GOTCOMM ANOP
         AIF   (&TRANSID EQ '').MISTRAN
         LA    R0,&COMMAREA       COMMAREA ADDRESS
         ST    R0,TCTTECA         SET COMMAREA ADDRESS
         AIF   (&LENGTH NE '').LENVAL
&LENGTH  SETC  'L''&COMMAREA'
         AGO   .LENNUM
.LENVAL  ANOP
         AIF   ('&LENGTH'(1,2) EQ 'L''').LENNUM
         AIF   ('&LENGTH'(1,1) LT '0' OR '&LENGTH'(1,1) GT '9').LENLABL
.LENNUM  ANOP
         MVC   TCTTECAL,=AL2(&LENGTH) SET NUMERIC COMMAREA LENGTH
         AGO   .EIRET
.LENLABL ANOP
         MVC   TCTTECAL,&LENGTH   SET COMMAREA LENGTH FROM LABEL
         AGO   .EIRET
.RETDONE ANOP
.* EXEC CICS RETURN ONLY
         XC    TCTTETC,TCTTETC    CLEAR PRESET TRANSID
         XC    TCTTECA,TCTTECA    CLEAR COMMAREA ADDRESS
.* ANY XCTL COPIED COMMAREA MUST BE FREED NOW
         CLI   TCTTECND,X'FF'     ANY XCTL COMMAREA TO BE FREED ?
         MVI   TCTTECND,X'00'     CLEAR INDICATOR
         BNE   CICS_RETURN_&SYSNDX._02 EXIT IF NONE
         LH    R0,EIBCALEN        R0=COMMAREA LENGTH
         FREEMAIN A=DFHEICAP,LV=(R0)
         XC    EIBCALEN,EIBCALEN  CLEAR COMMAREA LENGTH
CICS_RETURN_&SYSNDX._02 DS 0H
.EIRET   ANOP
         DFHEIRET
         MEXIT
.*
.BADTRAN MNOTE 12,'INVALID TRANSID'
         MEXIT
.MISTRAN MNOTE 12,'TRANSID IS MISSING'
         MEXIT
         MEND
