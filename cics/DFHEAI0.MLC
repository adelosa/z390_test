.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date     05/04/06                                                 *
.*********************************************************************
.* 05/05/06 RPI 308 - ADD SUPPORT FOR FIRST EXEC CICS PGM TESTCIC1    
.*********************************************************************
.* DFHEAI0 IS DYNAMICALLY LINKED FROM DFHEIENT AT FIRST CSECT
.* IN EXEC CICS PROGRAM.  DFHEIENT IS INSERTED IF CICS AND PROLOG
.* OPTIONS ARE ON.
.*********************************************************************
DFHEAI0  CSECT
         USING *,12
         LR    12,15
         LR    3,1            R3 = SAVE USER PARM POINTER               
*
* GET DSA AND CHAIN OLD SAVE AREA TO DSA IN R13
* 
         LR    2,0            R2 = CALLER DSA LENGTH PASSED IN R0
         GETMAIN RC,LV=(2) 
         LTR   15,15
         BNZ   ERR_DSA        ERROR DSA GETMAIN FAILED
         LR    1,0
         ST    1,8(13)        SAVE NEW DSA SAVE AREA IN PRIOR SAVE
         ST    13,4(1)        SAVE OLD SAVE AREA IN NEW DSA SAVE 
         LR    13,1                                   
         USING DFHEISTG,13    R13 > DSA
         ST    2,DFHEILEN     SAVE DSA LENGTH FOR DFHEAI1 FREEMAIN
*
* GET EIB FROM ZCVT AND SAVE IN DSA ELSE INIT CICS
*
         L     5,=A(ZCVT)
         USING IHAZCVT,5
         ICM   11,15,ZCVTEIBP    R11 > EIB
         BZ    INITCICS          GO INIT CICS IF EIBP 0
         ST    11,DFHEIBP        SET EIB IN DSA
         USING DFHEIBLK,11       R11 > EIB
*
* GET CA FROM ZCVT AND SAVE IN DSA (INITILIZE ZCVT CAP IF 0)
*
         L     0,ZCVTCAP         R0 > CA
         ST    0,DFHEICAP        SAVE DSA COM AREA POINTER
*
* RESTORE 2-12 AND RETURN TO CALLING PROGRAM WITH R13 > DSA
*
         L     15,4(13)
         LM    2,12,28(15)       RESTORE 2-12 REGS
         SR    15,15
         BR    14                RETURN TO DFHEIENT CALLER
*
* INIT CICS IF R1 NOT SET TO ZCVTEIBP OR ZCVTEIPB/ZCVTCAP ZERO
*
INITCICS EQU   *
         MVC   ZCVTPARM(2),=H'8'      SET PARM OPTION LENGTH
         MVC   ZCVTPARM+2(8),ZCVTUPGM SET EXEC CICS TRANSACTION NAME
         LINK  EP=CICS            EXEC Z390 EXEC CICS COMMAND PROC
*
* EXIT TO PREV LINK CALLER (CICS OR EZ390)
*
EXIT     EXIT                     EXIT TO EZ390 NOW INSTEAD OF TRAN
*
* ISSUE ERROR MESSAGE WTO AND ABORT TRANSACTION WITH RC=16
*
ERR_DSA  WTO   'DFHEAI0 DSA GETMAIN FAILED - INCREASE MEM(MB) OPTION'
         WTO   'DFHEAI0 TERMINATING WITH RC=16 NOW'
         LA    15,16
         B     EXIT                 EXIT TO EZ390 NOW INSTEAD OF TRAN
*
* DSECTS  
*
         DFHEISTG , DSA  DYNAMIC STORAGE AREA
         DFHEIBLK , EIB  INTERFACE CONTROL BLOCK
         DFHEICAD , CA   COMMON AREA
         ZCVTD    , ZCVT Z390 CVT
         END
