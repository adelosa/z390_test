.**********************************************************************
.* Copyright 2006 Automated Software Tools Corporation                *
.* This source code is part of z390 assembler/emulator package        *
.* The z390 package is distributed under GNU general public license   *
.* Author - Don Higgins                                               *
.**********************************************************************
.* 04/19/08 RPI 833 ADD STRING QUOTES FOR HLASM COMPATIBILITY         *
.**********************************************************************
         MACRO
         CICS_POP
         GBLB  &PARMS_020E
         LCLB  &NOHAND
         LCLC  &TYPE,&RESP,&RESP2
&TYPE    SETC  '&SYSLIST(1)'
         AIF   ('&TYPE' EQ 'HANDLE').GOTHAND
         MNOTE 12,'POP TYPE NOT RECOGNIZED'
         MEXIT
.*
.GOTHAND ANOP
&NP      SETA  N'&SYSLIST
&IP      SETA  1
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  '&SYSLIST(&IP)'
         AIF   ('&PARM'(1,8) EQ 'NOHANDLE').PLOOP
         AIF   ('&PARM'(1,5) EQ 'RESP(').RESP
         AIF   ('&PARM'(1,6) EQ 'RESP2(').RESP2
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.NOHAND  ANOP
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP    ANOP
&RESP    SETC  '&PARM'(6,K'&PARM-6)
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP2   ANOP
&RESP2   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.ELOOP   ANOP
         L     R15,=A(P020E)      LOAD PARM ADDRESS
         USING (P020E,P020EEND),R15
         XC    P020E(P020EEND-P020E),P020E CLEAR PARMS
         MVC   P020EFN,=X'020E'   SET FUNCTION CODE
         ST    DFHEIBR,P020EEIB   SET EIB ADDRESS
         ST    TCTTEAR,P020ETCT   SET TCTTE ADDRESS
         LA    R1,DFHEIAID        R1=ADDRESS OF DFHEIAID
         ST    R1,P020EAID        SAVE IT
         LA    R1,DFHEIHCN        R1=ADDRESS OF DFHEIHCN
         ST    R1,P020EHCN        SAVE IT
         AIF   (NOT &NOHAND).TRYRESP
         MVI   P020ENOH,X'FF'     SET NOHANDLE
.TRYRESP ANOP
         AIF   ('&RESP' EQ '').NORESP
         LA    R1,&RESP           ADDRESS OF RESP
         ST    R1,P020ERSP        SAVE IT
.NORESP  ANOP
         AIF   ('&RESP2' EQ '').NORESP2
         LA    R1,&RESP2          ADDRESS OF RESP2
         ST    R1,P020ERS2        SAVE IT
.NORESP2 ANOP
         ZCALL LCL020E,(P020E),MF=I CALL POP HANDLE PROCESSOR
.*
         L     R15,=A(P020E)      LOAD PARM ADDRESS
         CLI   P020ENOH,X'FF'     NOHANDLE ?
         BE    P020EBYP_&SYSNDX   EXIT IF IT IS
         OC    EIBRESP,EIBRESP    ANY BAD RESPONSE ?
         BZ    P020EBYP_&SYSNDX   EXIT IF NONE
* INVREQ
         DC    AL3(0),C'ABEND'    MARKER FOR Z390KCP ESTAE
         DC    AL4(P020EBYP_&SYSNDX) ENTRYPOINT FOR IGNORE CONDITION
*
         AIF   (&PARMS_020E).BYP020E
&PARMS_020E SETB 1
P020E    EQU   *
P020EFN  DS    XL2                FUNCTION CODE
P020EEIB DS    AL4                EIB
P020ETCT DS    AL4                TCTTE
P020EAID DS    AL4                A(DFHEIAID)
P020EHCN DS    AL4                A(DFHEIHCN)
P020ENOH DS    X                  FF=NOHANDLE
P020ERSP DS    AL4                RESP
P020ERS2 DS    AL4                RESP2
P020EEND EQU   *                  END MARKER
         DS    0H
*
.BYP020E ANOP
P020EBYP_&SYSNDX DS 0H
         DROP  R15
         MEXIT
         MEND
