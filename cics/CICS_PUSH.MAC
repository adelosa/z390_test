.**********************************************************************
.* Copyright 2006 Automated Software Tools Corporation                *
.* This source code is part of z390 assembler/emulator package        *
.* The z390 package is distributed under GNU general public license   *
.* Author - Don Higgins                                               *
.**********************************************************************
.* 04/19/08 RPI 833 ADD STRING QUOTES FOR HLASM COMPATIBILITY         *
.**********************************************************************
         MACRO
         CICS_PUSH
         GBLB  &PARMS_020C
         LCLC  &TYPE,&RESP,&RESP2
&TYPE    SETC  '&SYSLIST(1)'
         AIF   ('&TYPE' EQ 'HANDLE').GOTHAND
         MNOTE 12,'PUSH TYPE NOT RECOGNIZED'
         MEXIT
.*
.GOTHAND ANOP
&NP      SETA  N'&SYSLIST
&IP      SETA  1
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  '&SYSLIST(&IP)'
         AIF   ('&PARM'(1,8) EQ 'NOHANDLE').PLOOP
         AIF   ('&PARM'(1,5) EQ 'RESP(').RESP
         AIF   ('&PARM'(1,6) EQ 'RESP2(').RESP2
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.RESP    ANOP
&RESP    SETC  '&PARM'(6,K'&PARM-6)
         AGO   .PLOOP
.*
.RESP2   ANOP
&RESP2   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.ELOOP   ANOP
         L     R15,=A(P020C)      LOAD PARM ADDRESS
         USING (P020C,P020CEND),R15
         XC    P020C(P020CEND-P020C),P020C CLEAR PARMS
         MVC   P020CFN,=X'020C'   SET FUNCTION CODE
         ST    DFHEIBR,P020CEIB   SET EIB ADDRESS
         ST    TCTTEAR,P020CTCT   SET TCTTE ADDRESS
         MVC   P020CAID,DFHEIAID  SET AID BLOCK ADDRESS
         MVC   P020CHCN,DFHEIHCN  SET HANDLE CONDITION BLOCK ADDRESS
         AIF   ('&RESP' EQ '').NORESP
         LA    R1,&RESP           ADDRESS OF RESP
         ST    R1,P020CRSP        SAVE IT
.NORESP  ANOP
         AIF   ('&RESP2' EQ '').NORESP2
         LA    R1,&RESP2          ADDRESS OF RESP2
         ST    R1,P020CRS2        SAVE IT
.NORESP2 ANOP
         ZCALL LCL020C,(P020C),MF=I CALL PUSH HANDLE PROCESSOR
         DROP  R15
         AIF   (&PARMS_020C).BYP020C
         B     P020CBYP_&SYSNDX   EXIT
*
&PARMS_020C SETB 1
P020C    EQU   *
P020CFN  DS    XL2                FUNCTION CODE
P020CEIB DS    AL4                EIB
P020CTCT DS    AL4                TCTTE
P020CAID DS    AL4                AID BLOCK ADDRESS
P020CHCN DS    AL4                HANDLE CONDITION BLOCK ADDRESS
P020CRSP DS    AL4                RESP
P020CRS2 DS    AL4                RESP2
P020CEND EQU   *                  END MARKER
         DS    0H
*
P020CBYP_&SYSNDX DS 0H
.BYP020C ANOP
         MEXIT
         MEND
