.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
         MACRO
         CICS_FORMATTIME
         LCLA  &OFFSET
         LCLB  &DATESP2,&TIMESP2
         LCLC  &ABSTIME
         LCLC  &DATE,&DATEFRM,&DATESP1,&FULLDAT
         LCLC  &DMY2,&MDY2,&YDM2,&YMD2
         LCLC  &DMY4,&MDY4,&YDM4,&YMD4,&YD2,&YD4
         LCLC  &DAYCNT,&DAYOFWK,&DAYOFMN,&MNOFYR,&YEAR
         LCLC  &DSTRING,&TIME,&TIMESEP,&TIMESP1
.*
&NP      SETA  N'&SYSLIST
&IP      SETA  0
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  &SYSLIST(&IP)
.* DISCARDED PARM
         AIF   ('&PARM'(1,13) EQ 'STRINGFORMAT(').PLOOP
.*
         AIF   ('&PARM'(1,8)  EQ 'ABSTIME(').ABSTIME
         AIF   ('&PARM'(1,5)  EQ 'DATE(').DATE
         AIF   ('&PARM'(1,9)  EQ 'DATEFORM(').DATEFRM
         AIF   ('&PARM'(1,8)  EQ 'DATESEP(').DATESP1
         AIF   ('&PARM'(1,7)  EQ 'DATESEP').DATESP2
         AIF   ('&PARM'(1,9)  EQ 'FULLDATE(').FULLDAT
         AIF   ('&PARM'(1,7)  EQ 'DDMMYY(').DMY2
         AIF   ('&PARM'(1,7)  EQ 'MMDDYY(').MDY2
         AIF   ('&PARM'(1,7)  EQ 'YYDDMM(').YDM2
         AIF   ('&PARM'(1,7)  EQ 'YYMMDD(').YMD2
         AIF   ('&PARM'(1,9)  EQ 'DDMMYYYY(').DMY4
         AIF   ('&PARM'(1,9)  EQ 'MMDDYYYY(').MDY4
         AIF   ('&PARM'(1,9)  EQ 'YYYYDDMM(').YDM4
         AIF   ('&PARM'(1,9)  EQ 'YYYYMMDD(').YMD4
         AIF   ('&PARM'(1,6)  EQ 'YYDDD(').YD2
         AIF   ('&PARM'(1,8)  EQ 'YYYYDDD(').YD4
         AIF   ('&PARM'(1,9)  EQ 'DAYCOUNT(').DAYCNT
         AIF   ('&PARM'(1,10) EQ 'DAYOFWEEK(').DAYOFWK
         AIF   ('&PARM'(1,11) EQ 'DAYOFMONTH(').DAYOFMN
         AIF   ('&PARM'(1,12) EQ 'MONTHOFYEAR(').MNOFYR
         AIF   ('&PARM'(1,5)  EQ 'YEAR(').YEAR
         AIF   ('&PARM'(1,11) EQ 'DATESTRING(').DSTRING
         AIF   ('&PARM'(1,5)  EQ 'TIME(').TIME
         AIF   ('&PARM'(1,8)  EQ 'TIMESEP(').TIMESP1
         AIF   ('&PARM'(1,7)  EQ 'TIMESEP').TIMESP2
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.ABSTIME ANOP
&ABSTIME SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.*
.DATE    ANOP
&DATE    SETC  '&PARM'(6,K'&PARM-6)
         AGO   .PLOOP
.*
.DATEFRM ANOP
&DATEFRM SETC  '&PARM'(10,K'&PARM-10)
         AGO   .PLOOP
.*
.DATESP1 ANOP
&DATESP1 SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.*
.DATESP2 ANOP
&DATESP2 SETB  1
         AGO   .PLOOP
.*
.FULLDAT ANOP
&FULLDAT SETC  '&PARM'(10,K'&PARM-10)
         AGO   .PLOOP
.*
.DMY2    ANOP
&DMY2    SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.*
.MDY2    ANOP
&MDY2    SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.*
.YDM2    ANOP
&YDM2    SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.*
.YMD2    ANOP
&YMD2    SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.*
.DMY4    ANOP
&DMY4    SETC  '&PARM'(10,K'&PARM-10)
         AGO   .PLOOP
.*
.MDY4    ANOP
&MDY4    SETC  '&PARM'(10,K'&PARM-10)
         AGO   .PLOOP
.*
.YDM4    ANOP
&YDM4    SETC  '&PARM'(10,K'&PARM-10)
         AGO   .PLOOP
.*
.YMD4    ANOP
&YMD4    SETC  '&PARM'(10,K'&PARM-10)
         AGO   .PLOOP
.*
.YD2     ANOP
&YD2     SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.YD4     ANOP
&YD4     SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.*
.DAYCNT  ANOP
&DAYCNT  SETC  '&PARM'(10,K'&PARM-10)
         AGO   .PLOOP
.*
.DAYOFWK ANOP
&DAYOFWK SETC  '&PARM'(11,K'&PARM-11)
         AGO   .PLOOP
.*
.DAYOFMN ANOP
&DAYOFMN SETC  '&PARM'(12,K'&PARM-12)
         AGO   .PLOOP
.*
.MNOFYR  ANOP
&MNOFYR  SETC  '&PARM'(13,K'&PARM-13)
         AGO   .PLOOP
.*
.YEAR    ANOP
&YEAR    SETC  '&PARM'(6,K'&PARM-6)
         AGO   .PLOOP
.*
.DSTRING ANOP
&DSTRING SETC  '&PARM'(12,K'&PARM-12)
         AGO   .PLOOP
.*
.TIME    ANOP
&TIME    SETC  '&PARM'(6,K'&PARM-6)
         AGO   .PLOOP
.*
.TIMESP1 ANOP
&TIMESP1 SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.*
.TIMESP2 ANOP
&TIMESP2 SETB  1
         AGO   .PLOOP
.*
.ELOOP   ANOP
         AIF   (&ABSTIME EQ '').MISSABS
         TP    &ABSTIME.(8)       VALID PACKED FIELD ?
         BNZ   CICS_FORMATTIME_&SYSNDX._INVREQ EXIT IF BAD
         ZAP   &ABSTIME.(8),&ABSTIME.(8) CHECK -VE ?
         BM    CICS_FORMATTIME_&SYSNDX._INVREQ EXIT IF BAD
.*
         MVC   EIBFN,=X'4A04'     SET EIBFN
         XC    EIBRCODE,EIBRCODE  CLEAR EIBRCODE
         XC    EIBRESP,EIBRESP    CLEAR EIBRESP
         XC    EIBRESP2,EIBRESP2  CLEAR EIBRESP2
         ZAP   CICS_FORMATTIME_&SYSNDX._ABSTIME,&ABSTIME
         CVBG  R1,CICS_FORMATTIME_&SYSNDX._ABSTIME CONVERT TO BINARY
         SGR   R0,R0              CLEAR R0
         DSGF  R0,=AL4(1000*60*60*24) REDUCE TO DAYS
         AHI   R1,1               +1 FOR TRUE DAYCOUNT
         ST    R1,CICS_FORMATTIME_&SYSNDX._DAYCOUNT SAVE IT
         SR    R0,R0              CLEAR R0
         D     R0,=F'7'           DAYCOUNT MOD 7=DAY NUMBER
         ST    R0,CICS_FORMATTIME_&SYSNDX._DAYNUMB SAVE IT
         L     R1,CICS_FORMATTIME_&SYSNDX._DAYCOUNT RELOAD IT
         AHI   R1,-1              WHOLE DAYS ELAPSED
         MS    R1,=F'100'         *100
         SR    R0,R0              CLEAR R0
         D     R0,=F'36525'       YEARS SINCE 1900
         AHI   R1,1900            TRUE YEAR
         ST    R1,CICS_FORMATTIME_&SYSNDX._YEAR SAVE IT
         AHI   R1,-1900           YEARS SINCE 1900
         SRL   R1,2               /4=LEAP YEARS SINCE 1900
         ST    R1,CICS_FORMATTIME_&SYSNDX._LEAPS SAVE IT
         L     R1,CICS_FORMATTIME_&SYSNDX._YEAR RELOAD YEAR
         AHI   R1,-1900           YEARS SINCE 1900
         MS    R1,=F'365'         *365
         A     R1,CICS_FORMATTIME_&SYSNDX._LEAPS +LEAPS=DAYS IN WHOLE Y-
               EARS
         ST    R1,CICS_FORMATTIME_&SYSNDX._WHOLEYR SAVE IT
         L     R1,CICS_FORMATTIME_&SYSNDX._DAYCOUNT RELOAD DAYCOUNT
         AHI   R1,-1              WHOLE DAYS ELAPSED
         S     R1,CICS_FORMATTIME_&SYSNDX._WHOLEYR -DAYS IN WHOLE YEARS
         ST    R1,CICS_FORMATTIME_&SYSNDX._THISYR DAYS IN THIS YEAR
         TM    CICS_FORMATTIME_&SYSNDX._YEAR+3,X'03' LEAP YEAR ?
         BNZ   CICS_FORMATTIME_&SYSNDX._NOTLEAP EXIT IF NOT
         MVC   CICS_FORMATTIME_&SYSNDX._MNTHTAB+2(2),=H'29' SET FEB
CICS_FORMATTIME_&SYSNDX._NOTLEAP DS 0H
         LA    R2,CICS_FORMATTIME_&SYSNDX._MNTHTAB ADDRESS MONTH TABLE
         SR    R0,R0              CLEAR R0
CICS_FORMATTIME_&SYSNDX._MONTHLP DS 0H
         CH    R1,0(R2)           THIS MONTH ?
         BNH   CICS_FORMATTIME_&SYSNDX._THISMTH EXIT IF IT IS
         AHI   R0,1               BUMP MONTH NUMBER
         SH    R1,0(R2)           REDUCE BY MONTH'S DAYS
         AHI   R2,2               BUMP MONTH TABLE
         B     CICS_FORMATTIME_&SYSNDX._MONTHLP LOOP
*
CICS_FORMATTIME_&SYSNDX._THISMTH DS 0H
         AHI   R0,1               BUMP MONTH NUMBER
         AHI   R1,1               BUMP DAY NUMBER
         AHI   R2,-2              MOTHE TABLE ENTRY
         CH    R1,0(R2)           PAST END OF MONTH ?
         BNH   CICS_FORMATTIME_&SYSNDX._NEWMHYR EXIT IF NOT
         LA    R1,1               DAY NUMBER=1
         AHI   R0,1               BUMP MONTH NUMBER
         CH    R0,=H'13'          PAST END OF YEAR ?
         BNE   CICS_FORMATTIME_&SYSNDX._NEWMHYR EXIT IF NOT
         LA    R0,1               MONTH NUMBER=1
         L     R2,CICS_FORMATTIME_&SYSNDX._YEAR RELOAD YEAR
         AHI   R2,1               BUMP YEAR NUMBER
         ST    R2,CICS_FORMATTIME_&SYSNDX._YEAR SAVE YEAR
CICS_FORMATTIME_&SYSNDX._NEWMHYR DS 0H
         ST    R0,CICS_FORMATTIME_&SYSNDX._MONTH SAVE MONTH NUMBER
         ST    R1,CICS_FORMATTIME_&SYSNDX._DAYNO SAVE DAY NUMBER
         LGF   R1,CICS_FORMATTIME_&SYSNDX._WHOLEYR DAYS IN WHOLE YRS
         AGF   R1,CICS_FORMATTIME_&SYSNDX._THISYR +DAYS IN THIS YEAR
         MSGF  R1,=AL4(1000*60*60*24) CONVERT TO mS
         CVBG  R0,CICS_FORMATTIME_&SYSNDX._ABSTIME CONVERT TO BINARY
         SGR   R0,R1              R0=RESIDUAL TIME
         LR    R1,R0              SAVE IT
         SR    R0,R0              CLEAR R0
         D     R0,=AL4(1000*60*60) CALC HOURS
         ST    R1,CICS_FORMATTIME_&SYSNDX._HOURS SAVE HOURS
         LR    R1,R0              MOVE REMAINDER
         SR    R0,R0              CLEAR R0
         D     R0,=AL4(1000*60)   CALC MINUTES
         ST    R1,CICS_FORMATTIME_&SYSNDX._MINS SAVE MINUTES
         LR    R1,R0              MOVE REMAINDER
         SR    R0,R0              CLEAR R0
         D     R0,=AL4(1000)      CALC SECONDS
         ST    R1,CICS_FORMATTIME_&SYSNDX._SECS SAVE MINUTES
*
         L     R1,CICS_FORMATTIME_&SYSNDX._DAYNO LOAD DAY NUMBER
         CVD   R1,CICS_FORMATTIME_&SYSNDX._CVDOUT CONVERT TO DECIMAL
         MVC   CICS_FORMATTIME_&SYSNDX._D,=X'212020' MOVE MASK
         ED    CICS_FORMATTIME_&SYSNDX._D,CICS_FORMATTIME_&SYSNDX._CVDO-
               UT+6 EDIT DAY NUMBER
         L     R1,CICS_FORMATTIME_&SYSNDX._MONTH LOAD MONTH NUMBER
         CVD   R1,CICS_FORMATTIME_&SYSNDX._CVDOUT CONVERT TO DECIMAL
         MVC   CICS_FORMATTIME_&SYSNDX._M,=X'212020' MOVE MASK
         ED    CICS_FORMATTIME_&SYSNDX._M,CICS_FORMATTIME_&SYSNDX._CVDO-
               UT+6 EDIT MONTH NUMBER
         L     R1,CICS_FORMATTIME_&SYSNDX._YEAR LOAD YEAR
         CVD   R1,CICS_FORMATTIME_&SYSNDX._CVDOUT CONVERT TO DECIMAL
         MVC   CICS_FORMATTIME_&SYSNDX._Y,=X'2120202020' MOVE MASK
         ED    CICS_FORMATTIME_&SYSNDX._Y,CICS_FORMATTIME_&SYSNDX._CVDO-
               UT+5 EDIT YEAR
*
         L     R1,CICS_FORMATTIME_&SYSNDX._HOURS LOAD HOURS
         CVD   R1,CICS_FORMATTIME_&SYSNDX._CVDOUT CONVERT TO DECIMAL
         MVC   CICS_FORMATTIME_&SYSNDX._HH,=X'212020' MOVE MASK
         ED    CICS_FORMATTIME_&SYSNDX._HH,CICS_FORMATTIME_&SYSNDX._CVD-
               OUT+6 EDIT HOURS
         L     R1,CICS_FORMATTIME_&SYSNDX._MINS LOAD MINUTES
         CVD   R1,CICS_FORMATTIME_&SYSNDX._CVDOUT CONVERT TO DECIMAL
         MVC   CICS_FORMATTIME_&SYSNDX._MM,=X'212020' MOVE MASK
         ED    CICS_FORMATTIME_&SYSNDX._MM,CICS_FORMATTIME_&SYSNDX._CVD-
               OUT+6 EDIT MINUTES
         L     R1,CICS_FORMATTIME_&SYSNDX._SECS LOAD SECONDS
         CVD   R1,CICS_FORMATTIME_&SYSNDX._CVDOUT CONVERT TO DECIMAL
         MVC   CICS_FORMATTIME_&SYSNDX._SS,=X'212020' MOVE MASK
         ED    CICS_FORMATTIME_&SYSNDX._SS,CICS_FORMATTIME_&SYSNDX._CVD-
               OUT+6 EDIT SECONDS
         L     R1,CICS_FORMATTIME_&SYSNDX._THISYR LOAD DAY NUMBER
         AHI   R1,1               ADD THE NEXT DAY
         CVD   R1,CICS_FORMATTIME_&SYSNDX._CVDOUT CONVERT TO DECIMAL
         MVC   CICS_FORMATTIME_&SYSNDX._DDD,=X'F0202020' MOVE MASK
         ED    CICS_FORMATTIME_&SYSNDX._DDD,CICS_FORMATTIME_&SYSNDX._CV-
               DOUT+6 EDIT DAY NUMBER
         AIF   (NOT(&DATESP2)).NODEFSP
&DATESP1 SETC  '''/'''
.NODEFSP ANOP
         AIF   (&DATE EQ '').NODATE
.* PROCESS DATE
         CLI   EIBDTFRM,C'K'      DMY ?
         BNE   CICS_FORMATTIME_&SYSNDX._NOTK EXIT IF NOT
* DMY
         MVC   &DATE+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._D+1 MOVE DAY
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NOSEPK1
         AIF   ('&DATESP1'(1,1) NE '''').KLAB1
         MVI   &DATE+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NOSEPK1
.*
.KLAB1   ANOP
         MVC   &DATE+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NOSEPK1 ANOP
         MVC   &DATE+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._M+1 MOVE MONT-
               H
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NOSEPK2
         AIF   ('&DATESP1'(1,1) NE '''').KLAB2
         MVI   &DATE+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NOSEPK2
.*
.KLAB2   ANOP
         MVC   &DATE+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NOSEPK2 ANOP
         MVC   &DATE+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._Y+3 MOVE YEAR
         B     CICS_FORMATTIME_&SYSNDX._DUNDATE EXIT
*
CICS_FORMATTIME_&SYSNDX._NOTK DS 0H
&OFFSET  SETA  0
         CLI   EIBDTFRM,C'S'      MDY ?
         BNE   CICS_FORMATTIME_&SYSNDX._NOTS EXIT IF NOT
* MDY
         MVC   &DATE+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._M+1 MOVE MONT-
               H
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NOSEPS1
         AIF   ('&DATESP1'(1,1) NE '''').SLAB1
         MVI   &DATE+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NOSEPS1
.*
.SLAB1   ANOP
         MVC   &DATE+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NOSEPS1 ANOP
         MVC   &DATE+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._D+1 MOVE DAY
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NOSEPS2
         AIF   ('&DATESP1'(1,1) NE '''').SLAB2
         MVI   &DATE+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NOSEPS2
.*
.SLAB2   ANOP
         MVC   &DATE+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NOSEPS2 ANOP
         MVC   &DATE+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._Y+3 MOVE YEAR
         B     CICS_FORMATTIME_&SYSNDX._DUNDATE EXIT
*
CICS_FORMATTIME_&SYSNDX._NOTS DS 0H
* YMD
&OFFSET  SETA  0
         MVC   &DATE+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._Y+3 MOVE YEAR
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NOSEPR1
         AIF   ('&DATESP1'(1,1) NE '''').RLAB1
         MVI   &DATE+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NOSEPR1
.*
.RLAB1   ANOP
         MVC   &DATE+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NOSEPR1 ANOP
         MVC   &DATE+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._M+1 MOVE MONT-
               H
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NOSEPR2
         AIF   ('&DATESP1'(1,1) NE '''').RLAB2
         MVI   &DATE+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NOSEPR2
.*
.RLAB2   ANOP
         MVC   &DATE+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NOSEPR2 ANOP
         MVC   &DATE+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._D+1 MOVE DAY
CICS_FORMATTIME_&SYSNDX._DUNDATE DS 0H
.NODATE  ANOP
         AIF   (&DATEFRM EQ '').NODTFRM
         CLI   EIBDTFRM,C'K'      DMY ?
         BNE   CICS_FORMATTIME_&SYSNDX._NOTKDF EXIT IF NOT
.* DATEFORM=DMY
         MVC   &DATEFRM.(6),=C'DDMMYY' MOVE FORMAT
         B     CICS_FORMATTIME_&SYSNDX._DUNDTFM EXIT
*
CICS_FORMATTIME_&SYSNDX._NOTKDF DS 0H
         CLI   EIBDTFRM,C'S'      MDY ?
         BNE   CICS_FORMATTIME_&SYSNDX._NOTSDF EXIT IF NOT
.* DATEFORM=MDY
         MVC   &DATEFRM.(6),=C'MMDDYY' MOVE FORMAT
         B     CICS_FORMATTIME_&SYSNDX._DUNDTFM EXIT
*
CICS_FORMATTIME_&SYSNDX._NOTSDF DS 0H
.* DATEFORM=YMD
         MVC   &DATEFRM.(6),=C'YYMMDD' MOVE FORMAT
CICS_FORMATTIME_&SYSNDX._DUNDTFM DS 0H
.NODTFRM ANOP
         AIF   (&FULLDAT EQ '').NOFULDT
.* PROCESS FULLDATE
&OFFSET  SETA  0
         CLI   EIBDTFRM,C'K'      DMY ?
         BNE   CICS_FORMATTIME_&SYSNDX._NOTFK EXIT IF NOT
* DMY
         MVC   &FULLDAT+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._D+1 MOVE D-
               ATE
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NOSPFK1
         AIF   ('&DATESP1'(1,1) NE '''').FKLAB1
         MVI   &FULLDAT+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NOSPFK1
.*
.FKLAB1  ANOP
         MVC   &FULLDAT+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NOSPFK1 ANOP
         MVC   &FULLDAT+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._M+1 MOVE M-
               ONTH
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NOSPFK2
         AIF   ('&DATESP1'(1,1) NE '''').FKLAB2
         MVI   &FULLDAT+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NOSPFK2
.*
.FKLAB2  ANOP
         MVC   &FULLDAT+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NOSPFK2 ANOP
         MVC   &FULLDAT+&OFFSET.(4),CICS_FORMATTIME_&SYSNDX._Y+1 MOVE Y-
               EAR
         B     CICS_FORMATTIME_&SYSNDX._DUNFDATE EXIT
*
CICS_FORMATTIME_&SYSNDX._NOTFK DS 0H
&OFFSET  SETA  0
         CLI   EIBDTFRM,C'S'      MDY ?
         BNE   CICS_FORMATTIME_&SYSNDX._NOTFS EXIT IF NOT
* MDY
         MVC   &FULLDAT+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._M+1 MOVE M-
               ONTH
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NOSPFS1
         AIF   ('&DATESP1'(1,1) NE '''').FSLAB1
         MVI   &FULLDAT+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NOSPFS1
.*
.FSLAB1  ANOP
         MVC   &FULLDAT+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NOSPFS1 ANOP
         MVC   &FULLDAT+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._D+1 MOVE D-
               AY
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NOSPFS2
         AIF   ('&DATESP1'(1,1) NE '''').FSLAB2
         MVI   &FULLDAT+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NOSPFS2
.*
.FSLAB2  ANOP
         MVC   &FULLDAT+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NOSPFS2 ANOP
         MVC   &FULLDAT+&OFFSET.(4),CICS_FORMATTIME_&SYSNDX._Y+1 MOVE Y-
               EAR
         B     CICS_FORMATTIME_&SYSNDX._DUNFDATE EXIT
*
CICS_FORMATTIME_&SYSNDX._NOTFS DS 0H
* YMD
&OFFSET  SETA  0
         MVC   &FULLDAT+&OFFSET.(4),CICS_FORMATTIME_&SYSNDX._Y+1 MOVE Y-
               EAR
&OFFSET  SETA  &OFFSET+4
         AIF   (&DATESP1 EQ '').NOSPFR1
         AIF   ('&DATESP1'(1,1) NE '''').FRLAB1
         MVI   &FULLDAT+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NOSPFR1
.*
.FRLAB1  ANOP
         MVC   &FULLDAT+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NOSPFR1 ANOP
         MVC   &FULLDAT+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._M+1 MOVE M-
               ONTH
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NOSPFR2
         AIF   ('&DATESP1'(1,1) NE '''').FRLAB2
         MVI   &FULLDAT+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NOSPFR2
.*
.FRLAB2  ANOP
         MVC   &FULLDAT+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NOSPFR2 ANOP
         MVC   &FULLDAT+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._D+1 MOVE D-
               AY
CICS_FORMATTIME_&SYSNDX._DUNFDATE DS 0H
.NOFULDT ANOP
         AIF   (&DMY2 EQ '').NODMY2
* DDMMYY
&OFFSET  SETA  0
         MVC   &DMY2+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._D+1 MOVE DAY
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NSPDMY21
         AIF   ('&DATESP1'(1,1) NE '''').DMY2LAB1
         MVI   &DMY2+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NOSPDMY2
.*
.DMY2LAB1 ANOP
         MVC   &DMY2+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NSPDMY21 ANOP
         MVC   &DMY2+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._M+1 MOVE MONT-
               H
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NSPDMY22
         AIF   ('&DATESP1'(1,1) NE '''').DMY2LAB2
         MVI   &DMY2+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NSPDMY22
.*
.DMY2LAB2 ANOP
         MVC   &DMY2+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NSPDMY22 ANOP
         MVC   &DMY2+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._Y+3 MOVE YEAR
.NODMY2  ANOP
         AIF   (&MDY2 EQ '').NOMDY2
* MMDDYY
&OFFSET  SETA  0
         MVC   &MDY2+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._M+1 MOVE MONT-
               H
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NSPMDY21
         AIF   ('&DATESP1'(1,1) NE '''').MDY2LAB1
         MVI   &MDY2+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NSPMDY21
.*
.MDY2LAB1 ANOP
         MVC   &MDY2+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NSPMDY21 ANOP
         MVC   &MDY2+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._D+1 MOVE DAY
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NSPMDY22
         AIF   ('&DATESP1'(1,1) NE '''').MDY2LAB2
         MVI   &MDY2+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NSPMDY22
.*
.MDY2LAB2 ANOP
         MVC   &MDY2+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NSPMDY22 ANOP
         MVC   &MDY2+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._Y+3 MOVE YEAR
.NOMDY2  ANOP
         AIF   (&YDM2 EQ '').NOYDM2
* YYDDMM
&OFFSET  SETA  0
         MVC   &YDM2+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._Y+3 MOVE YEAR
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NSPYDM21
         AIF   ('&DATESP1'(1,1) NE '''').YDM2LAB1
         MVI   &YDM2+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NSPYDM21
.*
.YDM2LAB1 ANOP
         MVC   &YDM2+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NSPYDM21 ANOP
         MVC   &YDM2+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._D+1 MOVE DAY
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NSPYDM22
         AIF   ('&DATESP1'(1,1) NE '''').YDM2LAB2
         MVI   &YDM2+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NSPYDM22
.*
.YDM2LAB2 ANOP
         MVC   &YDM2+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NSPYDM22 ANOP
         MVC   &YDM2+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._M+1 MOVE MONT-
               H
.NOYDM2  ANOP
         AIF   (&YMD2 EQ '').NOYMD2
* YYMMDD
&OFFSET  SETA  0
         MVC   &YMD2+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._Y+3 MOVE YEAR
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NSPYMD21
         AIF   ('&DATESP1'(1,1) NE '''').YMD2LAB1
         MVI   &YMD2+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NSPYMD21
.*
.YMD2LAB1 ANOP
         MVC   &YMD2+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NSPYMD21 ANOP
         MVC   &YMD2+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._M+1 MOVE MONT-
               H
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NSPYMD22
         AIF   ('&DATESP1'(1,1) NE '''').YMD2LAB2
         MVI   &YMD2+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NSPYMD22
.*
.YMD2LAB2 ANOP
         MVC   &YMD2+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NSPYMD22 ANOP
         MVC   &YMD2+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._D+1 MOVE DAY
.NOYMD2  ANOP
         AIF   (&DMY4 EQ '').NODMY4
* DDMMYYYY
&OFFSET  SETA  0
         MVC   &DMY4+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._D+1 MOVE DAY
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NSPDMY41
         AIF   ('&DATESP1'(1,1) NE '''').DMY4LAB1
         MVI   &DMY4+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NSPDMY41
.*
.DMY4LAB1 ANOP
         MVC   &DMY4+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NSPDMY41 ANOP
         MVC   &DMY4+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._M+1 MOVE MONT-
               H
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NSPDMY42
         AIF   ('&DATESP1'(1,1) NE '''').DMY4LAB2
         MVI   &DMY4+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NSPDMY42
.*
.DMY4LAB2 ANOP
         MVC   &DMY4+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NSPDMY42 ANOP
         MVC   &DMY4+&OFFSET.(4),CICS_FORMATTIME_&SYSNDX._Y+1 MOVE YEAR
.NODMY4  ANOP
         AIF   (&MDY4 EQ '').NOMDY4
* MMDDYYYY
&OFFSET  SETA  0
         MVC   &MDY4+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._M+1 MOVE MONT-
               H
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NSPMDY41
         AIF   ('&DATESP1'(1,1) NE '''').MDY4LAB1
         MVI   &MDY4+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NSPMDY41
.*
.MDY4LAB1 ANOP
         MVC   &MDY4+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NSPMDY41 ANOP
         MVC   &MDY4+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._D+1 MOVE DAY
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NSPMDY42
         AIF   ('&DATESP1'(1,1) NE '''').MDY4LAB2
         MVI   &MDY4+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NSPMDY42
.*
.MDY4LAB2 ANOP
         MVC   &MDY4+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NSPMDY42 ANOP
         MVC   &MDY4+&OFFSET.(4),CICS_FORMATTIME_&SYSNDX._Y+1 MOVE YEAR
.NOMDY4  ANOP
         AIF   (&YDM4 EQ '').NOYDM4
* YYYYDDMM
&OFFSET  SETA  0
         MVC   &YDM4+&OFFSET.(4),CICS_FORMATTIME_&SYSNDX._Y+1 MOVE YEAR
&OFFSET  SETA  &OFFSET+4
         AIF   (&DATESP1 EQ '').NSPYDM41
         AIF   ('&DATESP1'(1,1) NE '''').YDM4LAB1
         MVI   &YDM4+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NSPYDM41
.*
.YDM4LAB1 ANOP
         MVC   &YDM4+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NSPYDM41 ANOP
         MVC   &YDM4+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._D+1 MOVE DAY
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NSPYDM42
         AIF   ('&DATESP1'(1,1) NE '''').YDM4LAB2
         MVI   &YDM4+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NSPYDM42
.*
.YDM4LAB2 ANOP
         MVC   &YDM4+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NSPYDM42 ANOP
         MVC   &YDM4+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._M+1 MOVE MONT-
               H
.NOYDM4  ANOP
         AIF   (&YMD4 EQ '').NOYMD4
* YYYYMMDD
&OFFSET  SETA  0
         MVC   &YMD4+&OFFSET.(4),CICS_FORMATTIME_&SYSNDX._Y+1 MOVE YEAR
&OFFSET  SETA  &OFFSET+4
         AIF   (&DATESP1 EQ '').NSPYMD41
         AIF   ('&DATESP1'(1,1) NE '''').YMD4LAB1
         MVI   &YMD4+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NSPYMD41
.*
.YMD4LAB1 ANOP
         MVC   &YMD4+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NSPYMD41 ANOP
         MVC   &YMD4+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._M+1 MOVE MONT-
               H
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NSPYMD42
         AIF   ('&DATESP1'(1,1) NE '''').YMD4LAB2
         MVI   &YMD4+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NSPYMD42
.*
.YMD4LAB2 ANOP
         MVC   &YMD4+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NSPYMD42 ANOP
         MVC   &YMD4+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._D+1 MOVE DAY
.NOYMD4  ANOP
         AIF   (&YD2 EQ '').NOYD2
* YYDDD
&OFFSET  SETA  0
         MVC   &YD2+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._Y+3 MOVE YEAR
&OFFSET  SETA  &OFFSET+2
         AIF   (&DATESP1 EQ '').NSPYD21
         AIF   ('&DATESP1'(1,1) NE '''').YD2LAB1
         MVI   &YD2+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NSPYD21
.*
.YD2LAB1 ANOP
         MVC   &YD2+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NSPYD21 ANOP
         MVC   &YD2+&OFFSET.(3),CICS_FORMATTIME_&SYSNDX._DDD+1 MOVE DAY-
                NUMBER
.NOYD2   ANOP
         AIF   (&YD4 EQ '').NOYD4
* YYYYDDD
&OFFSET  SETA  0
         MVC   &YD4+&OFFSET.(4),CICS_FORMATTIME_&SYSNDX._Y+1 MOVE YEAR
&OFFSET  SETA  &OFFSET+4
         AIF   (&DATESP1 EQ '').NSPYD41
         AIF   ('&DATESP1'(1,1) NE '''').YD4LAB1
         MVI   &YD4+&OFFSET,C&DATESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NSPYD41
.*
.YD4LAB1 ANOP
         MVC   &YD4+&OFFSET.(1),&DATESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NSPYD41 ANOP
         MVC   &YD4+&OFFSET.(3),CICS_FORMATTIME_&SYSNDX._DDD+1 MOVE DAY-
                NUMBER
.NOYD4   ANOP
         AIF   (&DAYCNT EQ '').NODYCNT
         MVC   &DAYCNT.(4),CICS_FORMATTIME_&SYSNDX._DAYCOUNT MOVE DAY C-
               OUNT
.NODYCNT ANOP
         AIF   (&DAYOFWK EQ '').NODYOFW
         MVC   &DAYOFWK.(4),CICS_FORMATTIME_&SYSNDX._DAYNUMB MOVE DAY O-
               F WEEK
.NODYOFW ANOP
         AIF   (&DAYOFMN EQ '').NODYOFM
         MVC   &DAYOFMN.(4),CICS_FORMATTIME_&SYSNDX._DAYNO MOVE DAY OF -
               MONTH
.NODYOFM ANOP
         AIF   (&MNOFYR EQ '').NOMNOFY
         MVC   &MNOFYR.(4),CICS_FORMATTIME_&SYSNDX._MONTH MOVE MONTH
.NOMNOFY ANOP
         AIF   (&YEAR EQ '').NOYEAR
         MVC   &YEAR.(4),CICS_FORMATTIME_&SYSNDX._YEAR MOVE YEAR
.NOYEAR  ANOP
         AIF   (&DSTRING EQ '').NODSTR
         L     R1,CICS_FORMATTIME_&SYSNDX._DAYNUMB R1=DAY NUMBER
         MH    R1,=H'3'           * 3
         LA    R1,CICS_FORMATTIME_&SYSNDX._DTAB(R1) INDEX TO DATE TABLE
         MVC   &DSTRING.(3),0(R1) MOVE CHAR DAY
         MVI   &DSTRING+3,C',' COMMA
         MVI   &DSTRING+4,C' ' SPACE
         MVC   &DSTRING+5(2),CICS_FORMATTIME_&SYSNDX._D+1 MOVE DAY NUMB
         MVI   &DSTRING+7,C' ' SPACE
         L     R1,CICS_FORMATTIME_&SYSNDX._MONTH R1=MONTH
         AHI   R1,-1              -1 FOR INDEX
         MH    R1,=H'3'           * 3
         LA    R1,CICS_FORMATTIME_&SYSNDX._MTAB(R1) INDEX TO MONTH TAB
         MVC   &DSTRING+8(3),0(R1) MOVE CHAR MONTH
         MVI   &DSTRING+11,C' ' SPACE
         MVC   &DSTRING+12(4),CICS_FORMATTIME_&SYSNDX._Y+1 MOVE YEAR
         MVI   &DSTRING+16,C' ' SPACE
         MVC   &DSTRING+17(2),CICS_FORMATTIME_&SYSNDX._HH+1 MOVE HOURS
         MVI   &DSTRING+19,C':' COLON
         MVC   &DSTRING+20(2),CICS_FORMATTIME_&SYSNDX._MM+1 MOVE MINS
         MVI   &DSTRING+22,C':' COLON
         MVC   &DSTRING+23(2),CICS_FORMATTIME_&SYSNDX._SS+1 MOVE SECS
         B     CICS_FORMATTIME_&SYSNDX._BYPTABS
CICS_FORMATTIME_&SYSNDX._DTAB DS 0C  DAY TABLE
         DC    C'SunMonTueWedThuFriSat'
CICS_FORMATTIME_&SYSNDX._MTAB DS 0C  MONTH TABLE
         DC    C'JanFebMarAprMayJunJulAugSepOctNovDec'
CICS_FORMATTIME_&SYSNDX._BYPTABS DS 0H
.*
.NODSTR  ANOP
         AIF   (&TIME EQ '').NOTIME
         AIF   (NOT(&TIMESP2)).NODEFTM
&TIMESP1 SETC  ''':'''
.NODEFTM ANOP
* HMS
         MVC   &TIME+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._HH+1 MOVE HOU-
               RS
&OFFSET  SETA  &OFFSET+2
         AIF   (&TIMESP1 EQ '').NOSEPT1
         AIF   ('&TIMESP1'(1,1) NE '''').TLAB1
         MVI   &TIME+&OFFSET,C&TIMESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NOSEPT1
.*
.TLAB1   ANOP
         MVC   &TIME+&OFFSET.(1),&TIMESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NOSEPT1 ANOP
         MVC   &TIME+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._MM+1 MOVE MIN-
               S
&OFFSET  SETA  &OFFSET+2
         AIF   (&TIMESP1 EQ '').NOSEPT2
         AIF   ('&TIMESP1'(1,1) NE '''').TLAB2
         MVI   &TIME+&OFFSET,C&TIMESP1 MOVE SEPARATOR
&OFFSET  SETA  &OFFSET+1
         AGO   .NOSEPT2
.*
.TLAB2   ANOP
         MVC   &TIME+&OFFSET.(1),&TIMESP1 MOVE SEPARATOR FROM LABEL
&OFFSET  SETA  &OFFSET+1
.NOSEPT2 ANOP
         MVC   &TIME+&OFFSET.(2),CICS_FORMATTIME_&SYSNDX._SS+1 MOVE SEC-
               S
*
.NOTIME  ANOP
         B     CICS_FORMATTIME_&SYSNDX._IGNORE
*
CICS_FORMATTIME_&SYSNDX._INVREQ DS 0H
.* INVOKE INVREQ/1
         MVC   EIBRESP2,=F'1'     SET EIBRESP2=BAD ABSTIME
         MVI   EIBRCODE+3,X'01'   SET EIBRCODE
         MVC   EIBRESP,DFHRESP(INVREQ) SET EIBRESP=INVREQ
         DC    AL3(0),C'ABEND'    MARKER FOR Z390KCP ESTAE
         DC    AL4(CICS_FORMATTIME_&SYSNDX._IGNORE) ENTPT FOR IGNRE CND
*
CICS_FORMATTIME_&SYSNDX._ABSTIME  DS PL16
CICS_FORMATTIME_&SYSNDX._DAYCOUNT DS F
CICS_FORMATTIME_&SYSNDX._LEAPS    DS F
CICS_FORMATTIME_&SYSNDX._WHOLEYR  DS F
         ORG   CICS_FORMATTIME_&SYSNDX._LEAPS
CICS_FORMATTIME_&SYSNDX._CVDOUT   DS FL8
.*
CICS_FORMATTIME_&SYSNDX._THISYR   DS F
CICS_FORMATTIME_&SYSNDX._YEAR     DS F
CICS_FORMATTIME_&SYSNDX._MONTH    DS F
CICS_FORMATTIME_&SYSNDX._DAYNO    DS F
CICS_FORMATTIME_&SYSNDX._HOURS    DS F
CICS_FORMATTIME_&SYSNDX._MINS     DS F
CICS_FORMATTIME_&SYSNDX._SECS     DS F
CICS_FORMATTIME_&SYSNDX._DAYNUMB  DS F
CICS_FORMATTIME_&SYSNDX._MNTHTAB  DC AL2(31,28,31,30,31,30,31,31,30,31,-
               30,31)
         ORG   CICS_FORMATTIME_&SYSNDX._MNTHTAB
CICS_FORMATTIME_&SYSNDX._DDD DS CL4
CICS_FORMATTIME_&SYSNDX._D DS CL3
CICS_FORMATTIME_&SYSNDX._M DS CL3
CICS_FORMATTIME_&SYSNDX._Y DS CL5
CICS_FORMATTIME_&SYSNDX._HH DS CL3
CICS_FORMATTIME_&SYSNDX._MM DS CL3
CICS_FORMATTIME_&SYSNDX._SS DS CL3
.*
CICS_FORMATTIME_&SYSNDX._IGNORE DS 0H
         MEXIT
.*
.MISSABS MNOTE 12,'ABSTIME IS MANDATORY'
         MEXIT
         MEND
