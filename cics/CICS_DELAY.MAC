.**********************************************************************
.* Copyright 2006 Automated Software Tools Corporation                *
.* This source code is part of z390 assembler/emulator package        *
.* The z390 package is distributed under GNU general public license   *
.* Author - Don Higgins                                               *
.**********************************************************************
.* 04/19/08 RPI 833 ADD STRING QUOTES FOR HLASM COMPATIBILITY         *
.**********************************************************************
         MACRO
         CICS_DELAY
         GBLB  &PARMS_1004
         LCLB  &FOR,&UNTIL,&NOHAND
         LCLB  &ONLYH,&ONLYM,&ONLYS
         LCLC  &INTVAL,&TIME,&HOURS,&MINS,&SECS,&REQID,&RESP,&RESP2
&NP      SETA  N'&SYSLIST
&IP      SETA  0
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  '&SYSLIST(&IP)'
         AIF   ('&PARM'(1,9) EQ 'INTERVAL(').INTVAL
         AIF   ('&PARM'(1,5) EQ 'TIME(').TIME
         AIF   ('&PARM'(1,6) EQ 'HOURS(').HOURS
         AIF   ('&PARM'(1,8) EQ 'MINUTES(').MINS
         AIF   ('&PARM'(1,8) EQ 'SECONDS(').SECS
         AIF   ('&PARM'(1,6) EQ 'REQID(').REQID
         AIF   ('&PARM'(1,3) EQ 'FOR').FOR
         AIF   ('&PARM'(1,5) EQ 'UNTIL').UNTIL
         AIF   ('&PARM'(1,8) EQ 'NOHANDLE').NOHAND
         AIF   ('&PARM'(1,5) EQ 'RESP(').RESP
         AIF   ('&PARM'(1,6) EQ 'RESP2(').RESP2
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.INTVAL  ANOP
&INTVAL  SETC  '&PARM'(10,K'&PARM-10)
         AGO   .PLOOP
.*
.TIME    ANOP
&TIME    SETC  '&PARM'(6,K'&PARM-6)
         AGO   .PLOOP
.*
.HOURS   ANOP
&HOURS   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.MINS    ANOP
&MINS    SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.*
.SECS    ANOP
&SECS    SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.*
.REQID   ANOP
&REQID   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.FOR     ANOP
&FOR     SETB  1
         AGO   .PLOOP
.*
.UNTIL   ANOP
&UNTIL   SETB  1
         AGO   .PLOOP
.*
.NOHAND  ANOP
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP    ANOP
&RESP    SETC  '&PARM'(6,K'&PARM-6)
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP2   ANOP
&RESP2   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.ELOOP   ANOP
         AIF   ('&REQID'  NE '').BADREQ
         AIF   ('&INTVAL' NE '' AND '&TIME' NE '').BOTHIT
         AIF   (&FOR AND &UNTIL).BOTHFU
         AIF   ('&INTVAL' NE '' AND &FOR).BADFORUN
         AIF   ('&INTVAL' NE '' AND &UNTIL).BADFORUN
         AIF   ('&TIME'   NE '' AND &FOR).BADFORUN
         AIF   ('&TIME'   NE '' AND &UNTIL).BADFORUN
         AIF   ('&INTVAL' NE '' AND '&HOURS' NE '').BADHMS
         AIF   ('&INTVAL' NE '' AND '&MINS'  NE '').BADHMS
         AIF   ('&INTVAL' NE '' AND '&SECS'  NE '').BADHMS
         AIF   ('&TIME'   NE '' AND '&HOURS' NE '').BADHMS
         AIF   ('&TIME'   NE '' AND '&MINS'  NE '').BADHMS
         AIF   ('&TIME'   NE '' AND '&SECS'  NE '').BADHMS
         AIF   ('&HOURS'  NE '' AND NOT(&FOR OR &UNTIL)).BADHMS2
         AIF   ('&MINS'   NE '' AND NOT(&FOR OR &UNTIL)).BADHMS2
         AIF   ('&SECS'   NE '' AND NOT(&FOR OR &UNTIL)).BADHMS2
         AIF   ((&FOR OR &UNTIL) AND '&HOURS' EQ '' AND '&MINS' EQ '' A-
               ND '&SECS' EQ '').BADFUN
.* DEFAULT SETTING IS INTERVAL(0)
         AIF   ('&INTVAL' EQ '' AND '&TIME' EQ '' AND '&HOURS' EQ '' AN-
               D '&MINS' EQ '' AND '&SECS' EQ '').DEFINT
         AGO   .PARMOK
.*
.DEFINT  ANOP
&INTVAL  SETC  '0'
.PARMOK  ANOP
         L     R15,=A(P1004)      LOAD PARM ADDRESS
         USING (P1004,P1004END),R15
         XC    P1004(P1004END-P1004),P1004 CLEAR PARMS
         MVC   P1004FN,=X'1004'   SET FUNCTION CODE
.* JOIN UP INTERVAL AND TIME
         AIF   ('&INTVAL' EQ '').NOTINT
&HHMMSS  SETC  '&INTVAL'
         MVI   P1004INT,X'FF'     SET INTERVAL/TIME INDICATOR
         AGO   .DOHMS
.*
.NOTINT  ANOP
         AIF   ('&TIME' EQ '').NOTIT
&HHMMSS  SETC  '&TIME'
         MVI   P1004INT,X'FF'     SET INTERVAL/TIME INDICATOR
         MVI   P1004TME,X'FF'     SET TIME/UNTIL
.DOHMS   ANOP
.* PROCESS INTERVAL/TIME
         AIF   (T'&HHMMSS NE 'N').TIMELAB
&KHMS    SETA  K'&HHMMSS
         AIF   (&KHMS GT 6).BADTIME
         ZAP   P1004HMS,=P'&HHMMSS' PACK HMS
         AGO   .DOCALL
.*
.TIMELAB ANOP
         PACK  P1004HRS+2(4),&HHMMSS.(7) PACK FROM LABEL+BYTE
         MVO   P1004HMS,P1004HRS+2(3) MOVE HMS FROM LABEL
         OI    P1004HMS+3,X'0C'   SET SIGN
         AGO   .DOCALL
.*
.NOTIT   ANOP
.* PROCESS HOURS MINUTES SECONDS
.* TEST IF MORE THAN 1 PARM
         AIF   ('&HOURS' NE '' AND '&MINS' EQ '' AND '&SECS' EQ '').DOS-
               ING
         AIF   ('&HOURS' EQ '' AND '&MINS' NE '' AND '&SECS' EQ '').DOS-
               ING
         AIF   ('&HOURS' EQ '' AND '&MINS' EQ '' AND '&SECS' NE '').DOS-
               ING
.* MORE THAN ONE PARM
         MVI   P1004IND,X'FF'     SET MORE THAN 1 PARM INDICATOR
.DOSING  ANOP
         AIF   ('&HOURS' NE '').TRYM
&HOURS   SETC  '0'
.TRYM    ANOP
         AIF   ('&MINS' NE '').TRYS
&MINS    SETC  '0'
.TRYS    ANOP
         AIF   ('&SECS' NE '').DOFUHMS
&SECS    SETC  '0'
.DOFUHMS ANOP
         ZAP   P1004HRS,=P'&HOURS' SET HOURS
         ZAP   P1004MIN,=P'&MINS' SET MINUTES
         ZAP   P1004SEC,=P'&SECS' SET SECONDS
         AIF   (NOT &UNTIL).DOCALL
         MVI   P1004TME,X'FF'     SET TIME/UNTIL
.DOCALL  ANOP
         ST    DFHEIBR,P1004EIB   SET EIB ADDRESS
         ST    TCTTEAR,P1004TCT   SET TCTTE ADDRESS
         AIF   (NOT &NOHAND).DUNNOH
         MVI   P1004NOH,X'FF'     SET NOHANDLE
.DUNNOH  ANOP
         AIF   ('&RESP' EQ '').NORESP
         LA    R1,&RESP           ADDRESS OF RESP
         ST    R1,P1004RSP        SAVE IT
.NORESP  ANOP
         AIF   ('&RESP2' EQ '').NORESP2
         LA    R1,&RESP2          ADDRESS OF RESP2
         ST    R1,P1004RS2        SAVE IT
.NORESP2 ANOP
         ZCALL LCL1004,(P1004),MF=I CALL DELAY PROCESSOR
.*
         L     R15,=A(P1004)      LOAD PARM ADDRESS
         CLI   P1004NOH,X'FF'     NOHANDLE ?
         BE    P1004BYP_&SYSNDX   EXIT IF IT IS
         OC    EIBRESP,EIBRESP    ANY BAD RESPONSE ?
         BZ    P1004BYP_&SYSNDX   EXIT IF NONE
* INVREQ, EXPIRED
         DC    AL3(0),C'ABEND'    MARKER FOR Z390KCP ESTAE
         DC    AL4(P1004BYP_&SYSNDX) ENTRYPOINT FOR IGNORE CONDITION
*
         AIF   (&PARMS_1004).BYP1004
&PARMS_1004 SETB 1
P1004    EQU   *
P1004FN  DS    XL2                FUNCTION CODE
P1004EIB DS    AL4                EIB
P1004TCT DS    AL4                TCTTE
P1004INT DS    X                  FF=INTERVAL/TIME
P1004TME DS    X                  00=INTERVAL/FOR  FF=TIME/UNTIL
P1004HMS DS    PL4                0HHMMSS+
P1004IND DS    X                  FF=MORE THAN 1 PARM IN NEXT 3
P1004HRS DS    PL8                HOURS
P1004MIN DS    PL8                MINUTES
P1004SEC DS    PL8                SECONDS
P1004NOH DS    X                  FF=NOHANDLE
P1004RSP DS    AL4                RESP
P1004RS2 DS    AL4                RESP2
P1004END EQU   *                  END MARKER
         DS    0H
*
.BYP1004 ANOP
P1004BYP_&SYSNDX DS 0H
         DROP  R15
         MEXIT
.*
.BADREQ  MNOTE 12,'REQID NOT SUPPORTED'
         MEXIT
.*
.BOTHIT  MNOTE 12,'BOTH INTERVAL AND TIME ARE SPECIFIED'
         MEXIT
.*
.BOTHFU  MNOTE 12,'BOTH FOR AND UNTIL ARE SPECIFIED'
         MEXIT
.*
.BADFORUN MNOTE 12,'INTERVAL/TIME CANNOT BE SPECIFIED WITH FOR/UNTIL'
         MEXIT
.*
.BADHMS  MNOTE 12,'HOURS/MINUTES/SECONDS ARE INVALID WITH INTERVAL OR T-
               IME'
         MEXIT
.*
.BADHMS2 MNOTE 12,'HOURS/MINUTES/SECONDS ARE SPECIFIED WITHOUT FOR/UNTI-
               L'
         MEXIT
.*
.BADTIME MNOTE 12,'INTERVAL/TIME MUST BE 1 TO 6 BYTES'
         MEXIT
.*
.BADFUN  MNOTE 12,'FOR/UNTIL SPECIFIED, BUT NO TIME PARAMETERS'
         MEXIT
         MEND
