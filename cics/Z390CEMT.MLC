*********************************************************************
* Copyright 2006 Automated Software Tools Corporation               *
* This source code is part of z390 assembler/emulator package       *
* The z390 package is distributed under GNU general public license  *
* Author - Don Higgins                                              *
*********************************************************************
         DFHEIGBL
         DFHEISTG
PARSE    DS    0H
PARSE1   DS    CL20               COMMAND
PARSE2   DS    CL20               OPERATION TYPE
PARSE3   DS    CL20               OPERATION MODIFIER
PARSELEN EQU   *-PARSE
*
DFHPCTAD DS    A                  PCT ADDRESS
*
* I SYS  MESSAGE AREA
SNDISYST DS    CL(ISYSTITE)
SNDISYSL DS    CL(ISYSLSTL)
* I TER  MESSAGE AREA
SNDITERT DS    CL(ITERTITE)
SNDITERL DS    CL(ITERLSTL)
* I TRA MESSAGE AREA
SNDITRNT DS    CL(ITRNTITE)
SNDITRNL DS    CL(ITRNLSTL)
* I FIL MESSAGE AREA
SNDIFILT DS    CL(IFILTITE)
SNDIFILL DS    CL(IFILLSTL)
*
Z390CEMT DFHEIENT CODEREG=(R12,R3)
*
* CEMT FUNCTIONS
*   THE FULL SYSTAX IS SUPPORTED, EG. CEMT PERFORM SHUTDOWN IMMEDIATE
*   THE FOLLOWING MINIMUMS ARE TESTED
*
*   I SYS        REQUEST Z390CICS.INI INFO AND DISPLAY
*   I TER        REQUEST TERMINAL INFO AND DISPLAY
*   I TRA        PCT DISPLAY
*   I FIL        FCT DISPLAY
*
*   S TER OUT    CLOSES THE Z390KCP THREAD (CLIENT)
*
*   P SHU        CLOSES THE Z390KCP THREAD (CLIENT)
*                WHEN ALL ACTIVE TASKS HAVE ENDED, CLOSE SERVER
*   P SHU IMM    CLOSE THE SERVER
*
         L     R6,TCTTEDA         INITIAL TIOA (NULL TERMINATED)
         AHI   R6,3               MISS AID, CURSOR ADDRESS
         CLI   0(R6),SBA          PRECEDING SBA ?
         BNE   NOSBA              EXIT IF NOT
         AHI   R6,3               MISS SBA
NOSBA    EQU   *
         AHI   R6,4               MISS CEMT
         XC    PARSE(PARSELEN),PARSE CLEAR PARSE AREA
*
* FIND 1ST PARM
*
         BAL   R14,NEXTPARM       LOCATE START OF 1ST PARM
         CLI   0(R6),X'00'        END OF TIOA ?
         BE    PARSERR            ERROR IF 1ST PARM NOT FOUND
         LA    R7,PARSE1          PARM ADDRESS
         BAL   R14,EXTRACT        EXTRACT PARM
*
* FIND 2ND PARM
*
         BAL   R14,NEXTPARM       LOCATE START OF 2ND PARM
         CLI   0(R6),X'00'        END OF TIOA ?
         BE    PARSERR            ERROR IF 2ND PARM NOT FOUND
         LA    R7,PARSE2          PARM ADDRESS
         BAL   R14,EXTRACT        EXTRACT PARM
*
* FIND OPTIONAL 3RD PARM
*
         BAL   R14,NEXTPARM       LOCATE START OF 2ND PARM
         CLI   0(R6),X'00'        END OF TIOA ?
         BE    PROCESS            EXIT IF NO 3RD PARM
         LA    R7,PARSE3          PARM ADDRESS
         BAL   R14,EXTRACT        EXTRACT PARM
*
PROCESS  EQU   *
         OI    PARSE1,C' '        CONVERT 1ST PARM TO UPPER CASE
         OC    PARSE2(3),=4C' '   CONVERT 2ND PARM TO UPPER CASE
         OC    PARSE3(4),=4C' '   CONVERT 3RD PARM TO UPPER CASE
* SYNTAX CHECKS
         CLI   PARSE1,C'I'        I ?
         BNE   TRYS               EXIT IF NOT
* CEMT I
         CLC   PARSE2(3),=C'SYS'  I SYS ?
         BE    ISYS               EXIT IF IT IS
         CLC   PARSE2(3),=C'TER'  I TER ?
         BE    ITER               EXIT IF IT IS
         CLC   PARSE2(3),=C'TRA'  I TRA ?
         BE    ITRN               EXIT IF IT IS
         CLC   PARSE2(3),=C'FIL'  I FIL ?
         BE    IFIL               EXIT IF IT IS
         B     PARSERR            EXIT IF BAD
*
ISYS     EQU   *
* PROCESS CEMT I SYS, IGNORE OTHER PARMS
         L     R8,CEMTI           R8=MESSAGE LENGTH
         LA    R5,CEMTI           R5=MESSAGE
         MVC   CEMTIREQ,=C'FE01'  SET REQUEST CODE
         MVC   CEMTITRM,TCTTETI   SET TERMID
         TCPIO SEND,MSG=(R5),LMSG=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   TCPIOERR           EXIT IF BAD
         L     R8,CEMTRCVE        R8=MESSAGE LENGTH
         TCPIO RECEIVE,MSG=CEMTRCVE,LMSG=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   TCPIOERR           EXIT IF BAD
         MVC   SNDISYST,ISYSTITL  SET UP TITLE
         LA    R6,SNDISYSL        R6=DESTINATION
         LA    R8,ISYSLIST        R8=SOURCE
         LA    R7,ISYSLSTL        DESTINATION LENGTH
         LR    R9,R7              SOURCE LENGTH
         MVCL  R6,R8              INITIALIZE DISPLAY
         LA    R6,SNDISYSL        R6=DISPLAY ADDRESS
         USING ISYSDSCT,R6
         LA    R7,CEMTDATA        R7=RECEIVED DATA
         USING ISYSDATA,R7
* MOVE THE DATA
         MVC   ISYSJPTH,ISYSDJRP  MOVE JARPATH
         MVC   ISYSCPTH,ISYSDCSP  MOVE CICSPATH
         MVC   ISYSTPFX,ISYSDPFX  MOVE TERMID_PREFIX
         MVC   ISYSTDAT,=C'DMY'   SET DATE FORMAT DMY
         CLI   ISYSDDTE,C'K'      IS DATE=K ?
         BE    ISYSDTDN           EXIT IF IT IS
         MVC   ISYSTDAT,=C'MDY'   SET DATE FORMAT MDY
         CLI   ISYSDDTE,C'U'      IS DATE=U ?
         BE    ISYSDTDN           EXIT IF IT IS
         MVC   ISYSTDAT,=C'YMD'   SET DATE FORMAT YMD
ISYSDTDN EQU   *
         MVC   ISYSMAXU+1(1),ISYSDTUS MOVE MAXTHREADS IN USE
         OI    ISYSMAXU+1,C'0'    CONVERT TO CHAR
         CLI   ISYSDTUS,X'0A'     IS IT 10 ?
         BNE   ISYSHEX1           EXIT IF NOT
         MVC   ISYSMAXU,=C'10'    SET 10
ISYSHEX1 EQU   *
         MVC   ISYSMAXT+1(1),ISYSDTMX MOVE MAXTHREADS
         OI    ISYSMAXT+1,C'0'      CONVERT TO CHAR
         CLI   ISYSDTMX,X'0A'     IS IT 10 ?
         BNE   ISYSHEX2           EXIT IF NOT
         MVC   ISYSMAXT,=C'10'    SET 10
ISYSHEX2 EQU   *
         MVC   ISYSLCLU+1(1),ISYSDLUS MOVE LOCAL TERMINALS IN USE
         OI    ISYSLCLU+1,C'0'      CONVERT TO CHAR
         CLI   ISYSDLUS,X'0A'     IS IT 10 ?
         BNE   ISYSHEX3           EXIT IF NOT
         MVC   ISYSLCLU,=C'10'    SET 10
ISYSHEX3 EQU   *
         MVC   ISYSLOCL+1(1),ISYSDLCL MOVE LOCAL_TERMINALS
         OI    ISYSLOCL+1,C'0'      CONVERT TO CHAR
         CLI   ISYSDLCL,X'0A'     IS IT 10 ?
         BNE   ISYSHEX4           EXIT IF NOT
         MVC   ISYSLOCL,=C'10'    SET 10
ISYSHEX4 EQU   *
         MVC   ISYSRMTU+1(1),ISYSDRUS MOVE REMOTE TERMINALS IN USE
         OI    ISYSRMTU+1,C'0'      CONVERT TO CHAR
         CLI   ISYSDRUS,X'0A'     IS IT 10 ?
         BNE   ISYSHEX5           EXIT IF NOT
         MVC   ISYSRMTU,=C'10'    SET 10
ISYSHEX5 EQU   *
         MVC   ISYSREMT+1(1),ISYSDRMT MOVE REMOTE_TERMINALS
         OI    ISYSREMT+1,C'0'      CONVERT TO CHAR
         CLI   ISYSDRMT,X'0A'     IS IT 10 ?
         BNE   ISYSHEX6           EXIT IF NOT
         MVC   ISYSREMT,=C'10'    SET 10
ISYSHEX6 EQU   *
         MVC   ISYSLTRC,ISYSDTRL  MOVE TRACE_LOCALS
         MVC   ISYSZTRC,ISYSDTRZ  MOVE TRACE_Z390CICS
         MVC   ISYSINIT,ISYSDINT  MOVE INITIAL_TRANSID
         MVC   ISYSSEQT,ISYSDSEQ  MOVE SEQ_TERM
         EXEC  CICS SEND FROM(SNDISYST) LENGTH(ISYSLEN)
         B     RETURN             END OF PROGRAM
*
ITER     EQU   *
* PROCESS CEMT I TER, IGNORE OTHER PARMS
         L     R8,CEMTI           R8=MESSAGE LENGTH
         LA    R5,CEMTI           R5=MESSAGE
         MVC   CEMTIREQ,=C'FE00'  SET REQUEST CODE
         MVC   CEMTITRM,TCTTETI   SET TERMID
         TCPIO SEND,MSG=(R5),LMSG=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   TCPIOERR           EXIT IF BAD
         L     R8,CEMTRCVE        R8=MESSAGE LENGTH
         TCPIO RECEIVE,MSG=CEMTRCVE,LMSG=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   TCPIOERR           EXIT IF BAD
         MVC   SNDITERT,ITERTITL  SET UP TITLE
         LA    R6,SNDITERL        R6=DESTINATION
         LA    R8,ITERLIST        R8=SOURCE
         LA    R7,ITERLSTL        DESTINATION LENGTH
         LR    R9,R7              SOURCE LENGTH
         MVCL  R6,R8              INITIALIZE LIST
         LA    R6,SNDITERL        R6=LIST ADDRESS
         USING ITERDSCT,R6
         LA    R7,CEMTDATA        R7=RECEIVED DATA
         USING THRDDSCT,R7
         LA    R8,11              R8=MAX THREADS + 1 FOR SEQ TERM
         LA    R9,C'0'            R9=TERMID SUFFIX
ITERLOOP EQU   *
         MVC   ITERTERM(3),CEMTDATA+11*THRDLEN MOVE TERMID PREFIX
         STC   R9,ITERTERM+3      MOVE TERMID SUFFIX
         C     R8,=F'1'           LAST TERMINAL ?
         BNE   ITERNTSQ           EXIT IF NOT
         MVC   ITERTERM,=C'SQ01'  SET SEQ TERM
ITERNTSQ EQU   *
         CLI   THRDTYPE,THRDTYPR  IS IT UNALLOCATED ?
         BE    ITERLOC            EXIT IF IT IS
         MVC   ITERTYPE,=CL6'LOCAL' PRESUME LOCAL
         CLI   THRDTYPE,LOCAL     IS IT LOCAL ?
         BE    ITERLOC            EXIT IF IT IS
         MVC   ITERTYPE,=C'REMOTE' SET REMOTE
ITERLOC  EQU   *
         MVC   ITERTRAN,THRDTRAN  MOVE TRANSID
         MVC   ITERIND,=CL9'RUNNING' PRESUME RUNNING
         CLI   THRDIND,RUNNING    RUNNING STATUS ?
         BE    ITERNXT            EXIT IF IT IS
         MVC   ITERIND,=C'AVAILABLE' SET AVAILABLE
         CLI   THRDIND,AVAIL      AVAILABLE STATUS ?
         BE    ITERNXT            EXIT IF IT IS
         MVC   ITERIND,=C'SHUT DOWN' SET SHUT DOWN
ITERNXT  EQU   *
         AHI   R6,ITERLEN         BUMP OUTPUT POINTER
         AHI   R7,THRDLEN         BUMP INPUT POINTER
         AHI   R9,1               BUMP TERMID SUFFIX
         BCT   R8,ITERLOOP        LOOP
         EXEC  CICS SEND FROM(SNDITERT) LENGTH(ITERMLEN)
         B     RETURN             END OF PROGRAM
*
ITRN     EQU   *
* PROCESS CEMT I TRA, IGNORE OTHER PARMS
* ### LIMIT OF 17
* ### LATER, EXTEND AND CODE PAGE FORWARD (PF7) AND BACK (PF8)
         EXEC  CICS LOAD PROGRAM('DFHPCT') SET(R6)
         ST    R6,DFHPCTAD        SAVE LOAD POINT
         MVC   SNDITRNT,ITRNTITL  SET UP TITLE
         LA    R6,SNDITRNL        R6=DESTINATION
         LA    R8,ITRNLIST        R8=SOURCE
         LA    R7,ITRNLSTL        DESTINATION LENGTH
         LR    R9,R7              SOURCE LENGTH
         MVCL  R6,R8              INITIALIZE LIST
         LA    R6,SNDITRNL        R6=LIST ADDRESS
         USING ITRNDSCT,R6
         L     R7,DFHPCTAD        R7=PCT ADDRESS
         LA    R8,17              R8=MAX TRANSIDS
ITRNLOOP EQU   *
         MVC   ITRNTRAN,0(R7)     MOVE TRANSID
         MVC   ITRNPROG,4(R7)     MOVE PROGRAM
         MVC   BLDLNAME,4(R7)     SAVE PROGRAM NAME
         BLDL  0,BLDLLIST         BLDL
         LTR   R15,R15            RC=4 IF ENTRY NOT FOUND
         BZ    ITRNINCR           EXIT IF FOUND
* PROGRAM NOT FOUND
         MVC   ITRNMSG,=C'PROGRAM NOT FOUND' SET MESSAGE
ITRNINCR EQU   *
         AHI   R6,ITRNLEN         BUMP OUTPUT POINTER
         AHI   R7,12              BUMP PCT POINTER
         CLI   0(R7),X'FF'        END OF PCT ?
         BE    ITRNSEND           EXIT IF IT IS
         BCT   R8,ITRNLOOP        LOOP
ITRNSEND EQU   *
         EXEC  CICS SEND FROM(SNDITRNT) LENGTH(ITRNMLEN)
         EXEC  CICS RELEASE PROGRAM('DFHPCT')
         B     RETURN             END OF PROGRAM
*
IFIL     EQU   *
* PROCESS CEMT I FIL, IGNORE OTHER PARMS
         L     R8,CEMTI           R8=MESSAGE LENGTH
         LA    R5,CEMTI           R5=MESSAGE
         MVC   CEMTIREQ,=C'FE05'  SET REQUEST CODE
         MVC   CEMTITRM,TCTTETI   SET TERMID
         TCPIO SEND,MSG=(R5),LMSG=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   TCPIOERR           EXIT IF BAD
* RECEIVE THE PREFIX ONLY
* THIS COULD BE A LARGE AREA, SO WE NEED TO GETMAIN A RECEIVE AREA
         TCPIO RECEIVE,MSG=CEMTRCVE,LMSG=12,PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   TCPIOERR           EXIT IF BAD
         L     R8,CEMTRCVE        R8=ACTUAL LENGTH TO RECEIVE
         MVC   CEMTRCVE,=F'200'   RESTORE THE STANDARD RECEIVE LENGTH
         AHI   R8,-12             -12 FOR DATA RECEIVED
         GETMAIN R,LV=(R8),A=IFILGETM
         ST    R0,IFILGETL        SAVE LENGTH FOR FREEMAIN
         TCPIO RECEIVE,MSG=0(R1),LMSG=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   TCPIOERR           EXIT IF BAD
         MVC   SNDIFILT,IFILTITL  SET UP TITLE
         LA    R6,SNDIFILL        R6=DESTINATION
         LA    R8,IFILLIST        R8=SOURCE
         LA    R7,IFILLSTL        DESTINATION LENGTH
         LR    R9,R7              SOURCE LENGTH
         MVCL  R6,R8              INITIALIZE LIST
         LA    R6,SNDIFILL        R6=LIST ADDRESS
         USING IFILDSCT,R6
         L     R7,IFILGETM        R7=RECEIVED DATA
         USING IFILDATA,R7
IFILLOOP EQU   *
         MVC   IFILFILE,IFILDFLE  MOVE FILENAME
         MVC   IFILOPE,=C'OPE'    PRESET OPEN
         TM    IFILDSTA,FCTOPCLO  IS IT OPEN ?
         BO    IFILOPEN           EXIT IF IT IS
         MVC   IFILOPE,=C'CLO'    SET CLOSED
IFILOPEN EQU   *
         MVC   IFILENA,=C'ENA'    PRESET ENABLED
         TM    IFILDSTA,FCTENDIS  IS IT ENABLED ?
         BO    IFILENAB           EXIT IF IT IS
         MVC   IFILOPE,=C'DIS'    SET DISABLED
IFILENAB EQU   *
         TM    IFILDSTA,FCTUNENA  IS IT UNENABLED ?
         BNO   IFILNTUN           EXIT IF NOT
         MVC   IFILOPE,=C'UNE'    SET UNENABLED
IFILNTUN EQU   *
         MVC   IFILREA,=C'REA'    SET READ
         TM    IFILDSRV,FCTREAD   IS IT READ ?
         BO    IFILREAD           EXIT IF IT IS
         MVC   IFILREA,=C'   '    SET NO READ
IFILREAD EQU   *
         MVC   IFILUPD,=C'UPD'    SET UPDATE
         TM    IFILDSRV,FCTUPDT   IS IT UPDATE ?
         BO    IFILUPDT           EXIT IF IT IS
         MVC   IFILUPD,=C'   '    SET NO UPDATE
IFILUPDT EQU   *
         MVC   IFILADD,=C'ADD'    SET ADD
         TM    IFILDSRV,FCTADD    IS IT ADD ?
         BO    IFILADD1           EXIT IF IT IS
         MVC   IFILADD,=C'   '    SET NO ADD
IFILADD1 EQU   *
         MVC   IFILBRO,=C'BRO'    SET BROWSE
         TM    IFILDSRV,FCTBRWSE  IS IT BROWSE ?
         BO    IFILBRWS           EXIT IF IT IS
         MVC   IFILBRO,=C'   '    SET NO READ
IFILBRWS EQU   *
         MVC   IFILDEL,=C'DEL'    SET DELETE
         TM    IFILDSRV,FCTDELT   IS IT DELETE ?
         BO    IFILGOFV           EXIT IF IT IS
         MVC   IFILDEL,=C'   '    SET NO DELETE
IFILGOFV EQU   *
         MVC   IFILFXVR,=C'FIX'   SET FIXED
         TM    IFILDFXV,FCTFXVIM  IS IT FIXED ?
         BO    IFILINCR           EXIT IF IT IS
         MVC   IFILFXVR,=C'VAR'   SET VARIABLE
IFILINCR EQU   *
         MVC   IFILDSN,IFILDDSN   MOVE DSNAME
         AHI   R6,IFILLEN         BUMP OUTPUT POINTER
         AHI   R7,IFILDLEN        BUMP INPUT POINTER
         CLI   IFILDFLE,X'FF'     END OF FILES ?
         BNE   IFILLOOP           LOOP IF NOT
         EXEC  CICS SEND FROM(SNDIFILT) LENGTH(IFILMLEN)
         FREEMAIN LA=IFILGETL,A=IFILGETM
         B     RETURN             END OF PROGRAM
IFILGETM DS    A                  GETMAIN ADDRESS
IFILGETL DS    F                  GETMAIN LENGTH
         DROP  R6,R7
*
TRYS     EQU   *
* SYNTAX CHECK
         CLI   PARSE1,C'S'        S ?
         BNE   TRYP               EXIT IF NOT
         CLC   PARSE2(3),=C'TER'  S TER ?
         BNE   PARSERR            ERROR IF NOT
         CLC   PARSE3(3),=C'OUT'  S TER OUT ?
         BNE   PARSERR            ERROR IF NOT
* PROCESS CEMT S TER OUT
         MVI   TCTTETS,X'FF'      SET SHUTTERM REQUEST
         B     RETURN             END OF PROGRAM
*
TRYP     EQU   *
         L     R8,CEMTI           R8=MESSAGE LENGTH
         LA    R5,CEMTI           R5=MESSAGE
         CLI   PARSE1,C'P'        P ?
         BNE   PARSERR            ERROR IF NOT
         CLC   PARSE2(3),=C'SHU'  P SHU ?
         BNE   PARSERR            ERROR IF NOT
         MVC   CEMTIREQ,=C'FE02'  SET REQUEST CODE
         MVC   CEMTITRM,TCTTETI   SET TERMID
         CLC   PARSE3(3),=C'IMM'  P SHU IMM ?
         BNE   PSEND              EXIT IF NOT
         MVC   CEMTIREQ,=C'FE03'  SET REQUEST CODE
PSEND    EQU   *
         TCPIO SEND,MSG=(R5),LMSG=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   TCPIOERR           EXIT IF BAD
         B     RETURN             EXIT
*
PARSERR  EQU   *
         EXEC  CICS SEND FROM(SYNTAX) LENGTH(SYNTAXL)
         B     RETURN             EXIT
*
TCPIOERR EQU   *
         EXEC  CICS SEND FROM(TCPERR) LENGTH(TCPERRL)
         B     RETURN             EXIT
*
RETURN   EQU   *
         EXEC  CICS RETURN
*
* SUBROUTINES
*
NEXTPARM EQU   *
* FIND START OF NEXT PARM
* R6=CURRENT LOCATION
         CLI   0(R6),X'00'        TIOA DONE ?
         BER   R14                EXIT IT IS IT
         CLI   0(R6),C' '         FIND BLANK ?
         BNER  R14                EXIT WHEN PARM FOUND
         AHI   R6,1               BUMP TIOA POINTER
         B     NEXTPARM           LOOP
*
EXTRACT  EQU   *
* EXTRACT PARM
* R6=1ST PARM CHAR
* R7=PARM LOCATION
         SR    R8,R8              ZERO PARM LENGTH
EXTRLOOP EQU   *
         TM    0(R6),X'BF'        END OF PARM (BLANK OR X'00') ?
         BER   R14                RETURN IF IT IS
         CH    R8,=H'21'          TOO MANY CHARS ?
         BE    PARSERR            YES, ERROR IF OVER 20 CHARS
         MVC   0(1,R7),0(R6)      MOVE PARM CHAR
         AHI   R6,1               BUMP TIOA POINTER
         AHI   R7,1               BUMP PARM POINTER
         AHI   R8,1               COUNT THEM
         B     EXTRLOOP           LOOP
*
CEMTI    DS    0F
* REQUEST INFO
* FE00  REQUEST I TER DATA
* FE01  REQUEST I SYS DATA
* FE02  REQUEST P SHU
* FE03  REQUEST P SHU IMM
* FE05  REQUEST I FIL DATA
         DC    A(CEMTIEND-*)
CEMTIREQ DS    CL4                REQUEST ID
CEMTITRM DS    CL4                TERMID
CEMTIEND EQU   *
*
CEMTRCVE DS    0F
* RECEIVE AREA
* MUST KEEP THIS CONSISTANT WITH ALL Z390CICS SENDS
         DC    A(200)             MAX LENGTH
         DS    CL4                REQUEST CODE
         DS    CL4                TERMINAL ID
CEMTDATA DS    CL188              RETURNED DATA
*
* I SYS -- STRUCTURES
*
* -----KEYWORD----  VALUE---------------
* JARPATH           E:\SOFTWARE\Z390
* TERMID_PREFIX     DON
* THREADS            4 OF 10 ALLOCATED
*
ISYSTITL EQU   *
         TN3270 ESC,WRT_EW,WCC_KD,(SBA,1,1),(SF,ATT_UA),IC
         DC    C'CEMT I SYS'
         TN3270 (SBA,3,1),(SF,ATT_PA)
         DC    C'-----KEYWORD----  VALUE---------------'
ISYSTITE EQU   *-ISYSTITL
*
ISYSLIST EQU   *
         TN3270 (SBA,4,2)
         DC    CL18'JARPATH'
         DC    CL50' '
         TN3270 (SBA,5,2)
         DC    CL18'CICSPATH'
         DC    CL50' '
         TN3270 (SBA,6,2)
         DC    CL18'TERMID_PREFIX'
         DC    CL3' '
         TN3270 (SBA,7,2)
         DC    CL18'DATE_FORMAT'
         DC    CL3' '
         TN3270 (SBA,8,2)
         DC    CL18'THREADS'
         DC    C' N OF  N ALLOCATED'
         TN3270 (SBA,9,2)
         DC    CL18'LOCAL_TERMINALS'
         DC    C' N OF  N ALLOCATED'
         TN3270 (SBA,10,2)
         DC    CL18'REMOTE_TERMINALS'
         DC    C' N OF  N ALLOCATED'
         TN3270 (SBA,11,2)
         DC    CL18'INITIAL_TRANSID'
         DC    CL4' '
         TN3270 (SBA,12,2)
         DC    CL18'TRACE_LOCALS'
         DC    CL3' '
         TN3270 (SBA,13,2)
         DC    CL18'TRACE_Z390CICS'
         DC    CL3' '
         TN3270 (SBA,14,2)
         DC    CL18'SEQ_TERM'
         DC    CL3' '
ISYSLSTL EQU   *-ISYSLIST
ISYSLEN  DC    AL2(*-ISYSTITL)
*
* I TER -- STRUCTURES
*
* TERM  -TYPE-  TRAN  -STATUS--
* DON0  REMOTE  GUI6  RUNNING
* DON1  LOCAL         AVAILABLE
* DON2  LOCAL         SHUT DOWN
*
ITERTITL EQU   *
         TN3270 ESC,WRT_EW,WCC_KD,(SBA,1,1),(SF,ATT_UA),IC
         DC    C'CEMT I TER'
         TN3270 (SBA,3,1),(SF,ATT_PA)
         DC    C'TERM  -TYPE-  TRAN  -STATUS--'
ITERTITE EQU   *-ITERTITL
*
ITERLIST EQU   *
         TN3270 (SBA,4,2)
         DC    CL(ITERLEN-3)' '
         TN3270 (SBA,5,2)
         DC    CL(ITERLEN-3)' '
         TN3270 (SBA,6,2)
         DC    CL(ITERLEN-3)' '
         TN3270 (SBA,7,2)
         DC    CL(ITERLEN-3)' '
         TN3270 (SBA,8,2)
         DC    CL(ITERLEN-3)' '
         TN3270 (SBA,9,2)
         DC    CL(ITERLEN-3)' '
         TN3270 (SBA,10,2)
         DC    CL(ITERLEN-3)' '
         TN3270 (SBA,11,2)
         DC    CL(ITERLEN-3)' '
         TN3270 (SBA,12,2)
         DC    CL(ITERLEN-3)' '
         TN3270 (SBA,13,2)
         DC    CL(ITERLEN-3)' '
         TN3270 (SBA,14,2)
         DC    CL(ITERLEN-3)' '
ITERLSTL EQU   *-ITERLIST
ITERMLEN DC    AL2(*-ITERTITL)
*
* I TRAN -- STRUCTURES
*
* TRAN  PROGRAM-  ----MESSAGES-----
* AAAA  MELPROG1  PROGRAM NOT FOUND
* CEMT  Z390CEMT
*
ITRNTITL EQU   *
         TN3270 ESC,WRT_EW,WCC_KD,(SBA,1,1),(SF,ATT_UA),IC
         DC    C'CEMT I TRA'
         TN3270 (SBA,3,1),(SF,ATT_PA)
         DC    C'TRAN  PROGRAM-  ----MESSAGES-----'
ITRNTITE EQU   *-ITRNTITL
*
ITRNLIST EQU   *
         TN3270 (SBA,4,2)
         DC    CL(ITRNLEN-3)' '
         TN3270 (SBA,5,2)
         DC    CL(ITRNLEN-3)' '
         TN3270 (SBA,6,2)
         DC    CL(ITRNLEN-3)' '
         TN3270 (SBA,7,2)
         DC    CL(ITRNLEN-3)' '
         TN3270 (SBA,8,2)
         DC    CL(ITRNLEN-3)' '
         TN3270 (SBA,9,2)
         DC    CL(ITRNLEN-3)' '
         TN3270 (SBA,10,2)
         DC    CL(ITRNLEN-3)' '
         TN3270 (SBA,11,2)
         DC    CL(ITRNLEN-3)' '
         TN3270 (SBA,12,2)
         DC    CL(ITRNLEN-3)' '
         TN3270 (SBA,13,2)
         DC    CL(ITRNLEN-3)' '
         TN3270 (SBA,14,2)
         DC    CL(ITRNLEN-3)' '
         TN3270 (SBA,15,2)
         DC    CL(ITRNLEN-3)' '
         TN3270 (SBA,16,2)
         DC    CL(ITRNLEN-3)' '
         TN3270 (SBA,17,2)
         DC    CL(ITRNLEN-3)' '
         TN3270 (SBA,18,2)
         DC    CL(ITRNLEN-3)' '
         TN3270 (SBA,19,2)
         DC    CL(ITRNLEN-3)' '
         TN3270 (SBA,20,2)
         DC    CL(ITRNLEN-3)' '
ITRNLSTL EQU   *-ITRNLIST
ITRNMLEN DC    AL2(*-ITRNTITL)
*
* I FIL -- STRUCTURES
*
* FILENAME  ----------- STATUS ------------  DSNAME--------------------
* MYFILE01  OPE ENA REA UPD ADD BRO DEL FIX  E:\MYFILE01
*
IFILTITL EQU   *
         TN3270 ESC,WRT_EW,WCC_KD,(SBA,1,1),(SF,ATT_UA),IC
         DC    C'CEMT I FIL'
         TN3270 (SBA,3,1),(SF,ATT_PA)
         DC    C'FILENAME  ----------- STATUS ------------  DSNAME-----X
               -------------------------'
IFILTITE EQU   *-IFILTITL
*
IFILLIST EQU   *
         TN3270 (SBA,4,2)
         DC    CL(IFILLEN-3)' '
         TN3270 (SBA,5,2)
         DC    CL(IFILLEN-3)' '
         TN3270 (SBA,6,2)
         DC    CL(IFILLEN-3)' '
         TN3270 (SBA,7,2)
         DC    CL(IFILLEN-3)' '
         TN3270 (SBA,8,2)
         DC    CL(IFILLEN-3)' '
         TN3270 (SBA,9,2)
         DC    CL(IFILLEN-3)' '
         TN3270 (SBA,10,2)
         DC    CL(IFILLEN-3)' '
         TN3270 (SBA,11,2)
         DC    CL(IFILLEN-3)' '
         TN3270 (SBA,12,2)
         DC    CL(IFILLEN-3)' '
         TN3270 (SBA,13,2)
         DC    CL(IFILLEN-3)' '
         TN3270 (SBA,14,2)
         DC    CL(IFILLEN-3)' '
         TN3270 (SBA,15,2)
         DC    CL(IFILLEN-3)' '
         TN3270 (SBA,16,2)
         DC    CL(IFILLEN-3)' '
         TN3270 (SBA,17,2)
         DC    CL(IFILLEN-3)' '
         TN3270 (SBA,18,2)
         DC    CL(IFILLEN-3)' '
IFILLSTL EQU   *-IFILLIST
IFILMLEN DC    AL2(*-IFILTITL)
*
SYNTAX   EQU   *
         TN3270 WCC_KD,(SBA,3,2)
         DC    C'CEMT SYNTAX ERROR'
SYNTAXL  DC    AL2(*-SYNTAX)
*
TCPERR   EQU   *
         TN3270 WCC_KD,(SBA,3,2)
         DC    C'CEMT TCPIO ERROR'
TCPERRL  DC    AL2(*-TCPERR)
*
BLDLLIST DC    H'1'       BLDL LIST WITH 1 ENTRY
         DC    H'12'      BLDL ENTRY WITH LENGTH EXCLUDING LENGTH FIELD
BLDLNAME DC    CL8' '     BLDL ENTRY NAME TO FIND IN SYS390 DIR OR CDE
         DC    XL4'00'    TTRK R=1 IF FOUND K=1 IF CDE OR 0 IF SYS390
*
THRDDSCT DSECT
* MUST KEEP THRDCNTL AND Z390CEMT IN SYNC
THRDIND  DS    X                  THREAD STATE
*                                    X'00'=AVAILABLE
*                                    X'80'=RUNNING
*                                    X'FF'=SHUTDOWN
AVAIL    EQU   X'00'
RUNNING  EQU   X'80'
THRDTYPE DS    C                  THREAD TYPE
*                                    X'00'=UNALLOCATED
*                                    L=LOCAL
*                                    R=REMOTE
LOCAL    EQU   C'L'
THRDTYPR EQU   X'00'
THRDTRAN DS    CL4                THREAD CURRENT TRANSACTION
THRDLEN  EQU   *-THRDDSCT
*
* I SYS -- DSECT OUTPUT
*
ISYSDSCT DSECT
         DS    CL3                SBA
         DS    CL18               HEADER
ISYSJPTH DS    CL50               JARPATH
         DS    CL3                SBA
         DS    CL18               HEADER
ISYSCPTH DS    CL50               CICSPATH
         DS    CL3                SBA
         DS    CL18               HEADER
ISYSTPFX DS    CL3                TERMID_PREFIX
         DS    CL3                SBA
         DS    CL18               HEADER
ISYSTDAT DS    CL3                DATE FORMAT
         DS    CL3                SBA
         DS    CL18               HEADER
* NN OF NN ALLOCATED
ISYSMAXU DS    CL2                MAXTHREADS IN USE
         DS    CL4                ' OF '
ISYSMAXT DS    CL2                MAXTHREADS
         DS    CL10               ' ALLOCATED'
         DS    CL3                SBA
         DS    CL18               HEADER
ISYSLCLU DS    CL2                LOCAL TERMINALS IN USE
         DS    CL4                ' OF '
ISYSLOCL DS    CL2                LOCAL_TERMINALS
         DS    CL10               ' ALLOCATED'
         DS    CL3                SBA
         DS    CL18               HEADER
ISYSRMTU DS    CL2                REMOTE TERMINALS IN USE
         DS    CL4                ' OF '
ISYSREMT DS    CL2                REMOTE_TERMINALS
         DS    CL10               ' ALLOCATED'
         DS    CL3                SBA
         DS    CL18               HEADER
ISYSINIT DS    CL4                INITIAL_TRANSID
         DS    CL3                SBA
         DS    CL18               HEADER
ISYSLTRC DS    CL3                TRACE_LOCALS
         DS    CL3                SBA
         DS    CL18               HEADER
ISYSZTRC DS    CL3                TRACE_Z390CICS
         DS    CL3                SBA
         DS    CL18               HEADER
ISYSSEQT DS    CL3                SEQ_TERM
*
* I SYS -- REQUESTED DATA FORMAT
*
ISYSDATA DSECT
ISYSDJRP DS    CL50               JARPATH
ISYSDCSP DS    CL50               CICSPATH
ISYSDPFX DS    CL3                TERMID_PREFIX
ISYSDDTE DS    C                  DATE FORMAT
ISYSDTMX DS    X                  MAXTHREADS
ISYSDTUS DS    X                  THREADS IN USE
ISYSDLCL DS    X                  LOCAL_TERMINALS
ISYSDLUS DS    X                  LOCAL TERMINALS IN USE
ISYSDRMT DS    X                  REMOTE_TERMINALS
ISYSDRUS DS    X                  REMOTE TERMINALS IN USE
ISYSDINT DS    CL4                INITIAL_TRANSID
ISYSDTRL DS    CL3                TRACE_LOCALS
ISYSDTRZ DS    CL3                TRACE_Z390CICS
ISYSDSEQ DS    CL3                SEQ_TERM
*
* I TER -- DSECT OUTPUT
*
ITERDSCT DSECT
         DS    CL3                SBA
ITERTERM DS    CL4                TERMID
         DS    CL2
ITERTYPE DS    CL6                LOCAL OR REMOTE
         DS    CL2
ITERTRAN DS    CL4                TRANSID
         DS    CL2
ITERIND  DS    CL9                RUNNING/AVAILABLE/SHUT DOWN
ITERLEN  EQU   *-ITERDSCT
*
* I TRA -- DSECT OUTPUT
*
ITRNDSCT DSECT
         DS    CL3                SBA
ITRNTRAN DS    CL4                TRANSID
         DS    CL2
ITRNPROG DS    CL8                PROGRAM
         DS    CL2
ITRNMSG  DS    CL17               MESSAGE
ITRNLEN  EQU   *-ITRNDSCT
*
* I FIL -- DSECT OUTPUT
*
IFILDSCT DSECT
         DS    CL3                SBA
IFILFILE DS    CL8                FILENAME
         DS    CL2
IFILOPE  DS    CL3                   OPE/CLO
         DS    C
IFILENA  DS    CL3                   ENA/DIS/UNE
         DS    C
IFILREA  DS    CL3
         DS    C
IFILUPD  DS    CL3
         DS    C
IFILADD  DS    CL3
         DS    C
IFILBRO  DS    CL3
         DS    C
IFILDEL  DS    CL3
         DS    C
IFILFXVR DS    CL3                   FIX/VAR
         DS    CL2
IFILDSN  DS    CL40               DSNAME
IFILLEN  EQU   *-IFILDSCT
*
*
* I FIL -- REQUESTED DATA FORMAT
*
IFILDATA DSECT
IFILDFLE DS    CL8                FILENAME
IFILDSTA DS    X                  STATUS
IFILDSRV DS    X                  SERVREQ
IFILDFXV DS    X                  FIXED/VARIABLE
IFILDDSN DS    CL40               DSNAME
IFILDLEN EQU   *-IFILDATA
*
         DFHFCTDS
         ACBD
*
         TN3270 SYMBOLS
         EQUREGS
         END
