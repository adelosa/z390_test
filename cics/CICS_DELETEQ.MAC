.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
         MACRO
         CICS_DELETEQ
         LCLC  &TYPE
         LCLC  &QUEUE,&QNAME
&TYPE    SETC  &SYSLIST(1)
         AIF   (&TYPE EQ 'TS').TS
         MNOTE 12,'DELETEQ TYPE NOT RECOGNIZED'
         MEXIT
.*
.TS      ANOP
&NP      SETA  N'&SYSLIST
&IP      SETA  1
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  &SYSLIST(&IP)
.* DISCARDED PARMS
         AIF   ('&PARM'(1,4)  EQ 'MAIN').PLOOP
         AIF   ('&PARM'(1,9)  EQ 'AUXILIARY').PLOOP
.*
         AIF   ('&PARM'(1,6)  EQ 'QUEUE(').QUEUE
         AIF   ('&PARM'(1,6)  EQ 'QNAME(').QNAME
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.QUEUE   ANOP
&QUEUE   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.QNAME   ANOP
&QNAME   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.ELOOP   ANOP
         AIF   (&QUEUE EQ '' AND &QNAME EQ '').NOQUEUE
         AIF   (&QUEUE NE '' AND &QNAME NE '').BOTHQ
.*
         MVC   EIBFN,=X'0A06'     SET EIBFN
         XC    EIBRCODE,EIBRCODE  CLEAR EIBRCODE
         XC    EIBRESP,EIBRESP    CLEAR EIBRESP
         XC    EIBRESP2,EIBRESP2  CLEAR EIBRESP2
.* GETMAIN AND MOVE THE DATA TO SEND
         LA    R1,TSPREFIX        R1=TS BLOCK LENGTH
         GETMAIN R,LV=(R1),A=DFHEITSP
         USING DFHTSBLK,R1
         ST    R0,TSPTCPIO        SAVE LENGTH FOR FREEMAIN
         MVC   TSPFN,=C'0A06'     SET FUNCTION CODE
         MVC   TSPTERM,TCTTETI    SET TERMINAL ID
         MVC   TSPTRAN,EIBTRNID   SET TRANSID
.*
         AIF   (&QUEUE EQ '').DOQNAME
.* GOT QUEUE
         AIF   ('&QUEUE'(1,1) EQ '''').QUSTRING
         MVC   EIBRSRCE,&QUEUE    SET EIBRSRCE AS LABEL
         MVC   TSPNAME(8),&QUEUE  SET TSP QUEUE NAME AS LABEL
         MVC   TSPNAME+8(8),=8C' ' CLEAR REMAINDER
         AGO   .CHKNAME
.*
.QUSTRING ANOP
&QULEN   SETA  K'&QUEUE
         AIF   (&QULEN GT 10).BADQ
         MVC   EIBRSRCE,=CL16&QUEUE SET EIBRSRCE AS STRING
         MVC   TSPNAME,=CL16&QUEUE SET TSP QUEUE NAME AS STRING
         AGO   .CHKNAME
.*
.* GOT QNAME
.DOQNAME ANOP
         AIF   ('&QNAME'(1,1) EQ '''').QNSTRING
         MVC   EIBRSRCE,&QNAME    SET EIBRSRCE AS LABEL
         MVC   TSPNAME,&QNAME     SET TSP QUEUE NAME AS LABEL
         AGO   .CHKNAME
.*
.QNSTRING ANOP
&QNLEN   SETA  K'&QNAME
         AIF   (&QNLEN GT 18).BADQ
         MVC   EIBRSRCE,=CL16&QNAME SET EIBRSRCE AS STRING
         MVC   TSPNAME,=CL16&QNAME SET TSP QUEUE NAME AS STRING
.*
.CHKNAME ANOP
         OC    TSPNAME,TSPNAME    ALL ZEROS ?
         BZ    CICS_DELETEQ_&SYSNDX._INVREQ ERROR IF IT IS
         CLI   TSPNAME,X'FA'      X'FA'-X'FF' ?
         BNL   CICS_DELETEQ_&SYSNDX._INVREQ ERROR IF IT IS
         CLC   TSPNAME(2),=C'**'  RESERVED ID ?
         BE    CICS_DELETEQ_&SYSNDX._INVREQ ERROR IF IT IS
         CLC   TSPNAME(2),=C'$$'  RESERVED ID ?
         BE    CICS_DELETEQ_&SYSNDX._INVREQ ERROR IF IT IS
         CLC   TSPNAME(2),=C'DF'  RESERVED ID ?
         BE    CICS_DELETEQ_&SYSNDX._INVREQ ERROR IF IT IS
* SEND REQUEST TO SERVER, GET REPLY
         LA    R15,TSPREFIX       R15=PREFIX LENGTH
         L     R14,DFHEITSP       RESTORE GETMAIN ADDRESS
         TCPIO SEND,MSG=(R14),LMSG=(R15),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   CICS_DELETEQ_&SYSNDX._INVREQ ERROR IF BAD
         TCPIO RECEIVE,MSG=(R14),LMSG==A(TSPREFIX),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   CICS_DELETEQ_&SYSNDX._INVREQ ERROR IF BAD
         LR    R1,R14             RE-ESTABLISH TS BLOCK BASE
         OC    TSPRETCD,TSPRETCD  BAD RETURN CODE ?
         BZ    CICS_DELETEQ_&SYSNDX._01 EXIT IF NONE
         CLC   TSPRETCD,DFHRESP(QIDERR) QIDERR ?
         BE    CICS_DELETEQ_&SYSNDX._QIDERR EXIT IF IT IS
* SERVER HAS SENT UNKNOWN VALUE IN TSPRETCD
         B     CICS_DELETEQ_&SYSNDX._INVREQ SHOULD NOT OCCUR
*
CICS_DELETEQ_&SYSNDX._01 DS 0H
         DROP  R1                 DROP TS BLOCK BASE
         B     CICS_DELETEQ_&SYSNDX._IGNORE EXIT
.*
CICS_DELETEQ_&SYSNDX._INVREQ DS 0H
.* INVOKE INVREQ
         MVI   EIBRCODE,X'20'     SET EIBRCODE
         MVC   EIBRESP,DFHRESP(INVREQ) SET EIBRESP=INVREQ
         DC    AL3(0),C'ABEND'    MARKER FOR Z390KCP ESTAE
         DC    AL4(CICS_DELETEQ_&SYSNDX._IGNORE) ENTPT FOR IGNORE COND
.*
.BADQGEN ANOP
CICS_DELETEQ_&SYSNDX._QIDERR DS 0H
.* INVOKE QIDERR
         MVI   EIBRCODE,X'02'     SET EIBRCODE
         MVC   EIBRESP,DFHRESP(QIDERR) SET EIBRESP=QIDERR
         DC    AL3(0),C'ABEND'    MARKER FOR Z390KCP ESTAE
         DC    AL4(CICS_DELETEQ_&SYSNDX._IGNORE) ENTPT FOR IGNORE COND
.*
CICS_DELETEQ_&SYSNDX._IGNORE DS 0H
         ICM   R1,15,DFHEITSP     ANY ADDRESS TO BE FREED ?
         BZ    CICS_DELETEQ_&SYSNDX._END EXIT IF NOT
         FREEMAIN LA=0(R1)
         XC    DFHEITSP,DFHEITSP  CLEAR ADDRESS
CICS_DELETEQ_&SYSNDX._END DS 0H
         MEXIT
.*
.NOQUEUE MNOTE 12,'QUEUE OR QNAME MUST BE SPECIFIED'
         MEXIT
.*
.BOTHQ   MNOTE 12,'BOTH QUEUE AND QNAME ARE SPECIFIED'
         MEXIT
.*
.BADQ    MNOTE 12,'INVALID QUEUE OR QNAME'
         MEXIT
.*
         MEND
