.**********************************************************************
.* Copyright 2006 Automated Software Tools Corporation                *
.* This source code is part of z390 assembler/emulator package        *
.* The z390 package is distributed under GNU general public license   *
.* Author - Don Higgins                                               *
.**********************************************************************
.* 04/19/08 RPI 833 ADD STRING QUOTES FOR HLASM COMPATIBILITY         *
.**********************************************************************
         MACRO
         CICS_DELETEQ
         GBLB  &PARMS_0A06
         LCLB  &NOHAND
         LCLC  &TYPE,&QUEUE,&QNAME,&RESP,&RESP2
&TYPE    SETC  '&SYSLIST(1)'
         AIF   ('&TYPE' EQ 'TS').TS
         MNOTE 12,'DELETEQ TYPE NOT RECOGNIZED'
         MEXIT
.*
.TS      ANOP
&NP      SETA  N'&SYSLIST
&IP      SETA  1
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  '&SYSLIST(&IP)'
.* DISCARDED PARMS
         AIF   ('&PARM'(1,4)  EQ 'MAIN').PLOOP
         AIF   ('&PARM'(1,9)  EQ 'AUXILIARY').PLOOP
.*
         AIF   ('&PARM'(1,6)  EQ 'QUEUE(').QUEUE
         AIF   ('&PARM'(1,6)  EQ 'QNAME(').QNAME
         AIF   ('&PARM'(1,8)  EQ 'NOHANDLE').NOHAND
         AIF   ('&PARM'(1,5)  EQ 'RESP(').RESP
         AIF   ('&PARM'(1,6)  EQ 'RESP2(').RESP2
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.QUEUE   ANOP
&QUEUE   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.QNAME   ANOP
&QNAME   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.NOHAND  ANOP
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP    ANOP
&RESP    SETC  '&PARM'(6,K'&PARM-6)
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP2   ANOP
&RESP2   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.ELOOP   ANOP
         AIF   ('&QUEUE' EQ '' AND '&QNAME' EQ '').NOQUEUE
         AIF   ('&QUEUE' NE '' AND '&QNAME' NE '').BOTHQ
.*
         L     R15,=A(P0A06)      LOAD PARM ADDRESS
         USING (P0A06,P0A06END),R15
         XC    P0A06(P0A06END-P0A06),P0A06 CLEAR PARMS
         MVC   P0A06FN,=X'0A06'   SET FUNCTION CODE
.*
         AIF   ('&QUEUE' EQ '').DOQNAME
.* GOT QUEUE
         AIF   ('&QUEUE'(1,1) EQ '''').QUSTRING
         MVC   P0A06QNM(8),&QUEUE SET QUEUE NAME AS LABEL
         MVC   P0A06QNM+8(8),=8C' ' CLEAR REMAINDER
         AGO   .CHKNAME
.*
.QUSTRING ANOP
&QULEN   SETA  K'&QUEUE
         AIF   (&QULEN GT 10).BADQ
         MVC   P0A06QNM,=CL16&QUEUE SET QUEUE NAME AS STRING
         AGO   .CHKNAME
.*
.* GOT QNAME
.DOQNAME ANOP
         AIF   ('&QNAME'(1,1) EQ '''').QNSTRING
         MVC   P0A06QNM,&QNAME    SET QUEUE NAME AS LABEL
         AGO   .CHKNAME
.*
.QNSTRING ANOP
&QNLEN   SETA  K'&QNAME
         AIF   (&QNLEN GT 18).BADQ
         MVC   P0A06QNM,=CL16&QNAME SET QUEUE NAME AS STRING
.CHKNAME ANOP
         ST    DFHEIBR,P0A06EIB   SET EIB ADDRESS
         ST    TCTTEAR,P0A06TCT   SET TCTTE ADDRESS
         AIF   (NOT &NOHAND).DUNNOH
         MVI   P0A06NOH,X'FF'     SET NOHANDLE
.DUNNOH  ANOP
         AIF   ('&RESP' EQ '').NORESP
         LA    R1,&RESP           ADDRESS OF RESP
         ST    R1,P0A06RSP        SAVE IT
.NORESP  ANOP
         AIF   ('&RESP2' EQ '').NORESP2
         LA    R1,&RESP2          ADDRESS OF RESP2
         ST    R1,P0A06RS2        SAVE IT
.NORESP2 ANOP
         ZCALL LCL0A06,(P0A06),MF=I CALL READQ PROCESSOR
.*
         L     R15,=A(P0A06)      LOAD PARM ADDRESS
         CLI   P0A06NOH,X'FF'     NOHANDLE ?
         BE    P0A06BYP_&SYSNDX   EXIT IF IT IS
         OC    EIBRESP,EIBRESP    ANY BAD RESPONSE ?
         BZ    P0A06BYP_&SYSNDX   EXIT IF NONE
* INVREQ, QIDERR
         DC    AL3(0),C'ABEND'    MARKER FOR Z390KCP ESTAE
         DC    AL4(P0A06BYP_&SYSNDX) ENTRYPOINT FOR IGNORE CONDITION
*
         AIF   (&PARMS_0A06).BYP0A06
&PARMS_0A06 SETB 1
P0A06    EQU   *
P0A06FN  DS    XL2                FUNCTION CODE
P0A06EIB DS    AL4                EIB
P0A06TCT DS    AL4                TCTTE
P0A06QNM DS    CL16               QUEUE NAME
P0A06NOH DS    X                  FF=NOHANDLE
P0A06RSP DS    AL4                RESP
P0A06RS2 DS    AL4                RESP2
P0A06END EQU   *                  END MARKER
         DS    0H
*
.BYP0A06 ANOP
P0A06BYP_&SYSNDX DS 0H
         DROP  R15
         MEXIT
.*
.NOQUEUE MNOTE 12,'QUEUE OR QNAME MUST BE SPECIFIED'
         MEXIT
.*
.BOTHQ   MNOTE 12,'BOTH QUEUE AND QNAME ARE SPECIFIED'
         MEXIT
.*
.BADQ    MNOTE 12,'INVALID QUEUE OR QNAME'
         MEXIT
.*
         MEND
