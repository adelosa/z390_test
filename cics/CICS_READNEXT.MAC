.**********************************************************************
.* Copyright 2006 Automated Software Tools Corporation                *
.* This source code is part of z390 assembler/emulator package        *
.* The z390 package is distributed under GNU general public license   *
.* Author - Don Higgins                                               *
.**********************************************************************
.* 04/19/08 RPI 833 ADD STRING QUOTES FOR HLASM COMPATIBILITY         *
.**********************************************************************
         MACRO
         CICS_READNEXT
.* XRBA AND FLENGTH ARE EXTENSIONS
         GBLB  &PARMS_060E
         LCLB  &RBA,&XRBA,&RRN,&NOHAND
         LCLC  &FILE,&DATASET,&INTO,&SET,&LENGTH,&FLENGTH,&RIDFLD
         LCLC  &REQID,&KLENGTH,&RESP,&RESP2
.*
&NP      SETA  N'&SYSLIST
&IP      SETA  0
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  '&SYSLIST(&IP)'
.*
         AIF   ('&PARM'(1,5)  EQ 'FILE(').FILE
         AIF   ('&PARM'(1,8)  EQ 'DATASET(').DATASET
         AIF   ('&PARM'(1,5)  EQ 'INTO(').INTO
         AIF   ('&PARM'(1,4)  EQ 'SET(').SET
         AIF   ('&PARM'(1,7)  EQ 'LENGTH(').LENGTH
         AIF   ('&PARM'(1,8)  EQ 'FLENGTH(').FLENGTH
         AIF   ('&PARM'(1,10) EQ 'KEYLENGTH(').KLENGTH
         AIF   ('&PARM'(1,7)  EQ 'RIDFLD(').RIDFLD
         AIF   ('&PARM'(1,6)  EQ 'REQID(').REQID
         AIF   ('&PARM'(1,3)  EQ 'RBA').RBA
         AIF   ('&PARM'(1,4)  EQ 'XRBA').XRBA
         AIF   ('&PARM'(1,3)  EQ 'RRN').RRN
         AIF   ('&PARM'(1,8)  EQ 'NOHANDLE').NOHAND
         AIF   ('&PARM'(1,5)  EQ 'RESP(').RESP
         AIF   ('&PARM'(1,6)  EQ 'RESP2(').RESP2
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.FILE    ANOP
&FILE    SETC  '&PARM'(6,K'&PARM-6)
         AGO   .PLOOP
.*
.DATASET ANOP
&DATASET SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.*
.INTO    ANOP
&INTO    SETC '&PARM'(6,K'&PARM-6)
         AGO   .PLOOP
.*
.SET     ANOP
&SET     SETC '&PARM'(5,K'&PARM-5)
         AGO   .PLOOP
.*
.LENGTH  ANOP
&LENGTH  SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.*
.FLENGTH ANOP
&FLENGTH SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.*
.KLENGTH ANOP
&KLENGTH SETC  '&PARM'(11,K'&PARM-11)
         AGO   .PLOOP
.*
.RIDFLD  ANOP
&RIDFLD  SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.*
.REQID   ANOP
&REQID   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.RBA     ANOP
&RBA     SETB  1
         AGO   .PLOOP
.*
.XRBA    ANOP
&XRBA    SETB  1
         AGO   .PLOOP
.*
.RRN     ANOP
&RRN     SETB  1
         AGO   .PLOOP
.*
.NOHAND  ANOP
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP    ANOP
&RESP    SETC  '&PARM'(6,K'&PARM-6)
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP2   ANOP
&RESP2   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.ELOOP   ANOP
         AIF   ('&FILE' EQ '' AND '&DATASET' EQ '').NOFILE
         AIF   ('&FILE' NE '' AND '&DATASET' NE '').BOTHF
         AIF   ('&INTO' EQ '' AND '&SET' EQ '').NOINTST
         AIF   ('&INTO' NE '' AND '&SET' NE '').BOTHINST
         AIF   ('&LENGTH' NE '' AND '&FLENGTH' NE '').BOTHLEN
         AIF   ('&RIDFLD' EQ '').NEEDRID
         AIF   (&RBA AND &XRBA).BADRBA
         AIF   (&RRN AND &RBA).BOTHTYP
         AIF   (&RRN AND &XRBA).BOTHTYP
.*
         L     R15,=A(P060E)      LOAD PARM ADDRESS
         USING (P060E,P060EEND),R15
         XC    P060E(P060EEND-P060E),P060E CLEAR PARMS
         MVC   P060EFN,=X'060E'   SET FUNCTION CODE
         LA    R1,&RIDFLD         R1=ADDRESS OF RIDFLD
         ST    R1,P060EARD        SAVE IT
         LA    R1,L'&RIDFLD       R1=LENGTH OF RIDFLD
         STC   R1,P060ELRD        SAVE IT
.*
         AIF   ('&FILE' EQ '').DODTAST
.* GOT FILE
         AIF   ('&FILE'(1,1) EQ '''').FLSTRING
         MVC   P060EFIL,&FILE     SET FILE NAME AS LABEL
         AGO   .CHKLEN
.*
.FLSTRING ANOP
&FLLEN   SETA  K'&FILE
         AIF   (&FLLEN GT 10).BADFILE
         MVC   P060EFIL,=CL8&FILE SET FILE NAME AS STRING
         AGO   .CHKLEN
.*
.* GOT DATASET
.DODTAST ANOP
         AIF   ('&DATASET'(1,1) EQ '''').DTSTRING
         MVC   P060EFIL,&DATASET  SET DATASET NAME AS LABEL
         AGO   .CHKLEN
.*
.DTSTRING ANOP
&DTLEN   SETA  K'&DATASET
         AIF   (&DTLEN GT 10).BADFILE
         MVC   P060EFIL,=CL8&DATASET SET DATASET NAME AS STRING
.*
.CHKLEN  ANOP
         AIF   ('&SET' EQ '').NOSET
         MVI   P060ESET,X'FF'     SET SET INDICATOR
         AGO   .DUNLEN
.*
.NOSET   ANOP
         LA    R1,&INTO           INTO ADDRESS
         ST    R1,P060EINT        SAVE IT
         AIF   ('&LENGTH' EQ '').NOLENG
         AIF   (T'&LENGTH NE 'N').LENLABL
         LA    R1,&LENGTH         R1=LENGTH
         ST    R1,P060ELEN        SAVE IT
         AGO   .DUNLEN
.*
.LENLABL ANOP
         LH    R1,&LENGTH         R1=LENGTH FROM LABEL
         ST    R1,P060ELEN        SAVE IT
         LA    R1,&LENGTH         R1=ADDRESS OF LENGTH
         ST    R1,P060ELAD        SAVE IT
         AGO   .DUNLEN
.*
.NOLENG  ANOP
         AIF   ('&FLENGTH' EQ '').NOLENS
         AIF   (T'&FLENGTH NE 'N').FLENLBL
         L     R1,=AL4(&FLENGTH)  R1=FLENGTH
         ST    R1,P060ELEN        SAVE IT
         AGO   .DUNLEN
.*
.FLENLBL ANOP
         L     R1,&FLENGTH        R1=FLENGTH FROM LABEL
         ST    R1,P060ELEN        SAVE IT
         LA    R1,&FLENGTH        R1=ADDRESS OF FLENGTH
         ST    R1,P060EFAD        SAVE IT
         AGO   .DUNLEN
.*
.NOLENS  ANOP
         LA    R1,L'&INTO         R1=IMPLIED LENGTH OF INTO
         ST    R1,P060ELEN        SAVE IT
.DUNLEN  ANOP
         AIF   ('&KLENGTH' EQ '').CHKOPT
         AIF   ('&KLENGTH'(2,1) EQ '''').KLENNUM
         AIF   (T'&KLENGTH NE 'N').KLENLAB
.KLENNUM ANOP
         LA    R1,&KLENGTH        R1=KEYLENGTH
         STH   R1,P060EKLN        SAVE IT
         MVI   P060EKIN,X'FF'     SET VALID KEYLENGTH
         AGO   .GENKEY
.*
.KLENLAB ANOP
         LH    R1,&KLENGTH        R1=KEYLENGTH FROM LABEL
         STH   R1,P060EKLN        SAVE IT
         MVI   P060EKIN,X'FF'     SET VALID KEYLENGTH
.GENKEY  ANOP
         LTR   R1,R1              IS IT KEYLENGTH(0) OR -VE ?
         BNP   CICS_READNEXT_&SYSNDX._NOKEY EXIT IF IT IS
         CH    R1,=H'128'         GT MAX ALLOWED ?
         BH    CICS_READNEXT_&SYSNDX._NOKEY EXIT IF IT IS
         BCTR  R1,0               -1 FOR DYNAMIC LENGTH
         STC   R1,CICS_READNEXT_&SYSNDX._MVCKEY+1 STORE LENGTH
CICS_READNEXT_&SYSNDX._MVCKEY EQU *
         MVC   P060ERID(0),&RIDFLD MOVE KEY
CICS_READNEXT_&SYSNDX._NOKEY EQU *
         AGO   .DUNOPT
.*
.CHKOPT  ANOP
         AIF   (NOT &RBA).NORBA
         MVC   P060ERID+4(4),&RIDFLD MOVE RIDFLD
         MVI   P060ERBA,X'FF'     SET RBA
         AGO   .DUNOPT
.*
.NORBA   ANOP
         AIF   (NOT &XRBA).NOXRBA
         MVC   P060ERID(8),&RIDFLD MOVE RIDFLD
         MVI   P060EXRB,X'FF'     SET XRBA
         AGO   .DUNOPT
.*
.NOXRBA  ANOP
         AIF   (NOT &RRN).NORRN
         MVC   P060ERID(4),&RIDFLD MOVE RIDFLD
         MVI   P060ERRN,X'FF'     SET RRN
         AGO   .DUNOPT
.*
.NORRN   ANOP
.* DEFAULT IS FULL KEY
         MVC   P060ERID,&RIDFLD   MOVE FULL KEY
.DUNOPT  ANOP
         AIF   ('&REQID' EQ '').DUNREQ
         AIF   (T'&REQID NE 'N').REQLABL
         MVC   P060EREQ,=AL2(&REQID) MOVE REQID
         AGO   .DUNREQ
.*
.REQLABL ANOP
         MVC   P060EREQ,&REQID    MOVE REQID AS LABEL
.DUNREQ  ANOP
         ST    DFHEIBR,P060EEIB   SET EIB ADDRESS
         ST    TCTTEAR,P060ETCT   SET TCTTE ADDRESS
         AIF   (NOT &NOHAND).DUNNOH
         MVI   P060ENOH,X'FF'     SET NOHANDLE
.DUNNOH  ANOP
         AIF   ('&RESP' EQ '').NORESP
         LA    R1,&RESP           ADDRESS OF RESP
         ST    R1,P060ERSP        SAVE IT
.NORESP  ANOP
         AIF   ('&RESP2' EQ '').NORESP2
         LA    R1,&RESP2          ADDRESS OF RESP2
         ST    R1,P060ERS2        SAVE IT
.NORESP2 ANOP
         ZCALL LCL060E,(P060E),MF=I CALL READNEXT PROCESSOR
.*
         L     R15,=A(P060E)      LOAD PARM ADDRESS
         AIF   ('&SET' EQ '').NOSET2
         L     &SET,P060ESTA      LOAD THE SET REGISTER
.NOSET2  ANOP
         CLI   P060ENOH,X'FF'     NOHANDLE ?
         BE    P060EBYP_&SYSNDX   EXIT IF IT IS
         OC    EIBRESP,EIBRESP    ANY BAD RESPONSE ?
         BZ    P060EBYP_&SYSNDX   EXIT IF NONE
* INVREQ, FILENOTFOUND, LENGERR, NOTFND, NOTOPEN, DISABLED, ENDFILE
* ILLOGIC
         DC    AL3(0),C'ABEND'    MARKER FOR Z390KCP ESTAE
         DC    AL4(P060EBYP_&SYSNDX) ENTRYPOINT FOR IGNORE CONDITION
*
         AIF   (&PARMS_060E).BYP060E
&PARMS_060E SETB 1
P060E    EQU   *
P060EFN  DS    XL2                FUNCTION CODE
P060EEIB DS    AL4                EIB
P060ETCT DS    AL4                TCTTE
P060EFIL DS    CL8                FILENAME
P060ESET DS    X                  FF=SET IS SPECIFIED
P060ESTA DS    AL4                ADDRESS FOR SET
P060EINT DS    AL4                ADDRESS OF INTO
P060ELEN DS    XL4                LENGTH
P060ELAD DS    AL4                ADDRESS OF LENGTH IF LABEL
P060EFAD DS    AL4                ADDRESS OF FLENGTH IF LABEL
P060EKIN DS    X                  FF=KEYLENGTH IS VALID
P060EKLN DS    XL2                KEYLENGTH
P060ERID DS    XL128              RIDFLD
P060EARD DS    AL4                A(RIDFLD)
P060ELRD DS    X                  L'RIDFLD
P060ERBA DS    X                  FF=RBA
P060EXRB DS    X                  FF=XRBA
P060ERRN DS    X                  FF=RRN
P060EREQ DS    XL2                REQID
P060ENOH DS    X                  FF=NOHANDLE
P060ERSP DS    AL4                RESP
P060ERS2 DS    AL4                RESP2
P060EEND EQU   *                  END MARKER
         DS    0H
*
.BYP060E ANOP
P060EBYP_&SYSNDX DS 0H
         DROP  R15
         MEXIT
.*
.NOFILE  MNOTE 12,'FILE OR DATASET MUST BE SPECIFIED'
         MEXIT
.*
.BOTHF   MNOTE 12,'BOTH FILE AND DATASET ARE SPECIFIED'
         MEXIT
.*
.BADFILE MNOTE 12,'INVALID FILE OR DATASET'
         MEXIT
.*
.NOINTST MNOTE 12,'INTO OR SET MUST BE SPECIFIED'
         MEXIT
.*
.BOTHINST MNOTE 12,'BOTH INTO AND SET ARE SPECIFIED'
         MEXIT
.*
.BOTHLEN MNOTE 12,'BOTH LENGTH AND FLENGTH ARE SPECIFIED'
         MEXIT
.*
.NEEDRID MNOTE 12,'RIDFLD IS MANDATORY'
         MEXIT
.*
.BADRBA  MNOTE 12,'BOTH RBA AND XRBA ARE SPECIFIED'
         MEXIT
.*
.BOTHTYP MNOTE 12,'BOTH RRN AND (X)RBA ARE SPECIFIED'
         MEXIT
         MEND
