.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/06/06 RPI 308 - ADD EXEC CICS SUPPORT FOR CICS PROLOG
.*********************************************************************
         MACRO
&N       DFHEIENT &CODEREG=(R12),&DATAREG=(R13)
.*
.* DEFINE EXEC CICS PROGRAM ENTRY
.*
.* THIS MACRO CALL REPLACES THE FIRST CSECT OF MLC SOURCE CODE IF
.* CICS AND PROLOG OPTIONS ON
.*
         GBLC  &DFHEIENT_CSECT
&DFHEIENT_CSECT SETC &N        SAVE CSECT FOR DFHEIEND RESET
&N       CSECT
*            R1 >>> A(COMMAREA)
* TCTTEAR (R10) >>> TCTTE
* DFHEIBR (R11) >>> EIB
*         (R14) >>> RETURN ADDRESS (LINK ONLY)
*
* SET ADDRESSING TO EIB
*
         USING DFHEIBLK,DFHEIBR
*
* SET ADDRESSING TO TCTTE
*
         USING DFHTCTTE,TCTTEAR
*
         LR    R2,R1              R2=SAVE USER PARM POINTER
* FIND LAST LINKER AND ESTABLISH LINKAGE
         LH    R15,TCTTELNK       R15=CURRENT LINK LEVEL
         AHI   R15,-1             -1
         SLL   R15,2              *4
         L     R1,TCTTELKA        R0=LKA ADDRESS
         AR    R1,R15             INDEX TO LAST LINK-LEVEL ENTRY
         L     R13,0(R1)          SAVE AREA OF LAST LINK-LEVEL
* DON'T SAVE THE REGS IF XCTL, THIS ISN'T A NEW LINK LEVEL
         CLI   EIBFN,X'0E'        PROGRAM CONTROL ?
         JNE   CICS_EIENT_&SYSNDX._NOSAVE EXIT IF NOT
         CLI   EIBFN+1,X'04'      XCTL ?
         JE    CICS_EIENT_&SYSNDX._NOSAVE EXIT IF IT IS
         SAVE  (R14,R12)          SAVE REGS IN PREV SAVE AREA
CICS_EIENT_&SYSNDX._NOSAVE EQU *
*
* SET 1ST CODE BASE
*
         BALR  &CODEREG(1),0      CODE BASE 1
         USING &N._BASE,&CODEREG(1)
.*
&N._BASE DS   0H
*
* GETMAIN THE DSA, AND SET UP THE CHAIN
*
         LAY   R1,(DFHEIEND-DFHEISTG) R1=DSA LENGTH
         GETMAIN R,LV=(R1)
         ST    R1,8(R13)          SAVE NEW DSA SAVE AREA IN PRIOR SAVE
         ST    R13,4(R1)          SAVE OLD SAVE AREA IN NEW DSA SAVE
         LR    R13,R1             DSA BASE 1
         USING DFHEISTG,R13
         XC    DFHEICAP(DFHEIUSR-DFHEICAP),DFHEICAP CLEAR PREFIX DSA
*
* SAVE DSA LENGTH
*
         ST    R0,DFHEILEN        SAVE DSA LENGTH FOR FREEMAIN
*
* SET COMMAREA ADDRESS
*
         MVC   DFHEICAP,0(R2)     SET COMMAREA ADDRESS IN DSA
*
* IF WE CAME FROM XCTL, WE MUST FREE THE PARM STORAGE AREA
*
         CLC   EIBFN,=X'0E04'     IS IT XCTL ?
         BNE   CICS_EIENT_&SYSNDX._01 EXIT IF NOT
         FREEMAIN A=(R2),LV=8
         B     CICS_EIENT_&SYSNDX._02 EXIT
         LTORG
.*
CICS_EIENT_&SYSNDX._01 DS 0H
*
* LINK ONLY
* FIND OUR LKA ENTRY AND STORE OUR DSA ADDRESS
*
         LH    R15,TCTTELNK       R15=CURRENT LINK LEVEL
         SLL   R15,2              *4
         L     R1,TCTTELKA        R0=LKA ADDRESS
         AR    R1,R15             INDEX TO LAST LINK-LEVEL ENTRY
         ST    R13,0(R1)          SAVE OUR DSA ADDRESS
CICS_EIENT_&SYSNDX._02 DS 0H
*
&CBASE   SETA  N'&CODEREG
         AIF   (&CBASE EQ 1).CEXIT
&S       SETA  1
.*
.CLOOP   ANOP
&S       SETA  &S+1
         AIF   (&S GT &CBASE).CEXIT
&COFFSET SETA  &COFFSET+4096
         LAY   &CODEREG(&S),4096(&CODEREG(&S-1)) CODE BASE &S
         USING &N._BASE+&COFFSET,&CODEREG(&S)
         AGO   .CLOOP
.*
.CEXIT   ANOP
&DBASE   SETA  N'&DATAREG
         AIF   (&DBASE EQ 1).DEXIT
&S       SETA  1
.*
.DLOOP   ANOP
&S       SETA  &S+1
         AIF   (&S GT &DBASE).DEXIT
&DOFFSET SETA  &DOFFSET+4096
         LAY   &DATAREG(&S),4096(&DATAREG(&S-1)) DSA BASE &S
         USING DFHEISTG+&DOFFSET,&DATAREG(&S)
         AGO   .DLOOP
.*
.DEXIT   ANOP
         MEND
