.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/06/06 RPI 308 - ADD EXEC CICS SUPPORT FOR CICS PROLOG
.*********************************************************************
         MACRO
&N       DFHEIENT &CODEBASE=R12   USER CODE BASE REG
.*
.*       DEFINE EXEC CICS PROGRAM ENTRY
.*
.*       THIS MACRO CALL REPLACES FIRST CSECT
.*       OF MLC SOURCE CODE IF CICS OPTION AND PROLOG OPTIONS ON
.*
         GBLC  &DFHEIENT_CSECT
&DFHEIENT_CSECT SETC &N        SAVE CSECT FOR DFHEIEND RESET
&N       CSECT
* R1 >>> A(TCTTE,EIB,COMMAREA)
*
         SAVE  (14,12)            SAVE REGS IN PREV DSA
         LR    R14,R1             R14=SAVE USER PARM POINTER
         BALR  &CODEBASE,0
         USING *,&CODEBASE        CODEBASE > USER CODE BASE
*
* GETMAIN THE DSA, AND SET UP THE CHAIN
*
         L     R1,=A(DFHEIEND-DFHEISTG) R1=DSA LENGTH
         GETMAIN R,LV=(R1)
         ST    R1,8(R13)      SAVE NEW DSA SAVE AREA IN PRIOR SAVE
         ST    R13,4(R1)      SAVE OLD SAVE AREA IN NEW DSA SAVE
         LR    R13,R1         ESTABLISH THE DSA BASE
         USING DFHEISTG,R13   R13 > DSA
         ST    R0,DFHEILEN    SAVE DSA LENGTH FOR DFHEAI1 FREEMAIN
*
* SET ADDRESSING TO EIB
*
         L     DFHEIBR,4(R14)     DFHEIBR > EIB
         ST    DFHEIBR,DFHEIEIB   SET EIB ADDRESS IN DSA
         USING DFHEIBLK,DFHEIBR
*
* SET ADDRESSING TO TCTTE
*
         L     TCTTEAR,0(R14)     TCTTEAR > TCTTE
         ST    TCTTEAR,DFHEITCT   SET TCTTE ADDRESS IN DSA
         USING DFHTCTTE,TCTTEAR
*
* SET COMMAREA ADDRESS
*
         MVC   DFHEICAP,8(R14)    SET COMMAREA ADDRESS IN DSA
*
* IF WE CAME FROM XCTL, WE MUST FREE THE PARM STORAGE AREA
*
         CLC   EIBFN,=X'0E04'     IS IT XCTL ?
         BNE   CICS_EIENT_&SYSNDX._01 EXIT IF NOT
         FREEMAIN A=(R14),LV=16
CICS_EIENT_&SYSNDX._01 DS 0H
         MEND
