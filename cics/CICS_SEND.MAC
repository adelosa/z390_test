.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/05/06 RPI 308 CICS RETURN CALLED FROM EXEC
.*********************************************************************
         MACRO
         CICS_SEND
         LCLC  &FROM,&LENGTH
&NP      SETA  N'&SYSLIST
&IP      SETA  0
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  &SYSLIST(&IP)
         AIF   ('&PARM'(1,5) EQ 'FROM(').FROM
         AIF   ('&PARM'(1,7) EQ 'LENGTH(').LENGTH
         AGO   .PLOOP
.FROM    ANOP
&FROM    SETC  '&PARM'(6,K'&PARM-6)
         AGO   .PLOOP
.LENGTH  ANOP
&LENGTH  SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.ELOOP   ANOP
         AIF   (&FROM EQ '').BADFROM
         AIF   (&LENGTH EQ '').BADLEN
         AIF   ('&LENGTH'(1,2) EQ 'L''').LENNUM
         AIF   ('&LENGTH'(1,1) LT '0' OR '&LENGTH'(1,1) GT '9').LENLABL
.LENNUM  ANOP
         LA    R0,&LENGTH         SET NUMERIC LENGTH
         AGO   .ALLDONE
.LENLABL ANOP
         LH    R0,&LENGTH         SET LENGTH FROM LABEL
.ALLDONE ANOP
         MVC   EIBFN,=X'0404'     SET EIBFN
         TPUT  &FROM,(0),FULLSCR  SEND TN3270 DATA STREAM
         MEXIT
.BADFROM ANOP
         MNOTE 12,'FROM IS MANDATORY'
         MEXIT
.BADLEN  ANOP
         MNOTE 12,'LENGTH IS MANDATORY'
         MEXIT
         MEND
