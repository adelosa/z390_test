.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/05/06 RPI 308 CICS RETURN CALLED FROM EXEC
.*********************************************************************
         MACRO
         CICS_SEND
         LCLC  &FROM,&LENGTH
&NP      SETA  N'&SYSLIST
&IP      SETA  0
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  &SYSLIST(&IP)
         AIF   ('&PARM'(1,5) EQ 'FROM(').FROM
         AIF   ('&PARM'(1,7) EQ 'LENGTH(').LENGTH
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.FROM    ANOP
&FROM    SETC  '&PARM'(6,K'&PARM-6)
         AGO   .PLOOP
.*
.LENGTH  ANOP
&LENGTH  SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.*
.ELOOP   ANOP
         AIF   (&FROM EQ '').BADFROM
         AIF   (&LENGTH EQ '').BADLEN
         AIF   ('&LENGTH'(2,1) EQ '''').LENNUM
         AIF   ('&LENGTH'(1,1) LT '0' OR '&LENGTH'(1,1) GT '9').LENLABL
.LENNUM  ANOP
         LA    R0,&LENGTH         SET NUMERIC LENGTH
         AGO   .ALLDONE
.LENLABL ANOP
         LH    R0,&LENGTH         SET LENGTH FROM LABEL
.ALLDONE ANOP
&FROMTQ  SETC  T'&FROM
         AIF   ('&FROM'(1,1) EQ '=' OR &FROMTQ EQ 'A').FRMADLIT
         LA    R1,&FROM           FROM ADDRESS
         AGO   .FROMOK
.*
.FRMADLIT ANOP
         L     R1,&FROM           INDIRECT FROM ADDRESS
.FROMOK  ANOP
         MVC   EIBFN,=X'0404'     SET EIBFN
         CLC   TCTTETI,=C'SQ01'   SEQUENTIAL TERMINAL ?
         BNE   CICS_SEND_&SYSNDX._TPUT EXIT IF NOT
         LR    R14,R1             SAVE FROM ADDRESS
         L     R1,TCTTEQDO        R2=SEND FILE DCB
         USING IHADCB,R1
         LR    R2,R0              R2=LENGTH
         AHI   R2,2               ALLOW FOR CRLF
         ST    R2,DCBBLKSIF       SAVE BLOCKSIZE
         ST    R2,DCBLRECLF       SAVE LRECL
         AHI   R2,-2              RESTORE LENGTH
         LA    R2,0(R14,R2)       ADDRESS 2 BYTES FOLLOWING DATA
         MVC   CICS_SEND_&SYSNDX._SAVE,0(R2) SAVE THE 2 BYTES
         MVC   0(2,R2),=X'0D0A'   SET CRLF
         PUT   (R1),(R14)         WRITE DATA STREAM
         MVC   0(2,R2),CICS_SEND_&SYSNDX._SAVE RESTORE THE 2 BYTES
         L     R1,TCTTEQDO        R2=SEND FILE DCB
         L     R0,DCBBLKSIF       RESTORE LENGTH+2
         AHI   R0,-2              LENGTH-2
         DROP  R1
         LR    R1,R14             RESTORE FROM ADDRESS
         B     CICS_SEND_&SYSNDX._TPUT EXIT
CICS_SEND_&SYSNDX._SAVE DS H
*
CICS_SEND_&SYSNDX._TPUT EQU *
         TPUT  (R1),(R0),FULLSCR  SEND TN3270 DATA STREAM
*
         DCBD
*
         MEXIT
.*
.BADFROM ANOP
         MNOTE 12,'FROM IS MANDATORY'
         MEXIT
.*
.BADLEN  ANOP
         MNOTE 12,'LENGTH IS MANDATORY'
         MEXIT
.*
         MEND
