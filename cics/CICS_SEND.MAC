.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/05/06 RPI 308 CICS RETURN CALLED FROM EXEC
.* 04/19/08 RPI 833 ADD STRING QUOTES FOR HLASM COMPATIBILITY
.*********************************************************************
         MACRO
         CICS_SEND
         GBLB  &PARMS_0404,&PARMS_1804,&PARMS_1812
         LCLB  &ALARM,&CURSSYM,&DATONLY,&ERASE,&ERASAUP
         LCLB  &FREEKB,&FRSET,&MAPONLY,&CONTROL,&NOHAND
         LCLC  &FROM,&LENGTH,&MAP,&MAPSET,&CURSPOS,&RESP,&RESP2
&NP      SETA  N'&SYSLIST
&IP      SETA  0
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  '&SYSLIST(&IP)'
         AIF   ('&PARM'(1,5) EQ 'FROM(').FROM
         AIF   ('&PARM'(1,7) EQ 'LENGTH(').LENGTH
         AIF   ('&PARM'(1,8) EQ 'NOHANDLE').NOHAND
         AIF   ('&PARM'(1,5) EQ 'RESP(').RESP
         AIF   ('&PARM'(1,6) EQ 'RESP2(').RESP2
.* ADDED FOR MAPPING SUPPORT
         AIF   ('&PARM'(1,4) EQ 'MAP(').MAP
         AIF   ('&PARM'(1,7) EQ 'MAPSET(').MAPSET
         AIF   ('&PARM'(1,5) EQ 'ALARM').ALARM
         AIF   ('&PARM'(1,7) EQ 'CURSOR(').CURSPOS
         AIF   ('&PARM'(1,6) EQ 'CURSOR').CURSSYM
         AIF   ('&PARM'(1,8) EQ 'DATAONLY').DATONLY
         AIF   ('&PARM'(1,8) EQ 'ERASEAUP').ERASAUP
         AIF   ('&PARM'(1,5) EQ 'ERASE').ERASE
         AIF   ('&PARM'(1,6) EQ 'FREEKB').FREEKB
         AIF   ('&PARM'(1,5) EQ 'FRSET').FRSET
         AIF   ('&PARM'(1,7) EQ 'MAPONLY').MAPONLY
         AIF   ('&PARM'(1,8) EQ 'TERMINAL').DISCARD
         AIF   ('&PARM'(1,4) EQ 'WAIT').DISCARD
         AIF   ('&PARM'(1,7) EQ 'DEFAULT').DISCARD
.* ADDED FOR SEND CONTROL SUPPORT
         AIF   ('&PARM'(1,7) EQ 'CONTROL').CONTROL
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.FROM    ANOP
&FROM    SETC  '&PARM'(6,K'&PARM-6)
         AGO   .PLOOP
.*
.LENGTH  ANOP
&LENGTH  SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.*
.NOHAND  ANOP
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP    ANOP
&RESP    SETC  '&PARM'(6,K'&PARM-6)
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP2   ANOP
&RESP2   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.MAP     ANOP
&MAP     SETC  '&PARM'(5,K'&PARM-5)
         AGO   .PLOOP
.*
.MAPSET  ANOP
&MAPSET  SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.*
.ALARM   ANOP
&ALARM   SETB  1
         AGO   .PLOOP
.*
.CURSPOS ANOP
&CURSPOS SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.*
.CURSSYM ANOP
&CURSSYM SETB  1
         AGO   .PLOOP
.*
.DATONLY ANOP
&DATONLY SETB  1
         AGO   .PLOOP
.*
.ERASE   ANOP
&ERASE   SETB  1
         AGO   .PLOOP
.*
.ERASAUP ANOP
&ERASAUP SETB  1
         AGO   .PLOOP
.*
.FREEKB  ANOP
&FREEKB  SETB  1
         AGO   .PLOOP
.*
.FRSET   ANOP
&FRSET   SETB  1
         AGO   .PLOOP
.*
.MAPONLY ANOP
&MAPONLY SETB  1
         AGO   .PLOOP
.*
.CONTROL ANOP
&CONTROL SETB  1
         AGO   .PLOOP
.*
.DISCARD ANOP
         AGO   .PLOOP
.*
.ELOOP   ANOP
         AIF   ('&MAP' EQ '' AND '&MAPSET' EQ '').NOTMAP
         AGO   .MAPPING
.*
.NOTMAP  ANOP
         AIF   (&CONTROL).SENDCON
.* NORMAL SEND (NOT MAP OR CONTROL)
         L     R15,=A(P0404)      LOAD PARM ADDRESS
         USING (P0404,P0404END),R15
         XC    P0404(P0404END-P0404),P0404 CLEAR PARMS
         MVC   P0404FN,=X'0404'   SET FUNCTION CODE
         AIF   ('&FROM' EQ '').BADFROM
         AIF   ('&LENGTH' EQ '').BADLEN
         AIF   ('&LENGTH'(2,1) EQ '''').LENNUM
         AIF   (T'&LENGTH NE 'N').LENLABL
.LENNUM  ANOP
         MVC   P0404LEN,=AL2(&LENGTH) SAVE LENGTH
         AGO   .ALLDONE
.*
.LENLABL ANOP
         MVC   P0404LEN,&LENGTH   SAVE LENGTH FROM LABEL
.ALLDONE ANOP
&FROMTQ  SETC  T'&FROM
         AIF   ('&FROM'(1,1) EQ '=' OR '&FROMTQ' EQ 'A').FRMADLIT
         LA    R1,&FROM           FROM ADDRESS
         ST    R1,P0404FRM        SAVE IT
         AGO   .FROMOK
.*
.FRMADLIT ANOP
         L     R1,&FROM           INDIRECT FROM ADDRESS
         ST    R1,P0404FRM        SAVE IT
.FROMOK  ANOP
         ST    DFHEIBR,P0404EIB   SET EIB ADDRESS
         ST    TCTTEAR,P0404TCT   SET TCTTE ADDRESS
         AIF   (NOT &NOHAND).DUNNOH
         MVI   P0404NOH,X'FF'     SET NOHANDLE
.DUNNOH  ANOP
         AIF   ('&RESP' EQ '').NORESP
         LA    R1,&RESP           ADDRESS OF RESP
         ST    R1,P0404RSP        SAVE IT
.NORESP  ANOP
         AIF   ('&RESP2' EQ '').NORESP2
         LA    R1,&RESP2          ADDRESS OF RESP2
         ST    R1,P0404RS2        SAVE IT
.NORESP2 ANOP
         ZCALL LCL0404,(P0404),MF=I CALL SEND PROCESSOR
.*
         AIF   (&PARMS_0404).BYP0404
         B     P0404BYP_&SYSNDX   EXIT
*
&PARMS_0404 SETB 1
P0404    EQU   *
P0404FN  DS    XL2                FUNCTION CODE
P0404EIB DS    AL4                EIB
P0404TCT DS    AL4                TCTTE
P0404FRM DS    AL4                FROM
P0404LEN DS    AL2                LENGTH
P0404RSP DS    AL4                RESP
P0404RS2 DS    AL4                RESP2
P0404END EQU   *                  END MARKER
         DS    0H
*
P0404BYP_&SYSNDX DS 0H
.BYP0404 ANOP
         DROP  R15
         MEXIT
.*
.BADFROM ANOP
         MNOTE 12,'FROM IS MANDATORY'
         MEXIT
.*
.BADLEN  ANOP
         MNOTE 12,'LENGTH IS MANDATORY'
         MEXIT
.*
.SENDCON ANOP
.*
.* SEND CONTROL
.*
.* ONLY THESE PARAMETERS ARE PROCESSED:
.*    CURSOR, CURSOR(), ERASE, ERASEAUP, FREEKB, ALARM, FRSET
         AIF   ('&CURSPOS' NE '' AND &CURSSYM).CURSERR
         AIF   (&ERASE AND &ERASAUP).ERASERR
.*
         L     R15,=A(P1812)      LOAD PARM ADDRESS
         USING (P1812,P1812END),R15
         XC    P1812(P1812END-P1812),P1812 CLEAR PARMS
         MVC   P1812FN,=X'1812'   SET FUNCTION CODE
         ST    DFHEIBR,P1812EIB   SET EIB ADDRESS
         ST    TCTTEAR,P1812TCT   SET TCTTE ADDRESS
         AIF   (NOT &NOHAND).DUNNOH2
         MVI   P1812NOH,X'FF'     SET NOHANDLE
.DUNNOH2 ANOP
         AIF   ('&RESP' EQ '').NORESPC
         LA    R1,&RESP           ADDRESS OF RESP
         ST    R1,P1812RSP        SAVE IT
.NORESPC ANOP
         AIF   ('&RESP2' EQ '').NORSP2C
         LA    R1,&RESP2          ADDRESS OF RESP2
         ST    R1,P1812RS2        SAVE IT
.NORSP2C ANOP
         MVC   P1812CRS,=X'FFFF'  INVALIDATE CURSOR POSITION
         AIF   ('&CURSPOS' EQ '').NOCURSC
         LA    R14,&CURSPOS       R14=CURSOR POSITION
         STH   R14,P1812CRS       SAVE IT
.NOCURSC ANOP
         MVI   P1812OPT,B'&ERASE&ERASAUP&CURSSYM.00&ALARM&FREEKB&FRSET'-
                SET OPTION BITS
         EXEC  CICS GETMAIN SET(R15) LENGTH(7) INITIMG(0)
         LR    R1,R15             COPY OUTPUT AREA ADDRESS
         L     R15,=A(P1812)      RESTORE PARM ADDRESS
         USING (P1812,P1812END),R15
         ST    R1,P1812OUT        SAVE OUTPUT AREA ADDRESS
         ZCALL LCL1812,(P1812),MF=I CALL SEND CONTROL PROCESSOR
.*
         L     R15,=A(P1812)      RESTORE PARM ADDRESS
         EXEC  CICS FREEMAIN DATA(P1812OUT)
         AIF   (&PARMS_1812).BYP1812
         B     P1812BYP_&SYSNDX   EXIT
*
&PARMS_1812 SETB 1
P1812    EQU   *
P1812FN  DS    XL2                FUNCTION CODE
P1812EIB DS    AL4                EIB
P1812TCT DS    AL4                TCTTE
P1812OUT DS    AL4                OUTPUT AREA
P1812CRS DS    AL2                CURSOR POSITION (X'FFFF'=NONE)
P1812OPT DS    X                  OPTION BITS...
*              X'80'                 ERASE
*              X'40'                 ERASEAUP
*              X'20'                 SYMBOLIC CURSOR
*              X'04'                 ALARM
*              X'02'                 FREEKB
*              X'01'                 FRSET
P1812RSP DS    AL4                RESP
P1812RS2 DS    AL4                RESP2
P1812END EQU   *                  END MARKER
         DS    0H
*
P1812BYP_&SYSNDX DS 0H
.BYP1812 ANOP
         DROP  R15
         MEXIT
.*
.MAPPING ANOP
.*
.* SEND MAP
.*
         AIF   (NOT &CONTROL).NOCTRL
         MNOTE 12,'CONTROL IS NOT COMPATABLE WITH SEND MAP'
         MEXIT
.*
.NOCTRL  ANOP
         AIF   ('&MAP' NE '').MAPOK
         MNOTE 12,'MAP IS MANDATORY'
         MEXIT
.*
.MAPOK   ANOP
         AIF   ('&CURSPOS' NE '' AND &CURSSYM).CURSERR
         AIF   (&ERASE AND &ERASAUP).ERASERR
         AIF   (&DATONLY AND &MAPONLY).ONLYERR
.*
         L     R15,=A(P1804)      LOAD PARM ADDRESS
         USING (P1804,P1804END),R15
         XC    P1804(P1804END-P1804),P1804 CLEAR PARMS
         MVC   P1804FN,=X'1804'   SET FUNCTION CODE
         ST    DFHEIBR,P1804EIB   SET EIB ADDRESS
         ST    TCTTEAR,P1804TCT   SET TCTTE ADDRESS
         AIF   (NOT &NOHAND).DUNNOH3
         MVI   P1804NOH,X'FF'     SET NOHANDLE
.DUNNOH3 ANOP
         AIF   ('&MAPSET' NE '').MSETOK
&MAPSET  SETC  '&MAP'
.MSETOK  ANOP
         AIF   ('&MAP'(1,1) EQ '''').MAPSTRG
.* MAP AS LABEL
         MVC   P1804MAP,&MAP      MOVE MAPNAME FROM LABEL
         AIF   ('&FROM' EQ '').BADFRM2
         AIF   ('&LENGTH' EQ '').BADLEN2
         AIF   (&MAPONLY).DOLEN
         LA    R14,&FROM          R14=MAP STRUCTURE ADDRESS
         ST    R14,P1804DSC       SAVE IT
         AGO   .DOLEN
.*
.MAPSTRG ANOP
.* MAP AS STRING
&MAPLEN  SETA  K'&MAP
         AIF   (&MAPLEN LT 3 OR &MAPLEN GT 9).BADMAP
         MVC   P1804MAP,=CL7&MAP  MOVE MAPNAME AS STRING
&MAPANDO SETC  '&MAP'(2,&MAPLEN-2).'O'
&MAPANDL SETC  '&MAP'(2,&MAPLEN-2).'L'
         AIF   (&MAPONLY).DOLEN
         LA    R14,&MAPANDO       R14=DEFAULT MAP STRUCTURE ADDRESS
         ST    R14,P1804DSC       SAVE IT
.DOLEN   ANOP
         AIF   ('&LENGTH' EQ '').NOLEN
         AGO   .GOTLEN
.*
.NOLEN   ANOP
.* LENGTH OMITTED
         AIF   (&MAPONLY).DEFMPST
         LA    R14,&MAPANDL       R14=DEFAULT MAP STRUCTURE LENGTH
         STH   R14,P1804DSL       SAVE IT
         AGO   .DEFMPST
.*
.GOTLEN  ANOP
.* LENGTH MAY BE L' OR CONSTANT LIKE X'..'
         AIF   ('&LENGTH'(2,1) EQ '''').LENNUM2
         AIF   (T'&LENGTH NE 'N').LENLAB2
.LENNUM2 ANOP
         LA    R14,&LENGTH        R14=NUMERIC LENGTH
         STH   R14,P1804DSL       SAVE IT
         AGO   .DEFMPST
.*
.LENLAB2 ANOP
         LH    R14,&LENGTH        R14=LENGTH FROM LABEL
         STH   R14,P1804DSL       SAVE IT
.DEFMPST ANOP
         AIF   ('&MAPSET'(1,1) NE '''').LOADIT
.* MAPSET AS STRING
&MAPSLEN SETA  K'&MAPSET
         AIF   (&MAPSLEN LT 3 OR &MAPSLEN GT 9).BADMAPS
.LOADIT  ANOP
         EXEC  CICS LOAD PROGRAM(&MAPSET) ENTRY(R14) LENGTH(P1804MLN)
         L     R15,=A(P1804)      LOAD PARM ADDRESS
         USING (P1804,P1804END),R15
         ST    R14,P1804MSA       SAVE MAPSET ADDRESS
.*
.DUNMAP  ANOP
         MVC   P1804CRS,=X'FFFF'  INVALIDATE CURSOR POSITION
         AIF   ('&CURSPOS' EQ '').NOCURS
         LA    R14,&CURSPOS       R14=CURSOR POSITION
         STH   R14,P1804CRS       SAVE IT
.NOCURS  ANOP
         MVI   P1804OPT,B'&ERASE&ERASAUP&CURSSYM&MAPONLY&DATONLY&ALARM&-
               FREEKB&FRSET' SET OPTION BITS
         EXEC  CICS GETMAIN SET(R15) LENGTH(2000) INITIMG(0)
         LR    R1,R15             COPY OUTPUT AREA ADDRESS
         L     R15,=A(P1804)      RESTORE PARM ADDRESS
         USING (P1804,P1804END),R15
         ST    R1,P1804OUT        SAVE OUTPUT AREA ADDRESS
         AIF   ('&RESP' EQ '').NORSPM
         LA    R1,&RESP           ADDRESS OF RESP
         ST    R1,P1804RSP        SAVE IT
.NORSPM  ANOP
         AIF   ('&RESP2' EQ '').NORSP2M
         LA    R1,&RESP2          ADDRESS OF RESP2
         ST    R1,P1804RS2        SAVE IT
.NORSP2M ANOP
         ZCALL LCL1804,(P1804),MF=I CALL SEND MAP PROCESSOR
.*
         L     R15,=A(P1804)      RESTORE PARM ADDRESS
         CLI   P1804NOH,X'FF'     NOHANDLE ?
         BE    P1804BYP_&SYSNDX   EXIT IF IT IS
         OC    EIBRESP,EIBRESP    ANY BAD RESPONSE ?
         BZ    P1804BYP_&SYSNDX   EXIT IF NONE
* MAPFAIL, INVMPSZ
         DC    AL3(0),C'ABEND'    MARKER FOR Z390KCP ESTAE
         DC    AL4(P1804BYP_&SYSNDX) ENTRYPOINT FOR IGNORE CONDITION
*
         AIF   (&PARMS_1804).BYP1804
&PARMS_1804 SETB 1
P1804    EQU   *
P1804FN  DS    XL2                FUNCTION CODE
P1804EIB DS    AL4                EIB
P1804TCT DS    AL4                TCTTE
P1804MSA DS    AL4                MAPSET ADDRESS
P1804MAP DS    CL7                MAP NAME
P1804MLN DS    XL2                MAPSET LENGTH
P1804DSC DS    AL4                STRUCTURE ADDRESS
P1804DSL DS    AL2                STRUCTURE LENGTH
P1804OUT DS    AL4                OUTPUT AREA
P1804CRS DS    AL2                CURSOR POSITION (X'FFFF'=NONE)
P1804OPT DS    X                  OPTION BITS...
*1804ERS EQU   X'80'                 ERASE
*1804EAU EQU   X'40'                 ERASEAUP
*1804CSM EQU   X'20'                 SYMBOLIC CURSOR
*1804MNY EQU   X'10'                 MAPONLY
*1804DNY EQU   X'08'                 DATAONLY
*1804ALM EQU   X'04'                 ALARM
*1804FKB EQU   X'02'                 FREEKB
*1804FRS EQU   X'01'                 FRSET
P1804NOH DS    X                  FF=NOHANDLE
P1804RSP DS    AL4                RESP
P1804RS2 DS    AL4                RESP2
P1804END EQU   *                  END MARKER
         DS    0H
*
.BYP1804 ANOP
P1804BYP_&SYSNDX DS 0H
         EXEC  CICS FREEMAIN DATA(P1804OUT)
         EXEC  CICS RELEASE PROGRAM(&MAPSET)
         DROP  R15
         MEXIT
.*
.CURSERR ANOP
         MNOTE 12,'CURSOR POSITION AND SYMBOLIC CURSOR SPECIFIED'
         MEXIT
.*
.ERASERR ANOP
         MNOTE 12,'ERASE AND ERASEAUP SPECIFIED'
         MEXIT
.*
.ONLYERR ANOP
         MNOTE 12,'DATAONLY AND MAPONLY SPECIFIED'
         MEXIT
.*
.BADMAP  ANOP
         MNOTE 12,'INVALID MAP NAME'
         MEXIT
.*
.BADMAPS ANOP
         MNOTE 12,'INVALID MAPSET NAME'
         MEXIT
.*
.BADFRM2 ANOP
         MNOTE 12,'FROM IS REQUIRED WHEN MAP IS A LABEL'
         MEXIT
.*
.BADLEN2 ANOP
         MNOTE 12,'LENGTH IS REQUIRED WHEN MAP IS A LABEL'
         MEXIT
.*
         MEND
