***********************************************************************
* Copyright 2006 Automated Software Tools Corporation                 *
* This source code is part of z390 assembler/emulator package         *
* The z390 package is distributed under GNU general public license    *
* Author - Don Higgins                                                *
***********************************************************************
LCL0C02  SUBENTRY
*
* FN=0C02  EXEC CICS GETMAIN
*
* R1  >>> P0C02 BLOCK POINTER
* R14 = RETURN ADDRESS
         LR    R3,R1              SET BASE
         L     R3,0(R3)                    FOR PARMS
         USING (P0C02,P0C02END),R3
         L     DFHEIBR,P0C02EIB   LOAD EIB ADDRESS
         USING DFHEIBLK,DFHEIBR
         L     TCTTEAR,P0C02TCT   LOAD TCT ADDRESS
         USING DFHTCTTE,TCTTEAR
         MVC   EIBFN,P0C02FN      SET EIBFN
         XC    EIBRCODE,EIBRCODE  CLEAR EIBRCODE
         XC    EIBRESP,EIBRESP    CLEAR EIBRESP
         XC    EIBRESP2,EIBRESP2  CLEAR EIBRESP2
         ICM   R1,15,P0C02RSP     R1=RESP ADDRESS
         BZ    NORESP             EXIT IF NONE
         XC    0(4,R1),0(R1)      CLEAR RESP
NORESP   EQU   *
         ICM   R1,15,P0C02RS2     R1=RESP2 ADDRESS
         BZ    NORESP2            EXIT IF NONE
         XC    0(4,R1),0(R1)      CLEAR RESP2
NORESP2  EQU   *
         L     R1,P0C02LEN        R1=GETMAIN LENGTH
         AHI   R1,8               +8 FOR PREFIX
         GETMAIN R,LV=(R1)
         ST    R1,P0C02GMA        SAVE GETMAIN ADDRESS
         LA    R14,TCTTESCC       R14=ANCHOR OF GETMAIN CHAIN
LOOP     EQU   *
         OC    0(4,R14),0(R14)    ANY GETMAIN CHAIN ?
         BNZ   CHAIN              EXIT IF THERE IS
         ST    R1,0(R14)          SAVE GETMAIN CHAIN
         XC    0(4,R1),0(R1)      CLEAR FORWARD CHAIN
         ST    R0,4(R1)           SAVE LENGTH ACQUIRED IN PREFIX
         B     INIT               EXIT
*
CHAIN    EQU   *
         L     R14,0(R14)         NEXT GETMAIN AREA
         B     LOOP               LOOP THRU CHAIN
*
INIT     EQU   *
         CLI   P0C02INM,X'FF'     ANY INITIMG ?
         BNE   NOINIT             EXIT IF NOT
         L     R14,P0C02GMA       SET ADDRESS TO CLEAR
         AHI   R14,8              ADJUST FOR PREFIX
         LR    R15,R0             SET LENGTH TO CLEAR
         AHI   R15,-8             ADJUST FOR PREFIX
         LR    R0,R14             SET 2ND ADDRESS
         SR    R1,R1              CLEAR 2ND LENGTH
         ICM   R1,8,P0C02INT      SET PADDING BYTE
         MVCL  R14,R0             CLEAR GETMAINED AREA WITH INITIMG
NOINIT   EQU   *
         SUBEXIT
*
         LTORG
*
P0C02    DSECT
P0C02FN  DS    XL2                FUNCTION CODE
P0C02EIB DS    AL4                EIB
P0C02TCT DS    AL4                TCTTE
P0C02LEN DS    AL4                LENGTH/FLENGTH
P0C02INM DS    X                  FF=INITIMG SPECIFIED
P0C02INT DS    X                  INITIMG
P0C02GMA DS    AL4                GETMAIN ADDRESS
         DS    X                  FF=NOHANDLE
P0C02RSP DS    AL4                RESP
P0C02RS2 DS    AL4                RESP2
P0C02END EQU   *                  END MARKER
*
         DFHEIBLK                 EIB
         DFHTCTTE                 TCTTE
*
         EQUREGS REGS=GPR
*
         END
