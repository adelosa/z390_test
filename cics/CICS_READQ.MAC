.**********************************************************************
.* Copyright 2006 Automated Software Tools Corporation                *
.* This source code is part of z390 assembler/emulator package        *
.* The z390 package is distributed under GNU general public license   *
.* Author - Don Higgins                                               *
.**********************************************************************
.* 04/19/08 RPI 833 ADD STRING QUOTES FOR HLASM COMPATIBILITY         *
.**********************************************************************
         MACRO
         CICS_READQ
         GBLB  &PARMS_0A04
         LCLB  &NEXT,&NOHAND
         LCLC  &TYPE,&QUEUE,&QNAME,&INTO,&SET,&LENGTH,&NUMITEMS,&ITEM
         LCLC  &RESP,&RESP2
&TYPE    SETC  '&SYSLIST(1)'
         AIF   ('&TYPE' EQ 'TS').TS
         MNOTE 12,'READQ TYPE NOT RECOGNIZED'
         MEXIT
.*
.TS      ANOP
&NP      SETA  N'&SYSLIST
&IP      SETA  1
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  '&SYSLIST(&IP)'
.* DISCARDED PARMS
         AIF   ('&PARM'(1,4) EQ 'MAIN').PLOOP
         AIF   ('&PARM'(1,9) EQ 'AUXILIARY').PLOOP
.*
         AIF   ('&PARM'(1,6) EQ 'QUEUE(').QUEUE
         AIF   ('&PARM'(1,6) EQ 'QNAME(').QNAME
         AIF   ('&PARM'(1,5) EQ 'INTO(').INTO
         AIF   ('&PARM'(1,4) EQ 'SET(').SET
         AIF   ('&PARM'(1,7) EQ 'LENGTH(').LENGTH
         AIF   ('&PARM'(1,9) EQ 'NUMITEMS(').NUMITEMS
         AIF   ('&PARM'(1,5) EQ 'ITEM(').ITEM
         AIF   ('&PARM'(1,4) EQ 'NEXT').NEXT
         AIF   ('&PARM'(1,8) EQ 'NOHANDLE').NOHAND
         AIF   ('&PARM'(1,5) EQ 'RESP(').RESP
         AIF   ('&PARM'(1,6) EQ 'RESP2(').RESP2
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.QUEUE   ANOP
&QUEUE   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.QNAME   ANOP
&QNAME   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.INTO    ANOP
&INTO    SETC '&PARM'(6,K'&PARM-6)
         AGO   .PLOOP
.*
.SET     ANOP
&SET     SETC '&PARM'(5,K'&PARM-5)
         AGO   .PLOOP
.*
.LENGTH  ANOP
&LENGTH  SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.*
.NUMITEMS ANOP
&NUMITEMS SETC  '&PARM'(10,K'&PARM-10)
         AGO   .PLOOP
.*
.ITEM    ANOP
&ITEM    SETC  '&PARM'(6,K'&PARM-6)
         AGO   .PLOOP
.*
.NEXT    ANOP
&NEXT    SETB  1
         AGO   .PLOOP
.*
.NOHAND  ANOP
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP    ANOP
&RESP    SETC  '&PARM'(6,K'&PARM-6)
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP2   ANOP
&RESP2   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.ELOOP   ANOP
         AIF   ('&QUEUE' EQ '' AND '&QNAME' EQ '').NOQUEUE
         AIF   ('&QUEUE' NE '' AND '&QNAME' NE '').BOTHQ
         AIF   ('&INTO'  EQ '' AND '&SET'   EQ '').NOINTOST
         AIF   ('&INTO'  NE '' AND '&SET'   NE '').BOTHINST
         AIF   ('&ITEM'  EQ '' AND (NOT &NEXT)).NOITMNXT
         AIF   ('&ITEM'  NE '' AND &NEXT).BOTHITNX
         AIF   ('&SET'   NE '' AND '&LENGTH' EQ '').NEEDLEN
.*
         L     R15,=A(P0A04)      LOAD PARM ADDRESS
         USING (P0A04,P0A04END),R15
         XC    P0A04(P0A04END-P0A04),P0A04 CLEAR PARMS
         MVC   P0A04FN,=X'0A04'   SET FUNCTION CODE
.*
         AIF   ('&QUEUE' EQ '').DOQNAME
.* GOT QUEUE
         AIF   ('&QUEUE'(1,1) EQ '''').QUSTRING
         MVC   P0A04QNM(8),&QUEUE SET QUEUE NAME AS LABEL
         MVC   P0A04QNM+8(8),=8C' ' CLEAR REMAINDER
         AGO   .CHKNAME
.*
.QUSTRING ANOP
&QULEN   SETA  K'&QUEUE
         AIF   (&QULEN GT 10).BADQ
         MVC   P0A04QNM,=CL16&QUEUE SET QUEUE NAME AS STRING
         AGO   .CHKNAME
.*
.* GOT QNAME
.DOQNAME ANOP
         AIF   ('&QNAME'(1,1) EQ '''').QNSTRING
         MVC   P0A04QNM,&QNAME    SET QUEUE NAME AS LABEL
         AGO   .CHKNAME
.*
.QNSTRING ANOP
&QNLEN   SETA  K'&QNAME
         AIF   (&QNLEN GT 18).BADQ
         MVC   P0A04QNM,=CL16&QNAME SET QUEUE NAME AS STRING
.CHKNAME ANOP
.* PROCESS ITEM
         AIF   ('&ITEM' NE '').DOITEM
         MVC   P0A04ITM,=X'FFFF'  SET ITEM NUMBER TO NEXT
         AGO   .DUNITEM
.*
.DOITEM  ANOP
         AIF   (T'&ITEM NE 'N').ITMLABL
         MVC   P0A04ITM,=AL2(&ITEM) MOVE ITEM
         AGO   .DUNITEM
.*
.ITMLABL ANOP
         MVC   P0A04ITM,&ITEM     MOVE ITEM AS LABEL
.DUNITEM ANOP
         AIF   ('&SET' EQ '').NOSET
         MVI   P0A04SET,X'FF'     SET SET INDICATOR
         AGO   .DOLENG
.*
.NOSET   ANOP
         LA    R1,&INTO           INTO ADDRESS
         ST    R1,P0A04INT        SAVE IT
.DOLENG  ANOP
         AIF   ('&LENGTH' EQ '').NOLENG
         AIF   ('&LENGTH'(2,1) EQ '''').LENNUM
         AIF   (T'&LENGTH NE 'N').LENLABL
.LENNUM  ANOP
         LA    R1,&LENGTH         R1=LENGTH
         STH   R1,P0A04LEN        SAVE IT
         AGO   .DUNLEN
.*
.LENLABL ANOP
         AIF   ('&SET' EQ '').NOSET2
         LHI   R1,32767           SET MAXIMUM LENGTH
         AGO   .LENADD
.*
.NOSET2  ANOP
         LH    R1,&LENGTH         R1=LENGTH FROM LABEL
.LENADD  ANOP
         STH   R1,P0A04LEN        SAVE IT
         LA    R1,&LENGTH         R1=ADDRESS OF LENGTH
         ST    R1,P0A04LAD        SAVE IT
         AGO   .DUNLEN
.*
.NOLENG  ANOP
         LA    R1,L'&INTO         R1=IMPLIED LENGTH OF INTO
         STH   R1,P0A04LEN        SAVE IT
.DUNLEN  ANOP
         AIF   ('&NUMITEMS' EQ '').NONUMIT
         LA    R1,&NUMITEMS       R1=ADDRESS OF NUMITEMS
         ST    R1,P0A04NUM        SAVE IT
.NONUMIT ANOP
         ST    DFHEIBR,P0A04EIB   SET EIB ADDRESS
         ST    TCTTEAR,P0A04TCT   SET TCTTE ADDRESS
         AIF   (NOT &NOHAND).DUNNOH
         MVI   P0A04NOH,X'FF'     SET NOHANDLE
.DUNNOH  ANOP
         AIF   ('&RESP' EQ '').NORESP
         LA    R1,&RESP           ADDRESS OF RESP
         ST    R1,P0A04RSP        SAVE IT
.NORESP  ANOP
         AIF   ('&RESP2' EQ '').NORESP2
         LA    R1,&RESP2          ADDRESS OF RESP2
         ST    R1,P0A04RS2        SAVE IT
.NORESP2 ANOP
         ZCALL LCL0A04,(P0A04),MF=I CALL READQ PROCESSOR
.*
         L     R15,=A(P0A04)      LOAD PARM ADDRESS
         AIF   ('&SET' EQ '').NOSET3
         L     &SET,P0A04STA      LOAD THE SET REGISTER
.NOSET3  ANOP
         CLI   P0A04NOH,X'FF'     NOHANDLE ?
         BE    P0A04BYP_&SYSNDX   EXIT IF IT IS
         OC    EIBRESP,EIBRESP    ANY BAD RESPONSE ?
         BZ    P0A04BYP_&SYSNDX   EXIT IF NONE
* INVREQ, ITEMERR, LENGERR, QIDERR
         DC    AL3(0),C'ABEND'    MARKER FOR Z390KCP ESTAE
         DC    AL4(P0A04BYP_&SYSNDX) ENTRYPOINT FOR IGNORE CONDITION
*
         AIF   (&PARMS_0A04).BYP0A04
&PARMS_0A04 SETB 1
P0A04    EQU   *
P0A04FN  DS    XL2                FUNCTION CODE
P0A04EIB DS    AL4                EIB
P0A04TCT DS    AL4                TCTTE
P0A04QNM DS    CL16               QUEUE NAME
P0A04ITM DS    XL2                ITEM
P0A04LEN DS    XL2                LENGTH
P0A04LAD DS    AL4                ADDRESS OF LENGTH IF LABEL
P0A04SET DS    X                  FF=SET IS SPECIFIED
P0A04STA DS    AL4                ADDRESS FOR SET
P0A04INT DS    AL4                ADDRESS OF INTO
P0A04NUM DS    AL4                ADDRESS OF NUMITEMS
P0A04NOH DS    X                  FF=NOHANDLE
P0A04RSP DS    AL4                RESP
P0A04RS2 DS    AL4                RESP2
P0A04END EQU   *                  END MARKER
         DS    0H
*
.BYP0A04 ANOP
P0A04BYP_&SYSNDX DS 0H
         DROP  R15
         MEXIT
.*
.NOQUEUE MNOTE 12,'QUEUE OR QNAME MUST BE SPECIFIED'
         MEXIT
.*
.BOTHQ   MNOTE 12,'BOTH QUEUE AND QNAME ARE SPECIFIED'
         MEXIT
.*
.BADQ    MNOTE 12,'INVALID QUEUE OR QNAME'
         MEXIT
.*
.NOINTOST MNOTE 12,'INTO OR SET MUST BE SPECIFIED'
         MEXIT
.*
.BOTHINST MNOTE 12,'BOTH INTO AND SET ARE SPECIFIED'
         MEXIT
.*
.NOITMNXT MNOTE 12,'ITEM OR NEXT MUST BE SPECIFIED'
         MEXIT
.*
.BOTHITNX MNOTE 12,'BOTH ITEM AND NEXT ARE SPECIFIED'
         MEXIT
.*
.NEEDLEN MNOTE 12,'SET REQUIRES LENGTH'
         MEXIT
.*
         MEND
