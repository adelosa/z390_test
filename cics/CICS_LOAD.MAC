.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/05/06 RPI 308 ADD EXEC CICS LOAD
.*********************************************************************
.* ### TEST LOAD RC AND SET HANDLE CONDITION
         MACRO
         CICS_LOAD
         LCLC  &PROGRAM,&ENTRY,&SET,&LENGTH,&FLENGTH
&NP      SETA  N'&SYSLIST
&IP      SETA  0
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  &SYSLIST(&IP)
         AIF   ('&PARM'(1,8) EQ 'PROGRAM(').PROGRAM
         AIF   ('&PARM'(1,6) EQ 'ENTRY(').ENTRY
         AIF   ('&PARM'(1,4) EQ 'SET(').SET
         AIF   ('&PARM'(1,7) EQ 'LENGTH(').LENGTH
         AIF   ('&PARM'(1,8) EQ 'FLENGTH(').FLENGTH
         AGO   .PLOOP
.PROGRAM ANOP
&PROGRAM SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.ENTRY   ANOP
&ENTRY   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.SET     ANOP
&SET     SETC  '&PARM'(5,K'&PARM-5)
         AGO   .PLOOP
.LENGTH  ANOP
&LENGTH  SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.FLENGTH ANOP
&FLENGTH SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.ELOOP   ANOP
         AIF   (&LENGTH NE '' AND &FLENGTH NE '').BADLEN
         AIF   (&PROGRAM EQ '').MISPROG
         MVC   EIBFN,=X'0E06'     SET EIBFN
         AIF   ('&PROGRAM'(1,1) EQ '''').STRING
         LOAD  EPLOC=&PROGRAM
         AGO   .LOADDUN
.STRING  ANOP
&PROGLEN SETA  K'&PROGRAM
         AIF   (&PROGLEN LT 3 OR &PROGLEN GT 10).BADPROG
&PROGRAM SETC  '&PROGRAM'(2,K'&PROGRAM-2)
         LOAD  EP=&PROGRAM
.LOADDUN ANOP
         AIF   (&ENTRY EQ '').NOSVENT
         LR    &ENTRY,R0          SET ENTRY POINT
.NOSVENT ANOP
         AIF   (&SET EQ '').NOSVSET
         LR    &SET,R0            SET ENTRY POINT
.NOSVSET ANOP                     SET LOAD POINT
         AIF   (&LENGTH EQ '').NOSVLEN
         SLL   R1,3               LENGTH * 8
         STH   R1,&LENGTH         SAVE LENGTH
.NOSVLEN ANOP
         AIF   (&FLENGTH EQ '').NOSVFLEN
         SLL   R1,3               LENGTH * 8
         ST    R1,&FLENGTH        SAVE FLENGTH
.NOSVFLEN ANOP
         MEXIT
.BADLEN  MNOTE 12,'LENGTH AND FLENGTH SPECIFIED'
         MEXIT
.MISPROG MNOTE 12,'PROGRAM IS MISSING'
         MEXIT
.BADPROG MNOTE 12,'INVALID PROGRAM'
         MEXIT
         MEND
