.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/05/06 RPI 308 ADD EXEC CICS LOAD
.*********************************************************************
         MACRO
         CICS_LOAD
         GBLB  &LOADBLDL
         LCLC  &PROGRAM,&ENTRY,&SET,&LENGTH,&FLENGTH
&NP      SETA  N'&SYSLIST
&IP      SETA  0
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  &SYSLIST(&IP)
         AIF   ('&PARM'(1,8) EQ 'PROGRAM(').PROGRAM
         AIF   ('&PARM'(1,6) EQ 'ENTRY(').ENTRY
         AIF   ('&PARM'(1,4) EQ 'SET(').SET
         AIF   ('&PARM'(1,7) EQ 'LENGTH(').LENGTH
         AIF   ('&PARM'(1,8) EQ 'FLENGTH(').FLENGTH
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.PROGRAM ANOP
&PROGRAM SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.*
.ENTRY   ANOP
&ENTRY   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.SET     ANOP
&SET     SETC  '&PARM'(5,K'&PARM-5)
         AGO   .PLOOP
.*
.LENGTH  ANOP
&LENGTH  SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.*
.FLENGTH ANOP
&FLENGTH SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.*
.ELOOP   ANOP
         AIF   (&LENGTH NE '' AND &FLENGTH NE '').BADLEN
         AIF   (&PROGRAM EQ '').MISPROG
         MVC   EIBFN,=X'0E06'     SET EIBFN
         XC    EIBRCODE,EIBRCODE  CLEAR EIBRCODE
         XC    EIBRESP,EIBRESP    CLEAR EIBRESP
         XC    EIBRESP2,EIBRESP2  CLEAR EIBRESP2
         AIF   (&LOADBLDL).NOBLDL
&LOADBLDL SETB 1
         B     CICS_LOAD_&SYSNDX._01 BRANCH ROUND BLDL BLOCK
LOAD_BLDLLIST DC H'1'             BLDL LIST WITH 1 ENTRY
         DC    H'12'              BLDL ENTRY LENGTH
LOAD_BLDLNAME DS CL8              BLDL ENTRY NAME TO FIND
         DS    XL4
CICS_LOAD_&SYSNDX._01 DS 0H
.NOBLDL  ANOP
         AIF   ('&PROGRAM'(1,1) EQ '''').STRING
         MVC   LOAD_BLDLNAME,&PROGRAM MOVE PROGRAM FOR BLDL
         MVC   EIBRSRCE,&PROGRAM  SET EIBRSRCE AS LABEL
         AGO   .NOSTRING
.*
.STRING  ANOP
&PROGLEN SETA  K'&PROGRAM
         AIF   (&PROGLEN LT 3 OR &PROGLEN GT 10).BADPROG
         MVC   LOAD_BLDLNAME,=CL8&PROGRAM MOVE PROGRAM FOR BLDL
         MVC   EIBRSRCE,=CL8&PROGRAM SET EIBRSRCE AS STRING
.NOSTRING ANOP
         BLDL  0,LOAD_BLDLLIST    BLDL
         LTR   R15,R15            RC=4 IF ENTRY NOT FOUND
         BZ    CICS_LOAD_&SYSNDX._02 EXIT IF OK
.* INVOKE PGMIDERR
         MVI   EIBRCODE,X'01'     SET EIBRCODE
         MVC   EIBRESP,DFHRESP(PGMIDERR) SET EIBRESP=PGMIDERR
         MVC   EIBRESP2,=F'3'     SET EIBRESP2=PROGRAM NOT FOUND
         DC    AL3(0),C'ABEND'    MARKER FOR Z390KCP ESTAE
         DC    AL4(CICS_LOAD_&SYSNDX._IGNORE) ENTPT FOR IGNORE COND
.*
CICS_LOAD_&SYSNDX._02 DS 0H
         LOAD  EPLOC=EIBRSRCE
         AIF   (&ENTRY EQ '').NOSVENT
         LR    &ENTRY,R0          SET ENTRY POINT
.NOSVENT ANOP
         AIF   (&SET EQ '').NOSVSET
         LR    &SET,R0            SET ENTRY POINT
.NOSVSET ANOP                     SET LOAD POINT
         AIF   (&LENGTH EQ '').NOSVLEN
         SLL   R1,3               LENGTH * 8
         STH   R1,&LENGTH         SAVE LENGTH
.NOSVLEN ANOP
         AIF   (&FLENGTH EQ '').NOSVFLEN
         SLL   R1,3               LENGTH * 8
         ST    R1,&FLENGTH        SAVE FLENGTH
.NOSVFLEN ANOP
CICS_LOAD_&SYSNDX._IGNORE DS 0H
         MEXIT
.*
.BADLEN  MNOTE 12,'LENGTH AND FLENGTH SPECIFIED'
         MEXIT
.*
.MISPROG MNOTE 12,'PROGRAM IS MISSING'
         MEXIT
.*
.BADPROG MNOTE 12,'INVALID PROGRAM'
         MEXIT
         MEND
