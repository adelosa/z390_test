.**********************************************************************
.* Copyright 2006 Automated Software Tools Corporation                *
.* This source code is part of z390 assembler/emulator package        *
.* The z390 package is distributed under GNU general public license   *
.* Author - Don Higgins                                               *
.**********************************************************************
         MACRO
         CICS_DEQ
         GBLB  &PARMS_1206
         LCLB  &NOHAND
         LCLC  &RES,&LENGTH,&RESP,&RESP2
&NP      SETA  N'&SYSLIST
&IP      SETA  0
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  '&SYSLIST(&IP)'
         AIF   ('&PARM'(1,9) EQ 'RESOURCE(').RES
         AIF   ('&PARM'(1,7) EQ 'LENGTH(').LENGTH
         AIF   ('&PARM'(1,8) EQ 'NOHANDLE').NOHAND
         AIF   ('&PARM'(1,5) EQ 'RESP(').RESP
         AIF   ('&PARM'(1,6) EQ 'RESP2(').RESP2
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.RES     ANOP
&RES     SETC  '&PARM'(10,K'&PARM-10)
         AGO   .PLOOP
.*
.LENGTH  ANOP
&LENGTH  SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.*
.NOHAND  ANOP
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP    ANOP
&RESP    SETC  '&PARM'(6,K'&PARM-6)
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP2   ANOP
&RESP2   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.ELOOP   ANOP
         AIF   ('&RES' EQ '').MISRES
         L     R15,=A(P1206)      LOAD PARM ADDRESS
         USING (P1206,P1206END),R15
         XC    P1206(P1206END-P1206),P1206 CLEAR PARMS
         MVC   P1206FN,=X'1206'   SET FUNCTION CODE
         AIF   ('&LENGTH' EQ '').DUNLEN
         AIF   (T'&LENGTH NE 'N').LENLABL
         LA    R1,&LENGTH         R1=LENGTH
         STH   R1,P1206LEN        SAVE IT
         AGO   .DUNLEN
.*
.LENLABL ANOP
         LH    R1,&LENGTH         R1=LENGTH FROM LABEL
         STH   R1,P1206LEN        SAVE IT
.DUNLEN  ANOP
.* PROCESS RESOURCE
.* ONLY THE RESOURCE ADDRESS IS PASSED, WE HAVE NOT VALIDATED LENGTH
         LA    R1,&RES            R1=ADDRESS OF RESOURCE
         ST    R1,P1206RES        SAVE IT
         ST    DFHEIBR,P1206EIB   SET EIB ADDRESS
         ST    TCTTEAR,P1206TCT   SET TCTTE ADDRESS
         AIF   (NOT &NOHAND).DUNNOH
         MVI   P1206NOH,X'FF'     SET NOHANDLE
.DUNNOH  ANOP
         AIF   ('&RESP' EQ '').NORESP
         LA    R1,&RESP           ADDRESS OF RESP
         ST    R1,P1206RSP        SAVE IT
.NORESP  ANOP
         AIF   ('&RESP2' EQ '').NORESP2
         LA    R1,&RESP2          ADDRESS OF RESP2
         ST    R1,P1206RS2        SAVE IT
.NORESP2 ANOP
         ZCALL LCL1206,(P1206),MF=I CALL DEQ PROCESSOR
.*
         L     R15,=A(P1206)      LOAD PARM ADDRESS
         CLI   P1206NOH,X'FF'     NOHANDLE ?
         BE    P1206BYP_&SYSNDX   EXIT IF IT IS
         OC    EIBRESP,EIBRESP    ANY BAD RESPONSE ?
         BZ    P1206BYP_&SYSNDX   EXIT IF NONE
* LENGERR
         DC    AL3(0),C'ABEND'    MARKER FOR Z390KCP ESTAE
         DC    AL4(P1206BYP_&SYSNDX) ENTRYPOINT FOR IGNORE CONDITION
*
         AIF   (&PARMS_1206).BYP1206
&PARMS_1206 SETB 1
P1206    EQU   *
P1206FN  DS    XL2                FUNCTION CODE
P1206EIB DS    AL4                EIB
P1206TCT DS    AL4                TCTTE
P1206RES DS    AL4                RESOURCE ADDRESS
P1206LEN DS    XL2                LENGTH
P1206NOH DS    X                  FF=NOHANDLE
P1206RSP DS    AL4                RESP
P1206RS2 DS    AL4                RESP2
P1206END EQU   *                  END MARKER
         DS    0H
*
.BYP1206 ANOP
P1206BYP_&SYSNDX DS 0H
         DROP  R15
         MEXIT
.*
.MISRES  MNOTE 12,'RESOURCE IS MANDATORY'
         MEXIT
         MEND
