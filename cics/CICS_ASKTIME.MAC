.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 04/19/08 RPI 833 ADD STRING QUOTES FOR HLASM COMPATIBILITY
.*********************************************************************
         MACRO
         CICS_ASKTIME
         LCLC  &ABSTIME
&NP      SETA  N'&SYSLIST
&IP      SETA  0
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  '&SYSLIST(&IP)'
         AIF   ('&PARM'(1,8) EQ 'ABSTIME(').ABSTIME
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.ABSTIME ANOP
&ABSTIME SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.*
.ELOOP   ANOP
         MVC   EIBFN,=X'1002'     SET EIBFN
         TIME  DEC                GET TIME/DATE
         ST    R0,EIBRESP         SAVE TIME AS HHMMSSTT
         MVO   EIBTIME,EIBRESP(3) MOVE AS PACKED
         OI    EIBTIME+3,X'0C'    SET +VE SIGN
         ST    R1,EIBDATE         SET DATE AS CCYYDDD+
         XC    EIBRCODE,EIBRCODE  CLEAR EIBRCODE
         XC    EIBRESP,EIBRESP    CLEAR EIBRESP
         XC    EIBRESP2,EIBRESP2  CLEAR EIBRESP2
         AIF   ('&ABSTIME' EQ '').DUNASK
         MVC   EIBFN,=X'4A02'     SET EIBFN
         TIME  CLOCK,&ABSTIME,CLOCKTYPE=STCK
         LG    R0,&ABSTIME        LOAD STCK VALUE
         SRLG  R0,R0,12           SHIFT RIGHT 12 BITS
         CVDG  R0,CICS_ASKTIME_&SYSNDX._AB DECIMAL TIME IN uS
         SRP   CICS_ASKTIME_&SYSNDX._AB,64-3,5 SHIFT AND ROUND
         MVC   &ABSTIME.(8),CICS_ASKTIME_&SYSNDX._AB+8 MOVE TO OUTPUT
         NI    &ABSTIME+7,X'0F'   mS WITH NO mS
         B     CICS_ASKTIME_&SYSNDX._BYP EXIT
CICS_ASKTIME_&SYSNDX._AB DS PL16 ABSTIME HOLDING AREA
CICS_ASKTIME_&SYSNDX._BYP EQU *
.DUNASK  ANOP
         MEXIT
         MEND
