.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
         MACRO
         CICS_LINK
         LCLC  &PROGRAM,&COMMAREA,&LENGTH
&NP      SETA  N'&SYSLIST
&IP      SETA  0
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  &SYSLIST(&IP)
         AIF   ('&PARM'(1,8)  EQ 'PROGRAM(').PROGRAM
         AIF   ('&PARM'(1,9)  EQ 'COMMAREA(').COMMAREA
         AIF   ('&PARM'(1,7)  EQ 'LENGTH(').LENGTH
         AGO   .PLOOP
.PROGRAM ANOP
&PROGRAM SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.COMMAREA ANOP
&COMMAREA SETC '&PARM'(10,K'&PARM-10)
         AGO   .PLOOP
.LENGTH  ANOP
&LENGTH  SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.ELOOP   ANOP
         AIF   (&PROGRAM EQ '').MISPROG
         AIF   (&COMMAREA NE '').COMMCHK
         AIF   (&LENGTH EQ '').COMMCHK
         MNOTE 12,'LENGTH WITHOUT COMMAREA'
         MEXIT
.COMMCHK ANOP
         MVC   DFHEICAL,EIBCALEN  SAVE EIB COMMAREA LENGTH
         AIF   (&COMMAREA NE '').GOTCOMM
         XC    DFHEICAP,DFHEICAP  CLEAR COMMAREA ADDRESS
         XC    EIBCALEN,EIBCALEN  CLEAR COMMAREA LENGTH
         AGO   .ALLDONE
.GOTCOMM ANOP
         LA    R0,&COMMAREA       COMMAREA ADDRESS
         ST    R0,DFHEICAP        SET COMMAREA ADDRESS
         AIF   (&LENGTH NE '').LENVAL
&LENGTH  SETC  'L''&COMMAREA'
         AGO   .LENNUM
.LENVAL  ANOP
         AIF   ('&LENGTH'(1,2) EQ 'L''').LENNUM
         AIF   ('&LENGTH'(1,1) LT '0' OR '&LENGTH'(1,1) GT '9').LENLABL
.LENNUM  ANOP
         MVC   EIBCALEN,=AL2(&LENGTH) SET NUMERIC COMMAREA LENGTH
         AGO   .ALLDONE
.LENLABL ANOP
         MVC   EIBCALEN,&LENGTH   SET COMMAREA LENGTH FROM LABEL
.ALLDONE ANOP
         MVC   EIBFN,=X'0E02'     SET EIBFN
         LH    R0,TCTTELNK        R0=CURRENT LINK-LEVEL
         SLL   R0,2               R0=LINK-LEVEL * 4
         L     R1,TCTTELKA        R1=LKA ADDRESS
         AR    R1,R0              INDEX TO LINK-LEVEL ENTRY
         ST    R13,0(R1)          SAVE OUR DSA ADDRESS
         LH    R0,TCTTELNK        R0=CURRENT LINK-LEVEL
         AHI   R0,1               +1
         STH   R0,TCTTELNK        STORE BACK
         LA    R1,DFHEITCT        SET THE PARMS ADDRESS
         AIF   ('&PROGRAM'(1,1) EQ '''').STRING
         LINK  EPLOC=&PROGRAM
         AGO   .POSTLINK
.STRING  ANOP
&PROGLEN SETA  K'&PROGRAM
         AIF   (&PROGLEN LT 3 OR &PROGLEN GT 10).BADPROG
&PROGRAM SETC  '&PROGRAM'(2,&PROGLEN-2)
         LINK  EP=&PROGRAM
.POSTLINK ANOP
         MVC   EIBCALEN,DFHEICAL  RESTORE EIB COMMAREA LENGTH
         MEXIT
.MISPROG MNOTE 12,'PROGRAM IS MISSING'
         MEXIT
.BADPROG MNOTE 12,'INVALID PROGRAM'
         MEXIT
         MEND
