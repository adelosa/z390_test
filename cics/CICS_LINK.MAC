.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 04/19/08 RPI 833 ADD STRING QUOTES FOR HLASM COMPATIBILITY
.*********************************************************************
         MACRO
         CICS_LINK
         GBLB  &LINKBLDL
         LCLC  &PROGRAM,&COMMAREA,&LENGTH
&NP      SETA  N'&SYSLIST
&IP      SETA  0
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  '&SYSLIST(&IP)'
         AIF   ('&PARM'(1,8)  EQ 'PROGRAM(').PROGRAM
         AIF   ('&PARM'(1,9)  EQ 'COMMAREA(').COMMAREA
         AIF   ('&PARM'(1,7)  EQ 'LENGTH(').LENGTH
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.PROGRAM ANOP
&PROGRAM SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.*
.COMMAREA ANOP
&COMMAREA SETC '&PARM'(10,K'&PARM-10)
         AGO   .PLOOP
.*
.LENGTH  ANOP
&LENGTH  SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.*
.ELOOP   ANOP
         AIF   ('&PROGRAM' EQ '').MISPROG
         MVC   EIBFN,=X'0E02'     SET EIBFN
         XC    EIBRCODE,EIBRCODE  CLEAR EIBRCODE
         XC    EIBRESP,EIBRESP    CLEAR EIBRESP
         XC    EIBRESP2,EIBRESP2  CLEAR EIBRESP2
         AIF   (&LINKBLDL).NOBLDL
&LINKBLDL SETB 1
         B     CICS_LINK_&SYSNDX._01 BRANCH ROUND BLDL BLOCK
LINK_BLDLLIST DC H'1'             BLDL LIST WITH 1 ENTRY
         DC    H'12'              BLDL ENTRY LENGTH
LINK_BLDLNAME DS CL8              BLDL ENTRY NAME TO FIND
         DS    XL4
CICS_LINK_&SYSNDX._01 DS 0H
.NOBLDL  ANOP
         AIF   ('&PROGRAM'(1,1) EQ '''').STRING
         MVC   LINK_BLDLNAME,&PROGRAM MOVE PROGRAM FOR BLDL
         MVC   EIBRSRCE,&PROGRAM  SET EIBRSRCE AS LABEL
         MVC   EIBPROG,&PROGRAM   SET EIBPROG  AS LABEL
         AGO   .NOSTRING
.*
.STRING  ANOP
&PROGLEN SETA  K'&PROGRAM
         AIF   (&PROGLEN LT 3 OR &PROGLEN GT 10).BADPROG
         MVC   LINK_BLDLNAME,=CL8&PROGRAM MOVE PROGRAM FOR BLDL
         MVC   EIBRSRCE,=CL8&PROGRAM SET EIBRSRCE AS STRING
         MVC   EIBPROG,=CL8&PROGRAM  SET EIBPROG  AS STRING
.NOSTRING ANOP
         BLDL  0,LINK_BLDLLIST    BLDL
         LTR   R15,R15            RC=4 IF ENTRY NOT FOUND
         BZ    CICS_LINK_&SYSNDX._02 EXIT IF OK
.* INVOKE PGMIDERR
         MVI   EIBRCODE,X'01'     SET EIBRCODE
         MVC   EIBRESP,DFHRESP(PGMIDERR) SET EIBRESP=PGMIDERR
         MVC   EIBRESP2,=F'3'     SET EIBRESP2=PROGRAM NOT FOUND
         DC    AL3(0),C'ABEND'    MARKER FOR Z390KCP ESTAE
         DC    AL4(CICS_LINK_&SYSNDX._IGNORE) ENTPT FOR IGNORE COND
.*
CICS_LINK_&SYSNDX._02 DS 0H
         AIF   ('&COMMAREA' NE '').COMMCHK
         AIF   ('&LENGTH' EQ '').COMMCHK
         MNOTE 12,'LENGTH WITHOUT COMMAREA'
CICS_LINK_&SYSNDX._IGNORE DS 0H
         MEXIT
.*
.COMMCHK ANOP
         MVC   DFHEICAL,EIBCALEN  SAVE EIB COMMAREA LENGTH
         AIF   ('&COMMAREA' NE '').GOTCOMM
         XC    DFHEICAP,DFHEICAP  CLEAR COMMAREA ADDRESS
         XC    EIBCALEN,EIBCALEN  CLEAR COMMAREA LENGTH
         AGO   .ALLDONE
.*
.GOTCOMM ANOP
&COMMTQ  SETC  T'&COMMAREA
         AIF   ('&COMMAREA'(1,1) EQ '=' OR '&COMMTQ' EQ 'A').COMADLIT
         LA    R0,&COMMAREA       COMMAREA ADDRESS
         AGO   .COMMLEN
.*
.COMADLIT ANOP
         L     R0,&COMMAREA       INDIRECT COMMAREA ADDRESS
.* LENGTH IS COMPULSORY FOR INDIRECT
         AIF   ('&LENGTH' EQ '').BADLEN
.COMMLEN ANOP
         ST    R0,DFHEICAP        SET COMMAREA ADDRESS
         AIF   ('&LENGTH' NE '').LENVAL
&LENGTH  SETC  'L''&COMMAREA'
         AGO   .LENNUM
.*
.LENVAL  ANOP
         AIF   ('&LENGTH'(2,1) EQ '''').LENNUM
         AIF   ('&LENGTH'(1,1) LT '0' OR '&LENGTH'(1,1) GT '9').LENLABL
.LENNUM  ANOP
         MVC   EIBCALEN,=AL2(&LENGTH) SET NUMERIC COMMAREA LENGTH
         AGO   .ALLDONE
.*
.LENLABL ANOP
         MVC   EIBCALEN,&LENGTH   SET COMMAREA LENGTH FROM LABEL
.ALLDONE ANOP
         LH    R0,TCTTELNK        R0=CURRENT LINK-LEVEL
         AHI   R0,1               +1
         STH   R0,TCTTELNK        STORE BACK
         LOAD  EPLOC=EIBRSRCE
         LA    R1,DFHEICAP        SET THE PARM ADDRESS
         LA    R14,CICS_LINK_&SYSNDX._RETURN SET RETURN ADDRESS
         BR    R0                 GO TO ENTRY POINT
.*
CICS_LINK_&SYSNDX._RETURN DS 0H
         MVC   EIBCALEN,DFHEICAL  RESTORE EIB COMMAREA LENGTH
CICS_LINK_&SYSNDX._IGNORE DS 0H
         MEXIT
.*
.MISPROG MNOTE 12,'PROGRAM IS MISSING'
         MEXIT
.*
.BADPROG MNOTE 12,'INVALID PROGRAM'
         MEXIT
.*
.BADLEN  MNOTE 12,'LENGTH IS MANDATORY FOR INDIRECT COMMAREA'
CICS_LINK_&SYSNDX._IGNORE DS 0H
         MEXIT
         MEND
