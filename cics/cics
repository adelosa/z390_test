.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/05/06 RPI 308 ADD EXEC CICS LOAD     
.*********************************************************************
         MACRO
         CICS_LOAD 
         LCLC &PROGRAM,&ENTRY
&NP      SETA N'&SYSLIST
&IP      SETA 0
.PLOOP   ANOP
&IP      SETA &IP+1
         AIF  (&IP GT &NP).ELOOP
&PARM    SETC &SYSLIST(&IP)
         AIF  ('&PARM'(1,8) EQ 'PROGRAM(').PROGRAM
         AIF  ('&PARM'(1,6) EQ 'ENTRY(').ENTRY
         AGO  .PLOOP
.PROGRAM ANOP
&PROGRAM SETC '&PARM'(9,K'&PARM-9)
         AGO  .PLOOP
.ENTRY   ANOP
&ENTRY   SETC '&PARM'(7,K'&PARM-7)
         AGO  .PLOOP
.ELOOP   ANOP
         LOAD  EP=&PROGRAM
         ST    0,&ENTRY
         MEND
.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/06/06 RPI 308 ADD EXEC CICS SUPPORT FOR RECEIVE     
.*********************************************************************
         MACRO
         CICS_RECEIVE                      
         LCLC &INTO,&LENGTH
&NP      SETA N'&SYSLIST
&IP      SETA 0
.PLOOP   ANOP
&IP      SETA &IP+1
         AIF  (&IP GT &NP).ELOOP
&PARM    SETC &SYSLIST(&IP)
         AIF  ('&PARM'(1,5) EQ 'INTO(').INTO
         AIF  ('&PARM'(1,7) EQ 'LENGTH(').LENGTH
         AGO  .PLOOP
.INTO    ANOP
&INTO    SETC '&PARM'(6,K'&PARM-6)
         AGO  .PLOOP
.LENGTH  ANOP
&LENGTH  SETC '&PARM'(8,K'&PARM-8)
         AGO  .PLOOP
.ELOOP   ANOP
         AIF   (&INTO EQ '').READAID
         AIF   (&LENGTH NE '').TGET
&LENGTH  SETC  'L''&INTO'
         AGO   .TGET
.READAID ANOP    
&INTO    SETC  'EIBAID'
&LENGTH  SETC  '1'
.TGET    ANOP
         TGET  &INTO,&LENGTH,ASIS 
         MEND
.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/05/06 RPI 308 CICS RETURN CALLED FROM EXEC     
.*********************************************************************
         MACRO
         CICS_RETURN
         DFHEIRET                                
         MEND
.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/05/06 RPI 308 CICS RETURN CALLED FROM EXEC     
.*********************************************************************
         MACRO
         CICS_SEND 
         LCLB &TEXT
         LCLC &FROM,&LENGTH
&NP      SETA N'&SYSLIST
&IP      SETA 0
.PLOOP   ANOP
&IP      SETA &IP+1
         AIF  (&IP GT &NP).ELOOP
&PARM    SETC &SYSLIST(&IP)
         AIF  (&PARM EQ 'TEXT').TEXT
         AIF  ('&PARM'(1,5) EQ 'FROM(').FROM
         AIF  ('&PARM'(1,7) EQ 'LENGTH(').LENGTH
         AGO  .PLOOP
.TEXT    ANOP
&TEXT    SETB 1
         AGO  .PLOOP
.FROM    ANOP
&FROM    SETC '&PARM'(6,K'&PARM-6)
         AGO  .PLOOP
.LENGTH  ANOP
&LENGTH  SETC '&PARM'(8,K'&PARM-8)
         AGO  .PLOOP
.ELOOP   ANOP
         AIF  (&TEXT).SENDTEXT
.SEND3270 ANOP
         TPUT &FROM,&LENGTH,FULLSCR  SEND TN3270 DATA STREAM          
         MEXIT
.SENDTEXT ANOP
         TPUT &FROM+4,&LENGTH-4,EDIT  SEND TEXT WITOUT 4 BYTE TERM ID
         MEXIT
         MEND
.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/06/06 RPI 308 - ADD EXEC CICS SUPPORT FOR EIB DSECT MAP
.*********************************************************************
         MACRO
         DFHEIBLK
.*
.* DEFINE INTERFACE CONTROL BLOCK  (CICS SETS ZCVTEIBP AND DFHEIBP)
.*
DFHEIBLK DSECT
EIBTIME  DS    PL4  0HHMMSS
EIBDATE  DS    PL4  0CYYDDD
EIBTRN   DS    0CL8 TRANSACTION PROGRAM NAME
EIBSYSID DS    CL4  FIRST 4 CHARACTERS = SYSTEM ID
EIBTRNID DS    CL4  LAST  4 CHARACTERS = TRAN   ID 
EIBCALEN DS    H    COMMON AREA LENGTH
EIBAID   DS    X    ATTENTION CODE FROM EXEC CICS RECEIVE
EIBLENG  EQU   *-DFHEIBLK
         MEND
.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/06/06 RPI 308 - ADD EXEC CICS SUPPORT FOR DFHEAI0 COMMAREA ALLOC     
.*********************************************************************
         MACRO
         DFHEICAD
.*
.* DEFINE COMM AREA DSECT  (SEE DFHEAI0 INIT OF ZCVTCAP AND DFHEICAP)
.*
DFHEICAD DSECT
         DS    XL(5000)     GLOBAL SHARED COMMON AREA 
DFHEICAL EQU   *-DFHEICAD
         MEND
.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/06/06 RPI 308 - ADD EXEC CICS SUPPORT FOR CICS EPILOG                  
.*********************************************************************
.*  If options CICS and EPILOG are on, then
.*  replace END statement with DFHEIEND       
.*********************************************************************
         MACRO
&N       DFHEIEND  &ENTRY
.*
.*       1. DEFINE DFHEIEND LABEL FOR END OF DSA DFHEISTG DSECT
.*       2. SWITCH BACK TO FIRST CSECT
.*       3. EXIT PROGRAM VIA DFHEIRET CALL TO DFHEAI1
.*
DFHEISTG DSECT
         ORG
DFHEIEND EQU   *
         GBLC  &DFHEIENT_CSECT
&DFHEIENT_CSECT CSECT
         DFHEIRET
         END
         MEND
.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/06/06 RPI 308 - ADD EXEC CICS SUPPORT FOR CICS PROLOG
.*********************************************************************
         MACRO
&N       DFHEIENT &CODEBASE=12,  USER CODE BASE REG                    X
               &EIBREG=11        EIB INTERFACE CB BASE FROM DFHEIBP
.*
.*       DEFINE EXEC CICS PROGRAM ENTRY 
.*                          
.*       THIS MACRO CALL REPLACES FIRST CSECT  
.*       OF MLC SOURCE CODE IF CICS OPTION AND PROLOG OPTIONS ON
.*
         GBLC  &DFHEIENT_CSECT
&DFHEIENT_CSECT SETC &N        SAVE CSECT FOR DFHEIEND RESET
&N       CSECT    
         STM   14,12,12(13)               SAVE REGS IN PREV DSA/SA
         BALR  &CODEBASE,0
         USING *,&CODEBASE                CODEBASE > USER CODE BASE
         L     0,=A(DFHEIEND-DFHEISTG)    R0 = DSA LENGTH
         CALL  DFHEAI0                    INITIALIZE R13 > DSA
         USING DFHEISTG,13                R13      > DSA
         L     &EIBREG,DFHEIBP
         USING DFHEIBLK,&EIBREG           EIBREG   > EIB
         BALR  &CODEBASE,0                
         USING *,&CODEBASE                CODEBASE > USER CODE
* NOTE DFHEAI0 EXITS TO R14+6 IF NO DSA FOR NON CICS PGM TO SKIP EIB L
         CNOP  0,4                        REALIGN FOR NON CICS SUBENTRY
         MEND














                                                    
.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/06/06 RPI 308 - ADD EXEC CICS SUPPORT FOR CICS PROLOG GLOBALS
.*********************************************************************
         MACRO
         DFHEIGBL
.*
.*       DEFINE GLOBAL EXEC CICS VARIABLES AND DFHEIBLK DSECT
.*                          
.*       THIS MACRO CALL IS INSERTED BEFORE FIRST LINE  
.*       OF MLC SOURCE CODE IF CICS OPTION AND PROLOG OPTIONS ON
.*
         DFHEIBLK ,  DEFINE INTERFACE CONTROL BLOCK
         ZCVTD    ,  Z390 CVT WITH ZCVTEIBP EIP AND ZCVTCAP COMMAREA 
         MEND
.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/06/06 RPI 308 - ADD EXEC CICS SUPPORT FOR EXIT FROM PROGRAM
.* 05/25/06 RPI 317 - PASS DSA LEN IN R0 SO DFHEAI1 CAN INGNORE IF 0
.*********************************************************************
.*  exit from EXEC CICS assembler program   
.*  Notes:
.*   1.  DFHEIRET called from CICS_RETURN and DFHEIEND.  Note DFHEIEND
.*       inserted at END statement if CICS and EPILOG options on.
.*   2.  DFHEIRET calls DFHEAI1 to free DSA and exit    
.*                                                   
.*********************************************************************
         MACRO
&N       DFHEIRET       
         L     0,=A(DFHEIEND-DFHEISTG)    R0 = DSA LENGTH
&N       CALL  DFHEAI1
         MEND
.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/06/06 RPI 308 - ADD EXEC CICS SUPPORT FOR DSA DSECT MAP
.*********************************************************************
         MACRO
         DFHEISTG
.*
.*       DEFINE EXEC CICS DYNAMIC STORAGE AREA DSECT
.*                          
.*       THIS MACRO CALL IS INSERTED AFTER FIRST DFHEISTG DSECT  
.*       OF MLC SOURCE CODE IF CICS OPTION AND PROLOG OPTIONS ON
.*
.*       USER FIELDS IN DSA SHOULD FOLLOW DFHEISTG DSECT STATEMENT
.*
.*       NOTE EPILOG OPTION INSERTS DHFEIEND AT END WHICH CALLS
.*       DFHEIRET AND DEFINES DFHEIEND LABEL AT END OF DSA.
.*
DFHEISTG DSECT
DFHSA    DS    18F  SAVE AREA
DFHEIBP  DS    A    INTERFACE BLOCK POINTER FROM ZCVTEIBP BY DFHEAI0
DFHEICAP DS    A    COMMON AREA POINTER FROM ZCVTCAP BY DFHEAI0
DFHEIPL  DS    64F  PARM AREA
DFHEILEN DS    A    LENGTH OF DSA SET BY DFHEAI0 AND USED BY DFHEAI1
DFHEIUSR DS    0D   USER PROGRAM STORAGE (SEE DFHEIEND FOR END)
         MEND
