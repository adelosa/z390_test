.**********************************************************************
.* Copyright 2006 Automated Software Tools Corporation                *
.* This source code is part of z390 assembler/emulator package        *
.* The z390 package is distributed under GNU general public license   *
.* Author - Don Higgins                                               *
.**********************************************************************
.* 04/19/08 RPI 833 ADD STRING QUOTES FOR HLASM COMPATIBILITY         *
.**********************************************************************
         MACRO
         CICS_XCTL
         GBLB  &PARMS_0E04
         LCLB  &NOHAND
         LCLC  &PROGRAM,&COMMAREA,&LENGTH,&RESP,&RESP2
&NP      SETA  N'&SYSLIST
&IP      SETA  0
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  '&SYSLIST(&IP)'
         AIF   ('&PARM'(1,8) EQ 'PROGRAM(').PROGRAM
         AIF   ('&PARM'(1,9) EQ 'COMMAREA(').COMMAREA
         AIF   ('&PARM'(1,7) EQ 'LENGTH(').LENGTH
         AIF   ('&PARM'(1,8) EQ 'NOHANDLE').NOHAND
         AIF   ('&PARM'(1,5) EQ 'RESP(').RESP
         AIF   ('&PARM'(1,6) EQ 'RESP2(').RESP2
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.PROGRAM ANOP
&PROGRAM SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.*
.COMMAREA ANOP
&COMMAREA SETC '&PARM'(10,K'&PARM-10)
         AGO   .PLOOP
.*
.LENGTH  ANOP
&LENGTH  SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.*
.NOHAND  ANOP
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP    ANOP
&RESP    SETC  '&PARM'(6,K'&PARM-6)
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP2   ANOP
&RESP2   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.ELOOP   ANOP
         L     R15,=A(P0E04)      LOAD PARM ADDRESS
         USING (P0E04,P0E04END),R15
         XC    P0E04(P0E04END-P0E04),P0E04 CLEAR PARMS
         MVC   P0E04FN,=X'0E04'   SET FUNCTION CODE
         AIF   ('&PROGRAM' EQ '').MISPROG
         AIF   ('&PROGRAM'(1,1) EQ '''').STRING
         MVC   P0E04PGM,&PROGRAM  MOVE PROGRAM FROM LABEL
         AGO   .NOSTRING
.*
.STRING  ANOP
&PROGLEN SETA  K'&PROGRAM
         AIF   (&PROGLEN LT 3 OR &PROGLEN GT 10).BADPROG
         MVC   P0E04PGM,=CL8&PROGRAM MOVE PROGRAM AS STRING
.NOSTRING ANOP
         AIF   ('&COMMAREA' NE '').COMMCHK
         AIF   ('&LENGTH' EQ '').COMMCHK
         AGO   .COMMERR
.*
.COMMCHK ANOP
         AIF   ('&COMMAREA' EQ '').ALLDONE
.* PROCESS COMMAREA
&COMMTQ  SETC  T'&COMMAREA
         AIF   ('&COMMAREA'(1,1) EQ '=' OR '&COMMTQ' EQ 'A').COMADLIT
         LA    R1,&COMMAREA       R1=COMMAREA ADDRESS FROM LABEL
         ST    R1,P0E04COM        SAVE IT
         AGO   .COMMLEN
.*
.COMADLIT ANOP
         L     R1,&COMMAREA       R1=INDIRECT COMMAREA ADDRESS
         ST    R1,P0E04COM        SAVE IT
.* LENGTH IS COMPULSORY FOR INDIRECT
         AIF   ('&LENGTH' EQ '').BADLEN
         AGO   .LENVAL
.*
.COMMLEN ANOP
         AIF   ('&LENGTH' NE '').LENVAL
.* DEFAULT LENGTH
         LA    R1,L'&COMMAREA     R1=IMPLIED LENGTH OF COMMAREA
         STH   R1,P0E04CLN        SAVE IT
         AGO   .ALLDONE
.*
.LENVAL  ANOP
         AIF   ('&LENGTH'(2,1) EQ '''').LENNUM
         AIF   (T'&LENGTH NE 'N').LENLABL
.LENNUM  ANOP
         LA    R1,&LENGTH         R1=COMMAREA LENGTH
         STH   R1,P0E04CLN        SAVE IT
         AGO   .ALLDONE
.*
.LENLABL ANOP
         LH    R1,&LENGTH         R1=COMMAREA LENGTH FROM LABEL
         STH   R1,P0E04CLN        SAVE IT
.ALLDONE ANOP
         AIF   (NOT &NOHAND).TRYRESP
         MVI   P0E04NOH,X'FF'     SET NOHANDLE
.TRYRESP ANOP
         AIF   ('&RESP' EQ '').NORESP
         LA    R1,&RESP           ADDRESS OF RESP
         ST    R1,P0E04RSP        SAVE IT
.NORESP  ANOP
         AIF   ('&RESP2' EQ '').NORESP2
         LA    R1,&RESP2          ADDRESS OF RESP2
         ST    R1,P0E04RS2        SAVE IT
.NORESP2 ANOP
* SPECIAL LINKAGE
         LA    R1,P0E04           R1=PARAMETER LIST
         LA    R14,P0E04RTN_&SYSNDX RETURN ADDRESS
         L     R15,=V(LCL0E04)    R15=EXTERNAL XCTL MODULE ADDRESS
         BR    R15                GO TO IT
*
P0E04RTN_&SYSNDX EQU *
         L     R15,=A(P0E04)      LOAD PARM ADDRESS
         CLI   P0E04NOH,X'FF'     NOHANDLE ?
         BE    P0E04BYP_&SYSNDX   EXIT IF IT IS
         OC    EIBRESP,EIBRESP    ANY BAD RESPONSE ?
         BZ    P0E04BYP_&SYSNDX   EXIT IF NONE
* PGMIDERR
         DC    AL3(0),C'ABEND'    MARKER FOR Z390KCP ESTAE
         DC    AL4(P0E04BYP_&SYSNDX) ENTRYPOINT FOR IGNORE CONDITION
*
         AIF   (&PARMS_0E04).BYP0E04
&PARMS_0E04 SETB 1
P0E04    EQU   *
P0E04FN  DS    XL2                FUNCTION CODE
P0E04PGM DS    CL8                PROGRAM
P0E04COM DS    AL4                COMMAREA ADDRESS
P0E04CLN DS    XL2                COMMAREA LENGTH
P0E04NOH DS    X                  FF=NOHANDLE
P0E04RSP DS    AL4                RESP
P0E04RS2 DS    AL4                RESP2
P0E04END EQU   *                  END MARKER
         DS    0H
*
.BYP0E04 ANOP
P0E04BYP_&SYSNDX DS 0H
         DROP  R15
         MEXIT
.*
.COMMERR MNOTE 12,'LENGTH WITHOUT COMMAREA'
         MEXIT
.*
.MISPROG MNOTE 12,'PROGRAM IS MISSING'
         MEXIT
.*
.BADPROG MNOTE 12,'INVALID PROGRAM'
         MEXIT
.*
.BADLEN  MNOTE 12,'LENGTH IS MANDATORY FOR INDIRECT COMMAREA'
         MEXIT
         MEND
