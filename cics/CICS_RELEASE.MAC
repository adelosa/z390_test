.**********************************************************************
.* Copyright 2006 Automated Software Tools Corporation                *
.* This source code is part of z390 assembler/emulator package        *
.* The z390 package is distributed under GNU general public license   *
.* Author - Don Higgins                                               *
.**********************************************************************
.* 04/19/08 RPI 833 ADD STRING QUOTES FOR HLASM COMPATIBILITY         *
.**********************************************************************
         MACRO
         CICS_RELEASE
         GBLB  &PARMS_0E0A
         GBLC  &DFHEIENT_CSECT
         LCLB  &NOHAND
         LCLC  &PROGRAM,&RESP,&RESP2
&NP      SETA  N'&SYSLIST
&IP      SETA  0
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  '&SYSLIST(&IP)'
         AIF   ('&PARM'(1,8) EQ 'PROGRAM(').PROGRAM
         AIF   ('&PARM'(1,8) EQ 'NOHANDLE').NOHAND
         AIF   ('&PARM'(1,5) EQ 'RESP(').RESP
         AIF   ('&PARM'(1,6) EQ 'RESP2(').RESP2
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.PROGRAM ANOP
&PROGRAM SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.*
.NOHAND  ANOP
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP    ANOP
&RESP    SETC  '&PARM'(6,K'&PARM-6)
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP2   ANOP
&RESP2   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.ELOOP   ANOP
         L     R15,=A(P0E0A)      LOAD PARM ADDRESS
         USING (P0E0A,P0E0AEND),R15
         XC    P0E0A(P0E0AEND-P0E0A),P0E0A CLEAR PARMS
         MVC   P0E0AFN,=X'0E0A'   SET FUNCTION CODE
         ST    DFHEIBR,P0E0AEIB   SET EIB ADDRESS
         ST    TCTTEAR,P0E0ATCT   SET TCTTE ADDRESS
         MVC   P0E0ACST,=CL8'&DFHEIENT_CSECT' MOVE OUR PROGRAM NAME
         AIF   ('&PROGRAM' EQ '').MISPROG
         AIF   ('&PROGRAM'(1,1) EQ '''').STRING
         MVC   P0E0APGM,&PROGRAM  MOVE PROGRAM FROM LABEL
         AGO   .NOSTRING
.*
.STRING  ANOP
&PROGLEN SETA  K'&PROGRAM
         AIF   (&PROGLEN LT 3 OR &PROGLEN GT 10).BADPROG
         MVC   P0E0APGM,=CL8&PROGRAM MOVE PROGRAM AS STRING
.NOSTRING ANOP
         AIF   (NOT &NOHAND).TRYRESP
         MVI   P0E0ANOH,X'FF'     SET NOHANDLE
.TRYRESP ANOP
         AIF   ('&RESP' EQ '').NORESP
         LA    R1,&RESP           ADDRESS OF RESP
         ST    R1,P0E0ARSP        SAVE IT
.NORESP  ANOP
         AIF   ('&RESP2' EQ '').NORESP2
         LA    R1,&RESP2          ADDRESS OF RESP2
         ST    R1,P0E0ARS2        SAVE IT
.NORESP2 ANOP
         ZCALL LCL0E0A,(P0E0A),MF=I CALL LOAD PROCESSOR
*
         L     R15,=A(P0E0A)      LOAD PARM ADDRESS
         CLI   P0E0ANOH,X'FF'     NOHANDLE ?
         BE    P0E0ABYP_&SYSNDX   EXIT IF IT IS
         OC    EIBRESP,EIBRESP    ANY BAD RESPONSE ?
         BZ    P0E0ABYP_&SYSNDX   EXIT IF NONE
* INVREQ
         DC    AL3(0),C'ABEND'    MARKER FOR Z390KCP ESTAE
         DC    AL4(P0E0ABYP_&SYSNDX) ENTRYPOINT FOR IGNORE CONDITION
.*
         AIF   (&PARMS_0E0A).BYP0E0A
*
&PARMS_0E0A SETB 1
P0E0A    EQU   *
P0E0AFN  DS    XL2                FUNCTION CODE
P0E0AEIB DS    AL4                EIB
P0E0ATCT DS    AL4                TCTTE
P0E0APGM DS    CL8                PROGRAM
P0E0ACST DS    CL8                OUR PROGRAM NAME
P0E0ANOH DS    X                  FF=NOHANDLE
P0E0ARSP DS    AL4                RESP
P0E0ARS2 DS    AL4                RESP2
P0E0AEND EQU   *                  END MARKER
         DS    0H
*
.BYP0E0A ANOP
P0E0ABYP_&SYSNDX DS 0H
         DROP  R15
         MEXIT
.*
.MISPROG MNOTE 12,'PROGRAM IS MISSING'
         MEXIT
.*
.BADPROG MNOTE 12,'INVALID PROGRAM'
         MEXIT
         MEND
