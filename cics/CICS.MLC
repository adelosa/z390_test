.*********************************************************************
.* Copyright 2006 Automated Software Tools Corporation               *
.* This source code is part of z390 assembler/emulator package       *
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.*********************************************************************
.* 05/07/06 RPI 308 - ADD INITIAL EXEC CICS SUPPORT COMMAND PROCESSOR 
.*********************************************************************
.* CICS.390 z390 EXEC CICS assembler command processor performs the
.* the following functions:                                            
.*   1.  Initialize ZCVTEIBP pointer to EIB interface control block
.*       and initialize fields as defined in DFHEIBLK.MAC DSECT.
.*   2.  Initialize ZCVTCAP pointer to CA common area control block
.*       as defined in DFHEICAD.MAC DSECT.
.*   3.  If ZCVTPARM field defined, run transaction specified before  
.*       next prompt for transaction or LOGOFF.
.*   4.  If transaction is 4 characters or less than use previous
.*       transaction first 4 characters as transaction system id.  
.*       The initial default transaction system id is TEST.                
.*   4.  If transaction is 5 characters or more set transaction      
.*       system id to first 4 and transaction id to last 4.    
.*********************************************************************
CICS     SUBENTRY
*
* INITIALIZE EIB AND SET ZCVTEIBP
*
         L     R3,=A(ZCVT)
         USING IHAZCVT,R3
         ICM   R0,15,ZCVTEIBP
         BNZ   ERR_EIB1          ERROR - EIB ALREADY DEFINED
         GETMAIN R,LV=EIBLENG   
         LTR   15,15
         BNZ   ERR_EIB2          ERROR - EIB GETMAIN FAILED
         LR    R4,R0
         ST    R4,ZCVTEIBP       SET EIB IN ZCVT FIRST TIME
         USING DFHEIBLK,R4     
         LH    1,=AL2(DFHEICAL)  GET COMMAREA LENGTH FROM DFHEICAD
         STH   1,EIBCALEN        SAVE CA LENGTH IN EIB
*
* INITIALIZE CA AND SET ZCVTCAP                                       
*
         ICM   0,15,ZCVTCAP      R0 > CA
         BNZ   ERR_CA1           ERROR - CA ALREADY DEFINED
         LH    R1,EIBCALEN
         GETMAIN R,LV=(1)
         LTR   15,15
         BNZ   ERR_CA2           ERROR - CA GETMAIN FAILED
         ST    0,ZCVTCAP         SET CAP IN ZCVT FIRST TIME
*
* ISSUE CICS STARTUP MESSAGE IN GUAM MCS CONSOLE WINDOW VIEW
*
         GUAM  WINDOW,TITLE,'Z390 EXEC CICS COMMAND PROCESSOR'
         GUAM  WINDOW,VIEW,MCS   SWITCH TO MCS CONSOLE WTO/WTOR VIEW
         WTO   'Z390 EXEC CICS STARTING'
*
* IF ZCVTPARM HAS NO TRANSACTION, PROMPT FOR NEXT TRANSACTION
*       
         LH    R5,ZCVTPARM
         LTR   R5,R5
         BZ    CICS_PROMPT
*
* INITIZE NEXT TRANSACTION ID IN EIB
*
         MVC   EIBTRN,=CL8' '
         BCTR  R5,0
         EX    R5,MVCPARM
         B     CICS_RUN_TRN
MVCPARM  MVC   EIBTRN(0),ZCVTPARM+2
*
* PROMPT FOR NEXT TRANSACTION ID
*
CICS_PROMPT EQU *
         GUAM  WINDOW,TITLE,'Z390 EXEC CICS COMMAND PROCESSOR'
         GUAM  WINDOW,VIEW,MCS   SWITCH TO MCS CONSOLE WTO/WTOR VIEW
         MVC   EIBTRN,=CL8' '
         MVC   WTORECB,=F'0'
         WTOR  'ENTER CICS TRANSACTION',EIBTRN,8,WTORECB
         WAIT  ECB=WTORECB
         CLC   EIBTRN,=CL8'LOGOFF'
         BE    EXIT
         CLC   EIBTRN,=CL8'logoff'
         BE    EXIT
*
* RUN TRANSACTION
*       
CICS_RUN_TRN EQU *
         CLC   EIBTRNID,=CL4' '
         BNE   CICS_FIND_TRN
         MVC   EIBTRNID,EIBSYSID
         MVC   EIBSYSID,SYSIDDEF
*
* BLDL TO SEE IF TRAN EXISTS
*         
CICS_FIND_TRN EQU *
         MVC   SYSIDDEF,EIBSYSID  UPDATE SYSID DEFAULT 
         MVC   BLDLNAME,EIBTRN
         BLDL  0,BLDLLIST
         LTR   R15,R15            RC=4 IF ENTRY NOT FOUND
         BNZ   CICS_INVALID_TRN
*
* LINK TO EXECUTE TRANSACTION
*
         MVC   TRN_NAME,EIBTRN
         GUAM  WINDOW,TITLE,TRN_TITLE   SHOW TRN ON WINDOW TITLE
         GUAM  WINDOW,VIEW,SCREEN,24,80,0 RESET SCREEN TO 24 X 80     
         MVC   DTRN,EIBTRN
         WTO   MF=(E,STARTMSG)
         LA    R1,ZCVTEIBP        SET R1 > ZCVT EIBP AND CAP POINTERS
         LINK  EPLOC=EIBTRN
         B     CICS_PROMPT
*
* EXIT CICS WITH RC=0
*
EXIT     SUBEXIT 
*
* ISSUE INVALID TRANSACTION MSG AND RETURN TO PROMT
*
CICS_INVALID_TRN EQU *
         MVC  DINVTRN,EIBTRN
         WTO  MF=(E,INVMSG)
         B    CICS_PROMPT
*
* ABORT CICS WITH RC=16
*
ABORT    EQU   *
         WTO   'CICS - ABORTING'
         SUBEXIT RC=16
ERR_EIB1 WTO   'CICS - EIP PONTER ZCVTEIPB ALREADY DEFINED'
         B     ABORT
ERR_EIB2 WTO   'CICS - EIB GETMAIN FAILED'
         B     ABORT
ERR_CA1  WTO   'CICS - CA  POINTER ZCVTCAP ALREADY DEFINED'
         B     ABORT
ERR_CA2  WTO   'CICS - CA  GETMAIN FAILED'
         B     ABORT
*
* CICS COMMAND PROCESSOR DATA AREAS
*
         LTORG
WTORECB  DC    F'0'       ECB FOR WTOR PROMPT FOR TRANSACTION
SYSIDDEF DC    CL4'TEST'  DEFAULT TRANSACTION SYSTEM ID
BLDLLIST DC    H'1'       BLDL LIST WITH 1 ENTRY
BLDLENT  DC    H'12'      BLDL ENTRY WITH LENGTH EXCLUDING LENGTH FIELD
BLDLNAME DS    CL8' '     BLDL ENTRY NAME TO FIND IN SYS390 DIR OR CDE
         DC    XL4        TTRK R=1 IF FOUND K=1 IF CDE OR 0 IF SYS390  
STARTMSG DC    AL2(STARTMSG_END-*,0),C'TRANSACTION STARTING - '
DTRN     DC    CL8' '
STARTMSG_END EQU *
INVMSG   DC    AL2(INVMSG_END-*,0),C'INVALID TRANSACTION ID - '
DINVTRN  DC    CL8' '
INVMSG_END EQU *
TRN_TITLE DC C'Z390 EXEC CICS RUNNING '
TRN_NAME  DC CL8' ',X'00' NULL TERM TITLE
         DFHEIBLK ,       EIB INTERFACE CONTROL BLOCK DSECT
         DFHEICAD ,       CA  COMMON AREA DSECT
         ZCVTD    ,       Z390 CVT DSECT
         TN3270 SYMBOLS   DEFINE AID ENTER AND PF3 CODES
         EQUREGS
         GUAM   SYMBOLS   DEFINE GUAM GUI DIALOG WINDOW OPTIONS
         END
